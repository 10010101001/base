showmainplayerprev = 1
setpersist showmainplayerprev 1
setcomplete showmainplayerprev 1

ui_tip_main_profile = [uitext "Changes your name and other player settings." $ui_texttip 0]
ui_tip_main_maps = [uitext "Select a map and mode to start playing on." $ui_texttip 0]
ui_tip_main_connect = [uitext "Connect to the main server." $ui_texttip 0]
ui_tip_main_disconnect = [uitext "Disconnect from the current server." $ui_texttip 0]
ui_tip_main_help = [uitext "Get help with playing the game and operating the environment." $ui_texttip 0]
ui_tip_main_credits = [uitext "Show the people who made this game possible." $ui_texttip 0]
ui_tip_main_quit = [uitext "Quits the game." $ui_texttip 0]

uimenu "main" (format "%1 v%2" $versionname $versionstring) "textures/icon" [
    uihlist $ui_padbutton [
        if $showmainplayerprev [
            uivlist $ui_padbutton [uiplayerpreview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) 1 1 0.4 0.5]
        ]
        uivlist $ui_padbutton [
            uialign 0 -1
            uispace 0 0.01
            uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh
            uispace 0 0.01
            uibuttonm "Profile Setup" "textures/player" [uiopen "profile"] 0 main_profile
            uibuttonm "Select Map/Mode" "textures/editing" [uiopen "maps"] 0 main_maps
            uibuttonm "Connect to Server" "textures/modes/multi" [uiclose; connect] 0 main_connect
            uibuttonm "Disconnect" "textures/hud/alert" [disconnect] 0 main_disconnect
            uibuttonm "Help and Support" "textures/question" [uiopen "help"] 0 main_help
            uibuttonm "Credits" "textures/info" [uiopen "credits"] 0 main_credits
            uibuttonm "Quit" "textures/warning" [quit] 0 main_quit
        ]
    ]
] [] [] [] [] [] [= (isconnected) 0]

ui_creditlist = [
    uititle [
        uispace $ui_padsmall $ui_padsmall [
            uialign -1 -1
            uihlist $ui_padbutton [
                uiimage "textures/icon" $ui_default 0 $ui_titlesize $ui_titlesize
                uitext [@@@@arg1] $ui_textbig
            ]
        ]
    ] 0 0.032
    uispace $ui_padtiny 0 [
        uifill $ui_default 0 0 [
            uiclamp 1 1
            uiclamp- 1 1 1 1
            uispace $ui_padtiny $ui_padtiny [
                uialign -1 -2
                uivlist 0 [
                    looplist i $arg2 [
                        uiwraptext (strreplace (strreplace $i "^"" "^fs^fr^"" "^"^fS") ": " "^fa: ") 0.69 $ui_texttip
                    ]
                    uialign* -1 -2
                ]
            ]
        ]
    ]
    uifill $ui_default 0 0.02
]

uimenu "credits" "Red Eclipse Contributors" "textures/info" [
    uihlist $ui_padbutton [
        uiscroll 0.5 0.5 [
            uifill $ui_main1 0.57
            uivlist 0.0008 [
                uiclamp 1 1
                uialign -2 -1
                // Alphabetized by alias
                ui_creditlist "Red Eclipse" [
                    [Quinton "quin" Reeves : Lead Developer]
                    [Lee "eihrul" Salzman : Lead Programmer]
                ]
                ui_creditlist "Cube Engine 2" [
                    [Wouter "Aardappel" van Oortmerssen]
                    [Lee "eihrul" Salzman]
                    [Mike "Gilt" Dysart]
                    [Robert "baby-rabbit" Pointon]
                    [Quinton "Quin" Reeves]
                    [John "geartrooper" Siar]
                    [and others...]
                    "http://sauerbraten.org/"
                ]
                uiclamp* 1 1
            ]
        ]
        uivscroll 0.02 0.5
    ]
]
