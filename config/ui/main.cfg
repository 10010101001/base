togglemainmenu = [|| hidetopui [toggleui main]]
bind ESCAPE [togglemainmenu]

showmainplayerprev = 1
setpersist showmainplayerprev 1
setcomplete showmainplayerprev 1

uimenu "main" (format "%1 v%2" $versionname $versionstring) [
    uihlist $ui_padbutton [
        if $showmainplayerprev [
            uivlist $ui_padbutton [ uiplayerpreview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) 1 1 0.4 0.5 ]
        ]
        uivlist $ui_padbutton [
            uialign 0 -1
            uispace 0 0.01
            uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh
            uispace 0 0.01
            uibuttonf "Profile Setup" "textures/privs/none" $ui_buttonw $ui_buttonh [ showui "profile" ]
            uibuttonf "Play Offline" "textures/player" $ui_buttonw $ui_buttonh [ hideui "main"; localconnect ]
            uibuttonf "Connect to Server" "textures/team" $ui_buttonw $ui_buttonh [ hideui "main"; connect ]
            uibuttonf "Disconnect" "textures/hud/alert" $ui_buttonw $ui_buttonh [ disconnect ] 0 [] (! (isconnected))
            uibuttonf "Help and Support" "textures/question" $ui_buttonw $ui_buttonh [ showui "help" ]
            uibuttonf "Credits" "textures/info" $ui_buttonw $ui_buttonh [ showui "credits" ]
            uibuttonf "Quit" "textures/warning" $ui_buttonw $ui_buttonh [ quit ]
        ]
    ]
]

ui_creditlist = [
    uititle [
        uispace $ui_padsmall $ui_padsmall [
            uialign -1 -2
            uihlist $ui_padbutton [
                uiimage "textures/icon" $c_default 0 $ui_titlesize $ui_titlesize
                uitext [@@@@arg1] 1.1
            ]
        ]
    ] 0 0.032
    uispace $ui_padtiny 0 [
        uifill $ui_default 0 0 [
            uiclamp 1 1
            uiclamp- 1 1 1 1
            uispace $ui_padtiny $ui_padtiny [
                uialign -1 -2
                uivlist 0 [
                    looplist i $arg2 [
                        uiwraptext (strreplace (strreplace $i "^"" "^fs^fr^"" "^"^fS") ": " "^fa: ") 0.69 0.8
                    ]
                    uialign* -1 -2
                ]
            ]
        ]
    ]
    uifill $c_default 0 0.02
]

uimenu "credits" "Red Eclipse Contributors" [
    uihlist $ui_padbutton [
        uiscroll 0.8 0.8 [
            uifill $c_menu 0.77
            uivlist 0.0008 [
                uiclamp 1 1
                uialign -2 -1
                // Alphabetized by alias
                ui_creditlist "Red Eclipse" [
                    [Quinton "quin" Reeves : Lead Developer]
                    [Lee "eihrul" Salzman : Lead Programmer]
                ]
                ui_creditlist "Cube Engine 2" [
                    [Wouter "Aardappel" van Oortmerssen]
                    [Lee "eihrul" Salzman]
                    [Mike "Gilt" Dysart]
                    [Robert "baby-rabbit" Pointon]
                    [Quinton "Quin" Reeves]
                    [John "geartrooper" Siar]
                    [and others...]
                    "http://sauerbraten.org/"
                ]
                uiclamp* 1 1
            ]
        ]
        uivscroll 0.02 0.8 2.0
    ]
]
