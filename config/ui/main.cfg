togglemainmenu = [|| hidetopui [toggleui main]]
bind ESCAPE [togglemainmenu]

showmainplayerprev = 1
setpersist showmainplayerprev 1
setcomplete showmainplayerprev 1

ui_tip_profile = [ uitext "Changes your name and other player settings." $ui_texttip ]
ui_tip_offline = [ uitext "Play offline against bots." $ui_texttip ]
ui_tip_connect = [ uitext "Connect to the main server." $ui_texttip ]
ui_tip_disconnect = [ uitext "Disconnect from the current server." $ui_texttip ]
ui_tip_help = [ uitext "Get help with playing the game and operating the environment." $ui_texttip ]
ui_tip_credits = [ uitext "Show the people who made this game possible." $ui_texttip ]
ui_tip_quit = [ uitext "Quits the game." $ui_texttip ]

uimenu "main" (format "%1 v%2" $versionname $versionstring) [
    uihlist $ui_padbutton [
        if $showmainplayerprev [
            uivlist $ui_padbutton [ uiplayerpreview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) 1 1 0.4 0.5 ]
        ]
        uivlist $ui_padbutton [
            uialign 0 -1
            uispace 0 0.01
            uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh
            uispace 0 0.01
            uibuttonm "Profile Setup" "textures/privs/none" [ uiopen "profile" ] 0 profile
            uibuttonm "Play Offline" "textures/player" [ uiclose; localconnect ] 0 offline
            uibuttonm "Connect to Server" "textures/team" [ uiclose; connect ] 0 connect
            uibuttonm "Disconnect" "textures/hud/alert" [ disconnect ] 0 disconnect
            uibuttonm "Help and Support" "textures/question" [ uiopen "help" ] 0 help
            uibuttonm "Credits" "textures/info" [ uiopen "credits" ] 0 credits
            uibuttonm "Quit" "textures/warning" [ quit ] 0 quit
        ]
    ]
]

ui_creditlist = [
    uititle [
        uispace $ui_padsmall $ui_padsmall [
            uialign -1 -2
            uihlist $ui_padbutton [
                uiimage "textures/icon" $ui_default 0 $ui_titlesize $ui_titlesize
                uitext [@@@@arg1] 1.1
            ]
        ]
    ] 0 0.032
    uispace $ui_padtiny 0 [
        uifill $ui_default 0 0 [
            uiclamp 1 1
            uiclamp- 1 1 1 1
            uispace $ui_padtiny $ui_padtiny [
                uialign -1 -2
                uivlist 0 [
                    looplist i $arg2 [
                        uiwraptext (strreplace (strreplace $i "^"" "^fs^fr^"" "^"^fS") ": " "^fa: ") 0.69 $ui_texttip
                    ]
                    uialign* -1 -2
                ]
            ]
        ]
    ]
    uifill $ui_default 0 0.02
]

uimenu "credits" "Red Eclipse Contributors" [
    uihlist $ui_padbutton [
        uiscroll 0.8 0.8 [
            uifill $ui_main1 0.77
            uivlist 0.0008 [
                uiclamp 1 1
                uialign -2 -1
                // Alphabetized by alias
                ui_creditlist "Red Eclipse" [
                    [Quinton "quin" Reeves : Lead Developer]
                    [Lee "eihrul" Salzman : Lead Programmer]
                ]
                ui_creditlist "Cube Engine 2" [
                    [Wouter "Aardappel" van Oortmerssen]
                    [Lee "eihrul" Salzman]
                    [Mike "Gilt" Dysart]
                    [Robert "baby-rabbit" Pointon]
                    [Quinton "Quin" Reeves]
                    [John "geartrooper" Siar]
                    [and others...]
                    "http://sauerbraten.org/"
                ]
                uiclamp* 1 1
            ]
        ]
        uivscroll 0.02 0.8 2.0
    ]
]
