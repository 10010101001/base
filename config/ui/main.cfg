defvarp showmainplayerprev 0 1 1

ui_tip_main_profile = [uitext "Changes your name and other player settings." $ui_texttip 0]
ui_tip_main_maps = [uitext "Select a map and mode to start playing on." $ui_texttip 0]
ui_tip_main_servers = [uitext "Select a server to connect to and play on." $ui_texttip 0]
ui_tip_main_disconnect = [uitext "Disconnect from the current server." $ui_texttip 0]
ui_tip_main_help = [uitext "Get help with playing the game and operating the environment." $ui_texttip 0]
ui_tip_main_credits = [uitext "Show the people who made this game possible." $ui_texttip 0]
ui_tip_main_quit = [uitext "Quits the game." $ui_texttip 0]

uimenu "main" (format "%1 v%2" $versionname $versionstring) "<grey>textures/icon" [
    uihlist $ui_padbutton [
        if $showmainplayerprev [
            uiplayerpreview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) 1 1 $ui_prevw $ui_prevh [ui_playerprev_easteregg]
        ]
        uivlist 0 [
            uiclamp 1 1 1 1
            uialign 0 -1
            uispace 0 $ui_padsmall [uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh "textures/emblem"]
            uivlist $ui_padbutton [
                uialign 0 -1
                uibuttonm "Profile Setup" "textures/player" [uiopen "profile"] 0 main_profile
                uibuttonm "Select Map/Mode" "textures/maps" [uiopen "maps"] 0 main_maps
                uibuttonm "Server Browser" "textures/servers/list" [uiopen "servers"] 0 main_servers
                uibuttonm "Disconnect" "textures/servers/disconnect" [disconnect] (= (isconnected) 0) main_disconnect
                uibuttonm "Help and Support" "textures/question" [uiopen "help"] 0 main_help
                uibuttonm "Quit" "textures/warning" [quit] 0 main_quit
            ]
        ]
    ]
] [] [] [] [] [] [= (isconnected) 0]

uimenu "loading" "Loading..." "<grey>textures/progring" [
    uifill 0.5 0 [
        uivlist 0 [
            uitext (? (&& (isloadingmap) (!=s $mapname "")) (getmaptitle $mapname) "Loading") $ui_textlarge
            uitext (? (&& (isloadingmap) (!=s $mapname "")) (concat "by" (getmapauthor $mapname)) "Please Wait") $ui_textbig
            uispace 0 $ui_padbutton
            if (&& (isloadingmap) (isconnected)) [
                if (= (isloadingmap) 2) [
                    uitext "^fs^fyLightmapping^fS" $ui_text
                ] [
                    gname = (gamename (gamemode) (mutators) 0 32)
                    uitext (format "^fs^fy%1^fS" $gname) $ui_text
                ]
            ]
            case (isloadingmap) 2 [] 1 [
                if (isconnected) [
                    ui_loading_modetexs = (modetexlist (gamemode) (mutators))
                    looplist ui_loading_modetex $ui_loading_modetexs [uiimage $ui_loading_modetex $ui_default 0 $ui_imgbut $ui_imgbut]
                    uitext (modedesc (gamemode) (mutators) 5) $ui_textsmall
                    if (> (mutators) 0) [
                        loop i $mutsidxnum [
                            ui_loading_mut = (<< 1 $i)
                            if (&& [& (mutators) $ui_loading_mut] [! (& (mutsimplied (gamemode)) $ui_loading_mut)]) [
                                ui_loading_muttxt = (mutsdesc (gamemode) $ui_loading_mut 4)
                                if (!=s $ui_loading_muttxt "") [uitext (concatword "^fa" $ui_loading_muttxt) $ui_textsmall]
                            ]
                        ]
                    ]
                    uispace 0 $ui_padbutton
                    if (!=s $connectname "") [
                        uitext (format "^faOn: ^fw%1:[%2]" $connectname $connectport) $ui_textsmall
                        if (&& (isconnected) (!=s $serverdesc "")) [uitext (format "^"^fs%1^fS^"" $serverdesc) $ui_textsmall]
                    ] [uitext (format "^faPlaying Offline" $connectname $connectport) $ui_textsmall]
                ]
            ]
        ]
    ]
] [] [] [
    ui_loading_title = (getprogresstitle)
    if (!=s $ui_loading_title "") [
        uiclamp 1 1 1 1
        uispace $ui_padwin $ui_padwin [
            uialign -1 1
            uihlist 0 [
                uialign -1 1
                uihudskin $ui_buttonzs $ui_buttonzh $ui_menu_a $ui_menu_a [
                    uialign -1 1
                    uifill $ui_buttonzs $ui_buttonzh [
                        uialign 0 0
                        uiclip (*f $ui_buttonzs $loadprogress) 0 0 0 [
                            uialign -1 0
                            uiimagehgradient "<grey><rotate:1>textures/meterskin" $colourpink $colourdarkred 0 $ui_buttonzs $ui_buttonzh [] [
                                uialign -1 0
                                uiaddcolour $colourred
                            ]
                        ]
                    ]
                ]
                uispace $ui_padsmall
                uitext $ui_loading_title $ui_text -1 [uialign -1 0]
            ]
        ]
    ]
]
