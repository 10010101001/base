persistalias showmainplayerprev 1

ui_tip_main_profile = [uitext "Changes your name and other player settings." $ui_texttip 0]
ui_tip_main_maps = [uitext "Select a map and mode to start playing on." $ui_texttip 0]
ui_tip_main_servers = [uitext "Select a server to connect to and play on." $ui_texttip 0]
ui_tip_main_disconnect = [uitext "Disconnect from the current server." $ui_texttip 0]
ui_tip_main_help = [uitext "Get help with playing the game and operating the environment." $ui_texttip 0]
ui_tip_main_credits = [uitext "Show the people who made this game possible." $ui_texttip 0]
ui_tip_main_quit = [uitext "Quits the game." $ui_texttip 0]

uimenu "main" (format "%1 v%2" $versionname $versionstring) "textures/icon" [
    uihlist $ui_padbutton [
        if $showmainplayerprev [
            uivlist $ui_padbutton [uiplayerpreview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) 1 1 0.4 0.5]
        ]
        uivlist $ui_padbutton [
            uialign 0 -1
            uispace 0 0.01
            uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh
            uispace 0 0.01
            uibuttonm "Profile Setup" "textures/player" [uiopen "profile"] 0 main_profile
            uibuttonm "Select Map/Mode" "textures/editing" [uiopen "maps"] 0 main_maps
            uibuttonm "Server Browser" "textures/servers/list" [uiopen "servers"] 0 main_servers
            uibuttonm "Disconnect" "textures/servers/disconnect" [disconnect] (= (isconnected) 0) main_disconnect
            uibuttonm "Help and Support" "textures/question" [uiopen "help"] 0 main_help
            uibuttonm "Credits" "textures/info" [uiopen "credits"] 0 main_credits
            uibuttonm "Quit" "textures/warning" [quit] 0 main_quit
        ]
    ]
] [] [] [] [] [] [= (isconnected) 0]

uimenu "loading" "Loading..." "textures/progring" [
    uifill $ui_default 0.5 0 [
        uivlist 0 [
            uitext (? (&& (isloadingmap) (!=s $mapname "")) (getmaptitle $mapname) "Loading") $ui_textlarge
            uitext (? (&& (isloadingmap) (!=s $mapname "")) (concat "by" (getmapauthor $mapname)) "Please Wait") $ui_textbig
            uispace 0 $ui_padbutton
            if (&& (isloadingmap) (isconnected)) [
                if (= (isloadingmap) 2) [
                    uitext "^fs^fyLightmapping^fS" $ui_text
                ] [
                    gname = (gamename (gamemode) (mutators) 0 32)
                    uitext (format "^fs^fy%1^fS" $gname) $ui_text
                ]
            ]
            case (isloadingmap) 2 [] 1 [
                if (isconnected) [
                    ui_loading_modetexs = (modetexlist (gamemode) (mutators))
                    looplist ui_loading_modetex $ui_loading_modetexs [ uiimage $ui_loading_modetex $ui_default 0 $ui_imgbut $ui_imgbut ]
                    uitext (modedesc (gamemode) (mutators) 5) $ui_textsmall
                    if (> $arg2 0) [
                        loop i $ui_loading_mutsidxnum [
                            ui_loading_mut = (<< 1 $i)
                            if (&& [& $arg2 $ui_loading_mut] [! (& (mutsimplied $arg1) $ui_loading_mut)]) [
                                ui_loading_muttxt = (mutsdesc $arg1 $ui_loading_mut 4)
                                if (!=s $ui_loading_muttxt "") [ uitext (concatword "^fa" $ui_loading_muttxt) ]
                            ]
                        ]
                    ]
                    uispace 0 $ui_padbutton
                    if (!=s $connectname "") [
                        uitext (format "^faOn: ^fw%1:[%2]" $connectname $connectport) $ui_textsmall
                        if (&& (isconnected) (!=s $serverdesc "")) [ uitext (format "^"^fs%1^fS^"" $serverdesc) $ui_textsmall ]
                    ] [uitext (format "^faPlaying Offline" $connectname $connectport) $ui_textsmall]
                ]
            ]
        ]
    ]
]

ui_creditlist = [
    uititle [
        uispace $ui_padsmall $ui_padsmall [
            uialign -1 -1
            uihlist $ui_padbutton [
                uiimage "textures/icon" $ui_default 0 $ui_titlesize $ui_titlesize
                uitext $arg1 $ui_textbig
            ]
        ]
    ] 0 0.032
    uispace $ui_padtiny 0 [
        uifill $ui_default 0 0 [
            uiclamp 1 1
            uiclamp- 1 1 1 1
            uispace $ui_padtiny $ui_padtiny [
                uialign -1 -2
                uivlist 0 [
                    looplist i $arg2 [
                        uiwraptext (strreplace (strreplace $i "^"" "^fs^fr^"" "^"^fS") ": " "^fa: ") 0.69 $ui_texttip
                    ]
                    uialign* -1 -2
                ]
            ]
        ]
    ]
    uifill $ui_default 0 0.02
]

uimenu "credits" "Red Eclipse Contributors" "textures/info" [
    uihlist $ui_padbutton [
        uiscroll 0.5 0.5 [
            uifill $ui_main1 0.57
            uivlist 0.0008 [
                uiclamp 1 1
                uialign -2 -1
                // Alphabetized by alias
                ui_creditlist "Red Eclipse" [
                    [Quinton "quin" Reeves : Lead Developer]
                    [Lee "eihrul" Salzman : Lead Programmer]
                ]
                ui_creditlist "Cube Engine 2" [
                    [Wouter "Aardappel" van Oortmerssen]
                    [Lee "eihrul" Salzman]
                    [Mike "Gilt" Dysart]
                    [Robert "baby-rabbit" Pointon]
                    [Quinton "Quin" Reeves]
                    [John "geartrooper" Siar]
                    [and others...]
                    "http://sauerbraten.org/"
                ]
                uiclamp* 1 1
            ]
        ]
        uivscroll 0.02 0.5
    ]
]
