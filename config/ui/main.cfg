defvarp showmainplayerprev 0 1 1

ui_tip_main_profile = [uitext "Changes your name and other player settings." $ui_texttip]
ui_tip_main_maps = [uitext "Select a map and mode to start playing on." $ui_texttip]
ui_tip_main_servers = [uitext "Select a server to connect to and play on." $ui_texttip]
ui_tip_main_disconnect = [uitext "Disconnect from the current server." $ui_texttip]
ui_tip_main_settings = [uitext "Change settings such as graphics, audio, input, etc." $ui_texttip]
ui_tip_main_help = [uitext "Get help with playing the game and operating the environment." $ui_texttip]
ui_tip_main_credits = [uitext "Show the people who made this game possible." $ui_texttip]
ui_tip_main_quit = [uitext "Quits the game." $ui_texttip]

ui_contents_main = [
    local isediting
    isediting = (&& (= $gamemode $modeidxediting) (isconnected))
    uialign 0 -1
    uibuttonm "Play Online" "textures/servers/list" [uiopen "servers"] 0 main_servers
    uibuttonm (? (isconnected) "Vote Map/Mode" "Offline Practice") "textures/icons/maps" [uiopen "maps"] 0 main_maps
    uibuttonm "Parkour Challenges" "textures/modes/race" [uiopen "challengemenu"] 0 main_sp
    uifill 0 $ui_padwin
    uibuttonm "Profile Setup" "textures/icons/player" [uiopen "profile"] 0 main_profile
    uibuttonm "Settings" "textures/icons/settings" [uiopen "settings"] 0 main_settings
    uibuttonm "Help and Support" $questiontex [uiopen "help"] 0 main_help
    if $isconnected [
        uibuttonm "Disconnect" "textures/servers/disconnect" [
            if $isediting [uiopen "disconnectwarn"] disconnect
        ] 0 main_disconnect
    ]
    uibuttonm "Quit" "textures/icons/warning" [
        if $isediting [uiopen "quitwarn"] quit
    ] 0 main_quit
]

uimenu "main" "Main Menu" "textures/icon" [
    uihlist $ui_padlarge [
        if $showmainplayerprev [
            uiplayerpreview 1 1 $ui_mainw $ui_mainh [
                uiplayerpreviewmodel (getplayermodel)
                uiplayerpreviewpattern (getplayerpattern)
                uiplayerpreviewcolour (getplayercolour -1)
                uiplayerpreviewteam (getplayerteam 1)
                uiplayerpreviewweapselect (weapselect)
                uiplayerpreviewvanity (getplayervanity)
            ] [
                if (! (isconnected)) [
                    uipreviewskycol (modcolour (skewcolour $backgroundcolour $colourwhite 0.2) 0.3)
                    uipreviewsuncol (skewcolour $backgroundcolour $colourwhite 0.4)
                    uipreviewsundir 0 1 2
                    uipreviewexcol (modcolour $backgroundcolour 1.5)
                    uipreviewexdir 0 0 -2
                ]
                ui_playerprev_easteregg
            ]
        ]
        uivlist (*f $ui_padlarge 3) [
            uialign 0 0
            uivlist 0 [
                uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh [
                    uiimageshadow $ui_menu_s
                    ui_main_version = (concatword "v" $versionstring " (" $versionrelease ")")
                    if $versionbuild [ui_main_version = (concatword $ui_main_version " [" $versionbranch ":" $versionbuild "]")]
                    uitext $ui_main_version $ui_textsmall [uialign 0 1]
                ]
            ]
            uivlist $ui_padbutton [
                ui_contents_main
            ]
        ]
    ]
]

uimenu "file" "Main Menu" "textures/icon" [
    uivlist $ui_padbutton [
        ui_contents_main
    ]
] [] [] [] [] []