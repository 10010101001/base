// uimenu 1:"NAME" 2:"TITLE" 3:[children] 4:[on-show] 5:[on-hide] 6:[ui-children]
uimenu = [
    newui $arg1 [
        @arg6
        uivlist 0 [
            uiclamp 1 1 1 1
            uiskin "textures/guiskin" $c_main1 1 0 0.04 [
                uiclamp 1 1
                uispace 0.012 0.002 [
                    uiclamp 1 1
                    uitext [@@@@@arg2] 1.2
                    uiskin "textures/guiskinborder" $c_main1 1 0.03 0.03 [
                        c_temp = 0xFFFFFF
                        uihover [uisetcolor $c_focus; c_temp = $c_focus] [uisetcolor $c_line]
                        uihold [uisetcolor $c_hold; c_temp = $c_hold]
                        uirelease [hideui [@@@@@@@arg1]]
                        uiclamp* 1 1 1 1
                        uiimage "textures/guiexit" $c_temp 0 0.03 0.03
                    ]
                    uialign- 1
                ]
            ]
            uiskin "textures/guiskin" $c_menu 1 0.01 0.01 [
                uiclamp 1 1 1 1
                uispace $uiwindow_padding $uiwindow_padding [
                    uiclamp 1 1 1 1
                    @@@@arg3
                ]
            ]
        ]
        uiclamp- 1 1 1 1
    ] $arg4 $arg5
]

uiskinborder = [
    uiskin "textures/guiskin" $arg3 1 $arg1 $arg2 [
        uihover [uisetcolor @arg4] [uisetcolor @arg3]
        uihold [uisetcolor @arg5]
        uiclamp* 1 1 1 1
        uiskin "textures/guiskinborder" $arg6 1 $arg1 $arg2 [
            uihover [uisetcolor @arg7] [uisetcolor @arg6]
            uihold [uisetcolor @arg8]
            uiclamp* 1 1 1 1
            doargs $arg9
        ]
    ]
]

// uibar 1:CLAMP-X 2:CLAMP-Y
uibar = [
    uiline $c_line
    uiclamp- $arg1 $arg1 $arg2 $arg2
]

// uidrawtriangle 1:0xCOLOR 2:(SIZE-W) 3:(SIZE-H) 4:(ORIENTATION)
uidrawtriangle = [
    if $arg1 [] [arg1 = 0xFFFFFF]
    uigroup [
        uitriangle $arg1 $arg2 $arg3 $arg4
        uitriangleoutline $arg1 $arg2 $arg3 $arg4
    ]
]

// uidrawcircle 1:0xCOLOR 2:(SIZE)
uidrawcircle = [
    if $arg1 [] [arg1 = 0xFFFFFF]
    uigroup [
        uicircle $arg1 $arg2
        uicircleoutline $arg1 $arg2
    ]
]

// uititle 1:[title children] 2:(SIZE-W) 3:(SIZE-H)
uititle = [
    uiskinborder $arg1 $arg2 $c_main1 $c_main2 $c_main3 $c_line $c_focus $c_hold [
        arg1
        uiclamp 1 1 1 1
    ]
]

// uibuttonarea 1:(STYLE) 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[children]
uibuttonarea = [
    uiskinborder 0 0 $c_main1 $c_main2 $c_main3 $c_line $c_focus $c_hold [
        doargs $arg4
    ]
]

// uibutton 1:(STYLE) 2:[button-children] 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:(USE-PADDING)
uibutton = [
    uiskinborder $arg3 $arg4 $c_main1 $c_main2 $c_main3 $c_line $c_focus $c_hold [
        doargs $arg7
        uirelease [doargs $arg5]
        uiclamp* 1 1 1 1
        uispace (? (>= $numargs 8) $arg8 $uibutton_padding) 0 [
            uiclamp 1 1
            uihlist 0.01 [
                uialign $arg6 0
                doargs $arg2
            ]
        ]
    ]
]

// uicheckbox 1:[condition] 2:(SIZE) 3:0xCOLOR-ON 4:0xCOLOR-OFF 5:[children]
uicheckbox = [
    if $arg4 [] [arg4 = $c_off]
    if $arg3 [] [arg3 = $c_on1]
    uicolor (? (arg1) $arg3 $arg4) $arg2 $arg2 [
        uimodvgradient 0xFFFFFF 0xB0B0B0
        doargs $arg5
        uioutline $c_line
        uiclamp* 1 1 1 1
    ]
]

// uiradio 1:[condition] 2:(SIZE) 3:0xCOLOR-ON 4:0xCOLOR-OFF 5:[children]
uiradio = [
    if $arg4 [] [arg4 = $c_off]
    if $arg3 [] [arg3 = $c_on2]
    uicircleoutline (? (arg1) $arg3 $arg4) $arg2 [
        uicircle (? (arg1) $arg3 $arg4) $arg2 [
            doargs $arg5
            uiclamp* 1 1 1 1
        ]
    ]
]

// uiarrowbutton 1:(SIZE-W) 2:(SIZE-H) 3:(ORIENTATION) 4:[on-release] 5:(ARROW-%-SIZE) 6:[children]
uiarrowbutton = [
    local arrowsize
    arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
    uitarget 0xFFFFFF $arg1 $arg2 [
        uicolor $c_menu
        doargs $arg6
        style_button_default
        uirelease [doargs $arg4]
        uiclamp* 1 1 1 1
        uidrawtriangle 0 $arrowsize $arrowsize $arg3
    ]
]

// uihtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..] 5:<BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
uihtab = [
    uifill 0xFFFFFF $arg1 $arg2 [
        uihlist 0 [
            uiclamp 1 1
            looplist3 v n c $arg4 [
                if (=s $$arg3 $v) [
                    uimodvgradient 0xFFFFFF $c_grdmod 0 $arg2 [
                        uibar 0 1 ; uialign- -1 0
                        uibar 0 1 ; uialign-  1 0
                        uispace 0.01 0 [uitext (concatword "^f8" $n) 0.8]
                    ]
                ] [
                    uitarget 0xFFFFFF 0 $arg2 [
                        uispace 0.01 0 [uitext $n 0.8]
                        case $arg5 0 [
                            uihover [$arg3 = $v]
                        ] 1 [
                            uipress [$arg3 = $v]
                        ] 2 [uirelease [$arg3 = $v]]
                        doargs $c
                    ]
                ]
            ]
            uiclamp* 1 1
        ]
        uibar 1 0 ; uialign- 0 1
    ]
]

// uivtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..] 5:<BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
uivtab = [
    uifill 0xFFFFFF $arg1 $arg2 [
        uivlist 0 [
            uiclamp 0 0 1 1
            looplist3 v n c $arg4 [
                if (=s $$arg3 $v) [
                    uimodhgradient 0xFFFFFF $c_grdmod $arg1 0 [
                        uibar 1 ; uialign- 0 -1
                        uibar 1 ; uialign- 0  1
                        uispace 0.01 0 [uitext (concatword "^f8" $n) 0.8]
                    ]
                ] [
                    uitarget 0xFFFFFF $arg1 0 [
                        uispace 0.01 0 [uitext $n 0.8]
                        case $arg5 0 [
                            uihover [$arg3 = $v]
                        ] 1 [
                            uipress [$arg3 = $v]
                        ] 2 [uirelease [$arg3 = $v]]
                        doargs $c
                    ]
                ]
            ]
            uiclamp* 1 1 1 1
        ]
        uibar 0 1 ; uialign- 1
    ]
]

// uivscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uivscroll = [
    if $arg4 [] [arg4 = 0.5]
    uivlist 0 [
        uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg1 $arg1 0 [] $arg4]
        uispace 0 0.001
        uivscrollbar [
            uifill 0xFFFFFF $arg1 (-f $arg2 (*f $arg1 2)) [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolor $c_menu $arg1
                style_button_hold2
                uiclamp* 1 1 1 1
                style_generic_linesh 3 0.004 (*f $arg1 0.5)
            ]
        ]
        uispace 0 0.001
        uiscrollarrow $arg3 [uiarrowbutton $arg1 $arg1 180 [] $arg4]
    ]
]

// uihscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uihscroll = [
    if $arg4 [] [arg4 = 0.5]
    uihlist 0 [
        uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg2 $arg2 90 [] $arg4]
        uispace 0.001
        uihscrollbar [
            uifill 0xFFFFFF (-f $arg1 (*f $arg2 2)) $arg2 [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolor $c_menu 0 $arg2
                style_button_hold1
                uiclamp* 1 1 1 1
                style_generic_linesv 3 0.004 (*f $arg2 0.5)
            ]
        ]
        uispace 0.001
        uiscrollarrow $arg3 [uiarrowbutton $arg2 $arg2 270 [] $arg4]
    ]
]

// uivertslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uivertslider = [
    if $arg9 [] [arg9 = 0.5]
    uivlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg5 $arg5 0 [] $arg9]
        uispace 0.1
        uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
            uifill 0xFFFFFF (-f $arg6 (*f $arg5 2)) $arg5 [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolor $c_menu $arg5
                style_button_hold2
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $uibutton_padding) 0 [arg7]
            ]
        ]
        uispace 0.1
        uisliderarrow 1 [uiarrowbutton $arg5 $arg5 180 [] $arg9]
    ]
]

// uihorzslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uihorzslider = [
    if $arg9 [] [arg9 = 0.5]
    uihlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg6 $arg6 90 [] $arg9]
        uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
            uifill 0xFFFFFF (-f $arg5 (*f $arg6 2)) $arg6 [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolor $c_menu 0 $arg6
                style_button_hold1
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $uibutton_padding) 0 [arg7]
            ]
        ]
        uisliderarrow 1 [uiarrowbutton $arg6 $arg6 270 [] $arg9]
    ]
]

// uivcolorslider 1:VAR 2:MIN 3:MAX 4:0xCOLOR1 5:0xCOLOR2 6:X 7:Y 8:[on-change]
uivcolorslider = [
    uigroup [
        uivgradient $arg5 $arg4 (*f 0.85 $arg6) $arg7 [uioutline $c_line ; uiclamp- 1 1 1 1]
        uifont "default" [uivlist -0.01 [
            loop i (strlen (abs $$arg1)) [
                uicolortext (substr $$arg1 $i 1) (? (|| uihover? uihold?) 0x80FFFFFF 0x40FFFFFF) 0.8
            ]
        ]]
        uivslider $arg1 $arg2 $arg3 1 $arg8 [
            uifill 0xFFFFFF $arg6 (*f 0.98 $arg7)
            uisliderbutton [uiline 0xFFFFFF $arg6 0]
        ]
    ]
]

// uihcolorslider 1:VAR 2:MIN 3:MAX 4:0xCOLOR1 5:0xCOLOR2 6:X 7:Y 8:[on-change]
uihcolorslider = [
    uigroup [
        uihgradient $arg4 $arg5 $arg6 (*f 0.85 $arg7) [uioutline $c_line ; uiclamp- 1 1 1 1]
        uihslider $arg1 $arg2 $arg3 1 $arg8 [
            uifill 0xFFFFFF (*f 0.98 $arg6) $arg7
            uisliderbutton [uiline 0xFFFFFF 0 $arg7]
        ]
    ]
]

//uilistslider 1:VAR 2:COMPARISON 3:[values] 4:X 5:Y 6:[on-change] 7:(ARROW-%-SIZE) 8:[children]
uilistslider = [
    if $arg7 [] [arg7 = 0.5]
    uihlist 0 [
        uiarrowbutton $arg5 $arg5 90 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (< $n 0) [n = 0] [
                n = (- $n 2)
                if (< $n 0) [n = (+ $n (listlen $arg3))]
            ]
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
        uifill 0xFFFFFF (-f $arg4 (*f $arg5 2)) $arg5 [
            uicolor $c_slider
            uiclamp* 1 1 1 1
            if (=s $arg8 "") [
                uitext (|| [[listassoc@arg2] $arg3 $$arg1] [result "custom"]) 0.7
            ] arg8
        ]
        uiarrowbutton $arg5 $arg5 270 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
    ]
]

//uiinput 1:VAR 2:LENGTH 3:[on-change] 4:SCALE 5:PARENT 6:PROMPT 7:[children] 8:ARGS
uiinput = [
    uiskinborder 0 0 $c_main1 $c_main2 $c_main3 $c_line $c_focus $c_hold [
        doargs $arg8
        uispace 0.002 0.002 [
            uifield $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 $arg7
        ]
    ]
]
