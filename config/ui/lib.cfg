uitoggle = [
    ui_topui = (topui)
    if (!=s $ui_topui "") [
        if (!= (ui_menu_prevent_@ui_topui) 1) [ui_menu_action_@ui_topui] 
    ] [uiopen "main"]
]
bind ESCAPE [uitoggle]

uiclose = [
    if $arg1 [] [arg1 = (topui)]
    if (< $numargs 2) [arg2 = 0]
    if $arg1 [
        uihidetip
        if (= $arg2 1) [
            [ui_menu_close_@@@arg1] = 0
            [ui_menu_sched_@@@arg1] = ""
            hideui $arg1
        ] [
            if (= $[ui_menu_close_@@@arg1] 0) [
                [ui_menu_close_@@@@arg1] = (getmillis 1)
                [ui_menu_sched_@@@@arg1] = ""
            ]
        ]
    ]
]

uiopen = [
    if (< $numargs 2) [arg2 = 1]
    ui_topui = (topui)
    if (!=s $ui_topui $arg1) [
        uihidetip
        if (&& (!= $arg2 0) (!=s $ui_topui "")) [
            if (= $[ui_menu_close_@ui_topui] 0) [[ui_menu_close_@ui_topui] = (getmillis 1)]
            [ui_menu_sched_@ui_topui] = $arg1
        ] [showui $arg1]
    ]
]

uireset = [
    if $arg2 [] [arg2 = 0]
    if $arg1 [
        [ui_menu_last_@@arg1] = [@@arg2]
        [ui_menu_time_@@arg1] = [@@arg2]
        [ui_menu_name_@@arg1] = [@@arg1]
        [ui_menu_close_@@arg1] = 0
        [ui_menu_alpha_@@arg1] = 0
        [ui_menu_sched_@@arg1] = ""
        [ui_menu_obscure_@@arg1] = 0
        [ui_menu_olast_@@arg1] = 0
    ]
]

uiobscure = [
    if $[ui_menu_obscure_@arg1] [
        [ui_menu_olast_@@arg1] = (getmillis 1)
        [ui_menu_oframe_@@arg1] = (- $[ui_menu_last_@@arg1] $[ui_menu_obscure_@@arg1])
        [ui_menu_oalpha_@@arg1] = (divf $[ui_menu_oframe_@@arg1] $ui_transition)
    ] [
        if $[ui_menu_olast_@@arg1] [
            [ui_menu_oframe_@@@arg1] = (- $[ui_menu_last_@@@arg1] $[ui_menu_olast_@@@arg1])
            [ui_menu_oalpha_@@@arg1] = (-f 1 (divf $[ui_menu_oframe_@@@arg1] $ui_transition))
        ] [[ui_menu_oalpha_@@@arg1] = 0]
    ]
    if (>f $[ui_menu_oalpha_@arg1] 0) [
        if (<=f $[ui_menu_oalpha_@@arg1] 1) [uichangecolours (-f 1 (*f $[ui_menu_oalpha_@@@arg1] (-f 1 $ui_obscure)))] [uichangecolours $ui_obscure]
    ] [
        if (! $[ui_menu_obscure_@@arg1]) [[ui_menu_olast_@@@arg1] = 0]
    ]
]

ui_menu_current = ""
// uimenu 1:<name> 2:<title> 3:[icon] 4:[children] 5:[on-show] 6:[on-hide] 7:[ui-children] 8:[tool] 9:[close-action] 10:[close-prevent] 11:[window-flags]
uimenu = [
    if $arg8 [] [arg8 = 0]
    if $arg9 [] [arg9 = [uiclose $arg1]]
    if $arg10 [] [arg10 = 0]
    if $arg11 [] [arg11 = 0]
    [ui_menu_title_@arg1] = $arg2
    [ui_menu_icon_@arg1] = $arg3
    [ui_menu_action_@arg1] = [doargs [@@arg9]]
    [ui_menu_prevent_@arg1] = $arg10
    uireset [@arg1]
    newui $arg1 [
        ui_menu_current = [@@arg1]
        [ui_menu_last_@@arg1] = (getmillis 1)
        ui_menu_topui = (topui)
        @arg7
        if (&& (!=s $ui_menu_topui "") (!=s $ui_menu_topui [@@arg1])) [if $[ui_menu_obscure_@@@arg1] [] [[ui_menu_obscure_@@@@arg1] = (getmillis 1)]] [[ui_menu_obscure_@@@arg1] = 0]
        uiallowinput (? (|| (& (uiwindowflags) 2) $[ui_menu_close_@@arg1] $[ui_menu_obscure_@@arg1] (=s [@@arg1] "loading")) 0 1)
        if (!= (@[ui_menu_prevent_@arg1]) 1) [uiescrelease [ui_menu_action_@@@arg1]]
        uivlist 0 [
            uivlist 0 [
                @@@(? (&& (!= $arg8 2) (!=s $arg1 "loading")) [
                    uiskinborder 0 $ui_buttonh @(? $arg8 $ui_menu $ui_menu_a) @(? $arg8 $ui_menu $ui_menu_a) @(? $arg8 $ui_menu $ui_menu_a) @(? $arg8 $ui_bline $ui_menu_a) @(? $arg8 $ui_bline $ui_menu_a) @(? $arg8 $ui_bline $ui_menu_a) [
                        uiclamp 1 1
                        uispace $ui_padsmall @@(? $arg8 $ui_padmini $ui_padsmall) [
                            uiclamp 1 1
                            uihlist 0 [
                                uialign -1
                                uispace $ui_padsmall 0 [uiimage $[ui_menu_icon_@@@@@@arg1] $ui_default 0 @@@@@(*f (? $arg8 $ui_toolsize $ui_titlesize) 0.8) @@@@@(*f (? $arg8 $ui_toolsize $ui_titlesize) 0.8)]
                            ]
                            uispace @@@(*f (? $arg8 $ui_toolsize $ui_titlesize) 1.4) 0 [uitext $[ui_menu_title_@@@@@arg1] @@@@(? $arg8 $ui_text $ui_textlarge)]
                            uihlist 0 [
                                uialign 1
                                uibuttonimg "" "textures/guiexit" @@@@(? $arg8 $ui_toolsize $ui_titlesize) @@@@(? $arg8 $ui_toolsize $ui_titlesize) [ui_menu_action_@@@@@arg1] 0 [] (@@@@[ui_menu_prevent_@arg1]) 0 [] $ui_default $colourred
                            ]
                        ]
                    ] [uiclamp 1 1 1 1]
                ])
                uiskinborder 0 0 @@@(? $arg8 $ui_menu $ui_menu_a) @@@(? $arg8 $ui_menu $ui_menu_a) @@@(? $arg8 $ui_menu $ui_menu_a) @@@(? $arg8 $ui_bline $ui_menu_a) @@@(? $arg8 $ui_bline $ui_menu_a) @@@(? $arg8 $ui_bline $ui_menu_a) [
                    uiclamp 1 1 1 1
                    uispace $ui_padwin $ui_padwin [
                        uiclamp 1 1 1 1
                        @@@@@arg4
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            @@(? (= $arg8 0) [
                uivlist 0 [
                    uiclamp 1 1
                    uiskinborder 0 0 $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a [
                        uiclamp 1 1
                        uispace $ui_padwin $ui_padsmall [
                            uiclamp 1 1
                            uiwraptext (tipshow) 0.75 $ui_textstatus 0
                        ]
                    ] [uiclamp 1 1]
                ]
            ])
        ]
        @(? (!=s $arg1 "loading") [
            if $[ui_menu_close_@@arg1] [
                [ui_menu_frame_@@@arg1] = (- $[ui_menu_last_@@@arg1] $[ui_menu_close_@@@arg1])
                [ui_menu_alpha_@@@arg1] = (-f 1 (divf $[ui_menu_frame_@@@arg1] $ui_transition))
                if (>f $[ui_menu_alpha_@@@arg1] 0) [
                    uichangeblends $[ui_menu_alpha_@@@@arg1]
                    uiobscure [@@@@arg1]
                ] [
                    if (!=s $[ui_menu_sched_@@@@arg1] "") [showui $[ui_menu_sched_@@@@@arg1]]
                    uihidetip
                    hideui [@@@@arg1]
                    [ui_menu_close_@@@@arg1] = 0
                    [ui_menu_sched_@@@@arg1] = ""
                ]
            ] [
                [ui_menu_alpha_@@@arg1] = (divf (- $[ui_menu_last_@@@arg1] $[ui_menu_time_@@@arg1]) $ui_transition)
                if (<=f $[ui_menu_alpha_@@@arg1] 1) [uichangeblends $[ui_menu_alpha_@@@@arg1]] [uiobscure [@@@@arg1]]
            ]
        ])
        ui_menu_current = ""
    ] [
        uireset [@@arg1] (getmillis 1)
        @arg5
    ] [
        uireset [@@arg1]
        @arg6
    ] (| 1 $arg11)
]

// uitipmenu 1:<name> 2:[children] 3:[on-show] 4:[on-hide] 5:[ui-children]
uitipmenu = [
    newui $arg1 [
        uiallowinput 0
        @arg5
        uivlist 0 [
            uiclamp 1 1 1 1
            uiskinborder 0 0 $ui_tool_a $ui_tool_a $ui_tool_a $ui_tline_a $ui_tline_a $ui_tline_a [
                uiclamp 1 1 1 1
                uispace $ui_padsmall $ui_padsmall [
                    uiclamp 1 1 1 1
                    @@@@arg2
                ]
            ] [uiclamp 1 1 1 1]
        ] [uiclamp 1 1 1 1]
    ] $arg4 $arg5 4
]

// uiskinborder 1:<w> 2:<h> 3:<bgcolour> 4:<bghover> 5:<bghold> 6:<bordercolour> 7:<borderhover> 8:<borderhold> 9:<children> 10:[children2]
uiskinborder = [
    uiborderedimage "textures/guiskin" $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        uihover [uisetcolour $arg4] [uisetcolour $arg3]
        uihold [uisetcolour $arg5]
        doargs $arg10
        uiborderedimage "textures/guiskinborder" $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
            uihover [uisetcolour $arg7] [uisetcolour $arg6]
            uihold [uisetcolour $arg8]
            doargs $arg9
        ]
    ]
]

uitabborder = [
    uiborderedimage "textures/tabskin" $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        uihover [uisetcolour $arg4] [uisetcolour $arg3]
        uihold [uisetcolour $arg5]
        doargs $arg10
        uiborderedimage "textures/tabskinborder" $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
            uihover [uisetcolour $arg7] [uisetcolour $arg6]
            uihold [uisetcolour $arg8]
            doargs $arg9
        ]
    ]
]

// uitableskin 1:<bgcolour> 2:<bghover> 3:<bghold> 4:<bordercolour> 5:<borderhover> 6:<borderhold> 7:<children> 8:<row-children>
uitableskin = [
    uitablerow [
        doargs $arg7
        uiskinborder 0.01 0.01 $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [doargs $arg8]
    ]
]

uielemskin = [
    if $arg5 [] [arg5 = 0]
    uiskinborder 0 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uiclamp 1 1 1 1
        doargs $arg4
        uispace (+f $arg5 $ui_padbutton) $ui_padsmaller [
            uiclamp 1 1 1 1
            doargs $arg3
        ]
    ] [uiclamp 1 1 1 1]
]

uibackskin = [
    if $arg5 [] [arg5 = 0]
    uiskinborder 0 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        doargs $arg4
        uispace (+f $arg5 $ui_padbutton) $ui_padsmaller [
            uihlist 0 [doargs $arg3]
        ]
    ]
]

uielemspace = [
    if $arg3 [] [arg3 = 0]
    uispace 0 0 [
        uiclamp 1 1 1 1
        doargs $arg2
        uispace (+f $arg3 $ui_padbutton) $ui_padsmaller [
            uiclamp 1 1 1 1
            doargs $arg1
        ]
    ] [uiclamp 1 1 1 1]
]

// uibar 1:<clampx> 2:<clampy>
uibar = [
    uiline $ui_line 0 0 [uiclamp $arg1 $arg1 $arg2 $arg2]
]

// uidrawtriangle 1:<colour> 2:<w> 3:<h> 4:<orient>
uidrawtriangle = [
    if $arg1 [] [arg1 = $ui_bline]
    uigroup [
        uitriangle $arg1 $arg2 $arg3 $arg4
        uitriangleoutline $arg1 $arg2 $arg3 $arg4
    ]
]

// uidrawcircle 1:<colour> 2:<size>
uidrawcircle = [
    if $arg1 [] [arg1 = $ui_bline]
    uigroup [
        uicircle $arg1 $arg2
        uicircleoutline $arg1 $arg2
    ]
]

// uititle 1:[title children] 2:(SIZE-W) 3:(SIZE-H)
uititle = [
    uiskinborder $arg1 $arg2 $ui_menu $ui_menu $ui_menu $ui_line $ui_line $ui_line [
        uiclamp 1 1 1 1
        doargs $arg1
    ]
]

// uitextalign 1:<text> 2:[scale] 3:[align]  4:[children]
uitextalign = [
    if $arg2 [] [arg2 = $ui_text]
    uitext $arg1 $arg2 $arg3 [
        uialign $arg3 0
        doargs $arg4
    ]
]

uitextleft = [
    if $arg2 [] [arg2 = $ui_text]
    uitextalign $arg1 $arg2 -1 $arg3
]

uitextright = [
    if $arg2 [] [arg2 = $ui_text]
    uitextalign $arg1 $arg2 1 $arg3
]

// uiwraptextalign 1:<text> 2:[scale] 3:[align] 4:[wrap] 5:[children]
uiwraptextalign = [
    if $arg2 [] [arg2 = $ui_text]
    uiwraptext $arg1 $arg4 $arg2 $arg3 [
        uialign $arg3 0
        doargs $arg5
    ]
]

uiwraptextleft = [
    if $arg2 [] [arg2 = $ui_text]
    uiwraptextalign $arg1 $arg2 -1 $arg3 $arg4
]

uiwraptextright = [
    if $arg2 [] [arg2 = $ui_text]
    uiwraptextalign $arg1 $arg2 1 $arg3 $arg4
]

// uibuttonarea 1:(BUTTON-WIDTH) 2:(BUTTON-HEIGHT) 3:[on-release] 4:(ALIGNMENT) 5:[children] 6:DISABLED 7:[alt-release] 8:[tooltip]
uibuttonarea = [
    uiskinborder $arg1 $arg2 (? $arg6 $ui_maind $ui_main1) (? $arg6 $ui_maind $ui_main2) (? $arg6 $ui_maind $ui_main3) (? $arg6 $ui_disa $ui_line) (? $arg6 $ui_disa $colourred) (? $arg6 $ui_disa $colourpink) [
        uiclamp 1 1
        ui_buttonarea_temp = $ui_default
        ui_buttonarea_line = $ui_line
        if $arg6 [ui_buttonarea_temp = $ui_disable; ui_buttonarea_line = $ui_disable] [
            uihover [ui_buttonarea_temp = $colourred; ui_buttonarea_line = $colourred]
            uihold [ui_buttonarea_temp = $colourpink; ui_buttonarea_line = $colourpink]
            uirelease [doargs $arg3]
            uialtrelease [doargs $arg7]
            uitooltip $arg8
        ]
        uiclamp* 1 1 1 1
        doargs $arg5
    ] []
]

// uibuttonf 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour] 13:[tooltip] 14:[text-size]
uibuttonf = [
    if $arg9 [] [arg9 = $ui_padbutton]
    if $arg11 [] [arg11 = $ui_default]
    if $arg12 [] [arg12 = $ui_default]
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        ui_buttonf_size = 0
        if $arg2 [
            ui_buttonf_size = (*f $arg4 0.8)
            uispace $arg9 0 [
                uiclamp 1 1
                uihlist 0 [
                    uialign (? $arg1 -1 0) 0
                    uiimage $arg2 (? $arg8 $ui_buttonarea_temp $arg12) 0 $ui_buttonf_size $ui_buttonf_size "textures/emblem"
                ]
            ]
        ]
        if $arg1 [
            uispace (+f $arg9 $ui_buttonf_size) 0 [
                uiclamp 1 1
                uihlist 0 [
                    uialign $arg6 0
                    uitext (format "^f[%1]%2" (? $arg8 $ui_buttonarea_temp $arg11) $arg1) $arg14
                ]
            ]
        ]
        doargs $arg7
    ] $arg8 $arg10 $arg13
]

// uibuttonm 1:<text> 2:<image> 3:[on-release] 4:DISABLED 5:[tooltip]
uibuttonm = [
    uibuttonf $arg1 $arg2 $ui_buttonw $ui_buttonh $arg3 0 [] $arg4 $ui_padbutton [] $ui_default $ui_default $arg5 $ui_textbig
]

// uibuttonw 1:<text> 2:[on-release] 3:DISABLED 4:[colour] 5:[tooltip]
uibuttonw = [
    uibuttonf $arg1 "" 0 $ui_buttonh $arg2 0 [] $arg3 $ui_padbutton [] $arg4 $arg4 $arg5 $ui_text
]

// uibuttonz 1:<text> 2:[on-release] 3:DISABLED 4:[colour] 5:[tooltip] 6:[size] 7:[text-size] 8:[padding-size]
uibuttonz = [
    uibuttonf $arg1 "" 0 $arg6 $arg2 0 [] $arg3 $arg8 [] $arg4 $arg4 $arg5 $arg7
]

// uibutton 1:<text> 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[on-release] 5:(ALIGNMENT) 6:[children] 7:DISABLED 8:[padding] 9:[alt-release] 10:[text-colour] 11:[image-colour] 12:[tooltip] 13:[text-size]
uibutton = [
    uibuttonf $arg1 "" $arg2 $arg3 $arg4 $arg5 $arg6 $arg7 $arg8 $arg9 $arg10 $arg11 $arg12 $arg13
]

// uibuttonimg 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour] 13:[tooltip] 14:[text-size]
uibuttonimg = [
    if $arg14 [] [arg14 = $ui_text]
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        uispace (? (>= $numargs 9) $arg9 $ui_padbutton) 0 [
            uiclamp 1 1
            uihlist 0 [
                uialign $arg6 0
                if $arg2 [
                    ui_size = (*f $arg4 0.85)
                    uiimage $arg2 (? (|| $arg8 (< $numargs 12)) $ui_buttonarea_temp $arg12) 0 $ui_size $ui_size
                ]
                if $arg1 [
                    if $arg2 [uispace (*f (? (>= $numargs 9) $arg9 $ui_padbutton) 0.35)]
                    uitext (format "^f[%1]%2" (? (|| $arg8 (< $numargs 11)) $ui_buttonarea_temp $arg11) $arg1) $arg14
                ]
            ]
        ]
        doargs $arg7
    ]  $arg8 $arg10 $arg13
]

// uiimgbutton 1:<image> 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[on-release] 5:(ALIGNMENT) 6:[children] 7:DISABLED 8:[padding] 9:[alt-release] 10:[image-colour] 11:[tooltip]
uiimgbutton = [
    uibuttonarea $arg2 $arg3 $arg4 $arg5 [
        uispace (? (>= $numargs 8) $arg8 $ui_padbutton) (? (>= $numargs 8) $arg8 $ui_padbutton) [
            uiclamp 1 1 1 1
            uihlist 0 [
                uiclamp 1 1 1 1
                uialign $arg5 0
                uiimage $arg1 (? (|| $arg7 (< $numargs 10)) $ui_buttonarea_temp $arg10) 0 $arg2 $arg3 "" [uioutline $ui_buttonarea_line 0 0; uiclamp- 1 1 1 1; @@@@arg6]
            ]
        ]
    ]  $arg7 $arg9 $arg11
]

// uicheckbox 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-ON2 7:0xcolour-BG 8:[children] 9:DISABLED 10:[text-size]
uicheckbox = [
    if $arg2 [] [arg2 = 0]
    if $arg7 [] [arg7 = $ui_bline]
    if $arg6 [] [arg6 = $colourmagenta]
    if $arg5 [] [arg5 = $colourred]
    uitarget 0 0 [
        ui_checkbox_temp = $arg7
        if $arg9 [ui_checkbox_temp = $ui_disable] [
            uihover [ui_checkbox_temp = $colourred]
            uihold [ui_checkbox_temp = $colourpink]
            uirelease [doargs $arg4]
        ]
        doargs $arg8
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiimage "textures/checkbox" $ui_checkbox_temp 0 $arg3 $arg3 "" [
                uiclamp* 1 1 1 1
                if $arg2 [
                    uiimage "textures/checkboxon" (? $arg9 $colouryellow (? $arg2 $arg5 $arg6)) 0 $arg3 $arg3 "" [uialign 0 0]
                ]
            ]
            if $arg1 [
                uispace $ui_padsmall 0 [
                    uitext (format "^f[%1]%2" $ui_checkbox_temp $arg1) $arg10
                ]
            ]
        ]
    ]
]

// uicheckbutton 1:<image> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children] 8:DISABLED
uicheckbutton = [
    if $arg2 [] [arg2 = 0]
    if $arg6 [] [arg6 = $ui_line]
    if $arg5 [] [arg5 = $colourred]
    uiskinborder $arg3 $arg3 (? $arg8 $ui_maind $ui_main1) (? $arg8 $ui_maind $ui_main2) (? $arg8 $ui_maind $ui_main3) (? $arg8 $ui_disa (? $arg2 $arg5 $ui_line)) (? $arg8 $ui_disa $colourred) (? $arg8 $ui_disa $colourpink) [
        ui_checkbutton_temp = (? $arg2 $arg5 $arg6)
        if $arg8 [ui_checkbutton_temp = (? $arg8 $colouryellow $ui_disable)] [
            uihover [ui_checkbutton_temp = $colourred]
            uihold [ui_checkbutton_temp = $colourpink]
            uirelease [doargs $arg4]
        ]
        doargs $arg7
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiclamp 1 1
            uialign 0 0
            ui_size = (*f $arg3 0.9)
            uiimage $arg1 $ui_checkbutton_temp 0 $ui_size $ui_size "" [uialign 0 0]
        ]
    ]
]

// uichecktext 1:<text> 2:[condition] 3:(W) 4:(H) 5:[on-release] 6:0xcolour-ON 7:0xcolour-BG 8:[children] 9:DISABLED 10:[text-size]
uichecktext = [
    if $arg2 [] [arg2 = 0]
    if $arg7 [] [arg7 = $ui_line]
    if $arg6 [] [arg6 = $colourred]
    if $arg10 [] [arg10 = $ui_text]
    uiskinborder $arg3 $arg4 $ui_main1 $ui_main2 $ui_main3 (? $arg2 $arg6 $ui_line) $colourred $colourpink [
        ui_checktext_temp = (? $arg2 $arg6 $arg7)
        if $arg9 [ui_checktext_temp = $ui_disable] [
            uihover [ui_checktext_temp = $colourred]
            uihold [ui_checktext_temp = $colourpink]
            uirelease [doargs $arg5]
        ]
        doargs $arg8
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiclamp 1 1
            uialign 0 0
            uispace $ui_padsmall 0 [
                uiclamp 1 1
                uialign 0 0
                uitext (format "^f[%1]%2" $ui_checktext_temp $arg1) $arg10
            ]
        ]
    ]
]

// uiradio 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children] 8:DISABLED 9:[text-size]
uiradio = [
    if $arg2 [] [arg2 = 0]
    if $arg6 [] [arg6 = $ui_bline]
    if $arg5 [] [arg5 = $colourred]
    uitarget 0 0 [
        ui_radio_temp = $arg6
        if $arg8 [ui_radio_temp = $ui_disable] [
            uihover [ui_radio_temp = $colourred]
            uihold [ui_radio_temp = $colourpink]
            uirelease [doargs $arg4]
        ]
        doargs $arg7
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiimage "textures/radiobox" $ui_radio_temp 0 $arg3 $arg3 "" [
                uiclamp* 1 1 1 1
                if $arg2 [uiimage "textures/radioboxon" (? $arg8 $colouryellow $arg5) 0 $arg3 $arg3 "" [uialign 0 0]]
            ]
            if $arg1 [
                uispace $ui_padsmall 0 [
                    uitext (format "^f[%1]%2" $ui_radio_temp $arg1) $arg9
                ]
            ]
        ]
    ]
]

// uiarrowbutton 1:(SIZE-W) 2:(SIZE-H) 3:(ORIENTATION) 4:[on-release] 5:(ARROW-%-SIZE) 6:[children]
uiarrowbutton = [
    local arrowsize
    arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
    uiskinborder $arg1 $arg2 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
        doargs $arg6
        uirelease [doargs $arg4]
        uiclamp* 1 1 1 1
        uidrawtriangle 0 $arrowsize $arrowsize $arg3
    ]
]

// uitabs 1:[FILL-W] 2:[FILL-H] 3:(TAB-W) 4:(TAB-H) 5:VAR 6:[id "name" [action] ..] 7:[contents]
uitabs = [
    if $arg3 [] [arg3 = $ui_tabw]
    if $arg4 [] [arg4 = $ui_tabh]
    uivlist 0 [
        uihlist 0 [
            looplist3 v n c $arg6 [
                uispace $ui_padsmall 0 [
                    uitabborder $arg3 $arg4 (? (=s $$arg5 $v) $ui_main2 $ui_main1) $ui_main2 $ui_main3 (? (=s $$arg5 $v) $ui_line $ui_disa) $colourred $colourpink [
                        if (!=s $arg5 $v) [
                            uirelease [$arg5 = $v]
                            doargs $c
                        ]
                        uispace $ui_padbutton $ui_padsmall [uitext (concatword (? (=s $$arg5 $v) "^fw" "^fa") $n)]
                    ]
                ]
            ]
        ]
        uiskinborder $arg1 $arg2 $ui_trans $ui_trans $ui_trans $ui_line $ui_line $ui_line [
            @@arg7
        ]
    ]
]

// uivscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uivscroll = [
    if $arg3 [] [arg3 = 1.0]
    if $arg4 [] [arg4 = 0.5]
    uiskinborder $arg1 $arg2 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
        uivlist 0 [
            uiclamp 1 1 1 1
            uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg1 $arg1 0 [] $arg4]
            uivscrollbar [
                uifill $arg1 (-f $arg2 (*f $arg1 2)) [uiclamp- 1 1 1 1]
                uiscrollbutton [
                    uiskinborder $arg1 0 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uiclamp 1 1 1 1
                        uiclamp* 1 1 1 1
                        style_linesh 3 0.004 (*f $arg1 0.5)
                    ] [uiclamp 1 1 1 1]
                ]
            ]
            uiscrollarrow $arg3 [uiarrowbutton $arg1 $arg1 180 [] $arg4]
        ]
    ]
]

// uihscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uihscroll = [
    if $arg3 [] [arg3 = 1.0]
    if $arg4 [] [arg4 = 0.5]
    uiskinborder $arg1 $arg2 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
        uihlist 0 [
            uiclamp 1 1 1 1
            uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg2 $arg2 90 [] $arg4]
            uihscrollbar [
                uifill (-f $arg1 (*f $arg2 2)) $arg2 [uiclamp- 1 1 1 1]
                uiscrollbutton [
                    uiskinborder 0 $arg2 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uiclamp 1 1 1 1
                        uiclamp* 1 1 1 1
                        style_linesv 3 0.004 (*f $arg1 0.5)
                    ] [uiclamp 1 1 1 1]
                ]
            ]
            uiscrollarrow $arg3 [uiarrowbutton $arg2 $arg2 270 [] $arg4]
        ]
    ]
]

// uivertslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uivertslider = [
    if $arg9 [] [arg9 = 0.5]
    uiskinborder $arg6 $arg5 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
        uivlist 0 [
            uisliderarrow -1 [uiarrowbutton $arg5 $arg5 0 [] $arg9]
            uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
                uifill (-f $arg6 (*f $arg5 2)) $arg5 [uiclamp- 1 1 1 1]
                uisliderbutton [
                    uiskinborder 0 $arg2 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uiclamp 1 1 1 1
                        uiclamp* 1 1 1 1
                        uispace (? (>= $numargs 10) $arg11 $ui_padbutton) 0 $arg7
                    ] [uiclamp 1 1 1 1]
                ]
            ]
            uisliderarrow 1 [uiarrowbutton $arg5 $arg5 180 [] $arg9]
        ]
    ]
]

// uihorzslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uihorzslider = [
    if $arg9 [] [arg9 = 0.5]
    uiskinborder $arg5 $arg6 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
        uihlist 0 [
            uisliderarrow -1 [uiarrowbutton $arg5 $arg5 90 [] $arg9]
            uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
                uifill (-f $arg5 (*f $arg6 2)) $arg6 [uiclamp- 1 1 1 1]
                uisliderbutton [
                    uiskinborder 0 $arg2 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uiclamp 1 1 1 1
                        uiclamp* 1 1 1 1
                        uispace (? (>= $numargs 10) $arg11 $ui_padbutton) 0 $arg7
                    ] [uiclamp 1 1 1 1]
                ]
            ]
            uisliderarrow 1 [uiarrowbutton $arg5 $arg5 270 [] $arg9]
        ]
    ]
]

// uivcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change]
uivcolourslider = [
    uigroup [
        uivgradient $arg5 $arg4 (*f 0.85 $arg6) $arg7 [uioutline $ui_line ; uiclamp- 1 1 1 1]
        uifont "default" [uivlist -0.01 [
            loop i (strlen (abs $$arg1)) [
                uicolourtext (substr $$arg1 $i 1) (? (|| uihover? uihold?) $ui_vc1 $ui_vc2) 0.8
            ]
        ]]
        uivslider $arg1 $arg2 $arg3 1 $arg8 [
            uifill $arg6 (*f 0.98 $arg7)
            uisliderbutton [uiline $ui_default $arg6 0]
        ]
    ]
]

// uihcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change]
uihcolourslider = [
    uigroup [
        uihgradient $arg4 $arg5 $arg6 (*f 0.85 $arg7) [uioutline $ui_line ; uiclamp- 1 1 1 1]
        uihslider $arg1 $arg2 $arg3 1 $arg8 [
            uiclamp- 1 1 1 1
            uifill (*f 0.98 $arg6) $arg7
            uisliderbutton [uiline $ui_default 0 $arg7]
        ]
    ]
]

//uilistslider 1:VAR 2:COMPARISON 3:[values] 4:X 5:Y 6:[on-change] 7:(ARROW-%-SIZE) 8:[children]
uilistslider = [
    if $arg7 [] [arg7 = 0.5]
    uihlist 0 [
        uiarrowbutton $arg5 $arg5 90 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (< $n 0) [n = 0] [
                n = (- $n 2)
                if (< $n 0) [n = (+ $n (listlen $arg3))]
            ]
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
        uifill (-f $arg4 (*f $arg5 2)) $arg5 [
            uicolour $ui_main2
            uiclamp* 1 1 1 1
            if (=s $arg8 "") [
                uitext (|| [[listassoc@arg2] $arg3 $$arg1] [result "custom"]) 0.7
            ] arg8
        ]
        uiarrowbutton $arg5 $arg5 270 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
    ]
]

//uiinput 1:VAR 2:LENGTH 3:[on-change] 4:SCALE 5:[immediate] 6:[prompt] 7:[children] 8:ARGS
uiinput = [
    uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
        doargs $arg8
        uispace $ui_padsmall $ui_padsmaller [
            if (&& (!=s $arg6 "") (=s $$arg1 "") (uifocus+? 0 1)) [ui_input_prompt = $arg6] [ui_input_prompt = ""]
            uifield $arg1 $arg2 $arg3 $arg4 $arg5 [
                @(? (!=s $ui_input_prompt "") [uitext $ui_input_prompt $ui_texttiny])
                @arg7
            ]
        ]
    ]
]

uirgbsliders = [
    // arg1 is an alias that takes a hex colour value
    colr = (& (>> $$arg1 16) 0xFF)
    colg = (& (>> $$arg1 8) 0xFF)
    colb = (& $$arg1 0xFF)
    uivlist 0 [
        uiclamp 1 1
        loop i 3 [
            n = [col@(at "r g b" $i)]
            uihlist 0 [
                uihcolourslider $n 0 255 0x000000 (at [0xFF0000 0x00FF00 0x0000FF] $i) $ui_rgbw $ui_rgbh [[@@@@@arg1] = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)]
            ]
        ]
    ]
]

uihexpreview = [
    uiskinborder $ui_hexprev 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uispace $ui_padsmall 0 [
            uivlist $ui_padbutton [
                uitext (format "^fw%1^fa,^fw%2^fa,^fw%3" (& (>> $arg1 16) 0xFF) (& (>> $arg1 8) 0xFF) (& $arg1 0xFF))
                uitext (hexcolour $arg1)
            ]
        ]
    ]
]
