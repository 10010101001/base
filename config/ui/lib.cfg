// uimenu 1:<name> 2:<title> 3:[children] 4:[on-show] 5:[on-hide] 6:[ui-children] 7:[tip]
uimenu = [
    newui $arg1 [
        @arg6
        uivlist 0 [
            uiclamp 1 1 1 1
            uiskinborder 0 0.04 $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a [
                uiclamp 1 1
                uispace $uiwindow_padding $uismall_padding [
                    uiclamp 1 1
                    uitext [@@@@@arg2] 1.2
                    if (|| (!=s @@@@arg1 "main") (isconnected)) [
                        uiskinborder 0.03 0.03 $c_main1 $c_main2 $c_main3 $c_line $c_focus $c_hold [
                            uirelease [hideui [@@@@@@@@arg1]]
                            uiclamp* 1 1 1 1
                            uiimage "textures/guiexit" $c_default 0 0.03 0.03 [
                                uihover [uisetcolor $c_focus]
                                uihold [uisetcolor $c_hold]
                            ]
                        ]
                        uialign- 1
                    ]
                ]
            ]
            uiclamp- 1 1 1 1
            uiskinborder 0.1 0.1 $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a [
                uiclamp 1 1 1 1
                uispace $uiwindow_padding $uiwindow_padding [
                    uiclamp 1 1 1 1
                    @@@@arg3
                ]
            ]
            uiclamp- 1 1 1 1
            uiskinborder 0 0 $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a [
                uiclamp 1 1 1 1
                uispace $uiwindow_padding $uismall_padding [
                    uiclamp 1 1 1 1
                    uiwraptext (format "TIP: ^fa%1" (? (>= @@@@numargs 7) [@@@@@arg7] (showtip))) 0.9 0.8
                ]
            ]
            uiclamp- 1 1 1 1
        ]
        uiclamp- 1 1 1 1
    ] $arg4 $arg5
]

// uiskinborder 1:<w> 2:<h> 3:<bgcolor> 4:<bghover> 5:<bghold> 6:<bordercolor> 7:<borderhover> 8:<borderhold>
uiskinborder = [
    uiskin "textures/guiskin" $arg3 1 $arg1 $arg2 [
        uihover [uisetcolor @arg4] [uisetcolor @arg3]
        uihold [uisetcolor @arg5]
        uiclamp* 1 1 1 1
        uiskin "textures/guiskinborder" $arg6 1 $arg1 $arg2 [
            uihover [uisetcolor @arg7] [uisetcolor @arg6]
            uihold [uisetcolor @arg8]
            uiclamp* 1 1 1 1
            doargs $arg9
        ]
    ]
]

// uibar 1:<clampx> 2:<clampy>
uibar = [
    uiline $c_line
    uiclamp- $arg1 $arg1 $arg2 $arg2
]

// uidrawtriangle 1:<colour> 2:<w> 3:<h> 4:<orient>
uidrawtriangle = [
    if $arg1 [] [arg1 = $c_bline]
    uigroup [
        uitriangle $arg1 $arg2 $arg3 $arg4
        uitriangleoutline $arg1 $arg2 $arg3 $arg4
    ]
]

// uidrawcircle 1:<colour> 2:<size>
uidrawcircle = [
    if $arg1 [] [arg1 = $c_bline]
    uigroup [
        uicircle $arg1 $arg2
        uicircleoutline $arg1 $arg2
    ]
]

// uititle 1:[title children] 2:(SIZE-W) 3:(SIZE-H)
uititle = [
    uiskinborder $arg1 $arg2 $c_menu $c_menu $c_menu $c_line $c_line $c_line [
        arg1
        uiclamp 1 1 1 1
    ]
]

// uibuttonarea 1:(STYLE) 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[children]
uibuttonarea = [
    uiskinborder 0 0 $c_main1 $c_main2 $c_main3 $c_line $c_focus $c_hold [
        doargs $arg4
    ]
]

// uibutton 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED
uibutton = [
    uiskinborder $arg3 $arg4 (? $arg8 $c_maind $c_main1) (? $arg8 $c_maind $c_main2) (? $arg8 $c_maind $c_main3) (? $arg8 $c_disa $c_line) (? $arg8 $c_disa $c_focus) (? $arg8 $c_disa $c_hold) [
        c_temp = $c_default
        if $arg8 [ c_temp = $c_disable ] [
            uihover [c_temp = $c_focus]
            uihold [c_temp = $c_hold]
            uirelease [doargs $arg5]
        ]
        doargs $arg7
        uiclamp* 1 1 1 1
        if $arg1 [ 
            uispace $uibutton_padding 0 [
                uiclamp 1 1
                uihlist 0.01 [
                    uialign -1 0
                    c_size = (*f $arg4 0.75)
                    uiimage $arg2 $c_temp 0 $c_size $c_size
                ]
            ]
        ]
        if $arg1 [ 
            uispace $uibutton_padding 0 [
                uiclamp 1 1
                uihlist 0.01 [
                    uialign $arg6 0
                    uitext (format "^f[%1]%2" $c_temp $arg1)
                ]
            ]
        ]
    ]
]

// uicheckbox 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xCOLOR-ON 6:0xCOLOR-ON2 7:0xCOLOR-BG 8:[children]
uicheckbox = [
    if $arg7 [] [arg7 = $c_bline]
    if $arg6 [] [arg6 = $c_on2]
    if $arg5 [] [arg5 = $c_on1]
    uitarget $c_default 0 0 [
        doargs $arg8
        c_temp = $arg7
        uihover [c_temp = $c_focus]
        uihold [c_temp = $c_hold]
        uirelease [doargs $arg4]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiimage "textures/checkbox" $c_temp 0 $arg3 $arg3 [
                uiclamp* 1 1 1 1
                retval = (arg2)
                if $retval [
                    uiimage "textures/checkboxon" (? (= $retval 1) $arg5 $arg6) 0 $arg3 $arg3 [ uiclamp* 1 1 1 1 ]
                ]
            ]
            uispace 0.0035
            uitext (format "^f[%1]%2" $c_temp $arg1)
        ]
    ]
]

// uiradio 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xCOLOR-ON 6:0xCOLOR-BG 7:[children]
uiradio = [
    if $arg6 [] [arg6 = $c_bline]
    if $arg5 [] [arg5 = $c_on1]
    uitarget $c_default 0 0 [
        doargs $arg7
        c_temp = $arg6
        uihover [c_temp = $c_focus]
        uihold [c_temp = $c_hold]
        uirelease [doargs $arg4]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiimage "textures/radiobox" $c_temp 0 $arg3 $arg3 [
                uiclamp* 1 1 1 1
                if (arg2) [ uiimage "textures/radioboxon" $arg5 0 $arg3 $arg3 [ uiclamp* 1 1 1 1 ] ]
            ]
            uispace 0.0035
            uitext (format "^f[%1]%2" $c_temp $arg1)
        ]
    ]
]

// uiarrowbutton 1:(SIZE-W) 2:(SIZE-H) 3:(ORIENTATION) 4:[on-release] 5:(ARROW-%-SIZE) 6:[children]
uiarrowbutton = [
    local arrowsize
    arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
    uitarget $c_default $arg1 $arg2 [
        uicolor $c_menu
        doargs $arg6
        style_button
        uirelease [doargs $arg4]
        uiclamp* 1 1 1 1
        uidrawtriangle 0 $arrowsize $arrowsize $arg3
    ]
]

// uihtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..] 5:<BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
uihtab = [
    uifill $c_default $arg1 $arg2 [
        uihlist 0 [
            uiclamp 1 1
            looplist3 v n c $arg4 [
                if (> $v 0) [ uispace 0.0035 ]
                uiskinborder 0 $arg2 $c_main1 $c_main2 $c_main3 (? (=s $$arg3 $v) $c_active $c_line) $c_focus $c_hold [
                    uispace 0.01 0 [uitext (concatword (? (=s $$arg3 $v) "^fw" "^fa") $n)]
                    if (!=s $$arg3 $v) [
                        case $arg5 0 [
                            uihover [$arg3 = $v]
                        ] 1 [
                            uipress [$arg3 = $v]
                        ] 2 [uirelease [$arg3 = $v]]
                        doargs $c
                    ]
                ]
            ]
            uiclamp* 1 1
        ]
        uialign- 0 1
    ]
]

// uivtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..] 5:<BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
uivtab = [
    uifill $c_default $arg1 $arg2 [
        uivlist 0 [
            uiclamp 1 1
            looplist3 v n c $arg4 [
                uiskinborder $arg1 0 $c_main1 $c_main2 $c_main3 (? (=s $$arg3 $v) $c_active $c_line) $c_focus $c_hold [
                    uispace 0.01 0 [uitext (concatword (? (=s $$arg3 $v) "^fw" "^fa") $n)]
                    if (!=s $$arg3 $v) [
                        case $arg5 0 [
                            uihover [$arg3 = $v]
                        ] 1 [
                            uipress [$arg3 = $v]
                        ] 2 [uirelease [$arg3 = $v]]
                        doargs $c
                    ]
                ]
            ]
            uiclamp* 1 1
        ]
        uialign- 0 1
    ]
]

// uivscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uivscroll = [
    if $arg4 [] [arg4 = 0.5]
    uivlist 0 [
        uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg1 $arg1 0 [] $arg4]
        uispace 0 0.001
        uivscrollbar [
            uifill $c_default $arg1 (-f $arg2 (*f $arg1 2)) [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolor $c_menu $arg1
                style_button
                uiclamp* 1 1 1 1
                style_linesh 3 0.004 (*f $arg1 0.5)
            ]
        ]
        uispace 0 0.001
        uiscrollarrow $arg3 [uiarrowbutton $arg1 $arg1 180 [] $arg4]
    ]
]

// uihscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uihscroll = [
    if $arg4 [] [arg4 = 0.5]
    uihlist 0 [
        uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg2 $arg2 90 [] $arg4]
        uispace 0.001
        uihscrollbar [
            uifill $c_default (-f $arg1 (*f $arg2 2)) $arg2 [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolor $c_menu 0 $arg2
                style_button
                uiclamp* 1 1 1 1
                style_linesv 3 0.004 (*f $arg2 0.5)
            ]
        ]
        uispace 0.001
        uiscrollarrow $arg3 [uiarrowbutton $arg2 $arg2 270 [] $arg4]
    ]
]

// uivertslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uivertslider = [
    if $arg9 [] [arg9 = 0.5]
    uivlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg5 $arg5 0 [] $arg9]
        uispace 0.1
        uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
            uifill $c_default (-f $arg6 (*f $arg5 2)) $arg5 [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolor $c_menu $arg5
                style_button
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $uibutton_padding) 0 [arg7]
            ]
        ]
        uispace 0.1
        uisliderarrow 1 [uiarrowbutton $arg5 $arg5 180 [] $arg9]
    ]
]

// uihorzslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uihorzslider = [
    if $arg9 [] [arg9 = 0.5]
    uihlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg6 $arg6 90 [] $arg9]
        uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
            uifill $c_default (-f $arg5 (*f $arg6 2)) $arg6 [uicolor $c_slider ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolor $c_menu 0 $arg6
                style_button
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $uibutton_padding) 0 [arg7]
            ]
        ]
        uisliderarrow 1 [uiarrowbutton $arg6 $arg6 270 [] $arg9]
    ]
]

// uivcolorslider 1:VAR 2:MIN 3:MAX 4:0xCOLOR1 5:0xCOLOR2 6:X 7:Y 8:[on-change]
uivcolorslider = [
    uigroup [
        uivgradient $arg5 $arg4 (*f 0.85 $arg6) $arg7 [uioutline $c_line ; uiclamp- 1 1 1 1]
        uifont "default" [uivlist -0.01 [
            loop i (strlen (abs $$arg1)) [
                uicolortext (substr $$arg1 $i 1) (? (|| uihover? uihold?) 0x80FFFFFF 0x40FFFFFF) 0.8
            ]
        ]]
        uivslider $arg1 $arg2 $arg3 1 $arg8 [
            uifill $c_default $arg6 (*f 0.98 $arg7)
            uisliderbutton [uiline $c_default $arg6 0]
        ]
    ]
]

// uihcolorslider 1:VAR 2:MIN 3:MAX 4:0xCOLOR1 5:0xCOLOR2 6:X 7:Y 8:[on-change]
uihcolorslider = [
    uigroup [
        uihgradient $arg4 $arg5 $arg6 (*f 0.85 $arg7) [uioutline $c_line ; uiclamp- 1 1 1 1]
        uihslider $arg1 $arg2 $arg3 1 $arg8 [
            uiclamp- 1 1 1 1
            uifill $c_default (*f 0.98 $arg6) $arg7
            uisliderbutton [uiline $c_default 0 $arg7]
        ]
    ]
]

//uilistslider 1:VAR 2:COMPARISON 3:[values] 4:X 5:Y 6:[on-change] 7:(ARROW-%-SIZE) 8:[children]
uilistslider = [
    if $arg7 [] [arg7 = 0.5]
    uihlist 0 [
        uiarrowbutton $arg5 $arg5 90 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (< $n 0) [n = 0] [
                n = (- $n 2)
                if (< $n 0) [n = (+ $n (listlen $arg3))]
            ]
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
        uifill $c_default (-f $arg4 (*f $arg5 2)) $arg5 [
            uicolor $c_slider
            uiclamp* 1 1 1 1
            if (=s $arg8 "") [
                uitext (|| [[listassoc@arg2] $arg3 $$arg1] [result "custom"]) 0.7
            ] arg8
        ]
        uiarrowbutton $arg5 $arg5 270 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
    ]
]

//uiinput 1:VAR 2:LENGTH 3:[on-change] 4:SCALE 5:PARENT 6:PROMPT 7:[children] 8:ARGS
uiinput = [
    uiskinborder 0 0 $c_main1 $c_main2 $c_main3 $c_line $c_focus $c_hold [
        doargs $arg8
        uispace 0.002 0.002 [
            uifield $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 $arg7
        ]
    ]
]

uirgbsliders = [
    // arg1 is an alias that takes a hex colour value
    colr = (& (>> $$arg1 16) 0xFF)
    colg = (& (>> $$arg1 8) 0xFF)
    colb = (& $$arg1 0xFF)
    uivlist 0 [
        uiclamp 1 1
        loop i 3 [
            n = [col@(at "r g b" $i)]
            uihlist 0 [
                uihcolorslider $n 0 255 0x000000 (at [0xFF0000 0x00FF00 0x0000FF] $i) 0.39 0.04 [@arg1 = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)]
            ]
        ]
    ]
]

uihexpreview = [
    uiskinborder 0.15 0.05 $arg1 $arg1 $arg1 $c_line $c_line $c_line [
        uivlist 0 [
            uitext @arg2
            uitext (hexcolour @arg1)
        ]
    ]
]
