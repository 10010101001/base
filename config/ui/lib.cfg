uiclose = [
    if $arg1 [] [arg1 = (topui)]
    if (< $numargs 2) [arg2 = 0]
    uihidetip
    if (= $arg2 1) [
        [ui_menu_close_@@arg1] = 0
        [ui_menu_hider_@@arg1] = 1
        [ui_menu_sched_@@arg1] = ""
        hideui [@@arg1]
    ] [
        [ui_menu_close_@@arg1] = (getmillis 1)
        [ui_menu_hider_@@arg1] = 1
        [ui_menu_sched_@@arg1] = ""
    ]
]

uiopen = [
    if (< $numargs 2) [arg2 = 1]
    ui_topui = (topui)
    if (!=s $ui_topui [@arg1]) [
        uihidetip
        if (&& (!= [@@arg2] 0) (!=s $ui_topui "")) [
            [ui_menu_close_@ui_topui] = (getmillis 1)
            [ui_menu_hider_@ui_topui] = [@@@arg2]
            [ui_menu_sched_@ui_topui] = [@@@arg1]
        ] [showui [@@@arg1]]
    ]
]

// uimenu 1:<name> 2:<title> 3:[icon] 4:[children] 5:[on-show] 6:[on-hide] 7:[ui-children] 8:[status] 9:[close-action]
uimenu = [
    [ui_menu_title_@arg1] = [@arg2]
    [ui_menu_icon_@arg1] = [@arg3]
    [ui_menu_status_@arg1] = [@arg8]
    newui $arg1 [
        [ui_menu_last_@@arg1] = (getmillis 1)
        if (!=s (topui) [@@arg1]) [if $[ui_menu_obscure_@@@arg1] [] [[ui_menu_obscure_@@@@arg1] = (getmillis 1)]] [[ui_menu_obscure_@@@arg1] = 0]
        uiallowinput (? (|| $[ui_menu_close_@@arg1] $[ui_menu_obscure_@@arg1]) 0 1)
        uiescrelease [if (|| (!=s [@@@arg1] "main") (isconnected)) [if (>= [@@@@numargs] 9) [doargs [@@@@@arg9]] [uiclose [@@@@@arg1]]]]
        @arg7
        uivlist 0 [
            uivlist 0 [
                uiskinborder 0 $ui_buttonh $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a [
                    uiclamp 1 1
                    uispace $ui_padsmall $ui_padsmall [
                        uiclamp 1 1
                        uihlist 0 [
                            uialign -1
                            uispace $ui_padsmall 0 [uiimage $[ui_menu_icon_@@@@@@@@arg1] $ui_default 0 (*f $ui_titlesize 0.9) (*f $ui_titlesize 0.9)]
                        ]
                        uispace (*f $ui_titlesize 1.5) 0 [uitext $[ui_menu_title_@@@@@@@arg1] $ui_textlarge]
                        uihlist 0 [
                            uialign 1
                            uibuttonimg "" "textures/guiexit" $ui_titlesize $ui_titlesize [
                                if (>= [@@@@@@@@numargs] 9) [doargs [@@@@@@@@@arg9]] [uiclose [@@@@@@@@@arg1]]
                            ] 0 [] (&& (=s [@@@@@@@arg1] "main") (! (isconnected))) 0 [] $ui_default $ui_on1
                        ]
                    ]
                ] [uiclamp 1 1 1 1]
                uiskinborder 0 0 $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a [
                    uiclamp 1 1 1 1
                    uispace $ui_padwin $ui_padwin [
                        uiclamp 1 1 1 1
                        @@@@@arg4
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            uivlist 0 [
                uiclamp 1 1
                uiskinborder 0 0 $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a [
                    uiclamp 1 1
                    uispace $ui_padwin $ui_padsmall [
                        uiclamp 1 1
                        uiwraptext (? (!=s $[ui_menu_status_@@@@@@arg1] "") $[ui_menu_status_@@@@@@arg1] (tipshow)) 0.75 $ui_textstatus 0
                    ]
                ] [uiclamp 1 1]
            ]
        ]
        if $[ui_menu_close_@@arg1] [
            [ui_menu_frame_@@@arg1] = (- $[ui_menu_last_@@@arg1] $[ui_menu_close_@@@arg1])
            [ui_menu_alpha_@@@arg1] = (-f 1 (divf $[ui_menu_frame_@@@arg1] $ui_transition))
            if (>f $[ui_menu_alpha_@@@arg1] 0) [uichangeblends $[ui_menu_alpha_@@@@arg1]] [
                if (!=s $[ui_menu_sched_@@@@arg1] "") [showui $[ui_menu_sched_@@@@@arg1]]
                if $[ui_menu_hider_@@@@arg1] [uihidetip; hideui [@@@@@arg1]]
                [ui_menu_close_@@@@arg1] = 0
                [ui_menu_hider_@@@@arg1] = 1
                [ui_menu_sched_@@@@arg1] = ""
            ]
        ] [
            [ui_menu_alpha_@@@arg1] = (divf (- $[ui_menu_last_@@@arg1] $[ui_menu_time_@@@arg1]) $ui_transition)
            if (<=f $[ui_menu_alpha_@@@arg1] 1) [uichangeblends $[ui_menu_alpha_@@@@arg1]] [
                if $[ui_menu_obscure_@@@@arg1] [
                    [ui_menu_olast_@@@@@arg1] = (getmillis 1)
                    [ui_menu_oframe_@@@@@arg1] = (- $[ui_menu_last_@@@@@arg1] $[ui_menu_obscure_@@@@@arg1])
                    [ui_menu_oalpha_@@@@@arg1] = (divf $[ui_menu_oframe_@@@@@arg1] $ui_transition)
                ] [
                    if $[ui_menu_olast_@@@@@arg1] [
                        [ui_menu_oframe_@@@@@@arg1] = (- $[ui_menu_last_@@@@@@arg1] $[ui_menu_olast_@@@@@@arg1])
                        [ui_menu_oalpha_@@@@@@arg1] = (-f 1 (divf $[ui_menu_oframe_@@@@@@arg1] $ui_transition))
                    ] [[ui_menu_oalpha_@@@@@@arg1] = 0]
                ]
                if (>f $[ui_menu_oalpha_@@@@arg1] 0) [
                    uiskinborder 0 0 $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a [
                        uiclamp 1 1 1 1
                        if (<=f $[ui_menu_oalpha_@@@@@@arg1] 1) [uichangeblend $[ui_menu_oalpha_@@@@@@@arg1]]
                    ] [
                        uiclamp 1 1 1 1
                        if (<=f $[ui_menu_oalpha_@@@@@@arg1] 1) [uichangeblend $[ui_menu_oalpha_@@@@@@@arg1]]
                    ]
                ]
            ]
        ]
    ] [
        uihidetip
        [ui_menu_last_@@arg1] = (getmillis 1)
        [ui_menu_name_@@arg1] = [@@arg1]
        [ui_menu_time_@@arg1] = $[ui_menu_last_@@arg1]
        [ui_menu_close_@@arg1] = 0
        [ui_menu_alpha_@@arg1] = 0
        [ui_menu_hider_@@arg1] = 1
        [ui_menu_sched_@@arg1] = ""
        [ui_menu_obscure_@@arg1] = 0
        [ui_menu_olast_@@arg1] = 0
        @arg5
    ] [
        [ui_menu_close_@@arg1] = 0
        [ui_menu_alpha_@@arg1] = 0
        [ui_menu_hider_@@arg1] = 1 
        [ui_menu_sched_@@arg1] = ""
        [ui_menu_obscure_@@arg1] = 0
        [ui_menu_olast_@@arg1] = 0
        uihidetip
        @arg6
    ] 1
]

// uitoolmenu 1:<name> 2:<title> 3:[icon] 4:[children] 5:[on-show] 6:[on-hide] 7:[ui-children] 8:[close-action]
uitoolmenu = [
    [ui_menu_title_@arg1] = [@arg2]
    [ui_menu_icon_@arg1] = [@arg3]
    newui $arg1 [
        [ui_menu_last_@@arg1] = (getmillis 1)
        if (!=s (topui) [@@arg1]) [if $[ui_menu_obscure_@@@arg1] [] [[ui_menu_obscure_@@@@arg1] = (getmillis 1)]] [[ui_menu_obscure_@@@arg1] = 0]
        uiallowinput (? (|| $[ui_menu_close_@@arg1] $[ui_menu_obscure_@@arg1]) 0 1)
        uiescrelease [if (|| (!=s [@@@arg1] "main") (isconnected)) [if (>= [@@@@numargs] 9) [doargs [@@@@@arg9]] [uiclose [@@@@@arg1]]]]
        @arg7
        uivlist 0 [
            uiclamp 1 1 1 1
            uiskinborder 0 $ui_toolsize $ui_menu $ui_menu $ui_menu $ui_bline $ui_bline $ui_bline [
                uiclamp 1 1
                uispace $ui_padsmall $ui_padsmall [
                    uiclamp 1 1
                    uihlist 0 [
                        uialign -1
                        uispace $ui_padsmall 0 [uiimage $[ui_menu_icon_@@@@@@@arg1] $ui_default 0 (*f $ui_toolsize 0.9) (*f $ui_toolsize 0.9)]
                    ]
                    uispace (*f $ui_toolsize 1.5) 0 [uitext $[ui_menu_title_@@@@@@arg1] $ui_text]
                    uihlist 0 [
                        uialign 1
                        uibuttonimg "" "textures/guiexit" $ui_toolsize $ui_toolsize [
                            if (>= [@@@@@@@numargs] 8) [doargs [@@@@@@@@arg8]] [uiclose [@@@@@@@@arg1]]
                        ] 0 [] 0 0 [] $ui_default $ui_on1
                    ]
                ]
            ] [uiclamp 1 1 1 1]
            uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $ui_bline $ui_bline $ui_bline [
                uiclamp 1 1 1 1
                uispace $ui_padwin $ui_padwin [
                    uiclamp 1 1 1 1
                    @@@@arg4
                ]
            ] [uiclamp 1 1 1 1]
        ]
        if (!=s (topui) [@@arg1]) [
            uiskinborder 0 0 $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a [
                uiclamp 1 1 1 1
            ] [uiclamp 1 1 1 1]
        ]
        if $[ui_menu_close_@@arg1] [
            [ui_menu_frame_@@@arg1] = (- $[ui_menu_last_@@@arg1] $[ui_menu_close_@@@arg1])
            [ui_menu_alpha_@@@arg1] = (-f 1 (divf $[ui_menu_frame_@@@arg1] $ui_transition))
            if (>f $[ui_menu_alpha_@@@arg1] 0) [uichangeblends $[ui_menu_alpha_@@@@arg1]] [
                if (!=s $[ui_menu_sched_@@@@arg1] "") [showui $[ui_menu_sched_@@@@@arg1]]
                if $[ui_menu_hider_@@@@arg1] [uihidetip; hideui [@@@@@arg1]]
                [ui_menu_close_@@@@arg1] = 0
                [ui_menu_hider_@@@@arg1] = 1
                [ui_menu_sched_@@@@arg1] = ""
            ]
        ] [
            [ui_menu_alpha_@@@arg1] = (divf (- $[ui_menu_last_@@@arg1] $[ui_menu_time_@@@arg1]) $ui_transition)
            if (<=f $[ui_menu_alpha_@@@arg1] 1) [uichangeblends $[ui_menu_alpha_@@@@arg1]] [
                if $[ui_menu_obscure_@@@@arg1] [
                    [ui_menu_olast_@@@@@arg1] = (getmillis 1)
                    [ui_menu_oframe_@@@@@arg1] = (- $[ui_menu_last_@@@@@arg1] $[ui_menu_obscure_@@@@@arg1])
                    [ui_menu_oalpha_@@@@@arg1] = (divf $[ui_menu_oframe_@@@@@arg1] $ui_transition)
                ] [
                    if $[ui_menu_olast_@@@@@arg1] [
                        [ui_menu_oframe_@@@@@@arg1] = (- $[ui_menu_last_@@@@@@arg1] $[ui_menu_olast_@@@@@@arg1])
                        [ui_menu_oalpha_@@@@@@arg1] = (-f 1 (divf $[ui_menu_oframe_@@@@@@arg1] $ui_transition))
                    ] [[ui_menu_oalpha_@@@@@@arg1] = 0]
                ]
                if (>f $[ui_menu_oalpha_@@@@arg1] 0) [
                    uiskinborder 0 0 $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a $ui_menu_a [
                        uiclamp 1 1 1 1
                        if (<=f $[ui_menu_oalpha_@@@@@@arg1] 1) [uichangeblend $[ui_menu_oalpha_@@@@@@@arg1]]
                    ] [
                        uiclamp 1 1 1 1
                        if (<=f $[ui_menu_oalpha_@@@@@@arg1] 1) [uichangeblend $[ui_menu_oalpha_@@@@@@@arg1]]
                    ]
                ]
            ]
        ]
    ] [
        uihidetip
        [ui_menu_last_@@arg1] = (getmillis 1)
        [ui_menu_name_@@arg1] = [@@arg1]
        [ui_menu_time_@@arg1] = $[ui_menu_last_@@arg1]
        [ui_menu_close_@@arg1] = 0
        [ui_menu_alpha_@@arg1] = 0
        [ui_menu_hider_@@arg1] = 1
        [ui_menu_sched_@@arg1] = ""
        [ui_menu_obscure_@@arg1] = 0
        [ui_menu_olast_@@arg1] = 0
        @arg5
    ] [
        [ui_menu_close_@@arg1] = 0
        [ui_menu_alpha_@@arg1] = 0
        [ui_menu_hider_@@arg1] = 1
        [ui_menu_sched_@@arg1] = ""
        [ui_menu_obscure_@@arg1] = 0
        [ui_menu_olast_@@arg1] = 0
        uihidetip
        @arg6
    ] 1
]

// uitipmenu 1:<name> 2:[children] 3:[on-show] 4:[on-hide] 5:[ui-children]
uitipmenu = [
    newui $arg1 [
        uiallowinput 0
        @arg5
        uivlist 0 [
            uiclamp 1 1 1 1
            uiskinborder 0 0 $ui_tool_a $ui_tool_a $ui_tool_a $ui_tline_a $ui_tline_a $ui_tline_a [
                uiclamp 1 1 1 1
                uispace $ui_padsmall $ui_padsmall [
                    uiclamp 1 1 1 1
                    @@@@arg2
                ]
            ] [uiclamp 1 1 1 1]
        ] [uiclamp 1 1 1 1]
    ] $arg4 $arg5 2
]

// uiskinborder 1:<w> 2:<h> 3:<bgcolour> 4:<bghover> 5:<bghold> 6:<bordercolour> 7:<borderhover> 8:<borderhold> 9:<children> 10:[children2]
uiskinborder = [
    uiskin "textures/guiskin" $arg3 1 $arg1 $arg2 [
        uihover [uisetcolour [@@@arg4]] [uisetcolour [@@@arg3]]
        uihold [uisetcolour [@@@arg5]]
        uiclamp* 1 1 1 1
        doargs [@@arg10]
        uiskin "textures/guiskinborder" [@@arg6] 1 [@@arg1] [@@arg2] [
            uihover [uisetcolour [@@@@arg7]] [uisetcolour [@@@@arg6]]
            uihold [uisetcolour [@@@@arg8]]
            uiclamp* 1 1 1 1
            doargs [@@@arg9]
        ]
    ]
]
// uitableskin 1:<bgcolour> 2:<bghover> 3:<bghold> 4:<bordercolour> 5:<borderhover> 6:<borderhold> 7:<children> 8:<row-children>
uitableskin = [
    uitablerow [
        doargs [@@arg7]
        uiskinborder 0.01 0.01 $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [doargs [@@@arg8]]
    ]
]

// uibar 1:<clampx> 2:<clampy>
uibar = [
    uiline $ui_line 0 0 [uiclamp [@@arg1] [@@arg1] [@@arg2] [@@arg2]]
]

// uidrawtriangle 1:<colour> 2:<w> 3:<h> 4:<orient>
uidrawtriangle = [
    if $arg1 [] [arg1 = $ui_bline]
    uigroup [
        uitriangle [@@arg1] [@@arg2] [@@arg3] [@@arg4]
        uitriangleoutline [@@arg1] [@@arg2] [@@arg3] [@@arg4]
    ]
]

// uidrawcircle 1:<colour> 2:<size>
uidrawcircle = [
    if $arg1 [] [arg1 = $ui_bline]
    uigroup [
        uicircle [@@arg1] [@@arg2]
        uicircleoutline [@@arg1] [@@arg2]
    ]
]

// uititle 1:[title children] 2:(SIZE-W) 3:(SIZE-H)
uititle = [
    uiskinborder $arg1 $arg2 $ui_menu $ui_menu $ui_menu $ui_line $ui_line $ui_line [
        uiclamp 1 1 1 1
        doargs [@@arg1]
    ]
]

// uitextalign 1:<text> 2:[align] 3:[scale] 4:[children]
uitextalign = [
    if $arg2 [] [arg2 = $ui_text]
    uitext $arg1 $arg2 $arg3 [
        uialign [@@arg3]
        doargs [@@arg4]
    ]
]

uitextleft = [
    if $arg2 [] [arg2 = $ui_text]
    uitextalign $arg1 $arg2 -1 $arg3
]

uitextright = [
    if $arg2 [] [arg2 = $ui_text]
    uitextalign $arg1 $arg2 1 $arg3
]

// uibuttonarea 1:(BUTTON-WIDTH) 2:(BUTTON-HEIGHT) 3:[on-release] 4:(ALIGNMENT) 5:[children] 6:DISABLED 8:[alt-release] 9:[tooltip]
uibuttonarea = [
    uiskinborder $arg1 $arg2 (? $arg6 $ui_maind $ui_main1) (? $arg6 $ui_maind $ui_main2) (? $arg6 $ui_maind $ui_main3) (? $arg6 $ui_disa $ui_line) (? $arg6 $ui_disa $ui_hover) (? $arg6 $ui_disa $ui_hold) [
        uiclamp 1 1
        ui_buttonarea_temp = $ui_default
        if [@@arg6] [ui_buttonarea_temp = $ui_disable] [
            uihover [ui_buttonarea_temp = $ui_hover]
            uihold [ui_buttonarea_temp = $ui_hold]
            uirelease [doargs [@@@@arg3]]
            uialtrelease [doargs [@@@@arg7]]
            uitooltip [@@@arg8]
        ]
        uiclamp* 1 1 1 1
        do [@@arg5]
    ]
]

// uibuttonf 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour] 13:[tooltip] 14:[text-size]
uibuttonf = [
    if $arg14 [] [arg14 = $ui_text]
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        ui_buttonf_size = 0
        if [@@arg2] [
            ui_buttonf_size = (*f [@@@arg4] 0.8)
            uispace (? (>= [@@@numargs] 9) [@@@arg9] $ui_padbutton) 0 [
                uiclamp 1 1
                uihlist 0 [
                    uialign (? [@@@@@arg1] -1 0) 0
                    uiimage [@@@@@arg2] (? (|| [@@@@@arg8] (< [@@@@@numargs] 12)) $ui_buttonarea_temp [@@@@@arg12]) 0 $ui_buttonf_size $ui_buttonf_size "textures/emblem"
                ]
            ]
        ]
        if [@@arg1] [
            uispace (+f (? (>= [@@@numargs] 9) [@@@arg9] $ui_padbutton) $ui_buttonf_size) 0 [
                uiclamp 1 1
                uihlist 0 [
                    uialign [@@@@@arg6] 0
                    uitext (format "^f[%1]%2" (? (|| [@@@@@arg8] (< [@@@@@numargs] 11)) $ui_buttonarea_temp [@@@@@arg11]) [@@@@@arg1]) [@@@@@arg14]
                ]
            ]
        ]
        doargs [@@arg7]
    ] $arg8 $arg10 $arg13
]

// uibuttonm 1:<text> 2:<image> 3:[on-release] 4:DISABLED 5:[tooltip]
uibuttonm = [
    uibuttonf $arg1 $arg2 $ui_buttonw $ui_buttonh $arg3 0 [] $arg4 $ui_padbutton [] $ui_default $ui_default $arg5 $ui_textbig
]

// uibuttonw 1:<text> 2:[on-release] 3:DISABLED 4:[colour] 5:[tooltip]
uibuttonw = [
    uibuttonf $arg1 "" 0 $ui_buttonh $arg2 0 [] $arg3 $ui_padbutton [] $arg4 $arg4 $arg5 $ui_text
]

// uibuttonz 1:<text> 2:[on-release] 3:DISABLED 4:[colour] 5:[tooltip] 6:[size] 7:[text-size] 8:[padding-size]
uibuttonz = [
    uibuttonf $arg1 "" 0 $arg6 $arg2 0 [] $arg3 $arg8 [] $arg4 $arg4 $arg5 $arg7
]

// uibutton 1:<text> 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[on-release] 5:(ALIGNMENT) 6:[children] 7:DISABLED 8:[padding] 9:[alt-release] 10:[text-colour] 11:[image-colour] 12:[tooltip] 13:[text-size]
uibutton = [
    uibuttonf $arg1 "" $arg2 $arg3 $arg4 $arg5 $arg6 $arg7 $arg8 $arg9 $arg10 $arg11 $arg12 $arg13
]

// uibuttonimg 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour] 13:[tooltip] 14:[text-size]
uibuttonimg = [
    if $arg14 [] [arg14 = $ui_text]
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        uispace (? (>= [@@numargs] 9) [@@arg9] $ui_padbutton) 0 [
            uiclamp 1 1
            uihlist 0 [
                uialign [@@@@arg6] 0
                if [@@@@arg2] [
                    ui_size = (*f [@@@@@arg4] 0.85)
                    uiimage [@@@@@arg2] (? (|| [@@@@@arg8] (< [@@@@@numargs] 12)) $ui_buttonarea_temp [@@@@@arg12]) 0 $ui_size $ui_size
                ]
                if [@@@@arg1] [
                    if [@@@@@arg2] [uispace (*f (? (>= [@@@@@@numargs] 9) [@@@@@@arg9] $ui_padbutton) 0.35)]
                    uitext (format "^f[%1]%2" (? (|| [@@@@@arg8] (< [@@@@@numargs] 11)) $ui_buttonarea_temp [@@@@@arg11]) [@@@@@arg1]) [@@@@@arg14]
                ]
            ]
        ]
        doargs [@@arg7]
    ]  $arg8 $arg10 $arg13
]

// uiimgbutton 1:<image> 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[on-release] 5:(ALIGNMENT) 6:[children] 7:DISABLED 8:[padding] 9:[alt-release] 10:[image-colour] 11:[tooltip]
uiimgbutton = [
    uibuttonarea $arg2 $arg3 $arg4 $arg5 [
        uispace (? (>= [@@numargs] 8) [@@arg8] $ui_padbutton) (? (>= [@@numargs] 8) [@@arg8] $ui_padbutton) [
            uiclamp 1 1 1 1
            uihlist 0 [
                uiclamp 1 1 1 1
                uialign [@@@@arg5] 0
                uiimage [@@@@arg1] (? (|| [@@@@arg7] (< [@@@@numargs] 10)) $ui_buttonarea_temp [@@@@arg10]) 0 [@@@@arg2] [@@@@arg3] "" [uioutline $ui_buttonarea_temp; uiclamp- 1 1 1 1; @@@@arg6]
            ]
        ]
    ]  $arg7 $arg9 $arg11
]

// uicheckbox 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-ON2 7:0xcolour-BG 8:[children] 9:DISABLED 10:[text-size]
uicheckbox = [
    if $arg7 [] [arg7 = $ui_bline]
    if $arg6 [] [arg6 = $ui_on2]
    if $arg5 [] [arg5 = $ui_on1]
    uitarget $ui_default 0 0 [
        ui_checkbox_temp = [@@arg7]
        if [@@arg9] [ui_checkbox_temp = $ui_disable] [
            uihover [ui_checkbox_temp = $ui_hover]
            uihold [ui_checkbox_temp = $ui_hold]
            uirelease [doargs [@@@arg4]]
        ]
        doargs [@@arg8]
        uiclamp* 1 1 1 1
        uihlist 0 [
            ui_retval = (arg2)
            uiimage "textures/checkbox" $ui_checkbox_temp 0 [@@@arg3] [@@@arg3] "" [
                uiclamp* 1 1 1 1
                if $ui_retval [
                    uiimage "textures/checkboxon" (? [@@@@@arg9] $ui_on3 (? (= $ui_retval 1) [@@@@@arg5] [@@@@@arg6])) 0 [@@@@@arg3] [@@@@@arg3] "" [uialign 0 0]
                ]
            ]
            if [@@@arg1] [
                uispace $ui_padsmall 0 [
                    uitext (format "^f[%1]%2" $ui_checkbox_temp [@@@@@arg1]) [@@@@@arg10]
                ]
            ]
        ]
    ]
]

// uicheckbutton 1:<image> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children] 8:DISABLED
uicheckbutton = [
    if $arg6 [] [arg6 = $ui_line]
    if $arg5 [] [arg5 = $ui_on1]
    ui_retval = (arg2)
    uiskinborder $arg3 $arg3 (? $arg8 $ui_maind $ui_main1) (? $arg8 $ui_maind $ui_main2) (? $arg8 $ui_maind $ui_main3) (? $arg8 $ui_disa (? $ui_retval $arg5 $ui_line)) (? $arg8 $ui_disa $ui_hover) (? $arg8 $ui_disa $ui_hold) [
        ui_checkbutton_temp = (? $ui_retval [@@arg5] [@@arg6])
        if [@@arg8] [ui_checkbutton_temp = (? [@@@arg8] $ui_on3 $ui_disable)] [
            uihover [ui_checkbutton_temp = $ui_hover]
            uihold [ui_checkbutton_temp = $ui_hold]
            uirelease [doargs [@@@arg4]]
        ]
        doargs [@@arg7]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiclamp 1 1
            uialign 0 0
            ui_size = (*f [@@@arg3] 0.9)
            uiimage [@@@arg1] $ui_checkbutton_temp 0 $ui_size $ui_size "" [uialign 0 0]
        ]
    ]
]

// uichecktext 1:<text> 2:[condition] 3:(W) 4:(H) 5:[on-release] 6:0xcolour-ON 7:0xcolour-BG 8:[children] 9:DISABLED 10:[text-size]
uichecktext = [
    if $arg7 [] [arg7 = $ui_line]
    if $arg6 [] [arg6 = $ui_on1]
    if $arg10 [] [arg10 = $ui_text]
    ui_retval = (arg2)
    uiskinborder $arg3 $arg4 $ui_main1 $ui_main2 $ui_main3 (? $ui_retval $arg6 $ui_line) $ui_hover $ui_hold [
        ui_checktext_temp = (? $ui_retval [@@arg6] [@@arg7])
        if [@@arg9] [ui_checktext_temp = $ui_disable] [
            uihover [ui_checktext_temp = $ui_hover]
            uihold [ui_checktext_temp = $ui_hold]
            uirelease [doargs [@@@arg5]]
        ]
        doargs [@@arg8]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiclamp 1 1
            uialign 0 0
            uispace $ui_padsmall 0 [
                uiclamp 1 1
                uialign 0 0
                uitext (format "^f[%1]%2" $ui_checktext_temp [@@@@arg1]) [@@@@arg10]
            ]
        ]
    ]
]

// uiradio 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children] 8:DISABLED 9:[text-size]
uiradio = [
    if $arg6 [] [arg6 = $ui_bline]
    if $arg5 [] [arg5 = $ui_on1]
    uitarget $ui_default 0 0 [
        ui_radio_temp = [@@arg6]
        if [@@arg8] [ui_radio_temp = $ui_disable] [
            uihover [ui_radio_temp = $ui_hover]
            uihold [ui_radio_temp = $ui_hold]
            uirelease [doargs [@@@arg4]]
        ]
        doargs [@@arg7]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiimage "textures/radiobox" $ui_radio_temp 0 [@@@arg3] [@@@arg3] "" [
                uiclamp* 1 1 1 1
                if (arg2) [uiimage "textures/radioboxon" (? [@@@@arg8] $ui_on3 [@@@@arg5]) 0 [@@@@arg3] [@@@@arg3] "" [uialign 0 0]]
            ]
            if [@@@arg1] [
                uispace $ui_padsmall 0 [
                    uitext (format "^f[%1]%2" $ui_radio_temp [@@@@arg1]) [@@@@arg9]
                ]
            ]
        ]
    ]
]

// uiarrowbutton 1:(SIZE-W) 2:(SIZE-H) 3:(ORIENTATION) 4:[on-release] 5:(ARROW-%-SIZE) 6:[children]
uiarrowbutton = [
    local arrowsize
    arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
    uitarget $ui_main1 $arg1 $arg2 [
        uicolour $ui_main1
        doargs [@@arg6]
        style_button
        uirelease [doargs [@@@arg4]]
        uiclamp* 1 1 1 1
        uidrawtriangle 0 $arrowsize $arrowsize [@arg3]
    ]
]

// uihtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..]
uihtab = [
    uifill $ui_default $arg1 $arg2 [
        uihlist 0 [
            uiclamp 1 1
            looplist3 v n c [@@@arg4] [
                uiskinborder 0 [@@@@arg2] $ui_main1 $ui_main2 $ui_main3 (? (=s $[@@@@arg3] $v) $ui_active $ui_line) $ui_hover $ui_hold [
                    if (!=s [@@@@@arg3] $v) [
                        uirelease [[@@@@@@@arg3] = $v]
                        doargs $c
                    ]
                    uispace $ui_padbutton $ui_padsmall [uitext (concatword (? (=s $[@@@@@@arg3] $v) "^fw" "^fa") $n)]
                ] [uiclamp 1 1]
            ]
            uiclamp* 1 1
        ]
        uialign- 0 1
    ]
]

// uivtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..]
uivtab = [
    uifill $ui_default $arg1 $arg2 [
        uivlist 0 [
            uiclamp 1 1
            looplist3 v n c [@@@arg4] [
                uiskinborder 0 [@@@@arg2] $ui_main1 $ui_main2 $ui_main3 (? (=s $[@@@@arg3] $v) $ui_active $ui_line) $ui_hover $ui_hold [
                    if (!=s [@@@@@arg3] $v) [
                        uirelease [[@@@@@@@arg3] = $v]
                        doargs $c
                    ]
                    uispace $ui_padbutton $ui_padsmall [uitext (concatword (? (=s $[@@@@@@arg3] $v) "^fw" "^fa") $n)]
                ] [uiclamp 1 1]
            ]
            uiclamp* 1 1
        ]
        uialign- 0 1
    ]
]

// uivscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uivscroll = [
    if $arg3 [] [arg3 = 1.0]
    if $arg4 [] [arg4 = 0.5]
    uivlist 0 [
        uiscrollarrow (-f 0 [@@arg3]) [uiarrowbutton [@@@arg1] [@@@arg1] 0 [] [@@@arg4]]
        uispace 0 $ui_padtiny
        uivscrollbar [
            uifill $ui_main1 [@@@arg1] (-f [@@@arg2] (*f [@@@arg1] 2)) [uicolour $ui_main1 ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolour $ui_main2 [@@@@arg1]
                style_button
                uiclamp* 1 1 1 1
                style_linesh 3 0.004 (*f [@@@@arg1] 0.5)
            ]
        ]
        uispace 0 $ui_padtiny
        uiscrollarrow [@@arg3] [uiarrowbutton [@@arg1] [@@arg1] 180 [] [@@arg4]]
    ]
]

// uihscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uihscroll = [
    if $arg3 [] [arg3 = 1.0]
    if $arg4 [] [arg4 = 0.5]
    uihlist 0 [
        uiscrollarrow (-f 0 [@@arg3]) [uiarrowbutton [@@@arg1] [@@@arg1] 90 [] [@@@arg4]]
        uispace $ui_padtiny
        uihscrollbar [
            uifill $ui_main1 [@@@arg1] (-f [@@@arg2] (*f [@@@arg1] 2)) [uicolour $ui_main1 ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolour $ui_main2 [@@@@arg1]
                style_button
                uiclamp* 1 1 1 1
                style_linesv 3 0.004 (*f [@@@@arg1] 0.5)
            ]
        ]
        uispace $ui_padtiny
        uiscrollarrow [@@arg3] [uiarrowbutton [@@arg1] [@@arg1] 270 [] [@@arg4]]
    ]
]

// uivertslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uivertslider = [
    if $arg9 [] [arg9 = 0.5]
    uivlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg5 $arg5 0 [] $arg9]
        uispace $ui_spacesize
        uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
            uifill $ui_main1 (-f $arg6 (*f $arg5 2)) $arg5 [uicolour $ui_main1 ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolour $ui_main2 $arg5
                style_button
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $ui_padbutton) 0 [arg7]
            ]
        ]
        uispace $ui_spacesize
        uisliderarrow 1 [uiarrowbutton $arg5 $arg5 180 [] $arg9]
    ]
]

// uihorzslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uihorzslider = [
    if $arg9 [] [arg9 = 0.5]
    uihlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg6 $arg6 90 [] $arg9]
        uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
            uifill $ui_main1 (-f $arg5 (*f $arg6 2)) $arg6 [uicolour $ui_main1 ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolour $ui_main2 0 $arg6
                style_button
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $ui_padbutton) 0 [arg7]
            ]
        ]
        uisliderarrow 1 [uiarrowbutton $arg6 $arg6 270 [] $arg9]
    ]
]

// uivcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change]
uivcolourslider = [
    uigroup [
        uivgradient $arg5 $arg4 (*f 0.85 $arg6) $arg7 [uioutline $ui_line ; uiclamp- 1 1 1 1]
        uifont "default" [uivlist -0.01 [
            loop i (strlen (abs $$arg1)) [
                uicolourtext (substr $$arg1 $i 1) (? (|| uihover? uihold?) $ui_vc1 $ui_vc2) 0.8
            ]
        ]]
        uivslider $arg1 $arg2 $arg3 1 $arg8 [
            uifill $ui_default $arg6 (*f 0.98 $arg7)
            uisliderbutton [uiline $ui_default $arg6 0]
        ]
    ]
]

// uihcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change]
uihcolourslider = [
    uigroup [
        uihgradient $arg4 $arg5 $arg6 (*f 0.85 $arg7) [uioutline $ui_line ; uiclamp- 1 1 1 1]
        uihslider $arg1 $arg2 $arg3 1 $arg8 [
            uiclamp- 1 1 1 1
            uifill $ui_default (*f 0.98 $arg6) $arg7
            uisliderbutton [uiline $ui_default 0 $arg7]
        ]
    ]
]

//uilistslider 1:VAR 2:COMPARISON 3:[values] 4:X 5:Y 6:[on-change] 7:(ARROW-%-SIZE) 8:[children]
uilistslider = [
    if $arg7 [] [arg7 = 0.5]
    uihlist 0 [
        uiarrowbutton $arg5 $arg5 90 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (< $n 0) [n = 0] [
                n = (- $n 2)
                if (< $n 0) [n = (+ $n (listlen $arg3))]
            ]
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
        uifill $ui_default (-f $arg4 (*f $arg5 2)) $arg5 [
            uicolour $ui_main2
            uiclamp* 1 1 1 1
            if (=s $arg8 "") [
                uitext (|| [[listassoc@arg2] $arg3 $$arg1] [result "custom"]) 0.7
            ] arg8
        ]
        uiarrowbutton $arg5 $arg5 270 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
    ]
]

//uiinput 1:VAR 2:LENGTH 3:[on-change] 4:SCALE 5:PARENT 6:PROMPT 7:[children] 8:ARGS 9:[immediate]
uiinput = [
    uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $ui_hover $ui_hold [
        doargs [@@arg8]
        uispace $ui_padbutton $ui_padsmall [
            uifield [@@@arg1] [@@@arg2] [@@@arg3] [@@@arg4] [@@@arg5] [@@@arg6] [@@@arg9] [@@@arg7]
        ]
    ]
]

uirgbsliders = [
    // arg1 is an alias that takes a hex colour value
    colr = (& (>> $$arg1 16) 0xFF)
    colg = (& (>> $$arg1 8) 0xFF)
    colb = (& $$arg1 0xFF)
    uivlist 0 [
        uiclamp 1 1
        loop i 3 [
            n = [col@(at "r g b" $i)]
            uihlist 0 [
                uihcolourslider $n 0 255 0x000000 (at [0xFF0000 0x00FF00 0x0000FF] $i) $ui_rgbw $ui_rgbh [[@@@@@arg1] = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)]
            ]
        ]
    ]
]

uihexpreview = [
    uiskinborder $ui_hexprev 0 $arg1 $arg1 $arg1 $ui_line $ui_line $ui_line [
        uispace $ui_padsmall 0 [
            uivlist $ui_padbutton [
                uitext (format "%1,%2,%3" (& (>> [@@@@arg1] 16) 0xFF) (& (>> [@@@@arg1] 8) 0xFF) (& [@@@@arg1] 0xFF))
                uitext (hexcolour [@@@@arg1])
            ]
        ]
    ]
]
