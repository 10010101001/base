ui_align_c = 0
ui_align_t = 0
ui_align_v = [ [0 0] [0 -1] [1 -1] [1 0] [1 1] [0 1] [-1 1] [-1 0] [-1 -1] ]

// uimenu 1:<name> 2:<title> 3:[children] 4:[on-show] 5:[on-hide] 6:[ui-children] 7:[tip]
uimenu = [
    newui $arg1 [
        ui_align_r = (at $ui_align_v $ui_align_c)
        uialign (at $ui_align_r 0) (at $ui_align_r 1)
        @arg6
        uivlist 0 [
            uiclamp 1 1 1 1
            uiskinborder 0 0.04 $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a [
                uiclamp 1 1
                uispace $uiwindow_padding $uismall_padding [
                    uiclamp 1 1
                    uitext [@@@@@arg2] 1.3
                    uihlist 0 [
                        uialign 1
                        uibuttonimg "" "textures/checkboxon" 0.03 0.03 [ 
                            ui_align_c = (+ $ui_align_c 1)
                            if (>= $ui_align_c (listlen $ui_align_v)) ui_align_c = 0
                        ] 0 [] 0 0 [
                            ui_align_c = (- $ui_align_c 1)
                            if (< $ui_align_c 0) ui_align_c = (- (listlen $ui_align_v) 1)
                        ] $c_default 0xFFFFFF00
                        uispace $uitiny_padding
                        uibuttonimg "" "textures/guiexit" 0.03 0.03 [ hideui [@@@@@@@arg1] ] 0 [] (&& (=s [@@@@@@arg1] "main") (! (isconnected))) 0 [] $c_default 0xFFFF0000
                    ]
                ]
            ]
            uiclamp- 1 1 1 1
            uiskinborder 0.1 0.1 $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a [
                uiclamp 1 1 1 1
                uispace $uiwindow_padding $uiwindow_padding [
                    uiclamp 1 1 1 1
                    @@@@arg3
                ]
            ]
            uiclamp- 1 1 1 1
            uiskinborder 0 0 $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a $c_menu_a [
                uiclamp 1 1 1 1
                uispace $uiwindow_padding $uismall_padding [
                    uiclamp 1 1 1 1
                    uiwraptext (format "TIP: ^fa%1" (? (>= [@@@@@numargs] 7) [@@@@@arg7] (showtip))) 0.9 0.8
                ]
            ]
            uiclamp- 1 1 1 1
        ]
        uiclamp- 1 1 1 1
    ] $arg4 $arg5
]

// uitoolmenu 1:<name> 2:<title> 3:[children] 4:[on-show] 5:[on-hide] 6:[ui-children] 7:[tip]
uitoolmenu = [
    newui $arg1 [
        ui_align_r = (at $ui_align_v $ui_align_t)
        uialign (at $ui_align_r 0) (at $ui_align_r 1)
        @arg6
        uivlist 0 [
            uiclamp 1 1 1 1
            uiskinborder 0 0.025 $c_menu $c_menu $c_menu $c_bline $c_bline $c_bline [
                uiclamp 1 1
                uispace $uiwindow_padding $uismall_padding [
                    uiclamp 1 1
                    uitext [@@@@@arg2] 1
                    uihlist 0 [
                        uialign 1
                        uibuttonimg "" "textures/checkboxon" 0.025 0.025 [ 
                            ui_align_t = (+ $ui_align_t 1)
                            if (>= $ui_align_t (listlen $ui_align_v)) ui_align_t = 0
                        ] 0 [] 0 0 [
                            ui_align_t = (- $ui_align_t 1)
                            if (< $ui_align_c 0) ui_align_t = (- (listlen $ui_align_v) 1)
                        ] $c_default 0xFFFFFF00
                        uispace $uitiny_padding
                        uibuttonimg "" "textures/guiexit" 0.025 0.025 [ hideui [@@@@@@@arg1] ] 0 [] 0 0 [] $c_default 0xFFFF0000
                    ]
                ]
            ]
            uiclamp- 1 1 1 1
            uiskinborder 0.1 0.1 $c_menu $c_menu $c_menu $c_bline $c_bline $c_bline [
                uiclamp 1 1 1 1
                uispace $uiwindow_padding $uiwindow_padding [
                    uiclamp 1 1 1 1
                    @@@@arg3
                ]
            ]
            uiclamp- 1 1 1 1
        ]
        uiclamp- 1 1 1 1
    ] $arg4 $arg5
]

// uiskinborder 1:<w> 2:<h> 3:<bgcolour> 4:<bghover> 5:<bghold> 6:<bordercolour> 7:<borderhover> 8:<borderhold> 9:<children>
uiskinborder = [
    uiskin "textures/guiskin" $arg3 1 $arg1 $arg2 [
        uihover [uisetcolour [@@@arg4]] [uisetcolour [@@@arg3]]
        uihold [uisetcolour [@@@arg5]]
        uiclamp* 1 1 1 1
        uiskin "textures/guiskinborder" [@@arg6] 1 [@@arg1] [@@arg2] [
            uihover [uisetcolour [@@@@arg7]] [uisetcolour [@@@@arg6]]
            uihold [uisetcolour [@@@@arg8]]
            uiclamp* 1 1 1 1
            doargs [@@@arg9]
        ]
    ]
]
// uitableskin 1:<bgcolour> 2:<bghover> 3:<bghold> 4:<bordercolour> 5:<borderhover> 6:<borderhold> 7:<children> 8:<row-children>
uitableskin = [
    uitablerow [
        doargs [@@arg7]
        uiskinborder 0.01 0.01 $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [doargs [@@@arg8]]
    ]
]

// uibar 1:<clampx> 2:<clampy>
uibar = [
    uiline $c_line
    uiclamp- $arg1 $arg1 $arg2 $arg2
]

// uidrawtriangle 1:<colour> 2:<w> 3:<h> 4:<orient>
uidrawtriangle = [
    if $arg1 [] [arg1 = $c_bline]
    uigroup [
        uitriangle [@@arg1] [@@arg2] [@@arg3] [@@arg4]
        uitriangleoutline [@@arg1] [@@arg2] [@@arg3] [@@arg4]
    ]
]

// uidrawcircle 1:<colour> 2:<size>
uidrawcircle = [
    if $arg1 [] [arg1 = $c_bline]
    uigroup [
        uicircle [@@arg1] [@@arg2]
        uicircleoutline [@@arg1] [@@arg2]
    ]
]

// uititle 1:[title children] 2:(SIZE-W) 3:(SIZE-H)
uititle = [
    uiskinborder $arg1 $arg2 $c_menu $c_menu $c_menu $c_line $c_line $c_line [
        doargs [@@arg1]
        uiclamp 1 1 1 1
    ]
]

// uibuttonarea 1:(BUTTON-WIDTH) 2:(BUTTON-HEIGHT) 3:[on-release] 4:(ALIGNMENT) 5:[children] 6:DISABLED 7:[padding] 8:[alt-release]
uibuttonarea = [
    uiskinborder $arg1 $arg2 (? $arg6 $c_maind $c_main1) (? $arg6 $c_maind $c_main2) (? $arg6 $c_maind $c_main3) (? $arg6 $c_disa $c_line) (? $arg6 $c_disa $c_hover) (? $arg6 $c_disa $c_hold) [
        uiclamp 1 1
        c_temp = $c_default
        if [@@arg6] [ c_temp = $c_disable ] [
            uihover [c_temp = $c_hover]
            uihold [c_temp = $c_hold]
            uirelease [doargs [@@@@arg3]]
            uialtrelease [doargs [@@@@arg8]]
        ]
        uiclamp* 1 1 1 1
        do [@@arg5]
    ]
]

// uibutton 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour]
uibutton = [
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        if [@@arg2] [ 
            uispace (? (>= [@@@numargs] 9) [@@@arg9] $uibutton_padding) 0 [
                uiclamp 1 1
                uihlist 0 [
                    uialign (? [@@@@@arg1] -1 0) 0
                    c_size = (*f [@@@@@arg4] 0.8)
                    uiimage [@@@@@arg2] (? (|| [@@@@@arg8] (< [@@@@@numargs] 12)) $c_temp [@@@@@arg12]) 0 $c_size $c_size
                ]
            ]
        ]
        if [@@arg1] [ 
            uispace (? (>= [@@@numargs] 9) [@@@arg9] $uibutton_padding) 0 [
                uiclamp 1 1
                uihlist 0 [
                    uialign [@@@@@arg6] 0
                    uitext (format "^f[%1]%2" (? (|| [@@@@@arg8] (< [@@@@@numargs] 11)) $c_temp [@@@@@arg11]) [@@@@@arg1])
                ]
            ]
        ]
        doargs [@@arg7]
    ] $arg8 $arg9 $arg10
]

// uibuttonimg 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour]
uibuttonimg = [
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        uispace (? (>= [@@numargs] 9) [@@arg9] $uibutton_padding) 0 [
            uiclamp 1 1
            uihlist 0 [
                uialign [@@@@@arg6] 0
                if [@@@@arg2] [ 
                    c_size = (*f [@@@@@arg4] 0.8)
                    uiimage [@@@@@arg2] (? (|| [@@@@@arg8] (< [@@@@@numargs] 12)) $c_temp [@@@@@arg12]) 0 $c_size $c_size
                ]
                if [@@@@arg1] [ 
                    if [@@@@@arg2] [ uispace (*f (? (>= [@@@@@@numargs] 9) [@@@@@@arg9] $uibutton_padding) 0.35) ]
                    uitext (format "^f[%1]%2" (? (|| [@@@@@arg8] (< [@@@@@numargs] 11)) $c_temp [@@@@@arg11]) [@@@@@arg1])
                ]
            ]
        ]
        doargs [@@arg7]
    ]  $arg8 $arg9 $arg10
]

// uicheckbox 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-ON2 7:0xcolour-BG 8:[children]
uicheckbox = [
    if $arg7 [] [arg7 = $c_bline]
    if $arg6 [] [arg6 = $c_on2]
    if $arg5 [] [arg5 = $c_on1]
    uitarget $c_default 0 0 [
        doargs [@@arg8]
        c_temp = [@@arg7]
        uihover [c_temp = $c_hover]
        uihold [c_temp = $c_hold]
        uirelease [doargs [@@@arg4]]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiimage "textures/checkbox" $c_temp 0 [@@@arg3] [@@@arg3] [
                uiclamp* 1 1 1 1
                c_retval = (arg2)
                if $c_retval [
                    uiimage "textures/checkboxon" (? (= $c_retval 1) [@@@@@arg5] [@@@@@arg6]) 0 [@@@@@arg3] [@@@@@arg3] [ uialign 0 0 ]
                ]
            ]
            uispace 0.0035
            uitext (format "^f[%1]%2" $c_temp [@@@arg1])
        ]
    ]
]

// uicheckbutton 1:<image> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children]
uicheckbutton = [
    if $arg6 [] [arg6 = $c_line]
    if $arg5 [] [arg5 = $c_on1]
    c_retval = (arg2)
    uiskinborder $arg3 $arg3 $c_main1 $c_main2 $c_main3 (? $c_retval $arg5 $c_line) $c_hover $c_hold [
        c_temp = (? $c_retval [@@arg5] [@@arg6])
        uihover [c_temp = $c_hover]
        uihold [c_temp = $c_hold]
        uirelease [doargs [@@@arg4]]
        doargs [@@arg7]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiclamp 1 1
            uialign 0 0
            c_size = (*f [@@@arg3] 0.9)
            uiimage [@@@arg1] $c_temp 0 $c_size $c_size [ uialign 0 0 ]
        ]
    ]
]

// uichecktext 1:<text> 2:[condition] 3:(W) 4:(H) 5:[on-release] 6:0xcolour-ON 7:0xcolour-BG 8:[children]
uichecktext = [
    if $arg7 [] [arg7 = $c_line]
    if $arg6 [] [arg6 = $c_on1]
    c_retval = (arg2)
    uiskinborder $arg3 $arg4 $c_main1 $c_main2 $c_main3 (? $c_retval $arg6 $c_line) $c_hover $c_hold [
        c_temp = (? $c_retval [@@arg6] [@@arg7])
        uihover [c_temp = $c_hover]
        uihold [c_temp = $c_hold]
        uirelease [doargs [@@@arg5]]
        doargs [@@arg8]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiclamp 1 1
            uialign 0 0
            uispace $uibutton_padding $uismall_padding [
                uiclamp 1 1
                uialign 0 0
                uitext (format "^f[%1]%2" $c_temp [@@@@arg1])
            ]
        ]
    ]
]

// uiradio 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children]
uiradio = [
    if $arg6 [] [arg6 = $c_bline]
    if $arg5 [] [arg5 = $c_on1]
    uitarget $c_default 0 0 [
        doargs [@@arg7]
        c_temp = [@@arg6]
        uihover [c_temp = $c_hover]
        uihold [c_temp = $c_hold]
        uirelease [doargs [@@@arg4]]
        uiclamp* 1 1 1 1
        uihlist 0 [
            uiimage "textures/radiobox" $c_temp 0 [@@@arg3] [@@@arg3] [
                uiclamp* 1 1 1 1
                if (arg2) [ uiimage "textures/radioboxon" [@@@@arg5] 0 [@@@@arg3] [@@@@arg3] [ uialign 0 0 ] ]
            ]
            uispace 0.0035
            uitext (format "^f[%1]%2" $c_temp [@@arg1])
        ]
    ]
]

// uiarrowbutton 1:(SIZE-W) 2:(SIZE-H) 3:(ORIENTATION) 4:[on-release] 5:(ARROW-%-SIZE) 6:[children]
uiarrowbutton = [
    local arrowsize
    arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
    uitarget $c_default $arg1 $arg2 [
        uicolour $c_menu
        doargs [@@arg6]
        style_button
        uirelease [doargs [@@@arg4]]
        uiclamp* 1 1 1 1
        uidrawtriangle 0 $arrowsize $arrowsize [@arg3]
    ]
]

// uihtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..]
uihtab = [
    uifill $c_default $arg1 $arg2 [
        uihlist 0 [
            uiclamp 1 1
            looplist3 v n c [@@@arg4] [
                uiskinborder 0 [@@@@arg2] $c_main1 $c_main2 $c_main3 (? (=s $[@@@@arg3] $v) $c_active $c_line) $c_hover $c_hold [
                    if (!=s [@@@@@arg3] $v) [
                        uirelease [[@@@@@@@arg3] = $v]
                        doargs $c
                    ]
                    uispace $uibutton_padding $uismall_padding [ uitext (concatword (? (=s $[@@@@@@arg3] $v) "^fw" "^fa") $n) ]
                ]
                uiclamp- 1 1
            ]
            uiclamp* 1 1
        ]
        uialign- 0 1
    ]
]

// uivtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..]
uivtab = [
    uifill $c_default $arg1 $arg2 [
        uivlist 0 [
            uiclamp 1 1
            looplist3 v n c [@@@arg4] [
                uiskinborder 0 [@@@@arg2] $c_main1 $c_main2 $c_main3 (? (=s $[@@@@arg3] $v) $c_active $c_line) $c_hover $c_hold [
                    if (!=s [@@@@@arg3] $v) [
                        uirelease [[@@@@@@@arg3] = $v]
                        doargs $c
                    ]
                    uispace $uibutton_padding $uismall_padding [ uitext (concatword (? (=s $[@@@@@@arg3] $v) "^fw" "^fa") $n) ]
                ]
                uiclamp- 1 1
            ]
            uiclamp* 1 1
        ]
        uialign- 0 1
    ]
]

// uivscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uivscroll = [
    if $arg4 [] [arg4 = 0.5]
    uivlist 0 [
        uiscrollarrow (-f 0 [@@arg3]) [uiarrowbutton [@@@arg1] [@@@arg1] 0 [] [@@@arg4]]
        uispace 0 $uitiny_padding
        uivscrollbar [
            uifill $c_default [@@@arg1] (-f [@@@arg2] (*f [@@@arg1] 2)) [uicolour $c_slider ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolour $c_menu [@@@@arg1]
                style_button
                uiclamp* 1 1 1 1
                style_linesh 3 0.004 (*f [@@@@arg1] 0.5)
            ]
        ]
        uispace 0 $uitiny_padding
        uiscrollarrow [@@arg3] [uiarrowbutton [@@arg1] [@@arg1] 180 [] [@@arg4]]
    ]
]

// uihscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uihscroll = [
    if $arg4 [] [arg4 = 0.5]
    uihlist 0 [
        uiscrollarrow (-f 0 [@@arg3]) [uiarrowbutton [@@@arg1] [@@@arg1] 90 [] [@@@arg4]]
        uispace $uitiny_padding
        uihscrollbar [
            uifill $c_default [@@@arg1] (-f [@@@arg2] (*f [@@@arg1] 2)) [uicolour $c_slider ; uiclamp- 1 1 1 1]
            uiscrollbutton [
                uicolour $c_menu [@@@@arg1]
                style_button
                uiclamp* 1 1 1 1
                style_linesv 3 0.004 (*f [@@@@arg1] 0.5)
            ]
        ]
        uispace $uitiny_padding
        uiscrollarrow [@@arg3] [uiarrowbutton [@@arg1] [@@arg1] 270 [] [@@arg4]]
    ]
]

// uivertslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uivertslider = [
    if $arg9 [] [arg9 = 0.5]
    uivlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg5 $arg5 0 [] $arg9]
        uispace 0.1
        uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
            uifill $c_default (-f $arg6 (*f $arg5 2)) $arg5 [uicolour $c_slider ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolour $c_menu $arg5
                style_button
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $uibutton_padding) 0 [arg7]
            ]
        ]
        uispace 0.1
        uisliderarrow 1 [uiarrowbutton $arg5 $arg5 180 [] $arg9]
    ]
]

// uihorzslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uihorzslider = [
    if $arg9 [] [arg9 = 0.5]
    uihlist 0 [
        uisliderarrow -1 [uiarrowbutton $arg6 $arg6 90 [] $arg9]
        uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
            uifill $c_default (-f $arg5 (*f $arg6 2)) $arg6 [uicolour $c_slider ; uiclamp- 1 1 1 1]
            uisliderbutton [
                uicolour $c_menu 0 $arg6
                style_button
                uiclamp* 1 1 1 1
                uispace (? (>= $numargs 10) $arg11 $uibutton_padding) 0 [arg7]
            ]
        ]
        uisliderarrow 1 [uiarrowbutton $arg6 $arg6 270 [] $arg9]
    ]
]

// uivcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change]
uivcolourslider = [
    uigroup [
        uivgradient $arg5 $arg4 (*f 0.85 $arg6) $arg7 [uioutline $c_line ; uiclamp- 1 1 1 1]
        uifont "default" [uivlist -0.01 [
            loop i (strlen (abs $$arg1)) [
                uicolourtext (substr $$arg1 $i 1) (? (|| uihover? uihold?) 0x80FFFFFF 0x40FFFFFF) 0.8
            ]
        ]]
        uivslider $arg1 $arg2 $arg3 1 $arg8 [
            uifill $c_default $arg6 (*f 0.98 $arg7)
            uisliderbutton [uiline $c_default $arg6 0]
        ]
    ]
]

// uihcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change]
uihcolourslider = [
    uigroup [
        uihgradient $arg4 $arg5 $arg6 (*f 0.85 $arg7) [uioutline $c_line ; uiclamp- 1 1 1 1]
        uihslider $arg1 $arg2 $arg3 1 $arg8 [
            uiclamp- 1 1 1 1
            uifill $c_default (*f 0.98 $arg6) $arg7
            uisliderbutton [uiline $c_default 0 $arg7]
        ]
    ]
]

//uilistslider 1:VAR 2:COMPARISON 3:[values] 4:X 5:Y 6:[on-change] 7:(ARROW-%-SIZE) 8:[children]
uilistslider = [
    if $arg7 [] [arg7 = 0.5]
    uihlist 0 [
        uiarrowbutton $arg5 $arg5 90 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (< $n 0) [n = 0] [
                n = (- $n 2)
                if (< $n 0) [n = (+ $n (listlen $arg3))]
            ]
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
        uifill $c_default (-f $arg4 (*f $arg5 2)) $arg5 [
            uicolour $c_slider
            uiclamp* 1 1 1 1
            if (=s $arg8 "") [
                uitext (|| [[listassoc@arg2] $arg3 $$arg1] [result "custom"]) 0.7
            ] arg8
        ]
        uiarrowbutton $arg5 $arg5 270 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
    ]
]

//uiinput 1:VAR 2:LENGTH 3:[on-change] 4:SCALE 5:PARENT 6:PROMPT 7:[children] 8:ARGS
uiinput = [
    uiskinborder 0 0 $c_main1 $c_main2 $c_main3 $c_line $c_hover $c_hold [
        doargs [@@arg8]
        uispace $uibutton_padding $uismall_padding [
            uifield [@@@arg1] [@@@arg2] [@@@arg3] [@@@arg4] [@@@arg5] [@@@arg6] [@@@arg7]
        ]
    ]
]

uirgbsliders = [
    // arg1 is an alias that takes a hex colour value
    colr = (& (>> $$arg1 16) 0xFF)
    colg = (& (>> $$arg1 8) 0xFF)
    colb = (& $$arg1 0xFF)
    uivlist 0 [
        uiclamp 1 1
        loop i 3 [
            n = [col@(at "r g b" $i)]
            uihlist 0 [
                uihcolourslider $n 0 255 0x000000 (at [0xFF0000 0x00FF00 0x0000FF] $i) 0.21 0.0315 [[@@@@@arg1] = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)]
            ]
        ]
    ]
]

uihexpreview = [
    uiskinborder 0.165 0 $arg1 $arg1 $arg1 $c_line $c_line $c_line [
        uispace $uismall_padding 0 [
            uivlist $uibutton_padding [
                uitext (format "%1,%2,%3" (& (>> [@@@@arg1] 16) 0xFF) (& (>> [@@@@arg1] 8) 0xFF) (& [@@@@arg1] 0xFF))
                uitext (hexcolour [@@@@arg1])
            ]
        ]
    ]
]
