ui_servers_opened = 0

uimenu "servers" "Server Browser" "textures/servers/list" [
    if (> (- (getmillis 1) $ui_servers_opened) $ui_transition) [updateservers]
    uivlist $ui_padbutton [
        uialign -1 -1
        ui_servers_num = (getserver)
        ui_servers_numplayers = (getserverplayers)
        uihlist $ui_padbutton [
            uiclamp 1 1 1 1
            uialign -1 0
            uihlist $ui_padbutton [
                uialign -1 0
                uibuttonz "Update List" [updatefrommaster] 0 $ui_ok [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Clear List" [clearservers] 0 $ui_cancel [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Disconnect" [disconnect] (= (isconnected) 0) $ui_change [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "LAN Connect" [lanconnect] 0 $ui_other [] $ui_buttonzh $ui_text $ui_padsmall
                uispace $ui_padtiny
                uicheckbox "Search LAN" (!= $searchlan 0) $ui_checksize [searchlan (! $searchlan)] $ui_on1 0 $ui_default [uialign -1]
                uicheckbox "Auto Sort" (!= $autosortservers 0) $ui_checksize [autosortservers (! $autosortservers)] $ui_on1 0 $ui_default [uialign -1]
                uibuttonz "Sort Now" [sortservers] 0 $ui_other2 [] $ui_buttonzh $ui_text $ui_padsmall
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uitextright (concatword "^fs^fc" $ui_servers_numplayers "^fS player"  (? (!= $ui_servers_numplayers 1) "s") " on ^fs^fc" $ui_servers_num "^fS server" (? (!= $ui_servers_num 1) "s"))
            ]
        ]
        uispace $ui_padtiny
        uihlist 0 [
            uiclamp 1 1 1 1
            uialign -1 -1
            uihlist $ui_padsmall [
                uiclamp 1 1 1 1
                uialign -1 -1
                uiscroll 1.35 0.5 [
                    uialign -1 -1
                    uivlist 0 [
                        uihlist 0 [uispace 0.5]
                        uialign -1 -1
                        loopservers ui_servers_cur [
                            ui_servers_stat = (getserver $ui_servers_cur 0 0)
                            ui_servers_name = (getserver $ui_servers_cur 0 1)
                            ui_servers_port = (getserver $ui_servers_cur 0 2)
                            ui_servers_npid = [@[ui_servers_name]:[@@ui_servers_port]]
                            ui_servers_lpid = [@[ui_servers_name]_@[ui_servers_port]]
                            ui_servers_desc = (getserver $ui_servers_cur 0 3)
                            ui_servers_mapn = (getserver $ui_servers_cur 0 4)
                            ui_servers_nump = (getserver $ui_servers_cur 0 5)
                            ui_servers_ping = (getserver $ui_servers_cur 0 6)
                            ui_servers_last = (getserver $ui_servers_cur 0 7)
                            ui_servers_handle = (getserver $ui_servers_cur 0 8)
                            ui_servers_flags = (getserver $ui_servers_cur 0 9)
                            ui_servers_bran = (getserver $ui_servers_cur 0 10)
                            ui_servers_prio = (getserver $ui_servers_cur 0 11)
                            ui_servers_statsflag = 0
                            loop fi (strlen $ui_servers_flags) [
                                if (=s (substring $ui_servers_flags $fi 1) "s") [ ui_servers_statsflag = 1 ]
                            ]
                            // attrs
                            ui_servers_attr = (getserver $ui_servers_cur 1 -1)
                            ui_servers_gver = (getserver $ui_servers_cur 1 0)
                            ui_servers_mode = (getserver $ui_servers_cur 1 1)
                            ui_servers_muts = (getserver $ui_servers_cur 1 2)
                            ui_servers_time = (getserver $ui_servers_cur 1 3)
                            ui_servers_maxp = (getserver $ui_servers_cur 1 4)
                            ui_servers_mstr = (getserver $ui_servers_cur 1 5)
                            ui_servers_vars = (getserver $ui_servers_cur 1 6)
                            ui_servers_mods = (getserver $ui_servers_cur 1 7)
                            ui_servers_verm = (getserver $ui_servers_cur 1 8)
                            ui_servers_vern = (getserver $ui_servers_cur 1 9)
                            ui_servers_verp = (getserver $ui_servers_cur 1 10)
                            ui_servers_vers = (getserver $ui_servers_cur 1 11)
                            ui_servers_vera = (getserver $ui_servers_cur 1 12)
                            ui_servers_gmst = (getserver $ui_servers_cur 1 13)
                            ui_servers_gmtl = (getserver $ui_servers_cur 1 14)
                            ui_servers_players = (getserver $ui_servers_cur 2)
                            ui_servers_offt = (? (>= $ui_servers_last 0) (div (max (- (getmillis 1) (- $ui_servers_last (div $ui_servers_ping 2))) 0) 1000) 0)
                            ui_servers_active = (? (< $ui_servers_ping $serverwaiting) 1 0)
                            [ui_tip_servers_@ui_servers_lpid] = [
                                if (> @ui_servers_players 0) [
                                    uiwraptext "Current Players" $ui_tipwrap $ui_texttip
                                    ui_servers_pname = ""
                                    loop j @@ui_servers_players [
                                        append ui_servers_pname (format ["%1"] (getserver @@@ui_servers_cur 2 $j) 1)
                                    ]
                                    uiwraptext (prettylist $ui_servers_pname) $ui_tipwrap $ui_texttipsmall
                                ] [uiwraptext "No Players" $ui_tipwrap $ui_texttip]
                            ]
                            ui_servers_img = "textures/emblem"
                            if (!=s $ui_servers_mapn "") [
                                if (=s $ui_servers_mapn "<random>") [
                                    ui_servers_img = "textures/question"
                                ] [
                                    if (< (strstr $ui_servers_mapn "/") 0) [
                                        ui_servers_img = (concatword "maps/" $ui_servers_mapn)
                                    ] [ui_servers_img = $ui_servers_mapn]
                                ]
                            ]
                            uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $ui_hover $ui_hold [
                                uirelease [connect $ui_servers_name $ui_servers_port]
                                uitooltip [servers_@ui_servers_lpid]
                                uialign -1 0
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uialign -1 0
                                    uihlist 0 [
                                        uialign -1 0
                                        uispace $ui_padsmall $ui_padsmall [
                                            uivlist 0 [
                                                uispace 0.03
                                                if $ui_servers_active [
                                                    uihlist 0 [
                                                        uitext $ui_servers_nump $ui_textlarge
                                                        uitext (concatword " ^fd/ ^fa" $ui_servers_maxp) $ui_text
                                                    ]
                                                    uitext (concatword (? (< $ui_servers_ping 200) "^fg" (? (< $ui_servers_ping 400) "^fy" "^fr")) $ui_servers_ping " ^fams") $ui_text
                                                    ui_servers_secs = (? (>= $ui_servers_last 0) (div (- (getmillis 1) $ui_servers_last) 1000) -1)
                                                    uitext (? (>= $ui_servers_secs 0) (concatword "^fa" $ui_servers_secs " ^fds ago") "^fdWaiting..") $ui_textsmall
                                                ] [uitext "?" $ui_textlarge]
                                            ]
                                        ]
                                        uispace $ui_padsmall $ui_padsmall [
                                            uiimage $ui_servers_img $ui_default 0 $ui_imglist $ui_imglist "textures/emblem" [uioutline $ui_line; uiclamp- 1 1 1 1]
                                        ]
                                        uispace $ui_padsmall $ui_padtiny [
                                            uialign -1 0
                                            uivlist 0 [
                                                uialign -1 0
                                                uihlist 0 [
                                                    uialign -1 0
                                                    uitextleft (concatword $ui_servers_desc " ^fd(^fa" $ui_servers_npid "^fd)") $ui_text
                                                    if (!=s $ui_servers_handle "") [uitextleft (concatword " ^fd[^fc" $ui_servers_handle "^fd]") $ui_textsmall]
                                                    if $ui_servers_statsflag [uitextleft "^fd[^fcstatistics^fd]" $ui_textsmall]
                                                    uitextleft (concatword "^fd[^fcpriority " $ui_servers_prio "^fd]") $ui_textsmall
                                                ]
                                                uispace $ui_padtiny
                                                uihlist 0 [
                                                    uialign -1 0
                                                    if $ui_servers_active [
                                                        if (= $ui_servers_gver (getversion 1)) [
                                                            uibackskin $ui_main1 $ui_line [
                                                                case $ui_servers_stat 0 [
                                                                    uiimage "textures/servers/default" $ui_default 0 $ui_iconsize $ui_iconsize "textures/question"
                                                                    uitextleft "^fgOpen" $ui_textsmall
                                                                ] 1 [
                                                                    uiimage "textures/servers/locked" $ui_default 0 $ui_iconsize $ui_iconsize "textures/question"
                                                                    uitextleft "^fyLocked" $ui_textsmall
                                                                ] 2 [
                                                                    uiimage "textures/servers/private" $ui_default 0 $ui_iconsize $ui_iconsize "textures/question"
                                                                    uitextleft "^fmPrivate" $ui_textsmall
                                                                ] 3 [
                                                                    uiimage "textures/servers/full" $ui_default 0 $ui_iconsize $ui_iconsize "textures/question"
                                                                    uitextleft "^frFull" $ui_textsmall
                                                                ] () [
                                                                    uiimage "textures/servers/unknown" $ui_default 0 $ui_iconsize $ui_iconsize "textures/question"
                                                                    uitextleft "^foUnknown" $ui_textsmall
                                                                ]
                                                            ]
                                                            if (!= $ui_servers_mode $modeidxediting) [
                                                                uitextleft (concatword " ^fd[^fc" (getgamestatestr $ui_servers_gmst 1) "^fd:^fw" (timestr (* (? $ui_servers_nump (max (- $ui_servers_gmtl $ui_servers_offt) 0) $ui_servers_time) 1000) 3) "^fd]") $ui_textsmall
                                                            ]
                                                            ui_servers_gname = (gamename $ui_servers_mode $ui_servers_muts 0 32)
                                                            uitextleft (concatword " ^fy" $ui_servers_gname " ^faon ^fo" $ui_servers_mapn) $ui_textsmall
                                                            uitextleft (concatword " ^fa(" (? $ui_servers_mods (concatword "^fc" (precf (*f (divf $ui_servers_mods $ui_servers_vars) 100) 2) "% ") "^fgun") "modified^fa)") $ui_textsmall
                                                            if (>= $ui_servers_attr 13) [uitextleft (concatword " ^fa[^fc" $ui_servers_verm "." $ui_servers_vern "." $ui_servers_verp "-" (platname $ui_servers_vers) $ui_servers_vera "-" $ui_servers_bran "^fa]") $ui_textsmall]
                                                        ] [
                                                            uibackskin $ui_main1 $ui_line [
                                                                uiimage "textures/servers/failed" $ui_default 0 $ui_iconsize $ui_iconsize "textures/question"
                                                                uitextleft "^foIncompatible" $ui_textsmall
                                                            ]
                                                            uitextleft (concat " ^fSserver is using" (? (> $ui_servers_gver (getversion 1)) "a ^fwnewer" "an ^fdolder") "protocol") $ui_textsmall
                                                        ]
                                                    ] [
                                                        uibackskin $ui_main1 $ui_line [
                                                            uiimage "textures/servers/failed" $ui_default 0 $ui_iconsize $ui_iconsize "textures/question"
                                                            uitextleft "^foUnresponsive" $ui_textsmall
                                                        ]
                                                        uitextleft " ^faServer is not replying to queries" $ui_textsmall
                                                    ]
                                                ]
                                                uispace $ui_padtiny
                                                uihlist 0 [
                                                    uialign -1 0
                                                    if (> $ui_servers_players 0) [
                                                        uitextleft "^fwWith: " $ui_text
                                                        ui_servers_pname = ""
                                                        ui_servers_pmore = 0
                                                        loop j $ui_servers_players [
                                                            if (|| $ui_servers_pmore (> (strlen (stripcolors $ui_servers_pname)) 128)) [ui_servers_pmore = (+ $ui_servers_pmore 1)] [
                                                                append ui_servers_pname (format ["%1"] (getserver $ui_servers_cur 2 $j) 1)
                                                            ]
                                                        ]
                                                        uiwraptext (concat (prettylist $ui_servers_pname) (? $ui_servers_pmore (concat "and^fy" $ui_servers_pmore "^fwmore"))) 0.8 $ui_textsmall -1 [uialign -1 0]
                                                    ] [uitextleft "^faNo current players."]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ] [
                            uialign -1 0
                            uiclamp 1 1 1 1
                            uispace $ui_padsmall $ui_padsmall [
                                uialign -1 0
                                uitextleft "There are no servers available to display."
                            ]
                        ]
                    ]
                ]
                uivscroll 0.02 0.5
                uialign- 1 -1
            ]
        ]
    ]
] [
    ui_servers_opened = (getmillis 1)
]