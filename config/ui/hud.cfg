defvarp showfps 0 0 3
defvarp showstats 0 0 2

ui_hud_states = [ALIVE DEAD EDIT SPEC WAIT]
ui_hud_texs = ["" deadtex editingtex spectatortex waitingtex]

newui "hud" [
    uiallowinput 0
    uiclamp 1 1 1 1
    ui_hud_focus = (getclientfocused)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_colour = (getteamcolour $ui_hud_team)
    ui_hud_colour1 = (| $ui_transor (modcolour $ui_hud_colour 0.5))
    ui_hud_colour2 = (| $ui_transor (modcolour $ui_hud_colour 0.1))
    uispace $ui_padwin $ui_padwin [
        uialign -1 1
        uihlist 0 [
            uialign -1 1
            uivlist 0 [
                uialign -1 1
                if (= $ui_hud_state 0) [
                    if (getclientshocking $ui_hud_focus) [
                        uisizeskin 0.05 0 $ui_hud_colour1 $ui_hud_colour2 [
                            uispace $ui_padtiny $ui_padtiny [
                                uilimitcolourtext "SHOCK" 1 (getclientrescolour $ui_hud_focus 3) $ui_textsmall
                            ]
                        ]
                        uispace 0 $ui_padtiny
                    ]
                    if (getclientbleeding $ui_hud_focus) [
                        uisizeskin 0.05 0 $ui_hud_colour1 $ui_hud_colour2 [
                            uispace $ui_padtiny $ui_padtiny [
                                uilimitcolourtext "BLEED" 1 (getclientrescolour $ui_hud_focus 4) $ui_textsmall
                            ]
                        ]
                        uispace 0 $ui_padtiny
                    ]
                    if (getclientburning $ui_hud_focus) [
                        uisizeskin 0.05 0 $ui_hud_colour1 $ui_hud_colour2 [
                            uispace $ui_padtiny $ui_padtiny [
                                uilimitcolourtext "BURN" 1 (getclientrescolour $ui_hud_focus 1) $ui_textsmall
                            ]
                        ]
                        uispace 0 $ui_padtiny
                    ]
                    if (getclientbuffing $ui_hud_focus) [
                        uielemskin $ui_hud_colour1 $ui_hud_colour2 [
                            uispace $ui_padtiny $ui_padtiny [
                                uilimitcolourtext "BUFF" 1 (getclientrescolour $ui_hud_focus 5) $ui_textsmall
                            ]
                        ]
                        uispace 0 $ui_padtiny
                    ]
                ]
                uisizeskin 0.05 (? $ui_hud_state 0.075 0.15) $ui_hud_colour1 $ui_hud_colour2 [
                    uialign -1 1
                    uivlist 0 [
                        uialign 0 1
                        ui_hud_health = (getclienthealth $ui_hud_focus)
                        ui_hud_spawnhealth = (getspawnhealth $ui_hud_actor)
                        ui_hud_healthpart = 1.0
                        uispace $ui_padtiny $ui_padtiny [
                            if $ui_hud_state [
                                uialign 0 0
                                uiimage $[@(at $ui_hud_texs $ui_hud_state)] $ui_default 0 0.05 0.05 [uialign 0 0]
                            ] [
                                uialign 0 1
                                ui_hud_healthclip = 0
                                ui_hud_healthpart = (divf $ui_hud_health $ui_hud_spawnhealth)
                                if (<f $ui_hud_healthpart 1.0) [
                                    ui_hud_healthclip = (*f 0.1 (clampf $ui_hud_healthpart 0.0 1.0))
                                ]
                                if (>f $ui_hud_healthpart 0) [
                                    uiclip 0 $ui_hud_healthclip 0 (-f 0.1 $ui_hud_healthclip) [
                                        uialign 0 1
                                        uiimagevgradient "textures/barskin" $colourgreen $colouryellow 0 0.05 0.1 "textures/blank" [
                                            uialign 0 1
                                            uiaddcolour $colourred
                                        ]
                                    ]
                                ] [uifill 0.05 0.1]
                            ]
                        ]
                        uispace $ui_padtiny $ui_padtiny [
                            uialign 0 1
                            if $ui_hud_state [
                                uilimitcolourtext (at $ui_hud_states $ui_hud_state) 1 $colourwhite $ui_text
                            ] [
                                caseif (>f $ui_hud_healthpart 1.0) [
                                    uilimitcolourtext $ui_hud_health 1 (getclientrescolour $ui_hud_focus 5) $ui_textlarge
                                ] (<=f $ui_hud_healthpart 0.25) [
                                    uilimitcolourtext $ui_hud_health 1 (getclientrescolour $ui_hud_focus 6) $ui_textlarge
                                ] () [uilimitcolourtext $ui_hud_health 1 $colourwhite $ui_textlarge]
                            ]
                        ]
                    ]
                ]
            ]
            uispace $ui_padtiny
            uihlist 0 [
                uialign -1 1
                case (gamemode) $modeidxcapture [
                    loopcapture ui_hud_flag [
                        ui_hud_flagteam = (getcaptureteam $ui_hud_flag)
                        ui_hud_flagname = (at $teamname $ui_hud_flagteam)
                        ui_hud_flagcol = $[team@[ui_hud_flagname]colour]
                        ui_hud_flagcol1 = (modcolour $ui_hud_flagcol 0.75)
                        ui_hud_flagcol2 = (modcolour $ui_hud_flagcol 0.5)
                        ui_hud_flagdrop = (getcapturedroptime $ui_hud_flag)
                        ui_hud_flagtake = (getcapturetaketime $ui_hud_flag)
                        ui_hud_flagowner = (getcaptureowner $ui_hud_flag)
                        ui_hud_flaglast = (getcapturelastowner $ui_hud_flag)
                        ui_hud_flagclip = 0.06
                        ui_hud_flagpart = 0.0
                        if $ui_hud_flagdrop [
                            ui_hud_flagoffs = (- (getmillis) $ui_hud_flagdrop)
                            ui_hud_flagpart = (-f 1 (divf $ui_hud_flagoffs (getcapturedelay)))
                            if (<f $ui_hud_flagpart 1.0) [ui_hud_flagclip = (*f 0.06 (clampf $ui_hud_flagpart 0.0 1.0))]
                        ]
                        uivlist 0 [
                            uifill 0.06 0.01 [
                                if (>f $ui_hud_flagpart 0) [
                                    uiclip $ui_hud_flagclip 0 0 0 [
                                        uialign -1 -1
                                        uiimagehgradient "<rotate:1>textures/barskin" $ui_hud_flagcol2 $ui_hud_flagcol1 0 0.06 0.01 "textures/blank" [
                                            uialign -1 -1
                                            uiaddcolour $ui_hud_flagcol
                                        ]
                                    ]
                                ]
                            ]
                            uispace 0 $ui_padtiny
                            uisizeskin 0.05 0.075 $ui_hud_colour1 (? (= $ui_hud_flagowner $ui_hud_focus) $ui_hud_colour $ui_hud_colour2) [
                                uivlist 0 [
                                    uialign 0 -1
                                    uiimage $flagtex $ui_hud_flagcol 0 0.05 0.05 [uialign 0 0]
                                    caseif (>= $ui_hud_flagowner 0) [
                                        caseif (= $ui_hud_flagowner $ui_hud_focus) [
                                            uilimitcolourtext "HOLDING" 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                                        ] (= (getclientteam $ui_hud_flagowner) $ui_hud_team) [
                                            uilimitcolourtext (getclientname $ui_hud_flagowner 1) 1 $colourwhite $ui_text 0 [uialign 0 1]
                                        ] () [
                                            uilimitcolourtext (getclientname $ui_hud_flagowner 1) 1 (getclientrescolour $ui_hud_focus 5) $ui_text 0 [uialign 0 1]
                                        ]
                                    ] (> $ui_hud_flagdrop 0) [
                                        uilimitcolourtext "DROPPED" 1 (getclientrescolour $ui_hud_focus 2) $ui_text 0 [uialign 0 1]
                                    ] () [uilimitcolourtext "SAFE" 1 $colourgreen $ui_text 0 [uialign 0 1]]
                                ]
                            ]
                        ]
                        uispace $ui_padtiny
                    ]
                ]
            ]
        ]
    ]
    uispace $ui_padwin $ui_padwin [
        uialign 1 1
        uihlist 0 [
            uialign 1 1
            uihlist 0 [
                uialign 1 1
                if (= $ui_hud_state 0) [
                    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
                    loopinventoryrev $ui_hud_focus ui_hud_weapon [
                        ui_hud_weapidx = (at $weapname $ui_hud_weapon)
                        ui_hud_weapstate = (getclientweapstate $ui_hud_focus $ui_hud_weapon)
                        ui_hud_weapload = (? (|| (= $ui_hud_weapstate 3) (= $ui_hud_weapstate 5)) (getclientweapload $ui_hud_focus $ui_hud_weapon) 0)
                        ui_hud_weapammo = (getclientweapammo $ui_hud_focus $ui_hud_weapon)
                        ui_hud_weapmax = $[@[ui_hud_weapidx]ammomax]
                        ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
                        ui_hud_weapcol1 = (modcolour $ui_hud_weapcol 0.75)
                        ui_hud_weapcol2 = (modcolour $ui_hud_weapcol 0.5)
                        ui_hud_weapclip = 0.0
                        ui_hud_weappart = 1.0
                        if (|| (<= $ui_hud_weapammo 0) $ui_hud_weapload) [
                            ui_hud_weapammo = (? $ui_hud_weapload (- $ui_hud_weapammo $ui_hud_weapload) 0)
                        ]
                        ui_hud_weappart = (divf $ui_hud_weapammo $ui_hud_weapmax)
                        if (<f $ui_hud_weappart 1.0) [ui_hud_weapclip = (*f 0.06 (clampf $ui_hud_weappart 0.0 1.0))]
                        uivlist 0 [
                            uifill 0.06 0.01 [
                                if (>f $ui_hud_weappart 0) [
                                    uiclip $ui_hud_weapclip 0 0 0 [
                                        uialign -1 -1
                                        uiimagehgradient "<rotate:1>textures/barskin" $ui_hud_weapcol2 $ui_hud_weapcol1 0 0.06 0.01 "textures/blank" [
                                            uialign -1 -1
                                            uiaddcolour $ui_hud_weapcol
                                        ]
                                    ]
                                ]
                            ]
                            uispace 0 $ui_padtiny
                            uisizeskin 0.05 0.075 $ui_hud_colour1 (? (= $ui_hud_weapselect $ui_hud_weapon) $ui_hud_colour $ui_hud_colour2) [
                                uivlist 0 [
                                    uialign 0 -1
                                    uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 0.05 0.05 [uialign 0 0]
                                    caseif (< $ui_hud_weapammo 0) [
                                        uilimitcolourtext "-" 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                                    ] (<=f $ui_hud_weappart 0.25) [
                                        uilimitcolourtext $ui_hud_weapammo 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                                    ] $ui_hud_weapload [
                                        uilimitcolourtext $ui_hud_weapammo 1 (getclientrescolour $ui_hud_focus 5) $ui_text 0 [uialign 0 1]
                                    ] () [uilimitcolourtext $ui_hud_weapammo 1 $colourwhite $ui_text 0 [uialign 0 1]]
                                ]
                            ]
                        ]
                        uispace $ui_padtiny
                    ]
                ]
            ]
            uivlist 0 [
                uialign 1 1
                if (= $ui_hud_state 0) [
                    if (getclientallowimpulse $ui_hud_focus) [
                        uisizeskin 0.05 0.15 $ui_hud_colour1 $ui_hud_colour2 [
                            uivlist 0 [
                                uialign 0 1
                                ui_hud_impulse = (getclientimpulse $ui_hud_focus)
                                ui_hud_impulseamt = 100
                                ui_hud_impulsepart = 1.0
                                ui_hud_impulseclip = 0
                                uispace $ui_padtiny $ui_padtiny [
                                    if (> $ui_hud_impulse 0) [
                                        ui_hud_impulsepart = (-f 1 (divf $ui_hud_impulse $impulsemeter))
                                        ui_hud_iprtm = (clampf $ui_hud_impulsepart 0.0 1.0)
                                        ui_hud_impulseclip = (*f 0.1 $ui_hud_iprtm)
                                        ui_hud_impulseamt = (ceil (*f $ui_hud_iprtm 100))
                                    ]
                                    if (>f $ui_hud_impulsepart 0) [
                                        uiclip 0 $ui_hud_impulseclip 0 (-f 0.1 $ui_hud_impulseclip) [
                                            uialign 0 1
                                            uiimagevgradient "textures/barskin" $colourpurple $colourcyan 0 0.05 0.1 "textures/blank" [
                                                uialign 0 1
                                                uiaddcolour $colourblue
                                            ]
                                        ]
                                    ] [uifill 0.05 0.1]
                                ]
                                uispace $ui_padtiny $ui_padtiny [
                                    uialign 0 1
                                    if (<=f $ui_hud_impulsepart 0.25) [
                                        uilimitcolourtext $ui_hud_impulseamt 1 (getclientrescolour $ui_hud_focus 6) $ui_textlarge
                                    ] [uilimitcolourtext $ui_hud_impulseamt 0 $colourwhite $ui_textlarge]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]