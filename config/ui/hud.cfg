ui_hud_states = [ALIVE DEAD EDIT SPEC WAIT]
ui_hud_texs = ["" deadtex editingtex spectatortex waitingtex]

newui "hud" [
    uiallowinput 0
    uiclamp 1 1 1 1
    ui_hud_focus = (getclientfocused)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_colour = (getteamcolour $ui_hud_team)
    ui_hud_colour1 = (| $ui_transor (modcolour $ui_hud_colour 0.5))
    ui_hud_colour2 = (| $ui_transor (modcolour $ui_hud_colour 0.1))
    uispace $ui_padwin $ui_padwin [
        uialign -1 1
        uivlist 0 [
            if (= $ui_hud_state 0) [
                if (getclientshocking $ui_hud_focus) [
                    uisizeskin 0.05 0 $ui_hud_colour1 $ui_hud_colour2 [
                        uispace $ui_padtiny $ui_padtiny [
                            uicolourtext "SHOCK" (getclientrescolour $ui_hud_focus 3) $ui_textsmall
                        ]
                    ]
                    uispace 0 $ui_padtiny
                ]
                if (getclientbleeding $ui_hud_focus) [
                    uisizeskin 0.05 0 $ui_hud_colour1 $ui_hud_colour2 [
                        uispace $ui_padtiny $ui_padtiny [
                            uicolourtext "BLEED" (getclientrescolour $ui_hud_focus 4) $ui_textsmall
                        ]
                    ]
                    uispace 0 $ui_padtiny
                ]
                if (getclientburning $ui_hud_focus) [
                    uisizeskin 0.05 0 $ui_hud_colour1 $ui_hud_colour2 [
                        uispace $ui_padtiny $ui_padtiny [
                            uicolourtext "BURN" (getclientrescolour $ui_hud_focus 1) $ui_textsmall
                        ]
                    ]
                    uispace 0 $ui_padtiny
                ]
                if (getclientbuffing $ui_hud_focus) [
                    uielemskin $ui_hud_colour1 $ui_hud_colour2 [
                        uispace $ui_padtiny $ui_padtiny [
                            uicolourtext "BUFF" (getclientrescolour $ui_hud_focus 5) $ui_textsmall
                        ]
                    ]
                    uispace 0 $ui_padtiny
                ]
            ]
            uisizeskin 0.05 (? $ui_hud_state 0.075 0.15) $ui_hud_colour1 $ui_hud_colour2 [
                uivlist 0 [
                    uialign 0 1
                    ui_hud_health = (getclienthealth $ui_hud_focus)
                    ui_hud_spawnhealth = (getspawnhealth $ui_hud_actor)
                    ui_hud_hpart = 1.0
                    uispace $ui_padtiny $ui_padtiny [
                        if $ui_hud_state [
                            uialign 0 0
                            uiimage $[@(at $ui_hud_texs $ui_hud_state)] $ui_default 0 0.05 0.05 [uialign 0 0]
                        ] [
                            uialign 0 1
                            ui_hud_hclip = 0
                            ui_hud_hpart = (divf $ui_hud_health $ui_hud_spawnhealth)
                            if (<f $ui_hud_hpart 1.0) [
                                ui_hud_hclip = (*f 0.1 (clampf $ui_hud_hpart 0.0 1.0))
                            ]
                            if (>f $ui_hud_hpart 0) [
                                uiclip 0 $ui_hud_hclip 0 (-f 0.1 $ui_hud_hclip) [
                                    uialign 0 1
                                    uiimagevgradient "textures/barskin" $colourgreen $colouryellow 0 0.05 0.1 "textures/blank" [
                                        uialign 0 1
                                        uiaddcolour $colourred
                                    ]
                                ]
                            ] [uifill 0.05 0.1]
                        ]
                    ]
                    uispace $ui_padtiny $ui_padtiny [
                        uialign 0 1
                        if $ui_hud_state [
                            uitext (at $ui_hud_states $ui_hud_state) $ui_text
                        ] [
                            caseif (>f $ui_hud_hpart 1.0) [
                                uicolourtext $ui_hud_health (getclientrescolour $ui_hud_focus 5) $ui_textlarge
                            ] (<=f $ui_hud_hpart 0.25) [
                                uicolourtext $ui_hud_health (getclientrescolour $ui_hud_focus 6) $ui_textlarge
                            ] () [uitext $ui_hud_health $ui_textlarge]
                        ]
                    ]
                ]
            ]
        ]
    ]
    uispace $ui_padwin $ui_padwin [
        uialign 1 1
        uihlist 0 [
            uialign 1 1
            uihlist 0 [
                uialign 1 1
                if (= $ui_hud_state 0) [
                    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
                    loopinventoryrev $ui_hud_focus ui_hud_weapon [
                        ui_hud_weapidx = (at $weapname $ui_hud_weapon)
                        ui_hud_weapstate = (getclientweapstate $ui_hud_focus $ui_hud_weapon)
                        ui_hud_weapload = (? (|| (= $ui_hud_weapstate 3) (= $ui_hud_weapstate 5)) (getclientweapload $ui_hud_focus $ui_hud_weapon) 0)
                        ui_hud_weapammo = (getclientweapammo $ui_hud_focus $ui_hud_weapon)
                        ui_hud_weapmax = $[@[ui_hud_weapidx]ammomax]
                        ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
                        ui_hud_weapcol1 = (modcolour $ui_hud_weapcol 0.6)
                        ui_hud_weapcol2 = (modcolour $ui_hud_weapcol 0.2)
                        ui_hud_wclip = 0.0
                        ui_hud_wpart = 1.0
                        if (|| (<= $ui_hud_weapammo 0) $ui_hud_weapload) [
                            ui_hud_weapammo = (? $ui_hud_weapload (- $ui_hud_weapammo $ui_hud_weapload) 0)
                        ]
                        ui_hud_wpart = (divf $ui_hud_weapammo $ui_hud_weapmax)
                        if (<f $ui_hud_wpart 1.0) [ui_hud_wclip = (*f 0.05 (clampf $ui_hud_wpart 0.0 1.0))]
                        uisizeskin 0.05 0.09 $ui_hud_colour1 (? (= $ui_hud_weapselect $ui_hud_weapon) $ui_hud_colour $ui_hud_colour2) [
                            uivlist 0 [
                                uialign 0 -1
                                uispace 0 $ui_padsmall
                                if (>f $ui_hud_wpart 0) [
                                    uiclip $ui_hud_wclip 0 0 0 [
                                        uialign -1 -1
                                        uiimagehgradient "<rotate:1>textures/barskin" $ui_hud_weapcol2 $ui_hud_weapcol1 0 0.05 0.01 "textures/blank" [
                                            uialign -1 -1
                                            uiaddcolour $ui_hud_weapcol
                                        ]
                                    ]
                                ] [uifill 0.05 0.01]
                                uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 0.05 0.05 [uialign 0 0]
                                caseif (< $ui_hud_weapammo 0) [
                                    uicolourtext "EMPTY" (getclientrescolour $ui_hud_focus 6) $ui_textsmall 0 [uialign 0 1]
                                ] (<=f $ui_hud_wpart 0.25) [
                                    uicolourtext $ui_hud_weapammo (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                                ] $ui_hud_weapload [
                                    uicolourtext $ui_hud_weapammo (getclientrescolour $ui_hud_focus 5) $ui_text 0 [uialign 0 1]
                                ] () [uitext $ui_hud_weapammo $ui_text 0 [uialign 0 1]]
                            ]
                        ]
                        uispace $ui_padtiny
                    ]
                ]
            ]
            uivlist 0 [
                uialign 1 1
                if (= $ui_hud_state 0) [
                    if (getclientallowimpulse $ui_hud_focus) [
                        uisizeskin 0.05 0.15 $ui_hud_colour1 $ui_hud_colour2 [
                            uivlist 0 [
                                uialign 0 1
                                ui_hud_impulse = (getclientimpulse $ui_hud_focus)
                                ui_hud_iamount = 100
                                ui_hud_ipart = 1.0
                                ui_hud_iclip = 0
                                uispace $ui_padtiny $ui_padtiny [
                                    if (> $ui_hud_impulse 0) [
                                        ui_hud_ipart = (-f 1 (divf $ui_hud_impulse $impulsemeter))
                                        ui_hud_iprtm = (clampf $ui_hud_ipart 0.0 1.0)
                                        ui_hud_iclip = (*f 0.1 $ui_hud_iprtm)
                                        ui_hud_iamount = (ceil (*f $ui_hud_iprtm 100))
                                    ]
                                    if (>f $ui_hud_ipart 0) [
                                        uiclip 0 $ui_hud_iclip 0 (-f 0.1 $ui_hud_iclip) [
                                            uialign 0 1
                                            uiimagevgradient "textures/barskin" $colourpurple $colourcyan 0 0.05 0.1 "textures/blank" [
                                                uialign 0 1
                                                uiaddcolour $colourblue
                                            ]
                                        ]
                                    ] [uifill 0.05 0.1]
                                ]
                                uispace $ui_padtiny $ui_padtiny [
                                    uialign 0 1
                                    if (<=f $ui_hud_ipart 0.25) [
                                        uicolourtext $ui_hud_iamount (getclientrescolour $ui_hud_focus 6) $ui_textlarge
                                    ] [uitext $ui_hud_iamount $ui_textlarge]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]