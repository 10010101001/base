ui_hud_states = [ALIVE DEAD EDIT SPEC WAIT]
ui_hud_texs = ["" deadtex editingtex spectatortex waitingtex]
newui "hud" [
    uiallowinput 0
    uiclamp 1 1 1 1
    ui_hud_focus = (getclientfocused)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_colour = (getteamcolour $ui_hud_team)
    ui_hud_colour1 = (| $ui_transor (modcolour $ui_hud_colour 0.5))
    ui_hud_colour2 = (| $ui_transor (modcolour $ui_hud_colour 0.1))
    uispace $ui_padwin $ui_padwin [
        uialign -1 1
        uivlist 0 [
            uialign -1 1
            if (&& (= $ui_hud_state 0) (getclientallowimpulse $ui_hud_focus)) [
                uisizeskin 0 0 $ui_hud_colour1 $ui_hud_colour2 [
                    uiclamp 1 1 1 1
                    uialign 0 1
                    uivlist 0 [
                        ui_hud_impulse = (getclientimpulse $ui_hud_focus)
                        ui_hud_iamount = 100
                        uispace $ui_padtiny $ui_padtiny [
                            uialign 0 0
                            uifill 0.05 0.105 [
                                uialign 0 1
                                ui_hud_iclip = 0
                                if (> $ui_hud_impulse 0) [
                                    ui_hud_ipart = (-f 1 (divf $ui_hud_impulse $impulsemeter))
                                    ui_hud_iclip = (*f 0.1 $ui_hud_ipart)
                                    ui_hud_iamount = (* (*f $ui_hud_ipart 100) 1)
                                ]
                                uiclip 0 $ui_hud_iclip 0 (-f 0.1 $ui_hud_iclip) [
                                    uialign 0 1
                                    uivgradient $colourpurple $colourcyan 0.05 0.1 [
                                        uialign 0 1
                                        uiaddcolour $colourblue
                                    ]
                                ]
                            ]
                        ]
                        uispace $ui_padtiny $ui_padtiny [
                            uialign 0 1
                            uitext $ui_hud_iamount $ui_textlarge
                        ]
                    ]
                ] [uialign 0 1; uiclamp 1 1 1 1]
                uispace 0 $ui_padtiny
            ]
            uisizeskin 0 0 $ui_hud_colour1 $ui_hud_colour2 [
                uiclamp 1 1 1 1
                uialign 0 1
                uivlist 0 [
                    uispace $ui_padtiny $ui_padtiny [
                        uialign 0 0
                        uifill (? $ui_hud_state 0.07 0.05) (? $ui_hud_state 0.05 0.105) [
                            if $ui_hud_state [
                                uialign 0 0
                                uiimage $[@(at $ui_hud_texs $ui_hud_state)] $ui_default 0 0.05 0.05 [uialign 0 0]
                            ] [
                                uialign 0 1
                                ui_hud_health = (getclienthealth $ui_hud_focus)
                                ui_hud_spawnhealth = (getspawnhealth $ui_hud_actor)
                                ui_hud_hclip = 0
                                if (< $ui_hud_health $ui_hud_spawnhealth) [
                                    ui_hud_hclip = (*f 0.1 (divf $ui_hud_health $ui_hud_spawnhealth))
                                ]
                                uiclip 0 $ui_hud_hclip 0 (-f 0.1 $ui_hud_hclip) [
                                    uialign 0 1
                                    uivgradient $colourgreen $colouryellow 0.05 0.1 [
                                        uialign 0 1
                                        uiaddcolour $colourred
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uispace $ui_padtiny $ui_padtiny [
                        uialign 0 1
                        if $ui_hud_state [
                            uitext (at $ui_hud_states $ui_hud_state) $ui_textlarge
                        ] [
                            uitext (getclienthealth $ui_hud_focus) $ui_textlarge
                        ]
                    ]
                ]
            ] [uialign 0 1; uiclamp 1 1 1 1]
        ]
    ]
]