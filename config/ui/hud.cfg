defvarp showfps 0 0 2
defvarp showstats 0 0 2
defvarp showvelocity 0 1 1
defvarp showtimestyle 0 3 4
defvarp showconsole 0 1 1

defvarp conintime 0 500 $varidxmax
defvarp conouttime 0 7500 $varidxmax
defvarp confadetime 0 1000 $varidxmax
deffvarp conblend 0 0.75 1

defvarp chatintime 0 500 $varidxmax
defvarp chatouttime 0 10000 $varidxmax
defvarp chatfadetime 0 1000 $varidxmax
deffvarp chatblend 0 0.75 1

defvarp showminimap 0 1 1
deffvarp minimapdist 0 300 $fvaridxmax
deffvarp minimapdistblend 0 500 $fvaridxmax
deffvarp minimapoffset 0 0.075 1
deffvarp minimapborder 0 0.1 1
deffvarp minimapblipsize 0 0.008 1
deffvarp minimapsmallblipsize 0 0.0055 1
defvarp minimapplayernames 0 1 1

defvarp showradar 0 1 1
deffvarp radardist 0 300 $fvaridxmax
deffvarp radardistblend 0 500 $fvaridxmax
deffvarp radaroffset 0 0.23 1
deffvarp radarblipsize 0 0.015 1
deffvarp radarsmallblipsize 0 0.01 1
defvarp radarplayernames 0 0 1
deffvarp radarsizew 0 0.5 2
deffvarp radarsizeh 0 0.5 2

ui_hud_sizew    = 0.075
ui_hud_sizeh    = 0.15
ui_hud_bar      = 0.14
ui_hud_barw     = 0.05
ui_hud_barh     = 0.1
ui_hud_bar2     = 0.025
ui_hud_top      = 0.03
ui_hud_mtrw     = 0.06
ui_hud_mtrh     = 0.03

ui_hud_states = [ALIVE DEAD EDIT SPEC WAIT]
ui_hud_texs = ["" deadtex editingtex spectatortex waitingtex]
ui_hud_connames = [Debug Event Game Chat]
ui_hud_concols = [red orange yellow green]

ui_hud_init = [
    refreshenginestats
    ui_hud_clientnum = (getplayercn)
    ui_hud_focus = (getclientfocused)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_carryaffinity = (getclientcarryaffinity $ui_hud_focus)
    ui_hud_colour = (? $ui_hud_carryaffinity (getclientrescolour $ui_hud_focus $pulseidxdisco (getteamcolour $ui_hud_team)) (getteamcolour $ui_hud_team))
    ui_hud_colour1 = (| $ui_transor (modcolour $ui_hud_colour 0.5))
    ui_hud_colour2 = (| $ui_transor (modcolour $ui_hud_colour 0.1))
    ui_hud_colour3 = (| $ui_transor (modcolour $ui_hud_colour 0.8))
    ui_hud_colourw = (| $ui_transor $colourwhite)
    ui_hud_commandmillis = (getcommandmillis)
    ui_hud_commandopen = (> $ui_hud_commandmillis 0)
    ui_hud_showhud = (&& (=s (getprogresstitle) "") (|| $ui_hud_commandopen $showhud))
]

ui_hud_showconsole = [
    uivlist 0 [
        uialign -1 -1
        if $ui_hud_commandopen [
            uihudskin 1 0 $ui_hud_colour1 $ui_hud_colour2 [
                uialign -1 -1
                uispace $ui_padsmall $ui_padsmall [
                    uiclamp 1 1
                    uialign -1 -1
                    uivlist 0 [
                        uiclamp 1 1
                        uialign -1 -1
                        loop i (? (isconnected) 3 4) [
                            if (getconlines $i) [
                                uihudskin 0 0 $ui_hud_colour2 $ui_hud_colour1 [
                                    uiclamp 1 1
                                    uialign -1 -1
                                    uispace $ui_padsmall $ui_padtiny [
                                        uiclamp 1 1
                                        uialign -1 -1
                                        uiwrapcolourtext (concat (at $ui_hud_connames $i) "Console") 0.95 $[colour@(at $ui_hud_concols $i)] $ui_textbig -1 [uialign -1 1]
                                    ]
                                ] [uiclamp 1 1]
                                uihudskin 0 0 $ui_hud_colour2 $ui_hud_colour1 [
                                    uiclamp 1 1
                                    uialign -1 -1
                                    uispace $ui_padnormal $ui_padsmall [
                                        uiclamp 1 1
                                        uialign -1 -1
                                        uivlist 0 [
                                            uialign -1 -1
                                            loopconlinesrev $i -8 0 ui_hud_consoleline [
                                                uiwrapcolourtext (getconlinecref $i $ui_hud_consoleline) 0.95 $colourwhite $ui_text -1 [uialign -1 1]
                                            ]
                                        ]
                                    ]
                                ] [uiclamp 1 1]
                            ]
                        ]
                        uispace 0 $ui_padsmall
                        uihudskin 0 0 $ui_hud_colour2 $colourred [
                            uiclamp 1 1
                            uialign -1 -1
                            uihlist 0 [
                                uialign -1 -1
                                ui_hud_commandicon = (getcommandicon)
                                if (=s $ui_hud_commandicon "") [ui_hud_commandicon = $inputtex]
                                uispace $ui_padsmall $ui_padsmall [uialign -1 -1; uiimage $ui_hud_commandicon (getclientrescolour $ui_hud_focus $pulseidxbuff (getcommandcolour)) 0 0.0225 0.0225]
                                uiwrappostext (getcommandbuf) 0.95 $ui_textlarge -1 (getcommandpos) [uialign -1 1]
                            ]
                        ] [uiclamp 1 1; uialign -1 -1]
                    ]
                ]
            ] [uialign -1 -1]
        ] [
            loop i (? (isconnected) 2 3) [
                local k
                k = (+ $i 1)
                loopconlinesrev $k -4 0 ui_hud_consoleline [
                    ui_hud_consoleblend = (ceil (*f 255 $conblend))
                    ui_hud_consoletime = (getconlinereftime $k $ui_hud_consoleline)
                    ui_hud_consolemillis = (- (getmillis 1) $ui_hud_consoletime)
                    ui_hud_consolegrowth = 1.0
                    caseif (> $ui_hud_consolemillis $conouttime) [
                        ui_hud_consolectime = (- $ui_hud_consolemillis $conouttime)
                        if (< $ui_hud_consolectime $confadetime) [
                            ui_hud_consolegrowth = (-f 1 (divf $ui_hud_consolectime $confadetime))
                            ui_hud_consoleblend = (clamp (*f $ui_hud_consolegrowth $conblend 255) 0 255)
                        ] [ui_hud_consoleblend = 0]
                    ] (< $ui_hud_consolemillis $conintime) [
                        ui_hud_consolegrowth = (divf $ui_hud_consolemillis $conintime)
                        ui_hud_consoleblend = (clamp (*f $ui_hud_consolegrowth $conblend 255) 0 255)
                    ]
                    if $ui_hud_consoleblend [
                        uiwrapcolourtext (getconlinecref $k $ui_hud_consoleline) 1 (| (<< $ui_hud_consoleblend 24) $colourwhite) $ui_text -1 [
                            if (<f $ui_hud_consolegrowth 1) [uisettextgrowth $ui_hud_consolegrowth]
                            uialign -1 1
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_chatconsole = [
    uispace $ui_padsmall $ui_padtiny [
        uialign -1 1
        uivlist 0 [
            uialign -1 1
            loopconlinesrev 3 (? $ui_hud_commandopen -4 -7) 0 ui_hud_consoleline [
                ui_hud_consoleblend = (ceil (*f 255 $chatblend))
                ui_hud_consoletime = (getconlinereftime 3 $ui_hud_consoleline)
                ui_hud_consolemillis = (- (getmillis 1) $ui_hud_consoletime)
                ui_hud_consolegrowth = 1.0
                caseif (> $ui_hud_consolemillis $chatouttime) [
                    ui_hud_consolectime = (- $ui_hud_consolemillis $chatouttime)
                    if (< $ui_hud_consolectime $chatfadetime) [
                        ui_hud_consolegrowth = (-f 1 (divf $ui_hud_consolectime $chatfadetime))
                        ui_hud_consoleblend = (clamp (*f $ui_hud_consolegrowth $chatblend 255) 0 255)
                    ] [ui_hud_consoleblend = 0]
                ] (< $ui_hud_consolemillis $chatintime) [
                    ui_hud_consolegrowth = (divf $ui_hud_consolemillis $chatintime)
                    ui_hud_consoleblend = (clamp (*f $ui_hud_consolegrowth $chatblend 255) 0 255)
                ]
                if $ui_hud_consoleblend [
                    uiwrapcolourtext (getconlinecref 3 $ui_hud_consoleline) 0.6 (| (<< $ui_hud_consoleblend 24) $colourwhite) $ui_text -1 [
                        if (<f $ui_hud_consolegrowth 1) [uisettextgrowth $ui_hud_consolegrowth]
                        uialign -1 1
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_residuals = [
    if (getclientshocking $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "SHOCK" 1 (getclientrescolour $ui_hud_focus $pulseidxshock) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
    if (getclientbleeding $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "BLEED" 1 (getclientrescolour $ui_hud_focus $pulseidxbleed) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
    if (getclientburning $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "BURN" 1 (getclientrescolour $ui_hud_focus $pulseidxburn) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
    if (getclientbuffing $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "BUFF" 1 (getclientrescolour $ui_hud_focus $pulseidxbuff) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
]

ui_hud_healthbar = [
    uihudskin $ui_hud_sizew (? $ui_hud_state $ui_hud_sizew $ui_hud_bar) $ui_hud_colour1 $ui_hud_colour2 [
        uivlist 0 [
            uialign 0 1
            if $ui_hud_state [
                uiimage $[@(at $ui_hud_texs $ui_hud_state)] $ui_default 0 $ui_hud_barw $ui_hud_barw [] [uialign 0 1]
                uilimitcolourtext (at $ui_hud_states $ui_hud_state) 1 $colourwhite $ui_textbig 0 [uialign 0 1]
            ] [
                ui_hud_health = (getclienthealth $ui_hud_focus)
                ui_hud_spawnhealth = (getspawnhealth $ui_hud_actor)
                ui_hud_regen = (getclientregen $ui_hud_focus)
                ui_hud_healthpart = 1.0
                ui_hud_healthclip = 0
                ui_hud_healthpart = (divf $ui_hud_health $ui_hud_spawnhealth)
                if (<f $ui_hud_healthpart 1.0) [
                    ui_hud_healthclip = (*f $ui_hud_barh (clampf $ui_hud_healthpart 0.0 1.0))
                ]
                if (>f $ui_hud_healthpart 0) [
                    uiclip 0 $ui_hud_healthclip 0 (-f $ui_hud_barh $ui_hud_healthclip) [
                        uialign 0 1
                        uiimagevgradient "<grey>textures/barskin" $colourgreen $colouryellow 0 $ui_hud_barw $ui_hud_barh [] [
                            uialign 0 1
                            uiaddcolour $colourred
                            if $ui_hud_regen [
                                ui_hud_regenlast = (- (getmillis) $ui_hud_regen)
                                if (<= $ui_hud_regenlast $regentime) [
                                    uirotategrad (divf $ui_hud_regenlast $regentime)
                                ]
                            ]
                        ]
                    ]
                ] [uifill $ui_hud_barw $ui_hud_barh]
                caseif (>f $ui_hud_healthpart 1.0) [
                    uilimitcolourtext $ui_hud_health 1 (getclientrescolour $ui_hud_focus $pulseidxbuff) $ui_textlarge 0 [uialign 0 1]
                ] (<=f $ui_hud_healthpart 0.25) [
                    uilimitcolourtext $ui_hud_health 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textlarge 0 [uialign 0 1]
                ] () [uilimitcolourtext $ui_hud_health 1 $colourwhite $ui_textlarge 0 [uialign 0 1]]
            ]
        ]
    ]
]

ui_hud_statebar = [
    ui_hud_playerstate = (getclientstate $ui_hud_clientnum)
    uivlist 0 [
        uialign 0 1
        uihudskin $ui_hud_sizew $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 [
            uiimage $[@(at $ui_hud_texs $ui_hud_playerstate)] $ui_default 0 $ui_hud_barw $ui_hud_barw [] [uialign 0 -1]
            uilimitcolourtext (at $ui_hud_states $ui_hud_playerstate) 1 $colourwhite $ui_textbig 0 [uialign 0 1]
        ]
    ]
    uispace 0 $ui_padtiny
]

ui_hud_leftbar = [
    if (= $ui_hud_state 0) [ui_hud_residuals]
    if (!= $ui_hud_focus $ui_hud_clientnum) [ui_hud_statebar]
    ui_hud_healthbar
]

ui_hud_captureinventory = [
    loopcapture 0 0 ui_hud_flag [
        ui_hud_flagteam = (getcaptureteam $ui_hud_flag)
        ui_hud_flagname = (at $teamnames $ui_hud_flagteam)
        ui_hud_flagcol = $[team@[ui_hud_flagname]colour]
        ui_hud_flagcol1 = (modcolour $ui_hud_flagcol 0.75)
        //ui_hud_flagcol2 = (modcolour $ui_hud_flagcol 0.5)
        ui_hud_flagdrop = (getcapturedroptime $ui_hud_flag)
        ui_hud_flagtake = (getcapturetaketime $ui_hud_flag)
        ui_hud_flagowner = (getcaptureowner $ui_hud_flag)
        //ui_hud_flaglast = (getcapturelastowner $ui_hud_flag)
        ui_hud_flagclip = $ui_hud_mtrw
        ui_hud_flagpart = 0.0
        if $ui_hud_flagdrop [
            ui_hud_flagoffs = (- (getmillis) $ui_hud_flagdrop)
            ui_hud_flagpart = (-f 1 (divf $ui_hud_flagoffs (getcapturedelay)))
            if (<f $ui_hud_flagpart 1.0) [ui_hud_flagclip = (*f $ui_hud_mtrw (clampf $ui_hud_flagpart 0.0 1.0))]
        ]
        uivlist 0 [
            uihudskin $ui_hud_sizew $ui_hud_top (? (= $ui_hud_flagowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                caseif (>= $ui_hud_flagowner 0) [
                   uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (getclientname $ui_hud_flagowner 2 0) 1 $colourwhite $ui_text 0 [uialign 0 0]]
                ] (>f $ui_hud_flagpart 0) [
                    uifill $ui_hud_mtrw $ui_hud_mtrh [
                        uialign 0 0
                        uiclip $ui_hud_flagclip 0 0 0 [
                            uialign -1 0
                            uiimagehgradient "<grey><rotate:1>textures/meterskin" $ui_hud_flagcol1 $ui_hud_flagcol 0 $ui_hud_mtrw $ui_hud_mtrh [] [
                                uialign -1 0
                                uiaddcolour $colourwhite
                            ]
                        ]
                    ]
                ] () [
                   uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (concatword "^f(" $[team@[ui_hud_flagname]tex] ")" $[team@[ui_hud_flagname]name]) 1 $ui_hud_flagcol $ui_textsmall 0 [uialign 0 0]]
                ]
            ]
            uispace 0 $ui_padtiny
            uihudskin $ui_hud_sizew $ui_hud_sizew (? (= $ui_hud_flagowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $flagtex $ui_hud_flagcol 0 $ui_hud_barw $ui_hud_barw [] [
                        uialign 0 0
                        caseif (>= $ui_hud_flagowner 0) [
                            ui_hud_flagownerteam = (getclientteam $ui_hud_flagowner)
                            ui_hud_flagownername = (at $teamnames $ui_hud_flagownerteam)
                            uiimage $flagtakentex $[team@[ui_hud_flagownername]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]
                        ] (> $ui_hud_flagdrop 0) [
                            uiimage $flagdroptex $colourcyan 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]
                        ] () [uiimage $pointtex $ui_hud_flagcol 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]]
                    ]
                    caseif (>= $ui_hud_flagowner 0) [
                        caseif (= $ui_hud_flagowner $ui_hud_focus) [
                            uilimitcolourtext "HOLDING" 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textbig 0 [uialign 0 1]
                        ] (= (getclientteam $ui_hud_flagowner) $ui_hud_team) [
                            uilimitcolourtext "RECLAIMED" 1 (getclientrescolour $ui_hud_focus $pulseidxbuff) $ui_textbig 0 [uialign 0 1]
                        ] () [
                            uilimitcolourtext "STOLEN" 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textbig 0 [uialign 0 1]
                        ]
                    ] (> $ui_hud_flagdrop 0) [
                        uilimitcolourtext "DROPPED" 1 (getclientrescolour $ui_hud_focus $pulseidxdisco) $ui_textbig 0 [uialign 0 1]
                    ] () [uilimitcolourtext "SAFE" 1 $colourgreen $ui_textbig 0 [uialign 0 1]]
                ]
            ]
        ]
        uispace $ui_padtiny
    ]
]

ui_hud_defendinventory = [
    loopdefend 0 0 ui_hud_point [
        ui_hud_pointowner = (getdefendowner $ui_hud_point)
        ui_hud_pointname1 = (at $teamnames $ui_hud_pointowner)
        ui_hud_pointenemy = (getdefendenemy $ui_hud_point)
        ui_hud_pointname2 = (at $teamnames $ui_hud_pointenemy)
        ui_hud_pointcol = $[team@[ui_hud_pointname1]colour]
        ui_hud_pointcol1 = (modcolour $ui_hud_pointcol 0.75)
        ui_hud_pointcol2 = (? (&& (= $ui_hud_pointowner $teamidxneutral) (!= $ui_hud_pointenemy $teamidxneutral)) $[team@[ui_hud_pointname2]colour] $[team@[ui_hud_pointname1]colour])
        ui_hud_pointcol3 = (modcolour $ui_hud_pointcol2 0.75)
        //ui_hud_pointcol2 = (modcolour $ui_hud_pointcol 0.5)
        //ui_hud_pointowners = (getdefendowners $ui_hud_point)
        //ui_hud_pointenemies = (getdefendenemies $ui_hud_point)
        //ui_hud_pointconverted = (getdefendconverted $ui_hud_point)
        ui_hud_pointoccupied = (getdefendoccupied $ui_hud_point)
        ui_hud_pointinside = (getdefendinside $ui_hud_point $ui_hud_focus 1)
        ui_hud_pointclip = $ui_hud_mtrw
        ui_hud_pointpart = 0.0
        if $ui_hud_pointenemy [
            ui_hud_pointpart = $ui_hud_pointoccupied
            if (<f $ui_hud_pointpart 1.0) [ui_hud_pointclip = (*f $ui_hud_mtrw (clampf $ui_hud_pointpart 0.0 1.0))]
        ]
        uivlist 0 [
            uihudskin $ui_hud_sizew $ui_hud_top (? $ui_hud_pointinside $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                caseif (&& (>f $ui_hud_pointpart 0) (<f $ui_hud_pointpart 1)) [
                    uifill $ui_hud_mtrw $ui_hud_mtrh [
                        uialign 0 0
                        uiclip $ui_hud_pointclip 0 0 0 [
                            uialign -1 0
                            uiimagehgradient "<grey><rotate:1>textures/meterskin" $ui_hud_pointcol3 $ui_hud_pointcol2 0 $ui_hud_mtrw $ui_hud_mtrh [] [
                                uialign -1 0
                                uiaddcolour $colourwhite
                            ]
                        ]
                    ]
                ] $ui_hud_pointowner [
                   uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (concatword "^f(" $[team@[ui_hud_pointname1]tex] ")" $[team@[ui_hud_pointname1]name]) 1 $ui_hud_pointcol $ui_textsmall 0 [uialign 0 0]]
                ] () [
                   uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (concatword "^f(" $pointtex ")" $[team@[ui_hud_pointname1]name]) 1 $ui_hud_pointcol $ui_textsmall 0 [uialign 0 0]]
                ]
            ]
            uispace 0 $ui_padtiny
            uihudskin $ui_hud_sizew $ui_hud_sizew (? $ui_hud_pointinside $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $pointtex $ui_hud_pointcol 0 $ui_hud_barw $ui_hud_barw [] [
                        uialign 0 0
                        caseif $ui_hud_pointenemy [
                            uiimage $attacktex $[team@[ui_hud_pointname2]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]
                        ] $ui_hud_pointowner [
                            uiimage $[team@[ui_hud_pointname1]tex] $[team@[ui_hud_pointname1]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]
                        ]
                    ]
                    caseif (|| $ui_hud_pointenemy $ui_hud_pointowner) [
                        caseif (&& $ui_hud_pointinside $ui_hud_pointenemy (= $ui_hud_pointenemy $ui_hud_team)) [
                            uilimitcolourtext "ATTACKING" 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textbig 0 [uialign 0 1]
                        ] (<f $ui_hud_pointoccupied 1) [
                            uilimitcolourtext "SECURING" 1 (? (= $ui_hud_pointenemy $ui_hud_team) $colourgreen $colouryellow) $ui_textbig 0 [uialign 0 1]
                        ] () [
                            uilimitcolourtext "SECURED" 1 (? (= $ui_hud_pointowner $ui_hud_team) $colourgreen $colourorange) $ui_textbig 0 [uialign 0 1]
                        ]
                    ] () [uilimitcolourtext "OPEN" 1 $colourcyan $ui_textbig 0 [uialign 0 1]]
                ]
            ]
        ]
        uispace $ui_padtiny
    ]
]

ui_hud_bomberinventory = [
    loopbomber 0 0 ui_hud_bomb [
        if (&& (getbomberenabled $ui_hud_bomb) (= (getbomberteam $ui_hud_bomb) $teamidxneutral)) [
            //ui_hud_bombteam = (getbomberteam $ui_hud_bomb)
            ui_hud_bombcol = (getclientrescolour $ui_hud_focus $pulseidxdisco)
            ui_hud_bombcol1 = (modcolour $ui_hud_bombcol 0.75)
            //ui_hud_bombcol2 = (modcolour $ui_hud_bombcol 0.5)
            ui_hud_bombdrop = (getbomberdroptime $ui_hud_bomb)
            ui_hud_bombtake = (getbombertaketime $ui_hud_bomb)
            ui_hud_bombowner = (getbomberowner $ui_hud_bomb)
            //ui_hud_bomblast = (getbomberlastowner $ui_hud_bomb)
            //ui_hud_bombtarget = (getbombertarget $ui_hud_bomb)
            ui_hud_bombclip = $ui_hud_mtrw
            ui_hud_bombpart = 0.0
            caseif $ui_hud_bombdrop [
                ui_hud_bomboffs = (- (getmillis) $ui_hud_bombdrop)
                ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bomberresetdelay))
                if (<f $ui_hud_bombpart 1.0) [ui_hud_bombclip = (*f $ui_hud_mtrw (clampf $ui_hud_bombpart 0.0 1.0))]
            ] $ui_hud_bombtake [
                ui_hud_bomboffs = (- (getmillis) $ui_hud_bombtake)
                ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bombercarrytime))
                if (<f $ui_hud_bombpart 1.0) [ui_hud_bombclip = (*f $ui_hud_mtrw (clampf $ui_hud_bombpart 0.0 1.0))]
            ]
            uivlist 0 [
                uihudskin $ui_hud_sizew $ui_hud_top (? (= $ui_hud_bombowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                    caseif (>f $ui_hud_bombpart 0) [
                        uifill $ui_hud_mtrw $ui_hud_mtrh [
                            uialign 0 0
                            uiclip $ui_hud_bombclip 0 0 0 [
                                uialign -1 0
                                uiimagehgradient "<grey><rotate:1>textures/meterskin" $ui_hud_bombcol1 $ui_hud_bombcol 0 $ui_hud_mtrw $ui_hud_mtrh [] [
                                    uialign -1 0
                                    uiaddcolour $colourwhite
                                ]
                            ]
                        ]
                    ] (>= $ui_hud_bombowner 0) [
                       uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (getclientname $ui_hud_bombowner 2 0) 1 $colourwhite $ui_text 0 [uialign 0 0]]
                    ] () [
                        uispace $ui_padsmall $ui_padsmall [uilimitcolourtext "Bomb" 1 (getclientrescolour $ui_hud_focus $pulseidxdisco) $ui_text 0 [uialign 0 0]]
                    ]
                ]
                uispace 0 $ui_padtiny
                uihudskin $ui_hud_sizew $ui_hud_sizew (? (= $ui_hud_bombowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                    uivlist 0 [
                        uialign 0 -1
                        uiimage $bombtex $ui_hud_bombcol 0 $ui_hud_barw $ui_hud_barw [] [
                            uialign 0 0
                            caseif (>= $ui_hud_bombowner 0) [
                                ui_hud_bombownerteam = (getclientteam $ui_hud_bombowner)
                                ui_hud_bombownername = (at $teamnames $ui_hud_bombownerteam)
                                uiimage $bombtakentex $[team@[ui_hud_bombownername]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]
                            ] (> $ui_hud_bombdrop 0) [
                                uiimage $bombdroptex $colourcyan 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]
                            ] () [uiimage $pointtex $ui_hud_bombcol 0 $ui_hud_bar2 $ui_hud_bar2 [] [uialign -1 1]]
                        ]
                        caseif (>= $ui_hud_bombowner 0) [
                            caseif (= $ui_hud_bombowner $ui_hud_focus) [
                                uilimitcolourtext "HOLDING" 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textbig 0 [uialign 0 1]
                            ] (= (getclientteam $ui_hud_bombowner) $ui_hud_team) [
                                uilimitcolourtext "CLAIMED" 1 (getclientrescolour $ui_hud_focus $pulseidxbuff) $ui_textbig 0 [uialign 0 1]
                            ] () [
                                uilimitcolourtext "STOLEN" 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textbig 0 [uialign 0 1]
                            ]
                        ] (> $ui_hud_bombdrop 0) [
                            uilimitcolourtext "DROPPED" 1 (getclientrescolour $ui_hud_focus $pulseidxdisco) $ui_textbig 0 [uialign 0 1]
                        ] () [uilimitcolourtext "READY" 1 $colourgreen $ui_textbig 0 [uialign 0 1]]
                    ]
                ]
            ]
            uispace $ui_padtiny
        ]
    ]
]

ui_hud_raceinventory = [
    if (|| (= $ui_hud_team $teamidxalpha) (! (& (mutators) $mutsbitgsp3))) [
        ui_hud_racetime = (getclientcptime $ui_hud_focus)
        ui_hud_racemillis = (getclientcpmillis $ui_hud_focus)
        ui_hud_racelast = (getclientcplast $ui_hud_focus)
        ui_hud_racepoints = (getclientpoints $ui_hud_focus)
        uivlist 0 [
            uihudskin $ui_hud_sizew $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext Laps 1 $colourwhite $ui_text 0 [uialign 0 0]]
            ]
            uihudskin $ui_hud_sizew $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext $ui_hud_racepoints 1 $colourwhite $ui_textlarge 0 [uialign 0 0]]
            ]
        ]
        uispace $ui_padtiny
        uivlist 0 [
            uihudskin $ui_hud_sizeh $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext Current 1 $colourwhite $ui_text 0 [uialign 0 0]]
            ]
            uihudskin $ui_hud_sizeh $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (timestr $ui_hud_racemillis $scoreracestyle) 1 $colourwhite $ui_textlarge 0 [uialign 0 0]]
            ]
        ]
        uispace $ui_padtiny
        uivlist 0 [
            uihudskin $ui_hud_sizeh $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext Best 1 $colourwhite $ui_text 0 [uialign 0 0]]
            ]
            uihudskin $ui_hud_sizeh $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (timestr $ui_hud_racetime $scoreracestyle) 1 $colourwhite $ui_textlarge 0 [uialign 0 0]]
            ]
        ]
        uispace $ui_padtiny
        uivlist 0 [
            uihudskin $ui_hud_sizeh $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext Last 1 $colourwhite $ui_text 0 [uialign 0 0]]
            ]
            uihudskin $ui_hud_sizeh $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (timestr $ui_hud_racelast $scoreracestyle) 1 $colourwhite $ui_textlarge 0 [uialign 0 0]]
            ]
        ]
        uispace $ui_padtiny
    ]
]

ui_hud_leftinventory = [
    uivlist 0 [
        uialign -1 1
        if $showconsole [ui_hud_chatconsole]
        uispace 0 $ui_padtiny
        uihlist 0 [
            uialign -1 1
            case (gamemode) $modeidxcapture [
                ui_hud_captureinventory
            ] $modeidxdefend [
                ui_hud_defendinventory
            ] $modeidxbomber [
                ui_hud_bomberinventory
            ] $modeidxrace [
                if (= $ui_hud_state 0) [ui_hud_raceinventory]
            ]
        ]
    ]
]

ui_hud_showfps = [
    uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                doif (>= $showfps 1) [
                    uilimitcolourtext (concat (getenginestat 12) "fps") 1 $colourwhite $ui_text
                ] (>= $showfps 2) [
                    uilimitcolourtext (concatword "+" (getenginestat 13) "-" (getenginestat 14)) 1 $colourwhite $ui_textsmall
                ]
            ]
        ]
    ]
    uispace 0 $ui_padtiny
]

ui_hud_velocity = [
    uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                uilimitcolourtext "SPEED" 1 $colourgreen $ui_text
                uilimitcolourtext (getenginestat 22) 1 $colourwhite $ui_textlarge
            ]
        ]
    ]
    uispace 0 $ui_padtiny
]

ui_hud_impulsebar = [
    if (getclientallowimpulse $ui_hud_focus) [
        uihudskin $ui_hud_sizew $ui_hud_bar $ui_hud_colour1 $ui_hud_colour2 [
            uivlist 0 [
                uialign 0 1
                ui_hud_impulse = (getclientimpulsemeter $ui_hud_focus)
                ui_hud_impulseregen = (getclientimpulseregen $ui_hud_focus)
                ui_hud_impulseamt = 100
                ui_hud_impulsepart = 1.0
                ui_hud_impulseclip = 0
                if (> $ui_hud_impulse 0) [
                    ui_hud_impulsepart = (-f 1 (divf $ui_hud_impulse $impulsemeter))
                    ui_hud_iprtm = (clampf $ui_hud_impulsepart 0.0 1.0)
                    ui_hud_impulseclip = (*f $ui_hud_barh $ui_hud_iprtm)
                    ui_hud_impulseamt = (ceil (*f $ui_hud_iprtm 100))
                ]
                if (>f $ui_hud_impulsepart 0) [
                    uiclip 0 $ui_hud_impulseclip 0 (-f $ui_hud_barh $ui_hud_impulseclip) [
                        uialign 0 1
                        uiimagevgradient "<grey><rotate:4>textures/barskin" $colourpurple $colourcyan 0 $ui_hud_barw $ui_hud_barh [] [
                            uialign 0 1
                            uiaddcolour $colourblue
                            if $ui_hud_impulseregen [
                                uirotategrad (divf $ui_hud_impulseregen 1000)
                            ]
                        ]
                    ]
                ] [uifill $ui_hud_barw $ui_hud_barh]
                if (<=f $ui_hud_impulsepart 0.25) [
                    uilimitcolourtext $ui_hud_impulseamt 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textlarge 0 [uialign 0 1]
                ] [uilimitcolourtext $ui_hud_impulseamt 0 $colourwhite $ui_textlarge 0 [uialign 0 1]]
            ]
        ]
    ]
]

ui_hud_rightbar = [
    if $showfps [ui_hud_showfps]
    if (= $ui_hud_state 0) [
        if $showvelocity [ui_hud_velocity]
        ui_hud_impulsebar
    ]
]

ui_hud_weaponinventory = [
    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
    loopinventoryrev $ui_hud_focus 0 0 ui_hud_weapon [
        ui_hud_weapidx = (at $weapname $ui_hud_weapon)
        ui_hud_weapstate = (getclientweapstate $ui_hud_focus $ui_hud_weapon)
        ui_hud_weapload = (? (|| (= $ui_hud_weapstate 3) (= $ui_hud_weapstate 5)) (getclientweapload $ui_hud_focus $ui_hud_weapon) 0)
        ui_hud_weapammo = (getclientweapammo $ui_hud_focus $ui_hud_weapon)
        ui_hud_weapmax = $[@[ui_hud_weapidx]ammomax]
        ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
        ui_hud_weapcol1 = (modcolour $ui_hud_weapcol 0.75)
        ui_hud_weapcol2 = (modcolour $ui_hud_weapcol 0.5)
        ui_hud_weapclip = 0.0
        ui_hud_weappart = 1.0
        if (|| (<= $ui_hud_weapammo 0) $ui_hud_weapload) [
            ui_hud_weapammo = (? $ui_hud_weapload (- $ui_hud_weapammo $ui_hud_weapload) 0)
        ]
        ui_hud_weappart = (divf $ui_hud_weapammo $ui_hud_weapmax)
        if (<f $ui_hud_weappart 1.0) [ui_hud_weapclip = (*f $ui_hud_mtrw (clampf $ui_hud_weappart 0.0 1.0))]
        uivlist 0 [
            uihudskin $ui_hud_sizew $ui_hud_top (? (= $ui_hud_weapselect $ui_hud_weapon) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                if (>f $ui_hud_weappart 0) [
                    uifill $ui_hud_mtrw $ui_hud_mtrh [
                        uialign 0 0
                        uiclip $ui_hud_weapclip $ui_hud_mtrh 0 0 [
                            uialign -1 0
                            uiimagehgradient "<grey><rotate:1>textures/meterskin" $ui_hud_weapcol2 $ui_hud_weapcol1 0 $ui_hud_mtrw $ui_hud_mtrh [] [
                                uialign -1 0
                                uiaddcolour $ui_hud_weapcol
                            ]
                        ]
                    ]
                ]
            ]
            uispace 0 $ui_padtiny
            uihudskin $ui_hud_sizew $ui_hud_sizew (? (= $ui_hud_weapselect $ui_hud_weapon) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 $ui_hud_barw $ui_hud_barw [] [uialign 0 0]
                    caseif (< $ui_hud_weapammo 0) [
                        uilimitcolourtext "-" 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textbig 0 [uialign 0 1]
                    ] (<=f $ui_hud_weappart 0.25) [
                        uilimitcolourtext $ui_hud_weapammo 1 (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_textbig 0 [uialign 0 1]
                    ] $ui_hud_weapload [
                        uilimitcolourtext $ui_hud_weapammo 1 (getclientrescolour $ui_hud_focus $pulseidxbuff) $ui_textbig 0 [uialign 0 1]
                    ] () [uilimitcolourtext $ui_hud_weapammo 1 $colourwhite $ui_textbig 0 [uialign 0 1]]
                ]
            ]
        ]
        uispace $ui_padtiny
    ]
]

ui_hud_rightinventory = [
    if (= $ui_hud_state 0) [ui_hud_weaponinventory]
]

ui_hud_timecols = [red red red red red green yellow orange orange]

ui_hud_timestate = [
    uihudskin $ui_hud_sizew $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 [
        uispace $ui_padsmall $ui_padsmall [
            uivlist 0 [
                caseif $gamepaused [
                    uilimitcolourtext Paused 1 $colourorange $ui_textlarge 0 [uialign 0 0]
                ] (= (gamemode) $modeidxediting) [
                    uilimitcolourtext Editing 1 $colourgreen $ui_textlarge 0 [uialign 0 0]
                ] () [
                    ui_hud_timeremain = (getgametimeremain) 
                    ui_hud_timecolour = (at $ui_hud_timecols (getgamestate))
                    if (getgametimelimit) [
                        uilimitcolourtext (getgamestatestr 1) 1 $[colour@[ui_hud_timecolour]] $ui_text 0 [uialign 0 0]
                        uilimitcolourtext (timestr $ui_hud_timeremain $showtimestyle) 1 (? (|| (!= (getgamestate) 5) (< $ui_hud_timeremain 60000)) (getclientrescolour $ui_hud_focus $pulseidxwarn) $colourwhite) $ui_textlarge 0 [uialign 0 0]
                    ] [
                        uilimitcolourtext (getgamestatestr 1) 1 $[colour@[ui_hud_timecolour]] $ui_textlarge 0 [uialign 0 0]
                    ]
                ]
            ]
        ]
    ] [uialign 1 -1]
]

// 1:dist 2:offset 3:border 4:w 5:h 6:blip 7:smallblip 8:playernames 9:distblend
ui_hud_radar = [
    uiradar $arg1 $arg2 $arg3 $arg4 $arg5 [
        loopclients 0 0 ui_hud_radarclient [
            if (!= $ui_hud_radarclient $ui_hud_focus) [
                ui_hud_radarclientradar = (getclientradaryaw $ui_hud_radarclient)
                if (!=s $ui_hud_radarclientradar "") [
                    ui_hud_radarclientvel = (getclientvelocity $ui_hud_radarclient)
                    ui_hud_radarclientdist = (getclientradardist $ui_hud_radarclient)
                    ui_hud_radarclientdominated = (getclientisdominated $ui_hud_radarclient $ui_hud_focus)
                    ui_hud_radarclientpcolour = (getclientcolour $ui_hud_radarclient -1 2)
                    if $ui_hud_radarclientdominated [
                        ui_hud_radarclientcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                        uiradarblip $playerbliptex (| 0x80000000 $ui_hud_radarclientpcolour) $ui_hud_radarclientradar $ui_hud_radarclientdist $arg6 $arg6 [
                            uiimage $dominatedtex $ui_hud_radarclientcolour 0 $arg6 $arg6
                            if $arg8 [uivlist 0 [uispace 0 $arg7; uicolourtext (getclientname $ui_hud_radarclient 0 0) $ui_hud_radarclientcolour $ui_textradar]]
                        ]
                    ] [
                        ui_hud_radarclientblend = (clamp (*f (divf $ui_hud_radarclientvel $movespeed) 255) 0 255)
                        if (>f $arg9 0) [
                            if (>f $ui_hud_radarclientdist $arg9) [
                                ui_hud_radarclientblend = 0
                            ] [
                                ui_hud_radarclientblend = (div (+ $ui_hud_radarclientblend (- 255 (clamp (*f (divf $ui_hud_radarclientdist $arg9) 255) 0 255))) 2)
                            ]
                        ]
                        if $ui_hud_radarclientblend [
                            ui_hud_radarclientteam = (getclientteam $ui_hud_radarclient)
                            ui_hud_radarclienttcolour = $[team@(at $teamnames $ui_hud_radarclientteam)colour]
                            doif (getclientburning $ui_hud_radarclient) [
                                ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxburn)
                            ] (getclientbleeding $ui_hud_radarclient) [
                                ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxbleed)
                            ] (getclientshocking $ui_hud_radarclient) [
                                ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxshock)
                            ] (getclientbuffing $ui_hud_radarclient) [
                                ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxbuff)
                            ]
                            uiradarblip $playerbliptex (| (<< (div $ui_hud_radarclientblend 2) 24) $ui_hud_radarclientpcolour) $ui_hud_radarclientradar $ui_hud_radarclientdist $arg6 $arg6 [
                                uiimage $playerbliptex (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour) 0 $arg7 $arg7
                                if $arg8 [uivlist 0 [uispace 0 $arg7; uicolourtext (getclientname $ui_hud_radarclient 0 0) (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour) $ui_textradar]]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        case (gamemode) $modeidxcapture [
            loopcapture 0 0 ui_hud_radarcapture [
                ui_hud_radarcaptureradar = (getcaptureradaryaw $ui_hud_radarcapture)
                if (!=s $ui_hud_radarcaptureradar "") [
                    ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture)
                    ui_hud_radarcaptureteam = (getcaptureteam $ui_hud_radarcapture)
                    ui_hud_radarcapturetcolour = $[team@(at $teamnames $ui_hud_radarcaptureteam)colour]
                    ui_hud_radarcaptureowner = (getcaptureowner $ui_hud_radarcapture)
                    ui_hud_radarcapturedrop = (getcapturedroptime $ui_hud_radarcapture)
                    ui_hud_radarcapturetex = ""
                    doif (&& ui_hud_carryaffinity (= $ui_hud_radarcaptureteam $ui_hud_team)) [
                        if (|| (< $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureowner $ui_hud_focus)) [
                            ui_hud_radarcaptureradar = (getcaptureradaryaw $ui_hud_radarcapture 1)
                            ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture 1)
                        ]
                        ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                        ui_hud_radarcapturetex = $arrowtex
                    ] (|| $ui_hud_radarcapturedrop (&& (>= $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureteam $ui_hud_team) (!= $ui_hud_radarcaptureowner $ui_hud_focus))) [
                        ui_hud_radarcapturetex = $flagtex
                        ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                    ] (!= $ui_hud_radarcaptureowner $ui_hud_focus) [
                        ui_hud_radarcapturetex = $flagtex
                    ]
                    if (!=s $ui_hud_radarcapturetex "") [
                        uiradarblip $ui_hud_radarcapturetex $ui_hud_radarcapturetcolour $ui_hud_radarcaptureradar $ui_hud_radarcapturedist (*f $arg6 2) (*f $arg6 2) [
                            uivlist 0 [uispace 0 (*f $arg7 2); uicolourtext $[team@(at $teamnames $ui_hud_radarcaptureteam)name] $ui_hud_radarcapturetcolour $ui_textradar]
                        ]
                    ]
                ]
            ]
        ] $modeidxdefend [
            loopdefend 0 0 ui_hud_radardefend [
                ui_hud_radardefendradar = (getdefendradaryaw $ui_hud_radardefend)
                if (!=s $ui_hud_radardefendradar "") [
                    ui_hud_radardefenddist = (getdefendradardist $ui_hud_radardefend)
                    ui_hud_radardefendteam = (getdefendowner $ui_hud_radardefend)
                    ui_hud_radardefendtcolour = $[team@(at $teamnames $ui_hud_radardefendteam)colour]
                    uiradarblip $pointtex $ui_hud_radardefendtcolour $ui_hud_radardefendradar $ui_hud_radardefenddist (*f $arg6 2) (*f $arg6 2) [
                        uivlist 0 [uispace 0 (*f $arg7 2); uicolourtext $[team@(at $teamnames $ui_hud_radardefendteam)name] $ui_hud_radardefendtcolour $ui_textradar]
                    ]
                ]
            ]
        ] $modeidxbomber [
            loopbomber 0 0 ui_hud_radarbomber [
                if (getbomberenabled $ui_hud_radarbomber) [
                    ui_hud_radarbomberradar = (getbomberradaryaw $ui_hud_radarbomber)
                    if (!=s $ui_hud_radarbomberradar "") [
                        ui_hud_radarbomberteam = (getbomberteam $ui_hud_radarbomber)
                        ui_hud_radarbomberowner = (getbomberowner $ui_hud_radarbomber)
                        ui_hud_radarbombertex = ""
                        ui_hud_radarbombername = ""
                        doif (!= $ui_hud_radarbomberteam $teamidxneutral) [
                            if (&& ui_hud_carryaffinity (!= $ui_hud_radarbomberteam $ui_hud_team)) [
                                ui_hud_radarbombertex = $arrowtex
                                ui_hud_radarbombername = "Destroy"
                            ]
                        ] (&& (= $ui_hud_radarbomberteam $teamidxneutral) (!= $ui_hud_radarbomberowner $ui_hud_focus)) [
                            ui_hud_radarbombertex = $bombtex
                            ui_hud_radarbombername = "Bomb"
                        ]
                        if (!=s $ui_hud_radarbombertex "") [
                            ui_hud_radarbomberdist = (getbomberradardist $ui_hud_radarbomber)
                            uiradarblip $ui_hud_radarbombertex (getclientrescolour $ui_hud_focus $pulseidxdisco) $ui_hud_radarbomberradar $ui_hud_radarbomberdist (*f $arg6 2) (*f $arg6 2) [
                                if (!=s $ui_hud_radarbombername "") [uivlist 0 [uispace 0 (*f $arg7 2); uicolourtext $ui_hud_radarbombername (getclientrescolour $ui_hud_focus $pulseidxdisco) $ui_textradar]]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_minimap = [
    uispace $ui_padtiny
    uihudskin $ui_hud_sizeh $ui_hud_sizeh $ui_hud_colour1 $ui_hud_colour2 [
        uispace $ui_padsmall $ui_padsmall [
            uiminimap "<grey>textures/hud/radar" $ui_hud_colour $minimapdist 0.1 $ui_hud_sizeh $ui_hud_sizeh [
                ui_hud_radar $minimapdist $minimapoffset $minimapborder $ui_hud_sizeh $ui_hud_sizeh $minimapblipsize $minimapsmallblipsize $minimapplayernames $minimapdistblend
            ]
        ]
    ] [uialign 1 -1]
]

ui_hud_midradar = [
    if (= $ui_hud_state 0) [
        ui_hud_radar $radardist $radaroffset 0 $radarsizew $radarsizeh $radarblipsize $radarsmallblipsize $radarplayernames $radardistblend
    ]
]

ui_hud_righttop = [
    ui_hud_timestate
    if $showminimap [ui_hud_minimap]
]

ui_hud_lefttop = [
    if $showconsole [ui_hud_showconsole]
]

newui "hud" [
    ui_hud_init
    uiallowinput 0
    uiclamp 1 1 1 1
    if $ui_hud_showhud [
        uiexclusive $ui_hud_commandopen
        if (isconnected) [
            uispace $ui_padwin $ui_padwin [
                uialign 1 -1
                uihlist 0 [
                    uialign 1 -1
                    uihlist 0 [
                        uialign 1 -1
                        ui_hud_righttop
                    ]
                ]
            ]
            uispace $ui_padwin $ui_padwin [
                uialign -1 1
                uihlist 0 [
                    uialign -1 1
                    uivlist 0 [
                        uialign -1 1
                        ui_hud_leftbar
                    ]
                    uispace $ui_padtiny
                    uihlist 0 [
                        uialign -1 1
                        ui_hud_leftinventory
                    ]
                ]
            ]
            uispace $ui_padwin $ui_padwin [
                uialign 1 1
                uihlist 0 [
                    uialign 1 1
                    uihlist 0 [
                        uialign 1 1
                        ui_hud_rightinventory
                    ]
                    uivlist 0 [
                        uialign 1 1
                        ui_hud_rightbar
                    ]
                ]
            ]
            uispace $ui_padtiny $ui_padtiny [
                ui_hud_midradar
            ]
        ]
        uispace $ui_padwin $ui_padwin [
            uialign -1 -1
            uihlist 0 [
                uialign -1 -1
                uihlist 0 [
                    uialign -1 -1
                    ui_hud_lefttop
                ]
            ]
        ]
    ] [uiexclusive 0]
] [] [] 16
