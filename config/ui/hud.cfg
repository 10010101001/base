defvarp showfps 0 0 2
defvarp showstats 0 0 2
defvarp showvelocity 0 1 1

ui_hud_states = [ALIVE DEAD EDIT SPEC WAIT]
ui_hud_texs = ["" deadtex editingtex spectatortex waitingtex]

ui_hud_sizew    = 0.075
ui_hud_sizeh    = 0.15
ui_hud_bar      = 0.14
ui_hud_barw     = 0.05
ui_hud_barh     = 0.1
ui_hud_top      = 0.03
ui_hud_mtrw     = 0.06
ui_hud_mtrh     = 0.03

ui_hud_init = [
    refreshenginestats
    ui_hud_focus = (getclientfocused)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_colour = (getteamcolour $ui_hud_team)
    ui_hud_colour1 = (| $ui_transor (modcolour $ui_hud_colour 0.5))
    ui_hud_colour2 = (| $ui_transor (modcolour $ui_hud_colour 0.1))
    ui_hud_colour3 = (| $ui_transor (modcolour $ui_hud_colour 0.8))
]

ui_hud_residuals = [
    if (getclientshocking $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "SHOCK" 1 (getclientrescolour $ui_hud_focus 3) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
    if (getclientbleeding $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "BLEED" 1 (getclientrescolour $ui_hud_focus 4) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
    if (getclientburning $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "BURN" 1 (getclientrescolour $ui_hud_focus 1) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
    if (getclientbuffing $ui_hud_focus) [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
            uispace $ui_padtiny $ui_padtiny [
                uilimitcolourtext "BUFF" 1 (getclientrescolour $ui_hud_focus 5) $ui_textsmall
            ]
        ]
        uispace 0 $ui_padtiny
    ]
]

ui_hud_healthbar = [
    uihudskin $ui_hud_sizew (? $ui_hud_state $ui_hud_sizew $ui_hud_bar) $ui_hud_colour1 $ui_hud_colour2 [
        uivlist 0 [
            if $ui_hud_state [
                uialign 0 -1
                uiimage $[@(at $ui_hud_texs $ui_hud_state)] $ui_default 0 $ui_hud_barw $ui_hud_barw [uialign 0 0]
                uilimitcolourtext (at $ui_hud_states $ui_hud_state) 1 $colourwhite $ui_text 0 [uialign 0 1]
            ] [
                uialign 0 0
                ui_hud_health = (getclienthealth $ui_hud_focus)
                ui_hud_spawnhealth = (getspawnhealth $ui_hud_actor)
                ui_hud_healthpart = 1.0
                ui_hud_healthclip = 0
                ui_hud_healthpart = (divf $ui_hud_health $ui_hud_spawnhealth)
                if (<f $ui_hud_healthpart 1.0) [
                    ui_hud_healthclip = (*f $ui_hud_barh (clampf $ui_hud_healthpart 0.0 1.0))
                ]
                if (>f $ui_hud_healthpart 0) [
                    uiclip 0 $ui_hud_healthclip 0 (-f $ui_hud_barh $ui_hud_healthclip) [
                        uialign 0 1
                        uiimagevgradient "textures/barskin" $colourgreen $colouryellow 0 $ui_hud_barw $ui_hud_barh "textures/blank" [
                            uialign 0 1
                            uiaddcolour $colourred
                        ]
                    ]
                ] [uifill $ui_hud_barw $ui_hud_barh]
                caseif (>f $ui_hud_healthpart 1.0) [
                    uilimitcolourtext $ui_hud_health 1 (getclientrescolour $ui_hud_focus 5) $ui_textlarge 0 [uialign 0 1]
                ] (<=f $ui_hud_healthpart 0.25) [
                    uilimitcolourtext $ui_hud_health 1 (getclientrescolour $ui_hud_focus 6) $ui_textlarge 0 [uialign 0 1]
                ] () [uilimitcolourtext $ui_hud_health 1 $colourwhite $ui_textlarge 0 [uialign 0 1]]
            ]
        ]
    ]
]

ui_hud_leftbar = [
    if (= $ui_hud_state 0) [ui_hud_residuals]
    ui_hud_healthbar
]

ui_hud_captureinventory = [
    loopcapture ui_hud_flag [
        ui_hud_flagteam = (getcaptureteam $ui_hud_flag)
        ui_hud_flagname = (at $teamname $ui_hud_flagteam)
        ui_hud_flagcol = $[team@[ui_hud_flagname]colour]
        ui_hud_flagcol1 = (modcolour $ui_hud_flagcol 0.75)
        //ui_hud_flagcol2 = (modcolour $ui_hud_flagcol 0.5)
        ui_hud_flagdrop = (getcapturedroptime $ui_hud_flag)
        ui_hud_flagtake = (getcapturetaketime $ui_hud_flag)
        ui_hud_flagowner = (getcaptureowner $ui_hud_flag)
        //ui_hud_flaglast = (getcapturelastowner $ui_hud_flag)
        ui_hud_flagclip = $ui_hud_mtrw
        ui_hud_flagpart = 0.0
        if $ui_hud_flagdrop [
            ui_hud_flagoffs = (- (getmillis) $ui_hud_flagdrop)
            ui_hud_flagpart = (-f 1 (divf $ui_hud_flagoffs (getcapturedelay)))
            if (<f $ui_hud_flagpart 1.0) [ui_hud_flagclip = (*f $ui_hud_mtrw (clampf $ui_hud_flagpart 0.0 1.0))]
        ]
        uivlist 0 [
            uihudskin $ui_hud_sizew $ui_hud_top (? (= $ui_hud_flagowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                caseif (>= $ui_hud_flagowner 0) [
                   uispace $ui_padsmall $ui_padsmall [uilimitcolourtext (getclientname $ui_hud_flagowner 1) 1 $colourwhite $ui_text 0 [uialign 0 0]]
                ] (>f $ui_hud_flagpart 0) [
                    uifill $ui_hud_mtrw $ui_hud_mtrh [
                        uialign 0 0
                        uiclip $ui_hud_flagclip 0 0 0 [
                            uialign -1 0
                            uiimagehgradient "<rotate:1>textures/meterskin" $ui_hud_flagcol1 $ui_hud_flagcol 0 $ui_hud_mtrw $ui_hud_mtrh "textures/blank" [
                                uialign -1 0
                                uiaddcolour $colourwhite
                            ]
                        ]
                    ]
                ] () [
                   uispace $ui_padsmall $ui_padsmall [uilimitcolourtext $[team@[ui_hud_flagname]name] 1 $ui_hud_flagcol $ui_text 0 [uialign 0 0]]
                ]
            ]
            uispace 0 $ui_padtiny
            uihudskin $ui_hud_sizew $ui_hud_sizew (? (= $ui_hud_flagowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $flagtex $ui_hud_flagcol 0 $ui_hud_barw $ui_hud_barw [uialign 0 0]
                    caseif (>= $ui_hud_flagowner 0) [
                        caseif (= $ui_hud_flagowner $ui_hud_focus) [
                            uilimitcolourtext "HOLDING" 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                        ] (= (getclientteam $ui_hud_flagowner) $ui_hud_team) [
                            uilimitcolourtext "RECLAIMED" 1 (getclientrescolour $ui_hud_focus 5) $ui_text 0 [uialign 0 1]
                        ] () [
                            uilimitcolourtext "STOLEN" 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                        ]
                    ] (> $ui_hud_flagdrop 0) [
                        uilimitcolourtext "DROPPED" 1 (getclientrescolour $ui_hud_focus 2) $ui_text 0 [uialign 0 1]
                    ] () [uilimitcolourtext "SAFE" 1 $colourgreen $ui_text 0 [uialign 0 1]]
                ]
            ]
        ]
        uispace $ui_padtiny
    ]
]

ui_hud_bomberinventory = [
    loopbomber 0 ui_hud_bomb [
        //ui_hud_bombteam = (getbomberteam $ui_hud_bomb)
        ui_hud_bombcol = (getclientrescolour $ui_hud_focus 2)
        ui_hud_bombcol1 = (modcolour $ui_hud_bombcol 0.75)
        //ui_hud_bombcol2 = (modcolour $ui_hud_bombcol 0.5)
        ui_hud_bombdrop = (getbomberdroptime $ui_hud_bomb)
        ui_hud_bombtake = (getbombertaketime $ui_hud_bomb)
        ui_hud_bombowner = (getbomberowner $ui_hud_bomb)
        //ui_hud_bomblast = (getbomberlastowner $ui_hud_bomb)
        //ui_hud_bombtarget = (getbombertarget $ui_hud_bomb)
        ui_hud_bombclip = $ui_hud_mtrw
        ui_hud_bombpart = 0.0
        caseif $ui_hud_bombdrop [
            ui_hud_bomboffs = (- (getmillis) $ui_hud_bombdrop)
            ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bomberresetdelay))
            if (<f $ui_hud_bombpart 1.0) [ui_hud_bombclip = (*f $ui_hud_mtrw (clampf $ui_hud_bombpart 0.0 1.0))]
        ] $ui_hud_bombtake [
            ui_hud_bomboffs = (- (getmillis) $ui_hud_bombtake)
            ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bombercarrytime))
            if (<f $ui_hud_bombpart 1.0) [ui_hud_bombclip = (*f $ui_hud_mtrw (clampf $ui_hud_bombpart 0.0 1.0))]
        ]
        uivlist 0 [
            uihudskin $ui_hud_sizew $ui_hud_top (? (= $ui_hud_bombowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                uispace $ui_padsmall $ui_padsmall [
                    caseif (>= $ui_hud_bombowner 0) [
                       uilimitcolourtext (getclientname $ui_hud_bombowner 1) 1 $colourwhite $ui_text 0 [uialign 0 0]
                    ] (>f $ui_hud_bombpart 0) [
                        uifill $ui_hud_mtrw $ui_hud_mtrh [
                            uialign 0 0
                            uiclip $ui_hud_bombclip 0 0 0 [
                                uialign -1 0
                                uiimagehgradient "<rotate:1>textures/meterskin" $ui_hud_bombcol1 $ui_hud_bombcol 0 $ui_hud_mtrw $ui_hud_mtrh "textures/blank" [
                                    uialign -1 0
                                    uiaddcolour $colourwhite
                                ]
                            ]
                        ]
                    ] () [
                        uilimitcolourtext "Bomb" 1 (getclientrescolour $ui_hud_focus 2) $ui_text 0 [uialign 0 0]
                    ]
                ]
            ]
            uispace 0 $ui_padtiny
            uihudskin $ui_hud_sizew $ui_hud_sizew $ui_hud_colour1 (? (= $ui_hud_bombowner $ui_hud_focus) $ui_hud_colour $ui_hud_colour2) [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $bombtex $ui_hud_bombcol 0 $ui_hud_barw $ui_hud_barw [uialign 0 0]
                    caseif (>= $ui_hud_bombowner 0) [
                        caseif (= $ui_hud_bombowner $ui_hud_focus) [
                            uilimitcolourtext "HOLDING" 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                        ] (= (getclientteam $ui_hud_bombowner) $ui_hud_team) [
                            uilimitcolourtext "CLAIMED" 1 (getclientrescolour $ui_hud_focus 5) $ui_text 0 [uialign 0 1]
                        ] () [
                            uilimitcolourtext "STOLEN" 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                        ]
                    ] (> $ui_hud_bombdrop 0) [
                        uilimitcolourtext "DROPPED" 1 (getclientrescolour $ui_hud_focus 2) $ui_text 0 [uialign 0 1]
                    ] () [uilimitcolourtext "READY" 1 $colourgreen $ui_text 0 [uialign 0 1]]
                ]
            ]
        ]
        uispace $ui_padtiny
    ]
]

ui_hud_leftinventory = [
    case (gamemode) $modeidxcapture [ui_hud_captureinventory] $modeidxbomber [ui_hud_bomberinventory]
]

ui_hud_showfps = [
    uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                doif (>= $showfps 1) [
                    uilimitcolourtext (concat (getenginestat 12) "fps") 1 $colourwhite $ui_text
                ] (>= $showfps 2) [
                    uilimitcolourtext (concatword "+" (getenginestat 13) "-" (getenginestat 14)) 1 $colourwhite $ui_textsmall
                ]
            ]
        ]
    ]
    uispace 0 $ui_padtiny
]

ui_hud_velocity = [
    uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                uilimitcolourtext "SPEED" 1 $colourgreen $ui_text
                uilimitcolourtext (getenginestat 21) 1 $colourwhite $ui_textlarge
            ]
        ]
    ]
    uispace 0 $ui_padtiny
]

ui_hud_impulsebar = [
    if (getclientallowimpulse $ui_hud_focus) [
        uihudskin $ui_hud_sizew $ui_hud_bar $ui_hud_colour1 $ui_hud_colour2 [
            uivlist 0 [
                uialign 0 1
                ui_hud_impulse = (getclientimpulse $ui_hud_focus)
                ui_hud_impulseamt = 100
                ui_hud_impulsepart = 1.0
                ui_hud_impulseclip = 0
                uispace $ui_padtiny $ui_padtiny [
                    if (> $ui_hud_impulse 0) [
                        ui_hud_impulsepart = (-f 1 (divf $ui_hud_impulse $impulsemeter))
                        ui_hud_iprtm = (clampf $ui_hud_impulsepart 0.0 1.0)
                        ui_hud_impulseclip = (*f $ui_hud_barh $ui_hud_iprtm)
                        ui_hud_impulseamt = (ceil (*f $ui_hud_iprtm 100))
                    ]
                    if (>f $ui_hud_impulsepart 0) [
                        uiclip 0 $ui_hud_impulseclip 0 (-f $ui_hud_barh $ui_hud_impulseclip) [
                            uialign 0 1
                            uiimagevgradient "<rotate:4>textures/barskin" $colourpurple $colourcyan 0 $ui_hud_barw $ui_hud_barh "textures/blank" [
                                uialign 0 1
                                uiaddcolour $colourblue
                            ]
                        ]
                    ] [uifill $ui_hud_barw $ui_hud_barh]
                ]
                uispace $ui_padtiny $ui_padtiny [
                    uialign 0 1
                    if (<=f $ui_hud_impulsepart 0.25) [
                        uilimitcolourtext $ui_hud_impulseamt 1 (getclientrescolour $ui_hud_focus 6) $ui_textlarge
                    ] [uilimitcolourtext $ui_hud_impulseamt 0 $colourwhite $ui_textlarge]
                ]
            ]
        ]
    ]
]

ui_hud_rightbar = [
    if (= $ui_hud_state 0) [
        if $showfps [ui_hud_showfps]
        if $showvelocity [ui_hud_velocity]
        ui_hud_impulsebar
    ]
]

ui_hud_weaponinventory = [
    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
    loopinventoryrev $ui_hud_focus ui_hud_weapon [
        ui_hud_weapidx = (at $weapname $ui_hud_weapon)
        ui_hud_weapstate = (getclientweapstate $ui_hud_focus $ui_hud_weapon)
        ui_hud_weapload = (? (|| (= $ui_hud_weapstate 3) (= $ui_hud_weapstate 5)) (getclientweapload $ui_hud_focus $ui_hud_weapon) 0)
        ui_hud_weapammo = (getclientweapammo $ui_hud_focus $ui_hud_weapon)
        ui_hud_weapmax = $[@[ui_hud_weapidx]ammomax]
        ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
        ui_hud_weapcol1 = (modcolour $ui_hud_weapcol 0.75)
        ui_hud_weapcol2 = (modcolour $ui_hud_weapcol 0.5)
        ui_hud_weapclip = 0.0
        ui_hud_weappart = 1.0
        if (|| (<= $ui_hud_weapammo 0) $ui_hud_weapload) [
            ui_hud_weapammo = (? $ui_hud_weapload (- $ui_hud_weapammo $ui_hud_weapload) 0)
        ]
        ui_hud_weappart = (divf $ui_hud_weapammo $ui_hud_weapmax)
        if (<f $ui_hud_weappart 1.0) [ui_hud_weapclip = (*f $ui_hud_mtrw (clampf $ui_hud_weappart 0.0 1.0))]
        uivlist 0 [
            uihudskin $ui_hud_sizew $ui_hud_top (? (= $ui_hud_weapselect $ui_hud_weapon) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                if (>f $ui_hud_weappart 0) [
                    uifill $ui_hud_mtrw $ui_hud_mtrh [
                        uialign 0 0
                        uiclip $ui_hud_weapclip $ui_hud_mtrh 0 0 [
                            uialign -1 0
                            uiimagehgradient "<rotate:1>textures/meterskin" $ui_hud_weapcol2 $ui_hud_weapcol1 0 $ui_hud_mtrw $ui_hud_mtrh "textures/blank" [
                                uialign -1 0
                                uiaddcolour $ui_hud_weapcol
                            ]
                        ]
                    ]
                ]
            ]
            uispace 0 $ui_padtiny
            uihudskin $ui_hud_sizew $ui_hud_sizew (? (= $ui_hud_weapselect $ui_hud_weapon) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 $ui_hud_barw $ui_hud_barw [uialign 0 0]
                    caseif (< $ui_hud_weapammo 0) [
                        uilimitcolourtext "-" 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                    ] (<=f $ui_hud_weappart 0.25) [
                        uilimitcolourtext $ui_hud_weapammo 1 (getclientrescolour $ui_hud_focus 6) $ui_text 0 [uialign 0 1]
                    ] $ui_hud_weapload [
                        uilimitcolourtext $ui_hud_weapammo 1 (getclientrescolour $ui_hud_focus 5) $ui_text 0 [uialign 0 1]
                    ] () [uilimitcolourtext $ui_hud_weapammo 1 $colourwhite $ui_text 0 [uialign 0 1]]
                ]
            ]
        ]
        uispace $ui_padtiny
    ]
]

ui_hud_rightinventory = [
    if (= $ui_hud_state 0) [ui_hud_weaponinventory]
]

newui "hud" [
    uiallowinput 0
    uiclamp 1 1 1 1
    ui_hud_init
    uispace $ui_padwin $ui_padwin [
        uialign -1 1
        uihlist 0 [
            uialign -1 1
            uivlist 0 [
                uialign -1 1
                ui_hud_leftbar
            ]
            uispace $ui_padtiny
            uihlist 0 [
                uialign -1 1
                ui_hud_leftinventory
            ]
        ]
    ]
    uispace $ui_padwin $ui_padwin [
        uialign 1 1
        uihlist 0 [
            uialign 1 1
            uihlist 0 [
                uialign 1 1
                ui_hud_rightinventory
            ]
            uivlist 0 [
                uialign 1 1
                ui_hud_rightbar
            ]
        ]
    ]
]