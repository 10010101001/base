exec "config/ui/lib.cfg"
exec "config/ui/style.cfg"

exec "config/ui/main.cfg"
exec "config/ui/profile.cfg"

uimenu "help" "Help and Support" [ uitext "Nothing here yet.." ]
uimenu "maps" "Vote for Map and Mode" [ uitext "Nothing here yet.." ]
uimenu "votes" "Current Votes" [ uitext "Nothing here yet.." ]
uimenu "team" "Choose Team" [ uitext "Nothing here yet.." ]
uimenu "clients" "Client Manager" [ uitext "Nothing here yet.." ]
uimenu "edit" "Editing Options" [ uitext "Nothing here yet.." ]
uimenu "materials" "Editing Options" [ uitext "Nothing here yet.." ]
uimenu "scoreboard" "Scoreboard" [ uitext "Nothing here yet.." ]

uimenu "changes" "Apply Changes" [
    uivlist 0.02 [
        uiclamp 1 1
        uititle [uitext "Apply changes for:"] 0 0.03
        uiclamp- 1 1
        uivlist $ui_padbutton [
            loop i $pendingchanges [
                uitext (concatword "^f8" (pendingchanges $i)) 0.8
            ]
        ]
        uihlist 0 [
            uibutton "Yes" 0.18 0.032 [applychanges]
            uibutton "No" 0.18 0.032 [uiclose "changes"]
        ]
    ]
] [if (= $pendingchanges 0) [uiclose "changes"]]

ui_tooltip_body = ""
ui_tooltip_time = 0
ui_tooltip_last = 0

uitipmenu "tooltip" [
    uihlist 0 [
        do [@@ui_tooltip_body]
    ]
    if (uivisible "tooltip") [
        ui_tooltip_frame = (- (getmillis) $ui_tooltip_last)
        if (|| (= $ui_tooltip_time 0) (> $ui_tooltip_frame $ui_tooltipwait)) [
            ui_tooltip_alpha = (-f 1 (divf (- $ui_tooltip_frame $ui_tooltipwait) $ui_tooltipblend))
            if (>f $ui_tooltip_alpha 0) [ uichangeblends $ui_tooltip_alpha ] [
                hideui "tooltip"
                ui_tooltip_body = ""
                ui_tooltip_time = 0
                ui_tooltip_last = 0
            ]
        ] [
            ui_tooltip_alpha = (divf (- (getmillis) $ui_tooltip_time $ui_tooltipdelay) $ui_tooltipblend)
            if (<=f $ui_tooltip_alpha 1) [ uichangeblends $ui_tooltip_alpha ]
        ]
    ]
]

uitooltip = [
    uihover [
        ui_tooltip_last = (getmillis)
        if (!=s $ui_tooltip_body [@@arg1]) [
            ui_tooltip_body = [@@@arg1]
            if (! (uivisible "tooltip")) [ ui_tooltip_time = $ui_tooltip_last ]
        ] [
            if (&& $ui_tooltip_time (> (- $ui_tooltip_last $ui_tooltip_time) $ui_tooltipdelay)) [
                if (! (uivisible "tooltip")) [ showui "tooltip" 0 ]
            ]
        ]
    ]
]

ui_notepadfile = "untitled.txt"
ui_cmc_from    = ""
ui_cmc_to      = ""

uimenu "notepad" "Notepad" [
    uivlist $ui_padbutton [
        uihlist $ui_padbutton [
            uialign -1 -2
            uifield ui_notepadfile -35 [] 0.6 [
                uioutline (uifocus? $ui_on1 $ui_line)
                uiclamp- 1 1 1 1
            ]
            uibutton "Load" 0.04 0.032 [textfocus $ui_notepadfile; textload $ui_notepadfile]
            uibutton "Save" 0.04 0.032 [textfocus $ui_notepadfile; textsave $ui_notepadfile]
            uibutton "Exec" 0.04 0.032 [textfocus $ui_notepadfile; textexec]
            uispace 0.01 0 [uibar 0 1]
            uibutton "Copy" 0.04 0.032 [textfocus $ui_notepadfile; textcopy]
            uibutton "Paste" 0.04 0.032 [textfocus $ui_notepadfile; textpaste]
            uibutton "Select All" 0.04 0.032 [textfocus $ui_notepadfile; textselectall]
            uispace 0.01 0 [uibar 0 1]
            uibutton "Clear All" 0.04 0.032 [textfocus $ui_notepadfile; textclear]
        ]
        uitexteditor $ui_notepadfile -80 20 1 "" 0 "" "" [
            uioutline (uifocus? $ui_on1 $ui_line)
            uiclamp- 1 1 1 1
        ]
        textinit $ui_notepadfile $ui_notepadfile
    ]
]

notepad = [
    if $numargs [ui_notepadfile = $arg1]
    uiopen "notepad"
]

mapnotepad = [
    ui_notepadfile = (concatword "maps/" (? $numargs $arg1 $mapname) ".cfg")
    uiopen "notepad"
]

copymapcfg = [
    if $numargs [
        ui_cmc_from = $arg1
        ui_cmc_to    = (? (= $numargs 2) $arg2 $mapname)
        if (findfile (concatword "maps/" $ui_cmc_from ".cfg")) [
            if (findfile (concatword "maps/" $ui_cmc_to ".cfg")) [uiopen "cmc_confirm"] [
                textfocus "#copymapcfg"
                textload (concatword "maps/" $ui_cmc_from ".cfg")
                textsave (concatword "maps/" $ui_cmc_to    ".cfg")
            ]
        ] [
            echo (concatword "^f7Map config "^f8^" $ui_cmc_from "^f7^" does not exist!")
            ui_cmc_from = ""; ui_cmc_to = ""
        ]
    ]
]

uimenu "cmc_confirm" "Copy Map" [
    uivlist 0.02 [
        uiclamp 1 1
        uititle [uitext "Copy map config:"] 0 0.03
        uiclamp- 1 1
        uivlist $ui_padbutton [
            uitext (concatword "^f2" $ui_cmc_from) 0.8
            uidrawtriangle 0 0.01 0.01 180
            uitext (concatword "^f2" $ui_cmc_to) 0.8
        ]
        uihlist 0 [
            uibutton "Proceed" 0.18 0.032 [
                textfocus "#copymapcfg"
                textload (concatword "maps/" $ui_cmc_from ".cfg")
                textsave (concatword "maps/" $ui_cmc_to    ".cfg")
                uiclose "cmc_confirm"
            ]
            uibutton "Cancel" 0.18 0.032 [uiclose "cmc_confirm"]
        ]
    ]
] [
    if (|| [=s $ui_cmc_from ""] [=s $ui_cmc_to ""]) [uiclose "cmc_confirm"]
] [ui_cmc_from = ""; ui_cmc_to = ""]

