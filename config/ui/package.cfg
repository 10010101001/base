exec "config/ui/lib.cfg"
exec "config/ui/style.cfg"
exec "config/ui/main.cfg"

uimenu "profile" [
     uitext "Nothing here yet.."
]

uimenu "maps" [
     uitext "Nothing here yet.."
]

uimenu "scoreboard" [
     uitext "Nothing here yet.."
]

UI_creditlist = [
    uititle [
        uispace 0.015 0 [
            uialign -1 -2
            uihlist 0.01 [
                uiimage "textures/icon.png" 0xFFFFFF 0 0.03 0.03
                uitext [@@@@arg1] 0.7
            ]
        ]
    ] 0 0.032
    uispace 0.03 0 [
        uifill 0xFFFFFF 0 0 [
            uiclamp 1 1
            uimodhgradient 0xC0C0C0 0xFFFFFF
            uiclamp- 1 1 1 1
            uispace 0.02 0.01 [
                uialign -1 -2
                uivlist 0 [
                    looplist i $arg2 [
                        uiwraptext (strreplace (strreplace $i "^"" "^fs^fr^"" "^"^fS") ": " "^fa: ") 0.69 0.6
                    ]
                    uialign* -1 -2
                ]
            ]
        ]
    ]
    uifill 0xFFFFFF 0 0.02
]

uimenu "credits" [
    uihlist 0.01 [
        uiscroll 0.8 0.8 [
            uifill $c_menu 0.77
            uivlist 0.0008 [
                uiclamp 1 1
                uialign -2 -1
                // Alphabetized by alias
                UI_creditlist "Red Eclipse" [
                    [Quinton "quin" Reeves : Lead Developer]
                    [Lee "eihrul" Salzman : Lead Programmer]
                ]
                UI_creditlist "Cube Engine 2" [
                    [Wouter "Aardappel" van Oortmerssen]
                    [Lee "eihrul" Salzman]
                    [Mike "Gilt" Dysart]
                    [Robert "baby-rabbit" Pointon]
                    [Quinton "Quin" Reeves]
                    [John "geartrooper" Siar]
                    [and others...]
                    "http://sauerbraten.org/"
                ]
                uiclamp* 1 1
            ]
        ]
        uivscroll 0.02 0.8 2.0
    ]
] [] [] [] "Red Eclipse Contributors"

///////////////////////////////////////////////////////////////////////////////
//  Display Changes                                                                             //
///////////////////////////////////////////////////////////////////////////////
uimenu "changes" [
    uivlist 0.02 [
        uiclamp 1 1
        uititle [uitext "Apply changes for:" 0.6] 0 0.03
        uiclamp- 1 1
        uivlist 0.01 [
            loop i $pendingchanges [
                uitext (concatword "^f8" (pendingchanges $i)) 0.6
            ]
        ]
        uihlist 0 [
            uibutton "hold2" [uitext "Yes" 0.6] 0.18 0.032 [applychanges]
            uibutton "hold2" [uitext "No"  0.6] 0.18 0.032 [hideui "changes"]
        ]
    ]
] [if (= $pendingchanges 0) [hideui "changes"]]

///////////////////////////////////////////////////////////////////////////////
//  Notepad                                                                                        //
///////////////////////////////////////////////////////////////////////////////
UI_notepadfile = "untitled.txt"
UI_cmc_from    = ""
UI_cmc_to      = ""

uimenu "notepad" [
    uivlist 0.01 [
        uihlist 0.01 [
            uialign -1 -2
            uifield UI_notepadfile -35 [] 0.6 [
                uioutline (uifocus? 0xFF0000 $c_line)
                uiclamp- 1 1 1 1
            ]
            uibutton "hold2" [uitext "Load"         0.6] 0.04 0.032 [textfocus $UI_notepadfile; textload $UI_notepadfile]
            uibutton "hold2" [uitext "Save"         0.6] 0.04 0.032 [textfocus $UI_notepadfile; textsave $UI_notepadfile]
            uibutton "hold2" [uitext "Exec"         0.6] 0.04 0.032 [textfocus $UI_notepadfile; textexec]
            uispace 0.01 0 [uibar 0 1]
            uibutton "hold2" [uitext "Copy"         0.6] 0.04 0.032 [textfocus $UI_notepadfile; textcopy]
            uibutton "hold2" [uitext "Paste"        0.6] 0.04 0.032 [textfocus $UI_notepadfile; textpaste]
            uibutton "hold2" [uitext "Select All" 0.6] 0.04 0.032 [textfocus $UI_notepadfile; textselectall]
            uispace 0.01 0 [uibar 0 1]
            uibutton "hold2" [uitext "Clear All"  0.6] 0.04 0.032 [textfocus $UI_notepadfile; textclear]
        ]
        uitexteditor $UI_notepadfile -80 20 1 "" 0 "" "" [
            uioutline (uifocus? 0xFF0000 $c_line)
            uiclamp- 1 1 1 1
        ]
        textinit $UI_notepadfile $UI_notepadfile
    ]
] [] [] [] "Notepad"

notepad = [
    if $numargs [UI_notepadfile = $arg1]
    showui "notepad"
]

mapnotepad = [
    UI_notepadfile = (concatword "maps/" (? $numargs $arg1 $mapname) ".cfg")
    showui "notepad"
]

copymapcfg = [
    if $numargs [
        UI_cmc_from = $arg1
        UI_cmc_to    = (? (= $numargs 2) $arg2 $mapname)
        if (findfile (concatword "maps/" $UI_cmc_from ".cfg")) [
            if (findfile (concatword "maps/" $UI_cmc_to ".cfg")) [showui "cmc_confirm"] [
                textfocus "#copymapcfg"
                textload (concatword "maps/" $UI_cmc_from ".cfg")
                textsave (concatword "maps/" $UI_cmc_to    ".cfg")
            ]
        ] [
            echo (concatword "^f7Map config "^f8^" $UI_cmc_from "^f7^" does not exist!")
            UI_cmc_from = ""; UI_cmc_to = ""
        ]
    ]
]

uimenu "cmc_confirm" [
    uivlist 0.02 [
        uiclamp 1 1
        uititle [uitext "Copy map config:" 0.6] 0 0.03
        uiclamp- 1 1
        uivlist 0.01 [
            uitext (concatword "^f2" $UI_cmc_from) 0.6
            uidrawtriangle 0 0.01 0.01 180
            uitext (concatword "^f2" $UI_cmc_to) 0.6
        ]
        uihlist 0 [
            uibutton "hold2" [uitext "Proceed" 0.6] 0.18 0.032 [
                textfocus "#copymapcfg"
                textload (concatword "maps/" $UI_cmc_from ".cfg")
                textsave (concatword "maps/" $UI_cmc_to    ".cfg")
                hideui "cmc_confirm"
            ]
            uibutton "hold2" [uitext "Cancel"  0.6] 0.18 0.032 [hideui "cmc_confirm"]
        ]
    ]
] [
    if (|| [=s $UI_cmc_from ""] [=s $UI_cmc_to ""]) [hideui "cmc_confirm"]
] [UI_cmc_from = ""; UI_cmc_to = ""]

