uimenu "scoreboard" "Scoreboard" "textures/dead" [
    uivlist $ui_padbutton [
        uiclamp 1 1 1 1
        uialign 0 -1
        uitable 0.1 0 [
            uitablerow [
                uispace $ui_padbutton $ui_padtiny [uitext "Name"]
                if (= (gamemode) $modeidxrace) [
                    uispace $ui_padbutton $ui_padtiny [uitext "Laps"]
                    uispace $ui_padbutton $ui_padtiny [uitext "Lap Time"]
                ] [
                    uispace $ui_padbutton $ui_padtiny [uitext "Points"]
                ]
                uispace $ui_padbutton $ui_padtiny [uitext "Frags"]
                uispace $ui_padbutton $ui_padtiny [uitext "Deaths"]
                uispace $ui_padbutton $ui_padtiny [uitext "Handle"]
            ]
            ui_score_count = (numscoregroups)
            ui_score_race = (= (gamemode) $modeidxrace)
            ui_score_racetime = (&& $ui_score_race (& (mutators) $mutsbitgsp1))
            loop ui_score_group (+ $ui_score_count 1) [
                ui_score_curgroup = (? (>= $ui_score_group $ui_score_count) -1 $ui_score_group)
                ui_score_num = (numscoreboard $ui_score_group)
                if $ui_score_num [
                    if (>= $ui_score_curgroup $ui_score_count) [
                        uitableheader [] [
                            uispace $ui_padbutton $ui_padtiny [
                                uialign -1 0
                                uitextleft "Spectators"
                            ]
                            uispace $ui_padbutton $ui_padtiny []
                        ]
                    ] [
                        uitableheader [] [
                            ui_score_team = (getscoreteam $ui_score_curgroup)
                            ui_score_colour = (getteamcolour $ui_score_team)
                            uispace $ui_padbutton $ui_padtiny [
                                uialign -1 0
                                uitextleft (concat "Team:" (getteamname $ui_score_team))
                            ]
                            uispace $ui_padbutton $ui_padtiny [
                                uialign 1 0
                                ui_score_total = (getscoretotal $ui_score_curgroup)
                                if $ui_score_race [
                                    if $ui_score_racetime [
                                        uitextright (concat "Best:" (? (> $ui_score_total 0) (timestr $ui_score_total $scoreracestyle) "-"))
                                    ] [
                                        uitextright (concat "Laps:" $ui_score_total)
                                    ]
                                ] [
                                    uitextright (concat "Score:" $ui_score_total)
                                ]
                            ]
                        ]
                    ]
                    loopscoreboard ui_score_player $ui_score_curgroup [
                        uitablerow [
                            uispace $ui_padbutton $ui_padtiny [uitext (getclientname $ui_score_player 1)]
                            uispace $ui_padbutton $ui_padtiny [uitext (getclientpoints $ui_score_player)]
                            if $ui_score_racetime [
                                uispace $ui_padbutton $ui_padtiny [
                                    ui_score_cptime = (getclientcptime $ui_score_player)
                                    uitext (? (> $ui_score_cptime 0) (timestr $ui_score_cptime $scoreracestyle) "-")
                                ]
                            ]
                            uispace $ui_padbutton $ui_padtiny [uitext (getclientfrags $ui_score_player)]
                            uispace $ui_padbutton $ui_padtiny [uitext (getclientdeaths $ui_score_player)]
                            uispace $ui_padbutton $ui_padtiny [uitext (getclienthandle $ui_score_player)]
                        ]
                    ]
                ]
            ]
        ]
    ]
] [] [] [] 0 [] [] 1
