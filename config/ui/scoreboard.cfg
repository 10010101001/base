uiscoreskin = [
    if $arg5 [] [arg5 = 0]
    uiskinborder 0 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uiclamp 1 1 1 1
        doargs $arg4
        uispace (+f $arg5 $ui_padbutton) $ui_padsmaller [
            uiclamp 1 1 1 1
            doargs $arg3
        ]
    ] [uiclamp 1 1 1 1]
]

ui_score_teamlist = [neutral alpha omega kappa sigma]
ui_score_gscolour = ["^fr" "^fr" "^fr" "^fr" "^fr" "^fg" "^fzoy" "^fg"]
uimenu "scoreboard" "Scoreboard" "textures/dead" [
    refreshscoreboard
    uivlist $ui_padbutton [
        uiclamp 1 1 1 1
        uialign 0 -1
        uivlist 0 [
            uitext (concatword "^fs^fo" $maptitle "^fS by ^fs^fy" $mapauthor)
            if (!= (gamemode) $modeidxediting) [
                uitext (concatword (getgamestatestr 1) ", ^fs" (at $ui_score_gscolour (getgamestate)) (timestr (getgametimeremain) $inventorytimestyle) "^fS remaining")
            ]
        ]
        uispace $ui_padbutton
        uitable 0 0 [
            uiclamp 1 1 1 1
            ui_score_count = (numscoregroups)
            ui_score_race = (= (gamemode) $modeidxrace)
            ui_score_racetime = (&& $ui_score_race (& (mutators) $mutsbitgsp1))
            uitableheader [
                uiclamp 1 1 1 1
                uiscoreskin $ui_menu $ui_disa [uitext "Name" $ui_text]
                if $ui_score_race [
                    uiscoreskin $ui_menu $ui_disa [uitext "Laps" $ui_text]
                    if $ui_score_racetime [uiscoreskin $ui_menu $ui_disa [uitext "Lap Time" $ui_text]]
                ] [uiscoreskin $ui_menu $ui_disa [uitext "Points" $ui_text]]
                uiscoreskin $ui_menu $ui_disa [uitext "Frags" $ui_text]
                uiscoreskin $ui_menu $ui_disa [uitext "Deaths" $ui_text]
                uiscoreskin $ui_menu $ui_disa [uitext "Ping" $ui_text]
                uiscoreskin $ui_menu $ui_disa [uitext "PJ" $ui_text]
                uiscoreskin $ui_menu $ui_disa [uitext "Handle" $ui_text]
            ]
            loop ui_score_group (+ $ui_score_count 1) [
                ui_score_curgroup = (? (>= $ui_score_group $ui_score_count) -1 $ui_score_group)
                ui_score_num = (numscoreboard $ui_score_curgroup)
                if $ui_score_num [
                    ui_score_colour = 0xFFFFFF
                    ui_score_colour1 = 0x111111
                    ui_score_colour2 = 0x888888
                    if (>= $ui_score_curgroup 0) [
                        ui_score_team = (getscoreteam $ui_score_curgroup)
                        ui_score_teamid = (at $ui_score_teamlist $ui_score_team)
                        ui_score_colour = (getteamcolour $ui_score_team)
                        ui_score_colour1 = (modcolour $ui_score_colour 0.1)
                        ui_score_colour2 = (modcolour $ui_score_colour 0.5)
                        if $ui_score_team [
                            uitablerow [uiclamp 1 1 1 1] [
                                uiclamp 1 1 1 1
                                uiscoreskin $ui_score_colour2 $ui_score_colour1 [
                                    uihlist 0 [
                                        uialign -1 0
                                        uitextleft (concat "Team" $[team@[ui_score_teamid]name]) $ui_textbig
                                    ]
                                    uihlist 0 [
                                        uialign 1 0
                                        ui_score_total = (getscoretotal $ui_score_curgroup)
                                        if $ui_score_race [
                                            if $ui_score_racetime [
                                                uitextright (concat "Best:" (? (> $ui_score_total 0) (timestr $ui_score_total $scoreracestyle) "-")) $ui_textbig
                                            ] [uitextright (concat "Laps:" $ui_score_total) $ui_textbig]
                                        ] [uitextright (concat "Score:" $ui_score_total) $ui_textbig]
                                        uispace $ui_padsmall
                                        uiimage $[team@[ui_score_teamid]tex] $ui_score_colour 0 $ui_iconbigsize $ui_iconbigsize $playertex
                                    ]
                                ]
                            ]
                        ]
                    ] [
                        ui_score_colour = (getteamcolour 0)
                        ui_score_colour1 = (modcolour $ui_score_colour 0.1)
                        ui_score_colour2 = (modcolour $ui_score_colour 0.5)
                        uitablerow [uiclamp 1 1 1 1] [
                            uiclamp 1 1 1 1
                            uiscoreskin $ui_score_colour2 $ui_score_colour1 [
                                uihlist 0 [
                                    uialign -1 0
                                    uitextleft "Spectators" $ui_textbig
                                ]
                                uihlist 0 [
                                    uialign 1 0
                                    uiimage $spectatortex $ui_score_colour 0 $ui_iconbigsize $ui_iconbigsize
                                ]
                            ]
                        ]
                    ]
                    ui_score_alt = 0
                    ui_score_rcol1 = (modcolour $ui_score_colour 0.2)
                    ui_score_rcol2 = (modcolour $ui_score_colour 0.4)
                    loopscoreboard ui_score_player $ui_score_curgroup [
                        ui_score_self = (= $ui_score_player (getplayercn))
                        ui_score_ccol1 = (? $ui_score_self $ui_score_colour $ui_score_colour1)
                        ui_score_ccol2 = (? $ui_score_alt $ui_score_rcol2 $ui_score_rcol1)
                        uitablerow [
                            uiclamp 1 1 1 1
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientname $ui_score_player)] [
                                uispace $ui_padbutton 0 [uialign -1 0; uiimage (getclientprivtex $ui_score_player) (getclientcolour $ui_score_player -1 2) 0 $ui_iconsize $ui_iconsize $privnonetex [uialign -1 0]]
                            ] (+f $ui_iconsize $ui_padbutton)
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientpoints $ui_score_player)]
                            if $ui_score_racetime [
                                uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [
                                    ui_score_cptime = (getclientcptime $ui_score_player)
                                    uitext (? (> $ui_score_cptime 0) (timestr $ui_score_cptime $scoreracestyle) "-")
                                ]
                            ]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientfrags $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientdeaths $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientping $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientpj $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclienthandle $ui_score_player)]
                        ]
                        ui_score_alt = (! $ui_score_alt)
                    ]
                ]
            ]
        ]
    ]
] [] [] [
    ui_menu_title_scoreboard = (gamename (gamemode) (mutators) 0 32)
    ui_menu_icon_scoreboard = (modetexlist (gamemode) (mutators) -1)
] 0 [] 1 1
