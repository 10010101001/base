uiscoreskin = [
    uiskinborder 0 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uiclamp 1 1 1 1
        uispace $ui_padbutton $ui_padtiny [
            uiclamp 1 1 1 1
            doargs $arg3
        ]
    ] [uiclamp 1 1 1 1]
]

uimenu "scoreboard" "Scoreboard" "textures/dead" [
    uivlist $ui_padbutton [
        uiclamp 1 1 1 1
        uialign 0 -1
        uitable 0 0 [
            uiclamp 1 1 1 1
            uitableheader [
                uiclamp 1 1 1 1
                uiscoreskin $ui_main3 $ui_line [uitext "Name"]
                if (= (gamemode) $modeidxrace) [
                    uiscoreskin $ui_main3 $ui_line [uitext "Laps"]
                    uispace $ui_padbutton $ui_padtiny [uitext "Lap Time"]
                ] [
                    uiscoreskin $ui_main3 $ui_line [uitext "Points"]
                ]
                uiscoreskin $ui_main3 $ui_line [uitext "Frags"]
                uiscoreskin $ui_main3 $ui_line [uitext "Deaths"]
                uiscoreskin $ui_main3 $ui_line [uitext "Handle"]
            ]
            ui_score_count = (numscoregroups)
            ui_score_race = (= (gamemode) $modeidxrace)
            ui_score_racetime = (&& $ui_score_race (& (mutators) $mutsbitgsp1))
            loop ui_score_group (+ $ui_score_count 1) [
                ui_score_curgroup = (? (>= $ui_score_group $ui_score_count) -1 $ui_score_group)
                ui_score_num = (numscoreboard $ui_score_group)
                if $ui_score_num [
                    ui_score_colour = 0xFFFFFF
                    ui_score_colour2 = 0x888888
                    uitablerow [uiclamp 1 1 1 1] [
                        uiclamp 1 1 1 1
                        if (>= $ui_score_curgroup $ui_score_count) [
                            ui_score_colour = (getteamcolour 0)
                            ui_score_colour2 = (modcolour $ui_score_colour 0.5)
                            uiscoreskin $ui_score_colour2 $ui_score_colour [
                                uialign -1 0
                                uitextleft "Spectators"
                            ] [uialign -1 0]
                        ] [
                            ui_score_team = (getscoreteam $ui_score_curgroup)
                            ui_score_colour = (getteamcolour $ui_score_team)
                            ui_score_colour2 = (modcolour $ui_score_colour 0.5)
                            uiscoreskin $ui_score_colour2 $ui_score_colour [
                                if $ui_score_team [
                                    uispace $ui_padtiny $ui_padtiny [
                                        uialign -1 0
                                        uitextleft (concat "Team:" (getteamname $ui_score_team))
                                    ]
                                    uispace $ui_padtiny $ui_padtiny [
                                        uialign 1 0
                                        ui_score_total = (getscoretotal $ui_score_curgroup)
                                        if $ui_score_race [
                                            if $ui_score_racetime [
                                                uitextright (concat "Best:" (? (> $ui_score_total 0) (timestr $ui_score_total $scoreracestyle) "-"))
                                            ] [
                                                uitextright (concat "Laps:" $ui_score_total)
                                            ]
                                        ] [
                                            uitextright (concat "Score:" $ui_score_total)
                                        ]
                                    ]
                                ] [
                                    uispace $ui_padtiny $ui_padtiny [
                                        uialign -1 0
                                        uitextleft "Players"
                                    ]
                                ]
                            ]
                        ]
                    ]
                    ui_score_alt = 0
                    ui_score_rcol = (modcolour $ui_score_colour 0.175)
                    ui_score_rcol2 = (modcolour $ui_score_colour 0.35)
                    loopscoreboard ui_score_player $ui_score_curgroup [
                        ui_score_ccol = (? $ui_score_alt $ui_score_rcol2 $ui_score_rcol)
                        uitablerow [
                            uiclamp 1 1 1 1
                            uiscoreskin $ui_score_ccol $ui_score_colour [uitext (getclientname $ui_score_player 1)]
                            uiscoreskin $ui_score_ccol $ui_score_colour [uitext (getclientpoints $ui_score_player)]
                            if $ui_score_racetime [
                                uiscoreskin $ui_score_ccol $ui_score_colour [
                                    ui_score_cptime = (getclientcptime $ui_score_player)
                                    uitext (? (> $ui_score_cptime 0) (timestr $ui_score_cptime $scoreracestyle) "-")
                                ]
                            ]
                            uiscoreskin $ui_score_ccol $ui_score_colour [uitext (getclientfrags $ui_score_player)]
                            uiscoreskin $ui_score_ccol $ui_score_colour [uitext (getclientdeaths $ui_score_player)]
                            uiscoreskin $ui_score_ccol $ui_score_colour [uitext (getclienthandle $ui_score_player)]
                        ]
                        ui_score_alt = (! $ui_score_alt)
                    ]
                ]
            ]
        ]
    ]
] [] [] [] 0 [] 1 1
