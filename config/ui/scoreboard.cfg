uiscoreskin = [
    uiskinborder 0 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uiclamp 1 1 1 1
        uispace $ui_padbutton $ui_padsmaller [
            uiclamp 1 1 1 1
            doargs $arg3
        ]
    ] [uiclamp 1 1 1 1]
]

uimenu "scoreboard" "Scoreboard" "textures/dead" [
    refreshscoreboard
    uivlist $ui_padbutton [
        uiclamp 1 1 1 1
        uialign 0 -1
        uitable 0 0 [
            uiclamp 1 1 1 1
            ui_score_count = (numscoregroups)
            ui_score_race = (= (gamemode) $modeidxrace)
            ui_score_racetime = (&& $ui_score_race (& (mutators) $mutsbitgsp1))
            uitableheader [
                uiclamp 1 1 1 1
                uiscoreskin $ui_main3 $ui_main1 [uitext "Name" $ui_textbig]
                if $ui_score_race [
                    uiscoreskin $ui_main3 $ui_main1 [uitext "Laps" $ui_textbig]
                    if $ui_score_racetime [uiscoreskin $ui_main3 $ui_main1 [uitext "Lap Time" $ui_textbig]]
                ] [uiscoreskin $ui_main3 $ui_main1 [uitext "Points" $ui_textbig]]
                uiscoreskin $ui_main3 $ui_main1 [uitext "Frags" $ui_textbig]
                uiscoreskin $ui_main3 $ui_main1 [uitext "Deaths" $ui_textbig]
                uiscoreskin $ui_main3 $ui_main1 [uitext "Ping" $ui_textbig]
                uiscoreskin $ui_main3 $ui_main1 [uitext "PJ" $ui_textbig]
                uiscoreskin $ui_main3 $ui_main1 [uitext "Handle" $ui_textbig]
            ]
            loop ui_score_group (+ $ui_score_count 1) [
                ui_score_curgroup = (? (>= $ui_score_group $ui_score_count) -1 $ui_score_group)
                ui_score_num = (numscoreboard $ui_score_curgroup)
                if $ui_score_num [
                    ui_score_colour = 0xFFFFFF
                    ui_score_colour1 = 0x111111
                    ui_score_colour2 = 0x888888
                    if (>= $ui_score_curgroup 0) [
                        ui_score_team = (getscoreteam $ui_score_curgroup)
                        ui_score_colour = (getteamcolour $ui_score_team)
                        ui_score_colour1 = (modcolour $ui_score_colour 0.15)
                        ui_score_colour2 = (modcolour $ui_score_colour 0.65)
                        if $ui_score_team [
                            uitablerow [uiclamp 1 1 1 1] [
                                uiclamp 1 1 1 1
                                uiscoreskin $ui_score_colour2 $ui_score_colour1 [
                                    uispace $ui_padsmall $ui_padtiny [
                                        uialign -1 0
                                        uitextleft (concat "Team:" (getteamname $ui_score_team)) $ui_textbig
                                    ]
                                    uispace $ui_padsmall $ui_padtiny [
                                        uialign 1 0
                                        ui_score_total = (getscoretotal $ui_score_curgroup)
                                        if $ui_score_race [
                                            if $ui_score_racetime [
                                                uitextright (concat "Best:" (? (> $ui_score_total 0) (timestr $ui_score_total $scoreracestyle) "-")) $ui_textbig
                                            ] [uitextright (concat "Laps:" $ui_score_total) $ui_textbig]
                                        ] [uitextright (concat "Score:" $ui_score_total) $ui_textbig]
                                    ]
                                ]
                            ]
                        ]
                    ] [
                        ui_score_colour = (getteamcolour 0)
                        ui_score_colour1 = (modcolour $ui_score_colour 0.15)
                        ui_score_colour2 = (modcolour $ui_score_colour 0.65)
                        uitablerow [uiclamp 1 1 1 1] [
                            uiclamp 1 1 1 1
                            uiscoreskin $ui_score_colour2 $ui_score_colour1 [
                                uispace $ui_padsmall $ui_padtiny [
                                    uialign -1 0
                                    uitextleft "Spectators" $ui_textbig
                                ]
                            ]
                        ]
                    ]
                    ui_score_alt = 0
                    ui_score_rcol1 = (modcolour $ui_score_colour 0.25)
                    ui_score_rcol2 = (modcolour $ui_score_colour 0.45)
                    loopscoreboard ui_score_player $ui_score_curgroup [
                        ui_score_self = (= $ui_score_player (getplayercn))
                        ui_score_ccol1 = (? $ui_score_self $ui_score_colour $ui_score_colour1)
                        ui_score_ccol2 = (? $ui_score_alt $ui_score_rcol2 $ui_score_rcol1)
                        uitablerow [
                            uiclamp 1 1 1 1
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientname $ui_score_player 1)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientpoints $ui_score_player)]
                            if $ui_score_racetime [
                                uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [
                                    ui_score_cptime = (getclientcptime $ui_score_player)
                                    uitext (? (> $ui_score_cptime 0) (timestr $ui_score_cptime $scoreracestyle) "-")
                                ]
                            ]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientfrags $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientdeaths $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientping $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclientpj $ui_score_player)]
                            uiscoreskin $ui_score_ccol2 $ui_score_ccol1 [uitext (getclienthandle $ui_score_player)]
                        ]
                        ui_score_alt = (! $ui_score_alt)
                    ]
                ]
            ]
        ]
    ]
] [] [] [] 0 [] 1 1
