//This file contains the team menu. It may contain other game-related menus in the future.

//This is the team menu, accessed by [.] while ingame. 
uimenu "team" "Choose Team" "<grey>textures/icons/team" [
    uivlist 0.005 [
        if (= (isspectator (getclientnum) )  0 ) [ //see if isspectator is true for player; 0 ~> currently in game 
            uichecktextbutton "Spectate" "textures/icons/player" 0 $ui_buttonw $ui_buttonh [
                spectate 1
                uiclose "team"
            ] $colourwhite $ui_default
        ] [        //if isspectator returns 1: player is spectating, join button appears instead   
            uichecktextbutton "Unspectate" "textures/icons/player" 0 $ui_buttonw $ui_buttonh [
                spectate 0
                uiclose "team"
            ] $colourwhite $ui_default
        ]  
        //checks if ffa is in the mutators; returns false if ffa present
        //checks if coop is in the game mutators; returns false if coop present
        //only shows ui buttons for alpha/omega if neither is present
        if (&& (!= (& (mutators) $mutsbitffa) $mutsbitffa) (!= (& (mutators) $mutsbitcoop) $mutsbitcoop) ) [ 
            uibar 1 0
            uichecktextbutton $teamalphaname "textures/icons/teamalpha" 0 $ui_buttonw $ui_buttonh [
                team 1
                uiclose "team"
            ] $teamalphacolour $ui_default
            uichecktextbutton $teamomeganame "textures/icons/teamomega" 0 $ui_buttonw $ui_buttonh [
                team 2
                uiclose "team"
            ] $teamomegacolour $ui_default
        ] [] //no else action taken     
        //check if multi is part of the game mutators, compare to mutsbitmulti and only show team3/4 buttons if present
        if (= (& (mutators) $mutsbitmulti) $mutsbitmulti) [
            uichecktextbutton $teamkappaname "textures/icons/teamkappa" 0 $ui_buttonw $ui_buttonh [
                team 3
                uiclose "team"
            ] $teamkappacolour $ui_default
            uichecktextbutton $teamsigmaname "textures/icons/teamsigma" 0 $ui_buttonw $ui_buttonh [
                team 4
                uiclose "team"
            ] $teamsigmacolour $ui_default
        ] [] //no else action taken 
        uibar 1 0
        uichecktextbutton "Close" "textures/icons/warning" 0 $ui_buttonw $ui_buttonh [
            uiclose "team"
        ] $colourred $ui_default 
    ]
]