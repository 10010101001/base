//This is the singleplayer challenges ui
ui_challenge_maps = "maps/tutorial maps/challenges-port-01 maps/challenges-port-02 maps/challenges-port-03 maps/challenges-port-04 maps/challenges-port-05 maps/challenges-port-06"
defvarp challengescompleted 0 0 6 //saves the number of challenges completed
defvarp tutorialcompleted 0 0 1 //for if the tutorial has already been done

//getnextchallenge, getcurrchallenge, getprevchallenge
//gets next, current, or previous challenge map in rotation, respectively
//Inputs
//    No arguments (takes current map)
//Outputs
//    result | path to next/current/previous map in challenge rotation
getnextchallenge = [
    result (sublist $ui_challenge_maps (+ 1 (indexof $ui_challenge_maps $mapname)) 1)
]

getcurrchallenge = [
    result (sublist $ui_challenge_maps (indexof $ui_challenge_maps $mapname) 1)
]

getprevchallenge = [
    result (sublist $ui_challenge_maps (- (indexof $ui_challenge_maps $mapname) 1) 1)
]

uimenu "challenges" "Parkour Levels" $alerttex [
    if (> (+ 1 (indexof $ui_challenge_maps $mapname)) $challengescompleted) [
        challengescompleted (+ 1 (indexof $ui_challenge_maps $mapname))
    ]
    if (&& (=s $mapname "maps/tutorial") (! $tutorialcompleted)) [ //if this is the first time completing the tutorial
        uivlist $ui_padsmall [
            uitext "You have sucessfully completed the movement tutorial."
            uifill 0 $ui_padnormal
            uitext "You can replay the tutorial at a later time"
            uitext "from the Parkour Challenges menu."
            uifill 0 $ui_padnormal
            uitext "It is recommended that you try out the game modes"
            uitext "in sandbox mode (bottom left of the maps UI) to"
            uitext "become familiar with the weapons."
            uibutton "Main Menu"      $ui_buttonzw $ui_buttonzh [if (isconnected) [disconnect] [showui main]]
        ]
    ] [
        uivlist 0 [
            uibutton "Next Challenge" $ui_buttonzw $ui_buttonzh [map (getnextchallenge)] 0 [] (if (=s "" (getnextchallenge)) [result 1] [result 0])
            uibutton "Redo Challenge" $ui_buttonzw $ui_buttonzh [map (getcurrchallenge)] 0 []
            uibutton "Prev Challenge" $ui_buttonzw $ui_buttonzh [map (getprevchallenge)] 0 [] (if (=s (getcurrchallenge) (getprevchallenge)) [result 1] [result 0])
            uibutton "Challenge Menu" $ui_buttonzw $ui_buttonzh [showui challengemenu]
            uibutton "Main Menu"      $ui_buttonzw $ui_buttonzh [if (isconnected) [disconnect] [showui main]]
        ]
    ]
]

uimenu "challengemenu" "Single Player Challenges" $alerttex [
    uivlist 0 [
        looplist i $ui_challenge_maps [
            uitextexec (sublist $ui_challenge_maps (indexof $ui_challenge_maps $i) 1) [
                race (sublist $ui_challenge_maps (indexof $ui_challenge_maps $i) 1) $mutsbitffa
                uiclose "challengemenu"
            ] [] [] [] [] (> (indexof $ui_challenge_maps $i) $challengescompleted) $ui_text
        ]
    ]
]
