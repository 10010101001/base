ui_tex_show_all = 1
ui_tex_search_criteria = ""
ui_tex_current_slot = 0

uimenu "textures" "Texture Browser" "" [
    uihlist 0.03125 [
        uialign 0 -1
        uiclamp 1 1 1 1

        uivlist $ui_padsmall [
            uiclamp 1 1 1 1

            uivlist $ui_padsmall [
                uivslotview $ui_tex_current_slot 0.2 0.2 [uioutline 0xC0C0C0; uiclamp- 1 1 1 1]

                ui_tex_current_slot_filename = (gettexname $ui_tex_current_slot)

                while [!= (strstr $ui_tex_current_slot_filename "/") -1] [ui_tex_current_slot_filename = (substring $ui_tex_current_slot_filename (+ 1 (strstr $ui_tex_current_slot_filename "/")))]

                if (!=s $ui_tex_current_slot "") [
                    uitext (format "#%1, %2" $ui_tex_current_slot $ui_tex_current_slot_filename)
                ] [
                    uitext $ui_tex_current_slot_filename
                ]
                uitext (gettexname $ui_tex_current_slot) $ui_texttiny
            ]


            uivlist $ui_padsmall [

                ui_tex_vpalette_types = [
                    [   // Pulse colours
                        "No palette"
                        "Dark fire pulse"
                        "Bright fire pulse"
                        "Disco pulse"
                        "Shock pulse"
                        "Bleed pulse"
                        "Buff pulse"
                        "Warn pulse"
                    ] [ // Team colours
                        "Team Neutral"
                        "Team Alpha"
                        "Team Omega"
                        "Team Kappa"
                        "Team Sigma"
                        "Team Neutral - Enforced"
                        "Team Alpha - Enforced"
                        "Team Omega - Enforced"
                        "Team Kappa - Enforced"
                        "Team Sigma - Enforced"
                    ] [ // Weapon colours
                        "Claw"
                        "Pistol"
                        "Sword"
                        "Shotgun"
                        "SMG"
                        "Flamer"
                        "Plasma"
                        "Zapper"
                        "Rifle"
                        "Grenade"
                        "Mine"
                        "Rocket"
                        "Melee"
                        "Claw - Enforced"
                        "Pistol - Enforced"
                        "Sword - Enforced"
                        "Shotgun - Enforced"
                        "SMG - Enforced"
                        "Flamer - Enforced"
                        "Plasma - Enforced"
                        "Zapper - Enforced"
                        "Rifle - Enforced"
                        "Grenade - Enforced"
                        "Mine - Enforced"
                        "Rocket - Enforced"
                        "Melee - Enforced"
                    ]
                ]

                ui_tex_vshaderparam_types = [
                    "envmin"
                    "envscale"
                    "gloss"
                    "glowcolor"
                    "parallaxscale"
                    "pulseglowcolor"
                    "pulseglowspeed"
                    "specscale"
                    "triplanarbias"
                ]

                uihlist 0 [
                    uitextleft "Scale:" $ui_textsmall
                    uitextright (format "%1x" (getvscale $ui_tex_current_slot))  $ui_textsmall

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Rotation:" $ui_textsmall
                    uitextright (format "%1 (%2)" (at ["0 deg" "90 deg" "180 deg" "270 deg" "X Mirror" "Y Mirror"] (getvrotate $ui_tex_current_slot)) (getvrotate $ui_tex_current_slot))  $ui_textsmall

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Offset:" $ui_textsmall
                    uitextright (format "X: %1, Y: %2" @(getvoffset $ui_tex_current_slot)) $ui_textsmall

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Scroll:" $ui_textsmall
                    uitextright (format "X: %1, Y: %2" @(getvscroll $ui_tex_current_slot)) $ui_textsmall

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Alpha:" $ui_textsmall
                    uitextright (format "F: %1, B: %2" @(getvalpha $ui_tex_current_slot)) $ui_textsmall

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Colour:" $ui_textsmall
                    uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" @(getvcolour $ui_tex_current_slot)) $ui_textsmall

                    uiclamp 1 1
                ]
                uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" (* (*f (at (getvcolour $ui_tex_current_slot) 0) 255) 1) (* (*f (at (getvcolour $ui_tex_current_slot) 1) 255) 1) (* (*f (at (getvcolour $ui_tex_current_slot) 2) 255) 1)) $ui_textsmall

                uihlist 0 [
                    uitextleft "Refract:" $ui_textsmall
                    uitextright (format "%1%%" (at (getvrefract $ui_tex_current_slot) 0)) $ui_textsmall

                    uiclamp 1 1
                ]
                uitextright (format "^fr%2^fw - ^fg%3^fw - ^fb%4" @(getvrefract $ui_tex_current_slot)) $ui_textsmall
                uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" (* (*f (at (getvrefract $ui_tex_current_slot) 1) 255) 1) (* (*f (at (getvrefract $ui_tex_current_slot) 2) 255) 1) (* (*f (at (getvrefract $ui_tex_current_slot) 3) 255) 1)) $ui_textsmall

                uitextleft "Palette:" $ui_textsmall
                uitextright (format "%3 (%1 %2)" @(getvpalette $ui_tex_current_slot) (at (at $ui_tex_vpalette_types (at (getvpalette $ui_tex_current_slot) 0)) (at (getvpalette $ui_tex_current_slot) 1))) $ui_textsmall

                uihlist 0 [
                    uitextleft "Coast:" $ui_textsmall
                    uitextright (format "%1x" (getvcoastscale $ui_tex_current_slot)) $ui_textsmall

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Layer:" $ui_textsmall

                    if (= (getvlayer $ui_tex_current_slot) 0) [
                        uitextright "None" $ui_textsmall
                    ] [
                        ui_tex_current_slot_vlayer_filename = (gettexname (getvlayer $ui_tex_current_slot))
                        while [!= (strstr $ui_tex_current_slot_vlayer_filename "/") -1] [ui_tex_current_slot_vlayer_filename = (substring $ui_tex_current_slot_vlayer_filename (+ 1 (strstr $ui_tex_current_slot_vlayer_filename "/")))]
                        uitextright (format "#%1, %2" (getvlayer $ui_tex_current_slot) $ui_tex_current_slot_vlayer_filename) $ui_textsmall
                    ]

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Detail:" $ui_textsmall

                    if (= (getvdetail $ui_tex_current_slot) 0) [
                        uitextright "None" $ui_textsmall
                    ] [
                        ui_tex_current_slot_vdetail_filename = (gettexname (getvdetail $ui_tex_current_slot))
                        while [!= (strstr $ui_tex_current_slot_vdetail_filename "/") -1] [ui_tex_current_slot_vdetail_filename = (substring $ui_tex_current_slot_vdetail_filename (+ 1 (strstr $ui_tex_current_slot_vdetail_filename "/")))]
                        uitextright (format "#%1, %2" (getvdetail $ui_tex_current_slot) $ui_tex_current_slot_vdetail_filename) $ui_textsmall
                    ]

                    uiclamp 1 1
                ]

                loop i (listlen $ui_tex_vshaderparam_types) [
                    if (!=s (getvshaderparam $ui_tex_current_slot (at $ui_tex_vshaderparam_types $i)) "") [
                        uihlist 0 [
                            uitextleft (format "%1:" (at $ui_tex_vshaderparam_types $i)) $ui_textsmall
                            uitextright (getvshaderparam $ui_tex_current_slot (at $ui_tex_vshaderparam_types $i)) $ui_textsmall

                            uiclamp 1 1
                        ]
                    ]
                ]


                uispace 0.115 0.000
                uiclamp 1 1 1 1
            ]

            uivlist $ui_padsmall [
                uicheckbox "Show variants" $ui_tex_show_all $ui_checksize [ui_tex_show_all = (! $ui_tex_show_all)]
                uiinput ui_tex_search_criteria 20 [] 1 1 "Search textures..." $colourgrey
            ]
        ]

        uihlist $ui_padsmall [
            uiscroll 1.0546875 0.65625 [
                uifill 1.0546875 0.65625 [
                    uigrid 8 0.0078125 0.0078125 [
                        uihover [] [ui_tex_current_slot = (getseltex)]
                        if (= $ui_tex_show_all 1) [
                            looptexmru tex [
                                if (|| (=s $ui_tex_search_criteria "") (!= (strstr (gettexname $tex) $ui_tex_search_criteria) -1)) [
                                    uivslotview $tex 0.125 0.125 [
                                        if (= $tex (getseltex)) [uioutline 0xFF0000; uiclamp- 1 1 1 1] // Outline currently selected texture
                                        uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1; ui_tex_current_slot = $tex]]
                                        uirelease [uiclose "textures"; settex $tex]
                                    ]
                                ]
                            ]
                        ] [
                            loop slot (numslots) [
                                if (|| (=s $ui_tex_search_criteria "") (!= (strstr (gettexname $slot) $ui_tex_search_criteria) -1)) [
                                    uislotview $slot 0.125 0.125 [
                                        if (= $slot (getseltex)) [uioutline 0xFF0000; uiclamp- 1 1 1 1] // Outline currently selected texture
                                        uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1; ui_tex_current_slot = $slot]]
                                        uirelease [uiclose "textures"; settex (getslottex $slot)]
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uialign* -1 -1
                ]
            ]
            uivscroll $ui_slidersize 0.65625 3
        ]
    ]
]
