if (getalias mapfavs) [] [mapfavs = ""]
setpersist mapfavs 1
setcomplete mapfavs 1
if (getalias gamefavs) [] [gamefavs = ""]
setpersist gamefavs 1
setcomplete gamefavs 1

ui_maps_init = [
    octapaks [
        ui_maps_selected = 0
        ui_maps_muts = 0
        ui_maps_mode = -1
        ui_maps_lastmode = -1
        ui_maps_lastmuts = 0
        ui_maps_reset = 1
        ui_maps_index = 0
        ui_maps_num = -1
        ui_maps_cur = ""
        ui_maps_file = ""
        ui_maps_list = ""
        ui_maps_path = ""
        ui_maps_extra = ""
        ui_maps_img = "textures/emblem"
        ui_maps_octa = 0
        ui_maps_filter = 0
        ui_maps_nummodes = 0
        ui_maps_curmode = 0
        loop g (- $modeidxnum 1) [
            q = (+ $g 1)
            if (! (ismodelocked $q)) [
                ui_maps_nummodes = (+ $ui_maps_nummodes 1)
                ui_maps_curmode = $q
            ]
        ]
        if (= $ui_maps_nummodes 1) [ui_maps_mode = $ui_maps_curmode]
    ]
]
ui_maps_init

ui_maps_iter = [
    if (|| [!= $ui_maps_lastmode $ui_maps_mode] [!= $ui_maps_lastmuts $ui_maps_muts]) [ui_maps_reset = 1]
    if (= $ui_maps_reset 1) [
        ui_maps_rot = 0
        ui_maps_list = ""
        ui_maps_path = ""
        if (= $ui_maps_mode $modeidxdemo) [
            ui_maps_muts = 0
            ui_maps_filelist = (sortlist (listfiles demos dmo) a b [<s $a $b])
            looplist ui_maps_lcurmap $ui_maps_filelist [
                if (< (listfind ui_maps_pcurmap $ui_maps_curprev [strcmp $ui_maps_pcurmap $ui_maps_lcurmap]) 0) [
                    append ui_maps_list $ui_maps_lcurmap
                    append ui_maps_path [demos/@ui_maps_lcurmap]
                ]
            ]
            ui_maps_rot = (listlen $ui_maps_list)
            append ui_maps_list "?"
            append ui_maps_path "?"
        ] [
            ui_maps_muts = (mutscheck $ui_maps_mode $ui_maps_muts)
            append ui_maps_list "<random>"
            append ui_maps_path "<random>"
            mapfavs = (sortlist $mapfavs a b [<s $a $b])
            ui_maps_curlist = (sortlist (? (>= $ui_maps_mode $modeidxplay) (getmaplist $ui_maps_mode $ui_maps_muts (? (isonline) (listlen (listclients 1)) 0)) $allowmaps) a b [<s $a $b])
            ui_maps_curprev = (sortlist (sublist $previousmaps 0 $maphistory) a b [<s $a $b])
            loop q 2 [
                looplist ui_maps_lcurmap $ui_maps_curlist [
                    if (< (listfind ui_maps_pcurmap $ui_maps_curprev [strcmp $ui_maps_pcurmap $ui_maps_lcurmap]) 0) [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_path [maps/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            ui_maps_rot = (listlen $ui_maps_list)
            ui_maps_wantlist = 1
            looplist ui_maps_lcurmap $ui_maps_curprev [
                if $ui_maps_wantlist [
                    append ui_maps_list "~"
                    append ui_maps_path "~"
                    ui_maps_wantlist = 0
                ]
                append ui_maps_list $ui_maps_lcurmap
                append ui_maps_path [maps/@ui_maps_lcurmap]
            ]
            ui_maps_wantlist = 1
            ui_maps_filelist = (sortlist (listfiles maps mpz) a b [<s $a $b])
            loop q 2 [
                looplist ui_maps_lcurmap $ui_maps_filelist [
                    if (< (listfind ui_maps_mcurmap $ui_maps_list [strcmp $ui_maps_mcurmap $ui_maps_lcurmap]) 0) [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            if $ui_maps_wantlist [
                                append ui_maps_list "*"
                                append ui_maps_path "*"
                                ui_maps_wantlist = 0
                            ]
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_path [maps/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& [> $ui_maps_octa 0] [> $hasoctapaks 0]) [
                ui_maps_wantlist = 1
                ui_maps_filelist = (sortlist (listfiles base ogz) a b [<s $a $b])
                loop q 2 [
                    looplist ui_maps_lcurmap $ui_maps_filelist [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            if $ui_maps_wantlist [
                                append ui_maps_list "."
                                append ui_maps_path "."
                                ui_maps_wantlist = 0
                            ]
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_path [base/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& (= $ui_maps_filter 1) (> (strlen $ui_maps_extra) 0)) [
                // apply the search filter and keep special tags $t
                ui_maps_t = "~*.?"
                ui_maps_list = (listfilter ui_maps_xcurmap $ui_maps_list [(|| (> (strstr $ui_maps_xcurmap $ui_maps_extra) -1) (> (strstr $ui_maps_t $ui_maps_xcurmap) -1))])
                ui_maps_path = (listfilter ui_maps_xcurmap $ui_maps_path [(|| (> (strstr $ui_maps_xcurmap $ui_maps_extra) -1) (> (strstr $ui_maps_t $ui_maps_xcurmap) -1))])
                // if there are no maps between two tags, remove the first tag
                // if no matching maps are available to vote, begin with a "-" tag
                if (> (strstr $ui_maps_t (at $ui_maps_list 0)) -1) [
                    ui_maps_xcurmap = "-"
                    ui_maps_pcurmap = "-"
                ] [
                    ui_maps_xcurmap = ""
                    ui_maps_pcurmap = ""
                ]
                loop i (listlen $ui_maps_list) [
                    if (|| (= (strstr $ui_maps_t (at $ui_maps_list $i)) -1) (= (strstr $ui_maps_t (at $ui_maps_list (+ 1 $i))) -1)) [
                        append ui_maps_xcurmap (at $ui_maps_list $i)
                        append ui_maps_pcurmap (at $ui_maps_path $i)
                    ]
                ]
                ui_maps_list = $ui_maps_xcurmap
                ui_maps_path = $ui_maps_pcurmap
            ]
            append ui_maps_list "?"
            append ui_maps_path "?"
            ui_maps_num = (listfind ui_maps_curmap $ui_maps_list [
                || [=s $ui_maps_curmap $ui_maps_cur] [=s [maps/@ui_maps_curmap] $ui_maps_cur] [
                    && [> $hasoctapaks 0] [> $ui_maps_octa 0] [=s [base/@ui_maps_curmap] $ui_maps_cur]
                ]
            ])
        ]
        ui_maps_count = (listlen $ui_maps_list)
        if (! $ui_maps_rot) [ui_maps_rot = $ui_maps_count]
        ui_maps_lastmode = $ui_maps_mode
        ui_maps_lastmuts = $ui_maps_muts
        ui_maps_index = 0
        ui_maps_reset = 0
    ]
    if (|| [< $ui_maps_num 0] [>= $ui_maps_num $ui_maps_count]) [
        ui_maps_num = (? (= $ui_maps_mode $modeidxdemo) -1 0)
        ui_maps_cur = ""
        ui_maps_file = ""
        ui_maps_dmo = ""
        ui_maps_img = "textures/emblem"
    ] [
    cases (at $ui_maps_list $ui_maps_num) "?" [
        ui_maps_cur = $ui_maps_extra
        if (= $ui_maps_mode $modeidxdemo) [
            ui_maps_file = [demos/@ui_maps_cur]
            ui_maps_dmo = (demoscan (format "%1.dmo" $ui_maps_img))
            ui_maps_img = (? (>= $ui_maps_dmo 0) (format "maps/%1" (demoinfo $ui_maps_dmo 1)) "textures/emblem")
        ] [
            ui_maps_file = $mapextra
            ui_maps_img = [maps/@ui_maps_extra]
        ]
    ] "<random>" [
        ui_maps_cur = "<random>"
        ui_maps_file = "<random>"
        ui_maps_img = "textures/icon"
    ] () [
        ui_maps_cur = (at $ui_maps_list $ui_maps_num)
        ui_maps_file = (at $ui_maps_path $ui_maps_num)
        if (= $ui_maps_mode $modeidxdemo) [
            ui_maps_dmo = (demoscan (format "%1.dmo" $ui_maps_file))
            ui_maps_img = (? (>= $ui_maps_dmo 0) (format "maps/%1" (demoinfo $ui_maps_dmo 1)) "textures/emblem")
        ] [
            ui_maps_img = (at $ui_maps_path $ui_maps_num)
        ]
    ]]
]

ui_maps_mutsvar = [
    [ui_tip_maps_muts_disable_@arg4] = (|| (< $[@arg2] 0) (& (mutsimplied $[@arg2] $[@arg3]) $arg4) (& $mutslockforce $arg4) (ismodelocked $[@arg2] (| $[@arg3] $arg4) $arg4 $[@arg5]))
    uicheckbox $arg1 [? (|| (& $[@@arg3] [@@arg4]) (& (mutsimplied $[@@arg2] $[@@arg3]) [@@arg4]) (& $mutslockforce [@@arg4])) 1 0] $ui_checksize [
        if (& $[@@arg3] [@@arg4]) [
            [@@@arg3] = (mutscheck $[@@@arg2] (&~ $[@@@arg3] [@@@arg4]))
        ] [
            [@@@arg3] = (mutscheck $[@@@arg2] (| $[@@@arg3] [@@@arg4]) [@@@arg4])
        ]
    ] 0 0 0 [
        if (= $[ui_tip_maps_muts_disable_@@arg4] 0) [
            [ui_tip_maps_muts_@@@arg4] = [
                uihlist 0 [
                    uiimage (at (modetexlist $[@@@@@arg2] [@@@@@arg4] 1) -1) $ui_default 0 $ui_imgtip $ui_imgtip "textures/hud/alert"
                    uispace $ui_padtiny
                    uitext (mutsdesc $[@@@@@arg2] [@@@@@arg4] 3) $ui_texttip
                ]
            ]
            uitooltip [maps_muts_@@arg4]
        ]
        uialign -1
    ] $[ui_tip_maps_muts_disable_@arg4]
]

ui_maps_modevar = [
    [ui_tip_maps_mode_disable_@arg4] = (ismodelocked $arg4 $[@arg3] 0 $[@arg5])
    uiradio $arg1 [= $[@@arg2] [@@arg4]] $ui_checksize [[@@arg2] = [@@arg4]] 0 0 [
        if (= $[ui_tip_maps_mode_disable_@@arg4] 0) [
            [ui_tip_maps_mode_@@@arg4] = [
                uihlist 0 [
                    uiimage (modetexlist [@@@@@arg4] [@@@@@arg5] -1) $ui_default 0 $ui_imgtip $ui_imgtip "textures/hud/alert"
                    uispace $ui_padtiny
                    uitext (modedesc [@@@@@arg4] [@@@@@arg5] 3) $ui_texttip
                ]
            ]
            uitooltip [maps_mode_@@arg4]
        ]
        uialign -1
    ] $[ui_tip_maps_mode_disable_@arg4]
]

ui_maps_exec = [
    sleep 1 [
        if (isconnected) [conout "open vote menu"]
        start @arg1 @arg2 @arg3
    ]
]

uimenu "maps" "Select a Map and Mode" "textures/editing" [
    ui_maps_iter
    octapaks [
        uivlist $ui_padbutton [
            uihlist $ui_padbutton [
                uialign -1 -1
                uivlist $ui_padbutton [
                    uialign -1 -1
                    uihlist $ui_padbutton [
                        uialign -1 -1
                        if (< $ui_maps_lastmode 0) [
                            uiimage "textures/question" $ui_default 0 $ui_doubleh $ui_doubleh
                            uivlist $ui_padbutton [
                                uialign -1 0
                                uitextleft "Game Select" $ui_text
                                uitextleft "Please select a map and mode to continue." $ui_textsmall
                            ]
                        ] [
                            ui_maps_gname = (gamename $ui_maps_mode $ui_maps_muts 0 36)
                            if (&& (strlen $ui_maps_cur) (= $ui_maps_mode $modeidxdemo)) [
                                ui_maps_dinfo = (demoscan (format "%1.dmo" $ui_maps_file))
                                ui_maps_dmode = (demoinfo $ui_maps_dinfo 2)
                                ui_maps_dmuts = (demoinfo $ui_maps_dinfo 3)
                                ui_maps_dname = (gamename $ui_maps_dmode $ui_maps_dmuts 0 30)
                                ui_maps_gname = (concat $ui_maps_dname "(demo)")
                                uiimage (modetexlist $ui_maps_dmode $ui_maps_dmuts -1) $ui_default 0 $ui_doubleh $ui_doubleh "textures/hud/alert"
                            ] [
                                uiimage (modetexlist $ui_maps_mode $ui_maps_muts -1) $ui_default 0 $ui_doubleh $ui_doubleh "textures/hud/alert"
                            ]
                            uivlist $ui_padbutton [
                                uialign -1 0
                                uitextleft (format "^fy%1" $ui_maps_gname) $ui_text
                                if (strlen $ui_maps_cur) [
                                    if (= $ui_maps_mode $modeidxdemo) [
                                        ui_maps_dinfo = (demoscan (format "%1.dmo" $ui_maps_file))
                                        ui_maps_dmapname = (demoinfo $ui_maps_dinfo 1)
                                        uitextleft (format "^farecorded on ^fo%1" $ui_maps_dmapname) $ui_textsmall
                                    ] [
                                        uitextleft (format "^faselected on ^fo%1" $ui_maps_cur) $ui_textsmall
                                    ]
                                ] [uitextleft "Please select a map to continue." $ui_textsmall]
                            ]
                        ]
                    ]
                    uihlist $ui_padbutton [
                        uialign -1 -1
                        uivlist 0 [
                            uialign -1 -1
                            uitextleft "Mode:" $ui_text
                            loop z $modeidxnum [
                                ui_maps_modevar (at $modename $z) ui_maps_mode ui_maps_muts $z ui_maps_cur
                            ]
                        ]
                        uivlist 0 [
                            uialign -1 -1
                            uiimgbutton (? (!=s $ui_maps_img "") $ui_maps_img "textures/emblem") 0.16 0.16 [
                                ui_maps_exec [@@ui_maps_cur] [@@ui_maps_mode] [@@ui_maps_muts]
                                if (&& [@@ui_maps_mode] (getclientcount) (! (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto"))) [uiopen "votes"] [uiclose "maps"]
                            ] 0 [] (|| (< $ui_maps_mode 0) (=s $ui_maps_cur "")) $ui_padbutton [
                                ui_maps_fav = (format "%1.%2.%3" [@@ui_maps_cur] [@@ui_maps_mode] [@@ui_maps_muts])
                                gamefavs = (concat $ui_maps_fav (listdel $gamefavs $ui_maps_fav)) 
                            ] $ui_default
                        ]
                    ]
                    uivlist $ui_padbutton [
                        uialign -1 -1
                        uitextleft "Mutators:" $ui_text
                        ui_maps_cnt = (- $mutsidxnum $mutsidxgsn)
                        uigrid 3 0 0 [
                            uialign -1 -1
                            loop n $ui_maps_cnt [
                                uihlist 0 [uialign -1 -1; ui_maps_mutsvar (at $mutsname $n) ui_maps_mode ui_maps_muts (<< 1 $n) ui_maps_cur; uispace $ui_padbutton]
                            ]
                            if (> (strlen (gspmutname $ui_maps_mode 0)) 0) [
                                loop n $mutsidxgsn [
                                    ui_maps_curmut = (gspmutname $ui_maps_mode $n)
                                    if (strlen $ui_maps_curmut) [
                                        uihlist 0 [uialign -1 -1; ui_maps_mutsvar $ui_maps_curmut ui_maps_mode ui_maps_muts (<< 1 (+ $mutsidxgsp $n)) ui_maps_cur; uispace $ui_padbutton]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
                uivlist 0 [
                    uihlist $ui_padsmall [
                        uiclamp 1 1 1 1
                        uialign -1 -1
                        uiscroll 0.31 0.338 [
                            //uihlist 0 [uispace 0.3]
                            uialign -1 -1
                            ui_maps_nummaps = (- (listlen $ui_maps_list) 1)
                            uivlist 0 [
                                uiclamp 1 1 1 1
                                uialign -1 -1
                                ui_maps_index = (min (max 0 $ui_maps_nummaps) $ui_maps_index)
                                ui_maps_num = (min $ui_maps_num $ui_maps_nummaps)
                                ui_maps_break = 0
                                loopwhile i (listlen $ui_maps_list) [= $ui_maps_break 0] [
                                    q = (+ $ui_maps_index $i)
                                    ui_maps_curmap = (at $ui_maps_list $q)
                                    cases $ui_maps_curmap "*" [
                                        uitext "^famaps not in the rotation" $ui_textsmall
                                    ] "~" [
                                        uihlist 0 [
                                            uiclamp 1 1 1 1
                                            uitextleft "famaps played recently" $ui_textsmall
                                        ]
                                    ] "." [
                                        uihlist 0 [
                                            uiclamp 1 1 1 1
                                            uitext "famaps from sauerbraten" $ui_textsmall
                                        ]
                                    ] "-" [
                                        uihlist 0 [
                                            uiclamp 1 1 1 1
                                            uitext "^fanone for this search filter" $ui_textsmall
                                        ]
                                    ] "?" [
                                        ui_maps_break = 1
                                    ] () [
                                        uihlist 0 [
                                            uiclamp 1 1 1 1
                                            uialign -1
                                            uiradio (strreplace $ui_maps_curmap $ui_maps_extra (format "^fs^fy%1^fS" $ui_maps_extra)) [= $ui_maps_num $q] $ui_radiosize [ui_maps_num = $q] 0 0 [uialign -1]
                                            if (!= $ui_maps_mode $modeidxdemo) [
                                                ui_maps_hasmap = (>= (indexof $mapfavs $ui_maps_curmap) 0)
                                                uicheckbox "" [!= [@@ui_maps_hasmap] 0] $ui_checksize [
                                                    mapfavs = (? [@@ui_maps_hasmap] (listdel $mapfavs [@@ui_maps_curmap]) (concat (listdel $mapfavs [@@ui_maps_curmap]) [@@ui_maps_curmap]))
                                                    ui_maps_reset = 1
                                                ] $ui_on4 0 $ui_default [uialign 1]
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                        uivscroll 0.02 0.338
                    ]
                    uihlist 0 [
                        uialign -1 -1
                        uispace $ui_padsmall 0 [uitext "custom map or" $ui_textsmall]
                        uicheckbox "search filter:" [!= [@@ui_maps_filter] 0] $ui_checksize [ui_maps_reset = 1; ui_maps_filter = (? [@@ui_maps_filter] 0 1)] $ui_on1 0 $ui_default [uialign -1] 0 $ui_textsmall
                    ]
                    uihlist 0 [
                        uialign -1 -1
                        ui_maps_extraval = $ui_maps_extra
                        ui_maps_extranum = $ui_maps_num
                        uiradio "" [= [@@ui_maps_num] [@@ui_maps_extranum]] $ui_radiosize [ui_maps_num = [@@ui_maps_extranum]] 0 0 [uialign -1]
                        uiinput ui_maps_extraval 22 [
                            ui_maps_extra = $ui_maps_extraval
                            ui_maps_num = $ui_maps_extranum
                            ui_maps_reset = $ui_maps_filter
                        ] 1 "" "^fd<enter map name>" [] [] $ui_maps_filter
                    ]
                ]
            ]
            uihlist $ui_padbutton [
                uialign 0 1
                uibuttonw (? $ui_maps_mode (? (getclientcount) (? (&& (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto")) "Force (VETO)" "Submit Vote") "Start Game") "Start Demo") [
                    ui_maps_exec [@@ui_maps_cur] [@@ui_maps_mode] [@@ui_maps_muts]
                    if (&& [@@ui_maps_mode] (getclientcount) (! (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto"))) [uiopen "votes"] [uiclose "maps"]
                ] (|| (< $ui_maps_mode 0) (=s $ui_maps_cur "")) $ui_ok
                uibuttonw "Reset" [ui_maps_init; ui_maps_reset = 1] 0 $ui_change
                uibuttonw "Cancel" [uiclose "maps"] 0 $ui_cancel
                uibuttonw "Show Votes" [uiopen "votes"] 0 $ui_other
            ]
        ]
    ]
] [ui_maps_init]

uimenu "votes" "Current Votes" "textures/hud/multi" [
    uivlist $ui_padbutton [
        uialign -1 -1
        ui_votes_num = (getvote)
        uihlist $ui_padbutton [
            uiclamp 1 1 1 1
            uialign -1 0
            uihlist $ui_padbutton [
                uialign -1 0
                uicheckbox "Dynamic Sort" [!= $sortvotes 0] $ui_checksize [sortvotes (! $sortvotes)] $ui_on1 0 $ui_default [uialign -1]
                uicheckbox "Cleanup List" [!= $cleanvotes 0] $ui_checksize [cleanvotes (! $cleanvotes)] $ui_on1 0 $ui_default [uialign -1]
                uibuttonz "Clear Vote" [clearvote] 0 $ui_cancel [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Show Maps" [uiopen "maps"] 0 $ui_change [] $ui_buttonzh $ui_text $ui_padsmall
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uitextright (format "^fc%1 ^fwgame%2 suggested" $ui_votes_num (? (!= $ui_votes_num 1) "s"))
            ]
        ]
        uispace $ui_padtiny
        uihlist 0 [
            uiclamp 1 1 1 1
            uialign -1 -1
            uihlist $ui_padsmall [
                uiclamp 1 1 1 1
                uialign -1 -1
                uiscroll 1 0.35 [
                    uialign -1 -1
                    uivlist 0 [
                        uihlist 0 [uispace 0.35]
                        uialign -1 -1
                        if (getvote) [
                            loop i $ui_votes_num [
                                ui_votes_players = (getvote $i 0)
                                ui_votes_mode = (getvote $i 1)
                                ui_votes_muts = (getvote $i 2)
                                ui_votes_map = (getvote $i 3)
                                ui_votes_self = 0
                                ui_votes_gname = (gamename $ui_votes_mode $ui_votes_muts 0 48)
                                [ui_tip_votes_@i] = [
                                    if (> $ui_votes_players 0) [
                                        uiwraptext "Current Votes" $ui_tipwrap $ui_texttip
                                        ui_votes_pname = ""
                                        loop j $ui_votes_players [
                                            append ui_votes_pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                        ]
                                        uiwraptext (prettylist $ui_votes_pname) $ui_tipwrap $ui_texttipsmall
                                    ] [uiwraptext "No Votes" $ui_tipwrap $ui_texttip]
                                ]
                                loop j $ui_votes_players [if (= (getclientnum) (getvote $i 0 $j)) [ui_votes_self = 1] ]
                                ui_votes_img = (? (!=s $ui_votes_map "") (? (=s $ui_votes_map "<random>") "textures/question" $ui_votes_map) "textures/emblem")
                                uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 (? $ui_votes_self $ui_on3 $ui_line) $ui_hover $ui_hold [
                                    uirelease [if (= [@@ui_votes_self] 1) [clearvote] [start [@@ui_votes_map] [@@ui_votes_mode] [@@ui_votes_muts]] ]
                                    uialtrelease [uiopen maps; ui_maps_mode = [@@ui_votes_mode]; ui_maps_muts = [@@ui_votes_muts]; ui_maps_cur = [@@ui_votes_map]]
                                    uitooltip [votes_@i]
                                    uialign -1 0
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uialign -1 0
                                        uihlist 0 [
                                            uialign -1 0
                                            uispace $ui_padsmall $ui_padsmall [
                                                uiimage $ui_votes_img $ui_default 0 $ui_imgbut $ui_imgbut "textures/emblem" [uioutline $ui_line; uiclamp- 1 1 1 1]
                                            ]
                                            uispace $ui_padsmall $ui_padtiny [
                                                uialign -1 0
                                                uivlist 0 [
                                                    uialign -1 0
                                                    uitextleft (format "^fy%1 ^favoted for on ^fo%2" $ui_votes_gname $ui_votes_map)
                                                    uihlist 0 [
                                                        uialign -1 0
                                                        if (> $ui_votes_players 0) [
                                                            uitextleft "^fwby: "
                                                            ui_votes_pname = ""
                                                            ui_votes_pmore = 0
                                                            loop j $ui_votes_players [
                                                                if (|| $ui_votes_pmore (> (strlen (stripcolors $ui_votes_pname)) 128)) [ui_votes_pmore = (+ $ui_votes_pmore 1)] [
                                                                    append ui_votes_pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                                                ]
                                                            ]
                                                            uiwraptext (concat (prettylist $ui_votes_pname) (? $ui_votes_pmore (concat "and^fy" $ui_votes_pmore "^fwmore"))) 0.6 $ui_textsmall -1 [uialign -1 0]
                                                        ] [uitextleft "^fano current votes"]
                                                    ]
                                                ]
                                            ]
                                        ]
                                    ]
                                ] [uiclamp 1 1 1 1]
                            ]
                        ] [
                            uialign -1 0
                            uiclamp 1 1 1 1
                            uispace $ui_padsmall $ui_padsmall [
                                uialign -1 0
                                uitextleft "There are no current votes."
                            ]
                        ]
                    ]
                ]
                uivscroll 0.02 0.35
                uialign- 1 -1
            ]
        ]
    ]
]
