if (getalias mapfavs) [] [mapfavs = ""]
setpersist mapfavs 1
setcomplete mapfavs 1
if (getalias demofavs) [] [demofavs = ""]
setpersist demofavs 1
setcomplete demofavs 1
if (getalias gamefavs) [] [gamefavs = ""]
setpersist gamefavs 1
setcomplete gamefavs 1

ui_maps_init = [
    octapaks [
        ui_maps_selected = 0
        ui_maps_muts = 0
        ui_maps_mode = -1
        ui_maps_lastmode = -1
        ui_maps_lastmuts = 0
        ui_maps_reset = 1
        ui_maps_index = 0
        ui_maps_num = -1
        ui_maps_cur = ""
        ui_maps_file = ""
        ui_maps_list = ""
        ui_maps_path = ""
        ui_maps_extra = ""
        ui_maps_img = "textures/emblem"
        ui_maps_octa = 0
        ui_maps_filter = 0
        ui_maps_nummodes = 0
        ui_maps_curmode = 0
        loop g (- $modeidxnum 1) [
            q = (+ $g 1)
            if (! (ismodelocked $q)) [
                ui_maps_nummodes = (+ $ui_maps_nummodes 1)
                ui_maps_curmode = $q
            ]
        ]
        if (= $ui_maps_nummodes 1) [ ui_maps_mode = $ui_maps_curmode ]
    ]
]
ui_maps_init

ui_maps_iter = [
    if (|| [!= $ui_maps_lastmode $ui_maps_mode] [!= $ui_maps_lastmuts $ui_maps_muts]) [ ui_maps_reset = 1 ]
    if (= $ui_maps_reset 1) [
        ui_maps_rot = 0
        ui_maps_list = ""
        ui_maps_path = ""
        if (= $ui_maps_mode $modeidxdemo) [
            ui_maps_muts = 0
            demofavs = (sortlist $demofavs a b [<s $a $b])
            ui_maps_filelist = (sortlist (listfiles demos dmo) a b [<s $a $b])
            loop q 2 [
                looplist ui_maps_lcurmap $ui_maps_filelist [
                    if (? $q (< (listfind ui_maps_xcurmap $demofavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $demofavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                        append ui_maps_list $ui_maps_lcurmap
                        append ui_maps_path [demos/@ui_maps_lcurmap]
                    ]
                ]
            ]
            ui_maps_rot = (listlen $ui_maps_list)
            append ui_maps_list "?"
            append ui_maps_path "?"
        ] [
            ui_maps_muts = (mutscheck $ui_maps_mode $ui_maps_muts)
            append ui_maps_list "<random>"
            append ui_maps_path "<random>"
            mapfavs = (sortlist $mapfavs a b [<s $a $b])
            ui_maps_curlist = (sortlist (? (>= $ui_maps_mode $modeidxplay) (getmaplist $ui_maps_mode $ui_maps_muts (? (isonline) (listlen (listclients 1)) 0)) $allowmaps) a b [<s $a $b])
            ui_maps_curprev = (sortlist (sublist $previousmaps 0 $maphistory) a b [<s $a $b])
            loop q 2 [
                looplist ui_maps_lcurmap $ui_maps_curlist [
                    if (< (listfind ui_maps_pcurmap $ui_maps_curprev [strcmp $ui_maps_pcurmap $ui_maps_lcurmap]) 0) [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_path [maps/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            ui_maps_rot = (listlen $ui_maps_list)
            ui_maps_wantlist = 1
            looplist ui_maps_lcurmap $ui_maps_curprev [
                if $ui_maps_wantlist [
                    append ui_maps_list "~"
                    append ui_maps_path "~"
                    ui_maps_wantlist = 0
                ]
                append ui_maps_list $ui_maps_lcurmap
                append ui_maps_path [maps/@ui_maps_lcurmap]
            ]
            ui_maps_wantlist = 1
            ui_maps_filelist = (sortlist (listfiles maps mpz) a b [<s $a $b])
            loop q 2 [
                looplist ui_maps_lcurmap $ui_maps_filelist [
                    if (< (listfind ui_maps_mcurmap $ui_maps_list [strcmp $ui_maps_mcurmap $ui_maps_lcurmap]) 0) [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            if $ui_maps_wantlist [
                                append ui_maps_list "*"
                                append ui_maps_path "*"
                                ui_maps_wantlist = 0
                            ]
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_path [maps/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& [> $ui_maps_octa 0] [> $hasoctapaks 0]) [
                ui_maps_wantlist = 1
                ui_maps_filelist = (sortlist (listfiles base ogz) a b [<s $a $b])
                loop q 2 [
                    looplist ui_maps_lcurmap $ui_maps_filelist [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [strcmp $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            if $ui_maps_wantlist [
                                append ui_maps_list "."
                                append ui_maps_path "."
                                ui_maps_wantlist = 0
                            ]
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_path [base/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& (= 1 $ui_maps_filter) (> (strlen $ui_maps_extra) 0)) [
                // apply the search filter and keep special tags $t
                ui_maps_t = "~*.?"
                ui_maps_list = (listfilter ui_maps_xcurmap $ui_maps_list [ (|| (> (strstr $ui_maps_xcurmap $ui_maps_extra) -1) (> (strstr $ui_maps_t $ui_maps_xcurmap) -1))])
                ui_maps_path = (listfilter ui_maps_xcurmap $ui_maps_path [ (|| (> (strstr $ui_maps_xcurmap $ui_maps_extra) -1) (> (strstr $ui_maps_t $ui_maps_xcurmap) -1))])
                // if there are no maps between two tags, remove the first tag
                // if no matching maps are available to vote, begin with a "-" tag
                if (> (strstr $ui_maps_t (at $ui_maps_list 0)) -1) [
                    ui_maps_xcurmap = "-"
                    ui_maps_pcurmap = "-"
                ] [
                    ui_maps_xcurmap = ""
                    ui_maps_pcurmap = ""
                ]
                loop i (listlen $ui_maps_list) [
                    if (|| (= (strstr $ui_maps_t (at $ui_maps_list $i)) -1) (= (strstr $ui_maps_t (at $ui_maps_list (+ 1 $i))) -1)) [
                        append ui_maps_xcurmap (at $ui_maps_list $i)
                        append ui_maps_pcurmap (at $ui_maps_path $i)
                    ]
                ]
                ui_maps_list = $ui_maps_xcurmap
                ui_maps_path = $ui_maps_pcurmap
            ]
            append ui_maps_list "?"
            append ui_maps_path "?"
            ui_maps_num = (listfind ui_maps_curmap $ui_maps_list [
                || [=s $ui_maps_curmap $ui_maps_cur] [=s [maps/@ui_maps_curmap] $ui_maps_cur] [
                    && [> $hasoctapaks 0] [> $ui_maps_octa 0] [=s [base/@ui_maps_curmap] $ui_maps_cur]
                ]
            ])
        ]
        ui_maps_count = (listlen $ui_maps_list)
        if (! $ui_maps_rot) [ ui_maps_rot = $ui_maps_count ]
        ui_maps_lastmode = $ui_maps_mode
        ui_maps_lastmuts = $ui_maps_muts
        ui_maps_index = 0
        ui_maps_reset = 0
    ]
    if (|| [< $ui_maps_num 0] [>= $ui_maps_num $ui_maps_count]) [
        ui_maps_num = (? (= $ui_maps_mode $modeidxdemo) -1 0)
        ui_maps_cur = ""
        ui_maps_file = ""
        ui_maps_dmo = ""
        ui_maps_img = "textures/emblem"
    ] [
    cases (at $ui_maps_list $ui_maps_num) "?" [
        ui_maps_cur = $ui_maps_extra
        if (= $ui_maps_mode $modeidxdemo) [
            ui_maps_file = [demos/@ui_maps_cur]
            ui_maps_dmo = (demoscan (format "%1.dmo" $ui_maps_img))
            ui_maps_img = (? (>= $ui_maps_dmo 0) (format "maps/%1" (demoinfo $ui_maps_dmo 1)) "textures/emblem")
        ] [
            ui_maps_file = $mapextra
            ui_maps_img = [maps/@ui_maps_extra]
        ]
    ] "<random>" [
        ui_maps_cur = "<random>"
        ui_maps_file = "<random>"
        ui_maps_img = "textures/question"
    ] () [
        ui_maps_cur = (at $ui_maps_list $ui_maps_num)
        ui_maps_file = (at $ui_maps_path $ui_maps_num)
        if (= $ui_maps_mode $modeidxdemo) [
            ui_maps_dmo = (demoscan (format "%1.dmo" $ui_maps_file))
            ui_maps_img = (? (>= $ui_maps_dmo 0) (format "maps/%1" (demoinfo $ui_maps_dmo 1)) "textures/emblem")
        ] [
            ui_maps_img = (at $ui_maps_path $ui_maps_num)
        ]
    ] ]
]

ui_maps_mutsvar = [
    uicheckbox $arg1 [? (|| (& $[@@arg3] [@@arg4]) (& (mutsimplied $[@@arg2] $[@@arg3]) [@@arg4]) (& $mutslockforce [@@arg4])) 1 0] $ui_checksize [
        if (& $[@@arg3] [@@arg4]) [
            [@@@arg3] = (mutscheck $[@@@arg2] (- $[@@@arg3] $[@@@arg3]))
        ] [
            [@@@arg3] = (mutscheck $[@@@arg2] (| $[@@@arg3] [@@@arg4]) [@@@arg4])
        ]
    ] 0 0 0 [uialign -1] (|| (< $[@arg2] 0) (& (mutsimplied $[@arg2] $[@arg3]) $arg4) (& $mutslockforce $arg4) (ismodelocked $[@arg2] (| $[@arg3] $arg4) $arg4 $[@arg5]))
]

ui_maps_modevar = [
    uiradio $arg1 [= $[@@arg2] [@@arg4]] $ui_checksize [ [@@arg2] = [@@arg4] ] 0 0 [uialign -1] (ismodelocked $arg4 $[@arg3] 0 $[@arg5])
]

ui_maps_exec = [
    sleep 1 [
        if (isconnected) [ conout "open vote menu" ]
        start @arg1 @arg2 @arg3
    ]
]

uimenu "maps" "Select a Map and Mode" [
    ui_maps_iter
    octapaks [
        uivlist $ui_padbutton [
            uihlist $ui_padbutton [
                uialign -1 -1
                uivlist $ui_padbutton [
                    uialign -1 -1
                    uivlist $ui_padbutton [
                        uialign -1 -1
                        if (< $ui_maps_lastmode 0) [
                            uitextleft "Game Select" $ui_text
                            uitextleft "Please select a Map and Mode to continue." $ui_textsmall
                        ] [
                            ui_maps_gname = (gamename $ui_maps_mode $ui_maps_muts 0 32)
                            if (&& (strlen $ui_maps_cur) (= $ui_maps_mode $modeidxdemo)) [
                                ui_maps_dinfo = (demoscan (format "%1.dmo" $ui_maps_file))
                                ui_maps_dmode = (demoinfo $ui_maps_dinfo 2)
                                ui_maps_dmuts = (demoinfo $ui_maps_dinfo 3)
                                ui_maps_dname = (gamename $ui_maps_dmode $ui_maps_dmuts 0 26)
                                ui_maps_gname = (concat $ui_maps_dname "(demo)")
                            ]
                            uitextleft $ui_maps_gname $ui_text
                            uihlist 0 [
                                uialign -1 -1
                                if (strlen $ui_maps_cur) [
                                    if (= $ui_maps_mode $modeidxdemo) [
                                        ui_maps_dinfo = (demoscan (format "%1.dmo" $ui_maps_file))
                                        ui_maps_dmapname = (demoinfo $ui_maps_dinfo 1)
                                        uitextleft " ^farecorded on " $ui_textsmall
                                        uitextleft $ui_maps_dmapname $ui_textsmall
                                    ] [
                                        uitextleft " ^faselected on " $ui_textsmall
                                        uitextleft $ui_maps_cur $ui_textsmall
                                    ]
                                ] [ uitextleft "Please select a map to continue" $ui_textsmall ]
                            ]
                        ]
                    ]
                    uihlist $ui_padbutton [
                        uialign -1 -1
                        uivlist 0 [
                            uialign -1 -1
                            uitextleft "Mode:" $ui_text
                            loop z $modeidxnum [
                                ui_maps_modevar (at $modename $z) ui_maps_mode ui_maps_muts $z ui_maps_cur
                            ]
                        ]
                        uivlist 0 [
                            uialign -1 -1
                            uiimgbutton (? (!=s $ui_maps_img "") $ui_maps_img "textures/emblem") 0.16 0.16 [
                                if [@@ui_maps_cur] [ ui_maps_exec [@@@ui_maps_cur] [@@@ui_maps_mode] [@@@ui_maps_muts] ]
                            ] 0 [] 0 $ui_padbutton [
                                if [@@ui_maps_cur] [
                                    ui_maps_fav = (format "%1.%2.%3" [@@@ui_maps_cur] [@@@ui_maps_mode] [@@@ui_maps_muts])
                                    gamefavs = (concat $ui_maps_fav (listdel $gamefavs $ui_maps_fav)) 
                                ]
                            ] $ui_default
                        ]
                    ]
                    uivlist $ui_padbutton [
                        uialign -1 -1
                        uitextleft "Mutators:" $ui_text
                        ui_maps_cnt = (- $mutsidxnum $mutsidxgsn)
                        uigrid 3 0 0 [
                            uialign -1 -1
                            loop n $ui_maps_cnt [
                                uihlist 0 [ uialign -1 -1; ui_maps_mutsvar (at $mutsname $n) ui_maps_mode ui_maps_muts (<< 1 $n) ui_maps_cur; uispace $ui_padbutton ]
                            ]
                            if (> (strlen (gspmutname $ui_maps_mode 0)) 0) [
                                loop n $mutsidxgsn [
                                    ui_maps_curmut = (gspmutname $ui_maps_mode $n)
                                    if (strlen $ui_maps_curmut) [
                                        uihlist 0 [ uialign -1 -1; ui_maps_mutsvar $ui_maps_curmut ui_maps_mode ui_maps_muts (<< 1 (+ $mutsidxgsp $n)) ui_maps_cur; uispace $ui_padbutton ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
                uihlist $ui_padbutton [
                    uiscroll 0.5 0.4 [
                        ui_maps_nummaps = (- (listlen $ui_maps_list) 1)
                        uivlist 0 [
                            uiclamp 1 1 1 1
                            uialign -1 -1
                            ui_maps_index = (min (max 0 $ui_maps_nummaps) $ui_maps_index)
                            ui_maps_num = (min $ui_maps_num $ui_maps_nummaps)
                            ui_maps_break = 0
                            loopwhile i (listlen $ui_maps_list) [= $ui_maps_break 0] [
                                q = (+ $ui_maps_index $i)
                                ui_maps_curmap = (at $ui_maps_list $q)
                                cases $ui_maps_curmap "*" [
                                    uitext "maps not in the rotation:"
                                ] "~" [
                                    uitext "maps played recently:"
                                ] "." [
                                    uitext "maps from sauerbraten:"
                                ] "-" [
                                    uitext "^fanone for this search filter" "textures/info"
                                ] "?" [
                                    ui_maps_break = 1
                                ] () [
                                    uispace $ui_padbutton $ui_padtiny [
                                        uiclamp 1 1 1 1
                                        uialign -1 0
                                        uiradio (strreplace $ui_maps_curmap $ui_maps_extra (format "^fs^fy%1^fS" $ui_maps_extra)) [= $ui_maps_num $q] $ui_radiosize [ui_maps_num = $q] 0 0 [uialign -1]
                                        //ui_maps_hasmap = (>= (indexof (? (= $ui_maps_mode $modeidxdemo) $demofavs $mapfavs) $ui_maps_curmap) 0)
                                        //guibutton "" [
                                        //    if (= $ui_maps_mode $modeidxdemo) [
                                        //        demofavs = (? @@ui_maps_hasmap (listdel $demofavs @@ui_maps_curmap) (concat (listdel $demofavs @@ui_maps_curmap) @@ui_maps_curmap))
                                        //    ] [
                                        //        mapfavs = (? @@ui_maps_hasmap (listdel $mapfavs @@ui_maps_curmap) (concat (listdel $mapfavs @@ui_maps_curmap) @@ui_maps_curmap))
                                        //    ]
                                        //    ui_maps_reset = 1
                                        //] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $ui_maps_hasmap "checkboxon" "") $guicheckboxcolour
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uivscroll 0.02 0.4
                ]
            ]
            uihlist $ui_padbutton [
                uialign 0 1
                uibuttonw (? $ui_maps_mode (? (getclientcount) (? (&& (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto")) "Force (VETO)" "Submit Vote") "Start Game") "Start Demo") [
                    uiclose "maps"
                    ui_maps_exec $ui_maps_cur $ui_maps_mode $ui_maps_muts
                ] (|| (< $ui_maps_mode 0) (=s $ui_maps_cur "")) $ui_ok
                uibuttonw "Reset" [ ui_maps_init; ui_maps_reset = 1 ] 0 $ui_change
                uibuttonw "Cancel" [ uiclose "maps" ] 0 $ui_cancel
            ]
        ]
    ]
] [ ui_maps_init ]