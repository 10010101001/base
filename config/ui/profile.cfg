ui_profile_loadweaps = [
    lwa = ""
    lwl = (listlen $ui_profile_prev_loadweap)
    lwo = (? (> $lwl $arg1) (at $ui_profile_prev_loadweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $ui_profile_prev_loadweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [lw3 = $lwo]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ui_profile_prev_loadweap = $lwa]
    ui_profile_prev_weap = $arg2
]

ui_profile_randweaps = [
    rwa = ""
    rwl = (listlen $ui_profile_prev_randweap)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $ui_profile_prev_randweap $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ui_profile_prev_randweap = $rwa]
]

// these names should match the types in config/vanities.cfg
ui_profile_vanity_name = [chest eyes scalp forehead ears face back [right foot] [left foot] [right shoulder] [left shoulder]]
ui_profile_vanity_names = [Chest Eyes Scalp Forehead Ears Face Back [Right Foot] [Left Foot] [Right Shoulder] [Left Shoulder]]
ui_profile_vanity_type = -1
ui_profile_vanity_sel = -1

uimenu "vanity" "Choose Vanity Item" $questiontex [
    uivlist $ui_padbutton [
        uitext (format "Which vanity item for your ^fs^fy%1^fS?" (at $ui_profile_vanity_name $ui_profile_vanity_type))
        uichecktext "nothing" (= $ui_profile_vanity_sel -1) $ui_buttonw $ui_buttonh [
            ui_profile_vanity_sel = -1
            loop j (getvanity) [
                if (= (getvanity $j 0) $ui_profile_vanity_type) [
                    ui_profile_prev_vanity = (listdel $ui_profile_prev_vanity (getvanity $j 1))
                ]
            ]
            uiclose "vanity"
        ] $colouryellow $ui_default
        loop i (getvanity) [
            if (= (getvanity $i 0) $ui_profile_vanity_type) [
                uichecktext (getvanity $i 2) (= $ui_profile_vanity_sel $i) $ui_buttonw $ui_buttonh [
                    ui_profile_vanity_sel = $i
                    loop j (getvanity) [
                        if (= (getvanity $j 0) $ui_profile_vanity_type) [
                            ui_profile_prev_vanity = (listdel $ui_profile_prev_vanity (getvanity $j 1))
                        ]
                    ]
                    append ui_profile_prev_vanity (getvanity $i 1)
                    uiclose "vanity"
                ] $colouryellow $ui_default
            ]
        ]
    ]
] [] [] [] 2 [] 0 8

ui_profile_loadout_type = -1
ui_profile_loadout_sel = -1

uimenu "loadout" "Choose Loadout Weapon" $questiontex [
    uivlist $ui_padbutton [
        uitext (format "Which weapon for loadout slot ^fs^fy#%1^fS?" (+ $ui_profile_loadout_type 1))
        uichecktextbutton "Random" $questiontex (= $ui_profile_loadout_sel 0) $ui_buttonw $ui_buttonh [
            ui_profile_loadout_sel = 0
            ui_profile_loadweaps $ui_profile_loadout_type $ui_profile_loadout_sel
            uiclose "loadout"
        ] $colourwhite $ui_default
        loop i $weapidxloadout [
            ui_profile_loadout_idx = (+ $i $weapidxoffset)
            ui_profile_loadout_weap = (at $weapname $ui_profile_loadout_idx)
            uichecktextbutton $[@[ui_profile_loadout_weap]longname] [textures/weapons/@ui_profile_loadout_weap] (= $ui_profile_loadout_sel $ui_profile_loadout_idx) $ui_buttonw $ui_buttonh [
                ui_profile_loadout_sel = $ui_profile_loadout_idx
                ui_profile_loadweaps $ui_profile_loadout_type $ui_profile_loadout_sel
                uiclose "loadout"
            ] $[@[ui_profile_loadout_weap]colour] $ui_default
        ]
    ]
] [] [] [] 2 [] 0 8

ui_profile_pattern_sel = -1

loop i $patternidxnum [
    ui_profile_pattern_tex = (format "<crop:256,256,512,512>actors/player/pattern/%1" (at $patternnames $i))
    ui_profile_pattern_id = (at $patternnames $i)
    ui_profile_pattern_name = (at $patternlongnames $i)
    [ui_tip_profile_pattern_@[ui_profile_pattern_id]] = [
        uispace $ui_padsmall $ui_padsmall [
            uivlist $ui_padtiny [
                uiimage @@@ui_profile_pattern_tex $ui_default 0 $ui_imglist $ui_imglist [uiimagealttex "textures/nothumb"; uioutline $ui_line 0 0 [uiclamp 1 1 1 1]]
                uitext @@@ui_profile_pattern_name $ui_texttip
            ]
        ]
    ]
]

uimenu "pattern" "Choose Player Pattern" $questiontex [
    uivlist $ui_padbutton [
        uitext "Which pattern would you like to use?"
        loop i $patternidxnum [
            ui_profile_pattern_tex = (format "<crop:256,256,256,256>actors/player/pattern/%1" (at $patternnames $i))
            ui_profile_pattern_id = (at $patternnames $i)
            ui_profile_pattern_name = (at $patternlongnames $i)
            uichecktextbutton $ui_profile_pattern_name $ui_profile_pattern_tex (= $ui_profile_pattern_sel $i) $ui_buttonw $ui_buttonh [
                ui_profile_pattern_sel = $i
                ui_profile_prev_pattern = $ui_profile_pattern_sel
                uiclose "pattern"
            ] $colourwhite $ui_default [uitooltip [profile_pattern_@[ui_profile_pattern_id]]]
        ]
    ]
] [] [] [] 2 [] 0 8

ui_profile_welcome = 1
uimenu "welcome" "Welcome to Red Eclipse" $alerttex [
    uivlist $ui_padbutton [
        uitext "To get ready for action, please set up your player profile!"
        uispace 0 $ui_padsmall
        uitext "Use the this menu to choose a name, a profile colour and a pair of loadout weapons." $ui_textsmall
        uitext "To learn more about the weapons and many other aspects of the game, you can consult the help menus." $ui_textsmall
        uispace 0 $ui_padtiny
        uitext (format "You can press %1 for help, and %2 to open the menu (or close the current one)." (dobindsearch [uiopen help]) (dobindsearch [togglemainmenu]))
        uispace 0 $ui_padtiny
        uitext "During a match, this allows you to quickly look up rules and tips for the current game mode." $ui_textsmall
        uitext "Most of this information is also available on the Red Eclipse wiki, ^fs^fchttps://redeclipse.net/wiki/^fS." $ui_textsmall
        uitext "Once you have set up your profile with the ^fs^fgOK^fS button, you can start playing offline or online." $ui_textsmall
        uitext "Learning by doing or watching experienced players, both are great ways to get you started." $ui_textsmall
        uispace 0 $ui_padtiny
        uitext (format "You can press %1 to enter spectator mode and %2 to toggle first or third person view." (dobindsearch [spectate 1]) (dobindsearch thirdpersonswitch))
        uispace 0 $ui_padtiny
        uitext "Use the mouse wheel to follow the view of different players while spectating." $ui_textsmall
        uispace 0 $ui_padtiny
        uitext (format "One last tip: You can perform wall-runs and kicks with the %1 key. Have fun!" (dobindsearch special))
        uispace 0 $ui_padtiny
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw "OK, Thanks!" [uiclose "welcome"] 0 $colourgreen
        ]
    ]
] [ui_profile_welcome = 0] [] [] 1

ui_profile_prev_reset = [
    ui_profile_prev_team = (getplayerteam 1)
    ui_profile_prev_weap = (weapselect)
    ui_profile_prev_name = (getplayername)
    ui_profile_prev_colour = (getplayercolour -1)
    ui_profile_prev_model = (getplayermodel)
    ui_profile_prev_pattern = (getplayerpattern)
    ui_profile_prev_vanity = (getplayervanity)
    ui_profile_prev_loadweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getloadweap $i)
        if (< $q 0) [break = 1] [ui_profile_prev_loadweap = (? $i (concat $ui_profile_prev_loadweap $q) $q)]
    ]
    ui_profile_prev_randweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getrandweap $i)
        ui_profile_prev_randweap = (? $i (concat $ui_profile_prev_randweap $q) $q)
    ]
    ui_profile_cur_name = $ui_profile_prev_name
    ui_profile_cur_colour = $ui_profile_prev_colour
    ui_profile_cur_model = $ui_profile_prev_model
    ui_profile_cur_pattern = $ui_profile_prev_pattern
    ui_profile_cur_vanity = $ui_profile_prev_vanity
    ui_profile_cur_loadweap = $ui_profile_prev_loadweap
    ui_profile_cur_randweap = $ui_profile_prev_randweap
]

ui_profile_commit = [
    playername $ui_profile_prev_name
    playercolour $ui_profile_prev_colour
    playermodel $ui_profile_prev_model
    playerpattern $ui_profile_prev_pattern
    playervanity $ui_profile_prev_vanity
    playerloadweap $ui_profile_prev_loadweap
    playerrandweap $ui_profile_prev_randweap
]

ui_profilesave_reset = 0
uimenu "profilesave" "Save Player Profile" $alerttex [
    uivlist 0 [
        uitext "There are changes to your player profile!"
        uitext "Do you want to save these settings?"
        uispace 0 $ui_padbutton
        looplist ui_profilesave_setting [name colour model pattern vanity loadweap randweap] [
            if (!=s $[ui_profile_cur_@ui_profilesave_setting] $[ui_profile_prev_@ui_profilesave_setting]) [
                uispace $ui_padtiny 0 [uitext (format "^fy%1" $ui_profilesave_setting) $ui_textsmall]
                uispace $ui_padtiny $ui_padtiny [uitext $[ui_profile_prev_@ui_profilesave_setting] $ui_textsmall [uitextwrap 0.35]]
            ]
        ]
        uispace 0 $ui_padbutton
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw "Save" [
                ui_profilesave_reset = 1
                ui_profile_commit
                uiclose "profilesave"
                uiclose "profile"
            ] 0 $colourgreen
            uibuttonw "Modify" [uiclose "profilesave"] 0 $colourorange
            uibuttonw "Discard" [
                ui_profilesave_reset = 1
                uiclose "profilesave"
                uiclose "profile"
            ] 0 $colourred
        ]
    ]
] [
    ui_profilesave_reset = 0
] [
    if $ui_profilesave_reset [ui_profile_prev_reset]
] [] 1 [uiclose "profilesave"; uiclose "profile"]

ui_tip_profile_name = [uitext "Click here to change your display name.^nThis is required in order to play." $ui_texttip]
ui_tip_profile_model_male = [uitext "Click here to change your display model to ^fs^fyMale^fS.^nYou can see how you look on the left." $ui_texttip]
ui_tip_profile_model_female = [uitext "Click here to change your display model to ^fs^fyFemale^fS.^nYou can see how you look on the left." $ui_texttip]
ui_tip_profile_pattern = [uitext "Click here to change your display pattern.^nYou can see how you look on the left." $ui_texttip]

ui_profile_tab = 0
uimenu "profile" "Player Profile Setup" "<grey>textures/icons/player" [
    uihlist $ui_padbutton [
        uiplayerpreview $ui_profile_prev_model $ui_profile_prev_pattern $ui_profile_prev_colour $ui_profile_prev_team $ui_profile_prev_weap $ui_profile_prev_vanity 1 1 $ui_prevw $ui_prevh [ui_playerprev_easteregg]
        uivlist 0 [
            uiclamp 1 1 1 1
            uihtab 0.48 0.4 $ui_tabw $ui_tabh ui_profile_tab [
                0 "Settings" []
                1 "Vanity Items" []
            ] [
                case $ui_profile_tab 0 [
                    uitable 0 0 [
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Name" $ui_text [uicolourset (? (=s $ui_profile_prev_name "") (getclientrescolour (getplayercn) $pulseidxwarn) $colourwhite)]]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [
                                        uiinput ui_profile_prev_name 24 [] 1 0 "[click here to set a name]" (? (=s $ui_profile_prev_name "") (getclientrescolour (getplayercn) $pulseidxwarn) $colourgrey) profile_name
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Model"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [
                                        uiradio "Male" (= $ui_profile_prev_model 0) $ui_radiosize [ui_profile_prev_model = 0] 0 0 [uitooltip profile_model_male]
                                        uiradio "Female" (= $ui_profile_prev_model 1) $ui_radiosize [ui_profile_prev_model = 1] 0 0 [uitooltip profile_model_female]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Pattern"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uiskinborder 0.25 0 $ui_main1 $ui_main2 $ui_main3 $ui_main1 $colourred $colourpink [
                                        uitooltip [profile_pattern]
                                        uicursorset
                                        uirelease [
                                            ui_profile_pattern_sel = $ui_profile_prev_pattern
                                            uiopen "pattern" 0
                                        ]
                                        uiclamp 1 1 1 1
                                        uispace $ui_padbutton $ui_padsmall [
                                            uiclamp 1 1 1 1
                                            uihlist $ui_padbutton [
                                                uitext (at $patternlongnames $ui_profile_prev_pattern)
                                            ]
                                        ]
                                    ] [uiclamp 1 1 1 1] 1
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Colour"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [
                                        uihexpreview $ui_profile_prev_colour $ui_main0
                                        uirgbsliders ui_profile_prev_colour
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Loadout"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padsmall $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uivlist $ui_padmini [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padmini [
                                            ui_profile_loadweaplen = (listlen $ui_profile_prev_loadweap)
                                            loop ui_profile_loadweapw2 $weapidxloadout [
                                                ui_profile_loadweapw3 = (? (> $ui_profile_loadweaplen $ui_profile_loadweapw2) (at $ui_profile_prev_loadweap $ui_profile_loadweapw2) -1)
                                                uivlist 0 [
                                                    uialign 0 0
                                                    uispace $ui_padtiny $ui_padtiny [uitext (+ $ui_profile_loadweapw2 1) $ui_textbig]
                                                    uispace $ui_padtiny $ui_padtiny [
                                                        [ui_tip_profile_loadout_@[ui_profile_loadweapw2]] = [
                                                            uitext (format "Click here to select a weapon for loadout slot ^fs^fy#%1^fS." @(+ $ui_profile_loadweapw2 1)) $ui_texttip
                                                        ]
                                                        if (<= $ui_profile_loadweapw3 0) [
                                                            uicheckbutton $questiontex (= $ui_profile_loadweapw3 0) $ui_titlesize [
                                                                ui_profile_loadout_sel = 0
                                                                ui_profile_loadout_type = $ui_profile_loadweapw2
                                                                uiopen "loadout" 0
                                                            ] $ui_default $ui_line [
                                                                uitooltip [profile_loadout_@[ui_profile_loadweapw2]]
                                                            ]
                                                        ] [
                                                            ui_profile_loadweapw4 = (at $weapname $ui_profile_loadweapw3)
                                                            uicheckbutton [textures/weapons/@ui_profile_loadweapw4] 1 $ui_titlesize [
                                                                ui_profile_loadout_sel = $ui_profile_loadweapw3
                                                                ui_profile_loadout_type = $ui_profile_loadweapw2
                                                                uiopen "loadout" 0
                                                            ] $[@[ui_profile_loadweapw4]colour] $ui_line [
                                                                uitooltip [profile_loadout_@[ui_profile_loadweapw2]]
                                                            ]
                                                        ]
                                                    ]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Random Filter"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padsmall $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uivlist $ui_padmini [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padmini [
                                            gdr = (listlen $ui_profile_prev_randweap)
                                            loop ui_profile_loadweapw1 $weapidxloadout [
                                                ui_profile_loadweapw4 = (+ $ui_profile_loadweapw1 $weapidxoffset)
                                                ui_profile_loadweapw5 = (at $weapname $ui_profile_loadweapw4)
                                                ui_profile_loadweapw3 = (? (> $gdr $ui_profile_loadweapw1) (at $ui_profile_prev_randweap $ui_profile_loadweapw1) 1)
                                                uispace $ui_padtiny $ui_padtiny [
                                                    uicheckbutton [textures/weapons/@ui_profile_loadweapw5] $ui_profile_loadweapw3 $ui_titlesize [
                                                        rwv = (? (> (listlen $ui_profile_prev_randweap) @ui_profile_loadweapw1) (at $ui_profile_prev_randweap @ui_profile_loadweapw1) 1)
                                                        ui_profile_randweaps @ui_profile_loadweapw1 (! $rwv)
                                                    ] $[@[ui_profile_loadweapw5]colour] $ui_line [
                                                        [ui_tip_profile_randweap_@[ui_profile_loadweapw4]] = [
                                                            uitext (format "Click here to ^fa^fa%1^fS the ^fs^f[%2]%3^fS from the filter." (? @ui_profile_loadweapw3 "remove" "add") $[@@[ui_profile_loadweapw5]colour] $[@@[ui_profile_loadweapw5]longname]) $ui_texttip
                                                            uitext "This will be used when you pick ^fs^fyRandom^fS for any of the weapon slots in the above ^fs^fyLoadout^fS section." $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                                        ]
                                                        uitooltip [profile_randweap_@[ui_profile_loadweapw4]]
                                                    ]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Options"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padsmall $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uivlist $ui_padmini [
                                        uiclamp 1 1 1 1
                                        uihlist 0 [
                                            uispace $ui_padtiny $ui_padsmall [uicheckbox "Show this menu on map start" (= $showloadoutmenu 1) $ui_checksize [showloadoutmenu = (! $showloadoutmenu)] $colourred $colourmagenta $ui_default]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                    ]
                ] 1 [
                    uitable 0 0 [
                        loop i (listlen $ui_profile_vanity_name) [
                            r = ""
                            loop j (getvanity) [
                                if (= (getvanity $j 0) $i) [
                                    if (< -1 (indexof  $ui_profile_prev_vanity (getvanity $j 1))) [
                                        r = (getvanity $j 2)
                                    ]
                                ]
                            ]
                            uitablerow [
                                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [uitext (at $ui_profile_vanity_names $i)]
                                    ]
                                ] [uiclamp 1 1 1 1]
                                uiskinborder 0.25 0 $ui_main1 $ui_main2 $ui_main3 $ui_main1 $colourred $colourpink [
                                    [ui_tip_profile_vanity_@[i]] = [uitext (format "Click here change the vanity item for your ^fs^fy%1^fS." (at $ui_profile_vanity_name @i)) $ui_texttip]
                                    uitooltip [profile_vanity_@[i]]
                                    uicursorset
                                    uirelease [
                                        ui_profile_vanity_sel = -1
                                        ui_profile_vanity_type = $i
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $i) [
                                                if (< -1 (indexof  $ui_profile_prev_vanity (getvanity $j 1))) [ui_profile_vanity_sel = $j]
                                            ]
                                        ]
                                        uiopen "vanity" 0
                                    ]
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [
                                            if (=s $r "") [uitext "^fa[click here to select a vanity item]" $ui_texttiny] [uitext $r]
                                        ]
                                    ]
                                ] [uiclamp 1 1 1 1] 1
                            ]
                        ]
                    ]
                ]
            ]
            uispace 0 $ui_padsmall
            uihlist $ui_padbutton [
                uialign 0 1
                uibuttonw "OK" [ui_profile_commit; uiclose "profile"] (=s $ui_profile_prev_name "") $colourgreen
                uibuttonw "Reset" [ui_profile_prev_reset] 0 $colourorange
                uibuttonw "Cancel" [
                    ui_profile_haschanges = 0
                    looplist i [name colour model pattern vanity loadweap randweap] [
                        if (!=s $[ui_profile_cur_@i] $[ui_profile_prev_@i]) [ui_profile_haschanges = 1]
                    ]
                    if $ui_profile_haschanges [uiopen "profilesave" 0] [uiclose "profile"]
                ] (needname) $colourred
            ]
        ]
    ]
] [
    ui_profile_prev_reset
    ui_profile_tab = 0
    ui_profile_weap = (at $ui_profile_prev_loadweap 0)
    if (>= $ui_profile_weap 0) [ui_profile_prev_weap = $ui_profile_weap]
    if (&& $ui_profile_welcome (=s $ui_profile_prev_name "")) [uiopen "welcome" 0]
] [] [] [] [
    ui_profile_haschanges = 0
    looplist i [name colour model pattern vanity loadweap randweap] [
        if (!=s $[ui_profile_cur_@i] $[ui_profile_prev_@i]) [ui_profile_haschanges = 1]
    ]
    if $ui_profile_haschanges [uiopen "profilesave" 0] [uiclose "profile"]
] [needname]
