loadweapall = 1
loadweaps = [
    echo (format "loadweaps: %1 %2" $arg1 $arg2)
    lwa = ""
    lwl = (listlen $playerprevloadweap)
    lwo = (? (> $lwl $arg1) (at $playerprevloadweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $playerprevloadweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [ lw3 = $lwo ]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ playerprevloadweap = $lwa ]
    playerprevweap = $arg2
]

randweaps = [
    rwa = ""
    rwl = (listlen $playerprevrandweap)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $playerprevrandweap $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ playerprevrandweap = $rwa ]
]

// these names should match the types in config/vanities.cfg
vantypename = [chest eyes scalp forehead ears face back [right foot] [left foot] [right shoulder] [left shoulder]]
vantypenames = [Chest Eyes Scalp Forehead Ears Face Back [Right Foot] [Left Foot] [Right Shoulder] [Left Shoulder]]
vantypesel = -1
vanselorig = -1
vanprevorig = ""
vansel = -1

uitoolmenu "vanity" "Choose Vanity Item" [
    uivlist $ui_padbutton [
        uitext (format "Which vanity item for your ^fs^fy%1^fS?" (at $vantypename $vantypesel))
        uichecktext "nothing" [= $vansel -1] $ui_buttonw $ui_buttonh [
            vansel = -1
            loop j (getvanity) [
                if (= (getvanity $j 0) $vantypesel) [
                    playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                ]
            ]
        ] $ui_on3 $ui_default
        loop i (getvanity) [
            if (= (getvanity $i 0) $vantypesel) [
                uichecktext (getvanity $i 2) [= $vansel $i] $ui_buttonw $ui_buttonh [
                    vansel = $i
                    loop j (getvanity) [
                        if (= (getvanity $j 0) $vantypesel) [
                            playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                        ]
                    ]
                    append playerprevvanity (getvanity $i 1)
                ] $ui_on3 $ui_default
            ]
        ]
        uihlist $ui_padbutton [
            uialign 0 1
            uibutton "OK" 0 $ui_buttonh [ hideui "vanity" ] 0 [] 0 $ui_padbutton [] $ui_ok $ui_ok
            uibutton "Reset" 0 $ui_buttonh [ vansel = $vanselorig; playerprevvanity = $vanprevorig ] 0 [] 0 $ui_padbutton [] $ui_change $ui_change
            uibutton "Cancel" 0 $ui_buttonh [ vansel = $vanselorig; playerprevvanity = $vanprevorig; hideui "vanity" ] 0 [] 0 $ui_padbutton [] $ui_cancel $ui_cancel
        ]
    ]
] [
    if (= $vantypesel -1) [ hideui "vanity" ] [ vanselorig = $vansel; vanprevorig = $playerprevvanity ]
] [] [] [ vansel = $vanselorig; playerprevvanity = $vanprevorig; hideui "vanity" ]

uitoolmenu "welcome" "Welcome to Red Eclipse" [
    uivlist $ui_padbutton [
        uitext "To get ready for action, please set up your player profile!"
        uispace 0 $ui_padbutton
        uitext "Use the this menu to choose a name, a profile colour and a pair of loadout weapons." 0.8
        uitext "To learn more about the weapons and many other aspects of the game, you can consult the help menus." 0.8
        uispace 0 $ui_padtiny
        uitext (format "You can press %1 for help, and %2 to open the menu (or close the current one)." (dobindsearch [showui help]) (dobindsearch [togglemainmenu]))
        uispace 0 $ui_padtiny
        uitext "During a match, this allows you to quickly look up rules and tips for the current game mode." 0.8
        uitext "Most of this information is also available on the Red Eclipse wiki, ^fs^fchttp://redeclipse.net/wiki/^fS." 0.8
        uitext "Once you have set up your profile with the ^fs^fgOK^fS button, you can start playing offline or online." 0.8
        uitext "Learning by doing or watching experienced players, both are great ways to get you started." 0.8
        uispace 0 $ui_padtiny
        uitext (format "You can press %1 to enter spectator mode and %2 to toggle first or third person view." (dobindsearch [spectate 1]) (dobindsearch thirdpersonswitch))
        uispace 0 $ui_padtiny
        uitext "Use the mouse wheel to follow the view of different players while spectating." 0.8
        uispace 0 $ui_padtiny
        uitext (format "One last tip: You can perform wall-runs and kicks with the %1 key. Have fun!" (dobindsearch special))
        uispace 0 $ui_padtiny
        uihlist $ui_padbutton [
            uialign 0 1
            uibutton "OK, Thanks!" 0 $ui_buttonh [ hideui "welcome" ] 0 [] 0 $ui_padbutton [] $ui_ok $ui_ok
        ]
    ]
]

playerprevreset = [
    playerprevteam = (getplayerteam 1)
    playerprevweap = (weapselect)
    playerprevname = (getplayername)
    playerprevcolour = (getplayercolour -1)
    playerprevmodel = (getplayermodel)
    playerprevvanity = (getplayervanity)
    playerprevloadweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getloadweap $i)
        if (< $q 0) [ break = 1 ] [ playerprevloadweap = (? $i (concat $playerprevloadweap $q) $q) ]
    ]
    playerprevrandweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getrandweap $i)
        playerprevrandweap = (? $i (concat $playerprevrandweap $q) $q)
    ]
    playercurname = $playerprevname
    playercurcolour = $playerprevcolour
    playercurmodel = $playerprevmodel
    playercurvanity = $playerprevvanity
    playercurloadweap = $playerprevloadweap
    playercurrandweap = $playerprevrandweap
]

uitoolmenu "profilesave" "Save Player Profile?" [
    uivlist 0 [
        skip = 0
        uitext "There are unsaved changes to your player profile!"
        uitext "Do you want to save these settings?"
        uispace 0 $ui_padbutton
        uitable 0 $ui_padtiny [
            looplist i [name colour model vanity loadweap randweap] [
                if (!=s $[playercur@i] $[playerprev@i]) [
                    uitablerow [
                        uihlist $ui_padbutton [uitext $i 0.8]
                        uispace $ui_padtiny
                        uihlist $ui_padbutton [uitext "=" 0.8]
                        uispace $ui_padtiny
                        uihlist $ui_padbutton [uitext $[playerprev@i] 0.8]
                    ]
                ]
            ]
        ]
        uispace 0 $ui_padbutton
        uihlist $ui_padbutton [
            uialign 0 1
            uibutton "Save" 0 $ui_buttonh [ setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevloadweap $playerprevrandweap; hideui "profilesave"; hideui "profile" ] 0 [] 0 $ui_padbutton [] $ui_ok $ui_ok
            uibutton "Modify" 0 $ui_buttonh [ hideui "profilesave" ] 0 [] 0 $ui_padbutton [] $ui_change $ui_change
            uibutton "Discard" 0 $ui_buttonh [ playerprevreset; hideui "profilesave"; hideui "profile" ] 0 [] 0 $ui_padbutton [] $ui_cancel $ui_cancel
        ]
    ]
] [] [] [] [ playerprevreset; hideui "profilesave"; hideui "profile" ]

uimenu "profile" "Player Profile Setup" [
    uihlist $ui_padbutton [
        uivlist $ui_padbutton [ uiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity 1 1 0.4 0.5 ]
        uivlist $ui_padbutton [
            uihlist $ui_padbutton [
                uiclamp 1 1 1 1
                uialign 0 -1
                uitable 0 0 [
                    uialign 0 -1
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [ uitext "Name" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $ui_padtiny
                        uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [ uiinput playerprevname 24 [] 1 "" "^fzad<enter name>" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                    ]
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [ uitext "Model" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $ui_padtiny
                        uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [ 
                                    uiradio "Male" [= $playerprevmodel 0] $ui_radiosize [ playerprevmodel = 0 ]
                                    uiradio "Female" [= $playerprevmodel 1] $ui_radiosize [ playerprevmodel = 1 ]
                                ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                    ]
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [ uitext "Colour" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $ui_padtiny
                        uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [
                                    uihexpreview $playerprevcolour "hex-value"
                                    uirgbsliders playerprevcolour
                                ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                    ]
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [ uitext "Loadout" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $ui_padtiny
                        uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padsmall $ui_padsmall [
                                uiclamp 1 1 1 1
                                uivlist $ui_padmini [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padmini [
                                        gdl = (listlen $playerprevloadweap)
                                        gdw = 0
                                        uiskinborder 0 0 $ui_main2 $ui_main3 $ui_main3 $ui_line_a $ui_hover $ui_hold [
                                            uiclamp 1 1
                                            uispace $ui_padsmall 0 [
                                                uiclamp 1 1
                                                uivlist 0 [
                                                    uiclamp 1 1
                                                    uialign 0 0
                                                    uitext "slot"
                                                    uiskinborder $ui_titlesize $ui_titlesize $ui_main1 $ui_main1 $ui_main1 $ui_line_a $ui_line_a $ui_line_a [
                                                        uiclamp 1 1
                                                        uispace $ui_padbutton 0 [ uitext "^farandom" ]
                                                    ]
                                                    uiclamp- 1 1 1 1
                                                    loop w1 $weapidxloadout [
                                                        w4 = (+ $w1 $weapidxoffset)
                                                        w5 = (at $weapname $w4)
                                                        uiskinborder $ui_titlesize $ui_titlesize $ui_main1 $ui_main1 $ui_main1 $ui_line_a $ui_line_a $ui_line_a [
                                                            uiclamp 1 1
                                                            uispace $ui_padbutton 0 [ uitext (format "^f[%1]%2" (? (allowedweap $w4) $[@[w5]colour] 0x444444) $w5) ]
                                                        ]
                                                        uiclamp- 1 1 1 1
                                                    ]
                                                    uispace 0 0.0035
                                                ]
                                            ]
                                        ]
                                        uiclamp- 1 1 1 1
                                        loop w2 (? $loadweapall $weapidxloadout $playermaxcarry) [
                                            w3 = (? (> $gdl $w2) (at $playerprevloadweap $w2) -1)
                                            hi = (mod $w2 2)
                                            al = (|| (= $w3 0) (allowedweap $w3))
                                            uiskinborder $ui_titlesize 0 $ui_main2 $ui_main3 $ui_main3 $ui_line_a $ui_hover $ui_hold [
                                                uivlist 0 [
                                                    uialign 0 0
                                                    uispace $ui_padsmall 0 [ uitext (+ $w2 1) ]
                                                    uispace $ui_padsmall 0 [ uicheckbutton "textures/question" [= $w3 0] $ui_titlesize [loadweaps $w2 0] $ui_default ]
                                                    loop w1 $weapidxloadout [
                                                        w4 = (+ $w1 $weapidxoffset)
                                                        w5 = (at $weapname $w4)
                                                        uispace $ui_padsmall 0 [ uicheckbutton (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) [= $w3 $w4] $ui_titlesize [loadweaps $w2 $w4] $[@[w5]colour] ]
                                                    ]
                                                    uispace 0 0.0035
                                                ]
                                            ]
                                            if $al [ gdw = (+ $gdw 1) ]
                                        ]
                                    ]
                                    uispace 0 $ui_padsmall [ uicheckbox "Show all slots" [= $loadweapall 1] $ui_checksize [loadweapall = (! $loadweapall)] $ui_on1 $ui_on2 $ui_default ]
                                ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                    ]
                ]
                uivlist $ui_padbutton [
                    uiclamp 1 1 1 1
                    uialign 0 -1
                    uitable 0 0 [
                        uialign 0 -1
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [ uitext "Location" 1.2 ]
                                ]
                            ]
                            uiclamp- 1 1 1 1
                            uispace $ui_padtiny
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [ uitext "Vanity Item" 1.2 ]
                                ]
                            ]
                            uiclamp- 1 1 1 1
                        ]
                        loop i (listlen $vantypename) [
                            r = ""
                            loop j (getvanity) [
                                if (= (getvanity $j 0) $i) [
                                    if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [
                                        r = (getvanity $j 2)
                                    ]
                                ]
                            ]
                            uitablerow [
                                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [ uitext (at $vantypenames $i) ]
                                    ]
                                ]
                                uiclamp- 1 1 1 1
                                uispace $ui_padtiny
                                uiskinborder 0.25 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $ui_hover $ui_hold [
                                    uirelease [
                                        vansel = -1
                                        vantypesel = $i
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $i) [
                                                if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [vansel = $j]
                                            ]
                                        ]
                                        showui "vanity" 0
                                    ]
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [
                                            uitext $r
                                        ]
                                    ]
                                ]
                                uiclamp- 1 1 1 1
                            ]
                        ]
                    ]
                    uihlist $ui_padbutton [
                        uialign 0 1
                        uibutton "OK" 0 $ui_buttonh [ setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevloadweap $playerprevrandweap; hideui "profile" ] 0 [] 0 $ui_padbutton [] $ui_ok $ui_ok
                        uibutton "Reset" 0 $ui_buttonh [ playerprevreset ] 0 [] 0 $ui_padbutton [] $ui_change $ui_change
                        uibutton "Cancel" 0 $ui_buttonh [
                            haschanges = 0
                            looplist i [name colour model vanity loadweap randweap] [
                                if (!=s $[playercur@i] $[playerprev@i]) [ haschanges = 1 ]
                            ]
                            if $haschanges [ showui "profilesave" 0 ] [ hideui "profile" ]
                        ] 0 [] 0 $ui_padbutton [] $ui_cancel $ui_cancel
                    ]
                ]
            ]
        ]
    ]
] [
    playerprevreset
    if (needname) [ showui welcome 0 ]
] [] [] [] [
    haschanges = 0
    looplist i [name colour model vanity loadweap randweap] [
        if (!=s $[playercur@i] $[playerprev@i]) [ haschanges = 1 ]
    ]
    if $haschanges [ showui "profilesave" 0 ] [ hideui "profile" ]
]
