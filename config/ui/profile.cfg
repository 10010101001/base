loadweapall = 1
loadweaps = [
    echo (format "loadweaps: %1 %2" $arg1 $arg2)
    lwa = ""
    lwl = (listlen $playerprevloadweap)
    lwo = (? (> $lwl $arg1) (at $playerprevloadweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $playerprevloadweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [ lw3 = $lwo ]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ playerprevloadweap = $lwa ]
    playerprevweap = $arg2
]

randweaps = [
    rwa = ""
    rwl = (listlen $playerprevrandweap)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $playerprevrandweap $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ playerprevrandweap = $rwa ]
]

// these names should match the types in config/vanities.cfg
vantypename = [chest eyes scalp forehead ears face back [right foot] [left foot] [right shoulder] [left shoulder]]
vantypenames = [Chest Eyes Scalp Forehead Ears Face Back [Right Foot] [Left Foot] [Right Shoulder] [Left Shoulder]]
vantypesel = -1
vanselorig = -1
vansel = -1

uitoolmenu "vanity" "Choose Vanity Item" [
    uivlist $uibutton_padding [
        uitext (format "Choose a vanity item for your %1" (at $vantypename $vantypesel))
        uichecktext "nothing" [= $vansel -1] 0.3 0.04 [
            vansel = -1
            loop j (getvanity) [
                if (= (getvanity $j 0) $vantypesel) [
                    playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                ]
            ]
        ] 0xFFFF00 0xFFFFFF
        loop i (getvanity) [
            if (= (getvanity $i 0) $vantypesel) [
                uichecktext (getvanity $i 2) [= $vansel $i] 0.3 0.04 [
                    vansel = $i
                    loop j (getvanity) [
                        if (= (getvanity $j 0) $vantypesel) [
                            playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                        ]
                    ]
                    append playerprevvanity (getvanity $i 1)
                ] 0xFFFF00 0xFFFFFF
            ]
        ]
        uihlist $uibutton_padding [
            uialign 0 1
            uibutton "Apply" "" 0 0.04 [hideui "vanity"] 0 [] 0 $uibutton_padding [] 0x00FF00 0x00FF00
            uibutton "Cancel" "" 0 0.04 [vansel = $vanselorig; hideui "vanity"] 0 [] 0 $uibutton_padding [] 0xFF8800 0xFF8800
            uibutton "Reset" "" 0 0.04 [vansel = $vanselorig] 0 [] 0 $uibutton_padding [] 0xFF2222 0xFF2222
        ]
    ]
] [
    if (= $vantypesel -1) [ hideui "vanity" ] [ vanselorig = $vansel ]
]

uimenu "welcome" "Welcome to Red Eclipse" [
    uivlist $uibutton_padding [
        uitext "To get ready for action, please set up your player profile!"
        uispace 0 0.01
        uitext "Use the other tabs of this menu to choose a name, a profile colour and a pair of loadout weapons." 0.8
        uitext "To learn more about the weapons and many other aspects of the game, you can consult the help menus." 0.8
        uispace 0 0.01
        uitext (format "You can press %1 any time to open these menus, and %2 to return to the previous menu." (dobindsearch [showgui help]) (dobindsearch [if (cleargui 1) [] [showgui main]]))
        uispace 0 0.01
        uitext "During a match, this allows you to quickly look up rules and tips for the current game mode." 0.8
        uitext "Most of this information is also available on the Red Eclipse wiki, ^fchttp://redeclipse.net/wiki/" 0.8
        uitext "Once you have set up your profile with the ^fgok^fw button, you can start playing offline or online." 0.8
        uitext "Learning by doing or watching experienced players, both are great ways to get you started." 0.8
        uispace 0 0.01
        uitext (format "You can press %1 to enter spectator mode and %2 to toggle first or third person view." (dobindsearch [spectate 1]) (dobindsearch thirdpersonswitch))
        uispace 0 0.01
        uitext "Use the mouse wheel to follow the view of different players while spectating." 0.8
        uispace 0 0.01
        uitext (format "One last tip: You can perform wall-runs and kicks with the %1 key. Have fun!" (dobindsearch special))
    ]
]

playerprevreset = [
    playerprevteam = (getplayerteam 1)
    playerprevweap = (weapselect)
    playerprevname = (getplayername)
    playerprevcolour = (getplayercolour -1)
    playerprevmodel = (getplayermodel)
    playerprevvanity = (getplayervanity)
    playerprevloadweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getloadweap $i)
        if (< $q 0) [ break = 1 ] [ playerprevloadweap = (? $i (concat $playerprevloadweap $q) $q) ]
    ]
    playerprevrandweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getrandweap $i)
        playerprevrandweap = (? $i (concat $playerprevrandweap $q) $q)
    ]
    playercurname = $playerprevname
    playercurcolour = $playerprevcolour
    playercurmodel = $playerprevmodel
    playercurvanity = $playerprevvanity
    playercurloadweap = $playerprevloadweap
    playercurrandweap = $playerprevrandweap
]

uimenu "profilesave" "Save Player Profile?" [
    uivlist 0 [
        skip = 0
        uitext "There are unsaved changes to your player profile!"
        uitext "Do you want to save these settings?"
        uispace 0 $uibutton_padding
        uitable 0 0.005 [
            looplist i [name colour model vanity loadweap randweap] [
                if (!=s $[playercur@i] $[playerprev@i]) [
                    uitablerow [
                        uihlist $uibutton_padding [uitext $i 0.8]
                        uispace $uitiny_padding
                        uihlist $uibutton_padding [uitext "=" 0.8]
                        uispace $uitiny_padding
                        uihlist $uibutton_padding [uitext $[playerprev@i] 0.8]
                    ]
                ]
            ]
        ]
        uispace 0 $uibutton_padding
        uihlist $uibutton_padding [
            uialign 0 1
            uibutton "Save" "" 0 0.04 [setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevloadweap $playerprevrandweap; hideui "profilesave"] 0 [] 0 $uibutton_padding [] 0x00FF00 0x00FF00
            uibutton "Modify" "" 0 0.04 [showui "profile"] 0 [] 0 $uibutton_padding [] 0xFF8800 0xFF8800
            uibutton "Discard" "" 0 0.04 [playerprevreset; hideui "profilesave"] 0 [] 0 $uibutton_padding [] 0xFF2222 0xFF2222
        ]
    ]
]

uimenu "profile" "Player Profile Setup" [
    uihlist $uibutton_padding [
        uivlist $uibutton_padding [ uiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity 1 1 0.4 0.5 ]
        uivlist $uibutton_padding [
            uihlist $uibutton_padding [
                uiclamp 1 1 1 1
                uitable 0 0.005 [
                    uialign 0 0
                    uitablerow [
                        uiskinborder 0 0 $c_main2 $c_main2 $c_main2 $c_line $c_line $c_line [
                            uiclamp 1 1 1 1
                            uispace $uibutton_padding $uismall_padding [
                                uiclamp 1 1 1 1
                                uihlist $uibutton_padding [ uitext "Name" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $uitiny_padding
                        uihlist $uibutton_padding [ uiinput playerprevname 24 [] 1 "" "^fzad<enter name>" ]
                    ]
                    uitablerow [
                        uiskinborder 0 0 $c_main2 $c_main2 $c_main2 $c_line $c_line $c_line [
                            uiclamp 1 1 1 1
                            uispace $uibutton_padding $uismall_padding [
                                uiclamp 1 1 1 1
                                uihlist $uibutton_padding [ uitext "Model" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $uitiny_padding
                        uiskinborder 0 0 $c_main1 $c_main1 $c_main1 $c_line $c_line $c_line [
                            uiclamp 1 1 1 1
                            uispace $uibutton_padding $uismall_padding [
                                uiclamp 1 1 1 1
                                uihlist $uibutton_padding [ 
                                    uiradio "Male" [= $playerprevmodel 0] 0.025 [ playerprevmodel = 0 ]
                                    uiradio "Female" [= $playerprevmodel 1] 0.025 [ playerprevmodel = 1 ]
                                ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                    ]
                    uitablerow [
                        uiskinborder 0 0 $c_main2 $c_main2 $c_main2 $c_line $c_line $c_line [
                            uiclamp 1 1 1 1
                            uispace $uibutton_padding $uismall_padding [
                                uiclamp 1 1 1 1
                                uihlist $uibutton_padding [ uitext "Colour" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $uitiny_padding
                        uiskinborder 0 0 $c_main1 $c_main1 $c_main1 $c_line $c_line $c_line [
                            uiclamp 1 1 1 1
                            uispace $uibutton_padding $uismall_padding [
                                uiclamp 1 1 1 1
                                uihlist $uibutton_padding [
                                    uihexpreview $playerprevcolour "hex-value"
                                    uirgbsliders playerprevcolour
                                ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                    ]
                    uitablerow [
                        uiskinborder 0 0 $c_main2 $c_main2 $c_main2 $c_line $c_line $c_line [
                            uiclamp 1 1 1 1
                            uispace $uibutton_padding $uismall_padding [
                                uiclamp 1 1 1 1
                                uihlist $uibutton_padding [ uitext "Loadout" ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                        uispace $uitiny_padding
                        uiskinborder 0 0 $c_main1 $c_main1 $c_main1 $c_line $c_line $c_line [
                            uiclamp 1 1 1 1
                            uispace $uibutton_padding $uismall_padding [
                                uiclamp 1 1 1 1
                                uihlist $uibutton_padding [
                                    gdl = (listlen $playerprevloadweap)
                                    gdw = 0
                                    loop w2 (? $loadweapall $weapidxloadout $playermaxcarry) [
                                        w3 = (? (> $gdl $w2) (at $playerprevloadweap $w2) -1)
                                        hi = (mod $w2 2)
                                        al = (|| (= $w3 0) (allowedweap $w3))
                                        uiskinborder 0.04 0 $c_main2 $c_main3 $c_main3 $c_line_a $c_hover $c_hold [
                                            uivlist 0 [
                                                uialign 0 0
                                                uitext (+ $w2 1)
                                                uicheckbutton "textures/question" [= $w3 0] 0.03 [loadweaps $w2 0] 0xFFFFFF
                                                loop w1 $weapidxloadout [
                                                    w4 = (+ $w1 $weapidxoffset)
                                                    w5 = (at $weapname $w4)
                                                    uicheckbutton (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) [= $w3 $w4] 0.03 [loadweaps $w2 $w4] $[@[w5]colour]
                                                ]
                                                uispace 0 0.0035
                                            ]
                                        ]
                                        if $al [ gdw = (+ $gdw 1) ]
                                    ]
                                ]
                            ]
                        ]
                        uiclamp- 1 1 1 1
                    ]
                ]
                uivlist $uibutton_padding [
                    //uiskinborder 0 0 $c_main2 $c_main2 $c_main2 $c_line $c_line $c_line [
                    //    uiclamp 1 1 1 1
                    //    uispace $uibutton_padding $uismall_padding [
                    //        uiclamp 1 1 1 1
                    //        uihlist $uibutton_padding [ uitext "Vanity Items" ]
                    //    ]
                    //]
                    uiclamp- 1 1 1 1
                    uitable 0 0.005 [
                        loop i (listlen $vantypename) [
                            r = ""
                            loop j (getvanity) [
                                if (= (getvanity $j 0) $i) [
                                    if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [
                                        r = (getvanity $j 2)
                                    ]
                                ]
                            ]
                            uitablerow [
                                uiskinborder 0 0 $c_main2 $c_main2 $c_main2 $c_line $c_line $c_line [
                                    uiclamp 1 1 1 1
                                    uispace $uibutton_padding $uismall_padding [
                                        uiclamp 1 1 1 1
                                        uihlist $uibutton_padding [ uitext (at $vantypenames $i) ]
                                    ]
                                ]
                                uiclamp- 1 1 1 1
                                uispace $uitiny_padding
                                uiskinborder 0.25 0 $c_main1 $c_main2 $c_main3 $c_line $c_hover $c_hold [
                                    uirelease [
                                        vansel = -1
                                        vantypesel = $i
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $i) [
                                                if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [vansel = $j]
                                            ]
                                        ]
                                        showui "vanity" 0
                                    ]
                                    uiclamp 1 1 1 1
                                    uispace $uibutton_padding $uismall_padding [
                                        uiclamp 1 1 1 1
                                        uihlist $uibutton_padding [
                                            uitext $r
                                        ]
                                    ]
                                ]
                                uiclamp- 1 1 1 1
                            ]
                        ]
                    ]
                ]
            ]
            uihlist $uibutton_padding [
                uialign 0 1
                uibutton "Apply" "" 0 0.04 [setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevloadweap $playerprevrandweap] 0 [] 0 $uibutton_padding [] 0x00FF00 0x00FF00
                uibutton "Cancel" "" 0 0.04 [hideui "profile"] 0 [] 0 $uibutton_padding [] 0xFF8800 0xFF8800
                uibutton "Reset" "" 0 0.04 [playerprevreset] 0 [] 0 $uibutton_padding [] 0xFF2222 0xFF2222
            ]
        ]
    ]
] [
    playerprevreset
    if (needname) [ showui welcome 0 ]
] [
    haschanges = 0
    looplist i [name colour model vanity loadweap randweap] [
        if (!=s $[playercur@i] $[playerprev@i]) [ haschanges = 1 ]
    ]
    if $haschanges [showui "profilesave"]
]
