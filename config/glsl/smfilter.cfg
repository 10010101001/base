// shadow map filters, arg1 enables color sampling

smfilterg5 = [result [
    @(? (> $usetexgather 1) [
        #define shadowgather(center, xoff, yoff) textureGatherOffset(tex4, center, shadowtc.z, ivec2(xoff, yoff))
    ] [
        #define shadowgather(center, xoff, yoff) step(shadowtc.z, textureGatherOffset(tex4, center, ivec2(xoff, yoff)))
    ])
    #define colorshadowval(center, xoff, yoff) textureOffset(tex10, center, ivec2(xoff, yoff)).rgb

    @(? $arg1 "vec3" "float") filtershadow(vec3 shadowtc)
    {
        vec2 offset = fract(shadowtc.xy - 0.5);
        vec2 center = (shadowtc.xy - offset) * shadowatlasscale;
        vec4 group1 = shadowgather(center, -2, -2);
        vec4 group2 = shadowgather(center,  0, -2);
        vec4 group3 = shadowgather(center,  2, -2);
        vec4 group4 = shadowgather(center, -2,  0);
        vec4 group5 = shadowgather(center,  0,  0);
        vec4 group6 = shadowgather(center,  2,  0);
        vec4 group7 = shadowgather(center, -2,  2);
        vec4 group8 = shadowgather(center,  0,  2);
        vec4 group9 = shadowgather(center,  2,  2);

        @(if $arg1 [result [
            vec4 row0 = vec4(group1.ab, group2.ab);
            vec4 row1 = vec4(group1.rg, group2.rg);
            vec4 row2 = vec4(group4.ab, group5.ab);
            vec4 row3 = vec4(group4.rg, group5.rg);
            vec4 row4 = vec4(group7.ab, group8.ab);
            vec4 row5 = vec4(group7.rg, group8.rg);

            row0 *= 1.0 - offset.y;
            group3.ab *= 1.0 - offset.y;
            row5 *= offset.y;
            group9.rg *= offset.y;

            vec4 weights = (1.0/25.0)*vec4(1.0 - offset.x, 1.0, 1.0, offset.x);
            row0 *= weights.xyyy;
            row1 *= weights.xyyy;
            row2 *= weights.xyyy;
            row3 *= weights.xyyy;
            row4 *= weights.xyyy;
            row5 *= weights.xyyy;

            group3 *= weights.zwwz;
            group6 *= weights.zwwz;
            group9 *= weights.zwwz;

            return row0.x * colorshadowval(center, -2, -2) +
                   row0.y * colorshadowval(center, -1, -2) +
                   row0.z * colorshadowval(center,  0, -2) +
                   row0.w * colorshadowval(center,  1, -2) +
                   group3.a * colorshadowval(center, 2, -2) +
                   group3.b * colorshadowval(center, 3, -2) +
                   row1.x * colorshadowval(center, -2, -1) +
                   row1.y * colorshadowval(center, -1, -1) +
                   row1.z * colorshadowval(center,  0, -1) +
                   row1.w * colorshadowval(center,  1, -1) +
                   group3.r * colorshadowval(center, 2, -1) +
                   group3.g * colorshadowval(center, 3, -1) +
                   row2.x * colorshadowval(center, -2, 0) +
                   row2.y * colorshadowval(center, -1, 0) +
                   row2.z * colorshadowval(center,  0, 0) +
                   row2.w * colorshadowval(center,  1, 0) +
                   group6.a * colorshadowval(center, 2, 0) +
                   group6.b * colorshadowval(center, 3, 0) +
                   row3.x * colorshadowval(center, -2, 1) +
                   row3.y * colorshadowval(center, -1, 1) +
                   row3.z * colorshadowval(center,  0, 1) +
                   row3.w * colorshadowval(center,  1, 1) +
                   group6.r * colorshadowval(center, 2, 1) +
                   group6.g * colorshadowval(center, 3, 1) +
                   row4.x * colorshadowval(center, -2, 2) +
                   row4.y * colorshadowval(center, -1, 2) +
                   row4.z * colorshadowval(center,  0, 2) +
                   row4.w * colorshadowval(center,  1, 2) +
                   group9.a * colorshadowval(center, 2, 2) +
                   group9.b * colorshadowval(center, 3, 2) +
                   row5.x * colorshadowval(center, -2, 3) +
                   row5.y * colorshadowval(center, -1, 3) +
                   row5.z * colorshadowval(center,  0, 3) +
                   row5.w * colorshadowval(center,  1, 3) +
                   group9.r * colorshadowval(center, 2, 3) +
                   group9.g * colorshadowval(center, 3, 3);
        ]] [result [
            vec4 locols = vec4(group1.ab, group3.ab);
            vec4 hicols = vec4(group7.rg, group9.rg);
            locols.yz += group2.ab;
            hicols.yz += group8.rg;
            vec4 midcols = vec4(group1.rg, group3.rg) + vec4(group7.ab, group9.ab) +
                           vec4(group4.rg, group6.rg) + vec4(group4.ab, group6.ab) +
                           mix(locols, hicols, offset.y);
            vec4 cols = group5 + vec4(group2.rg, group8.ab);
            cols.xyz += mix(midcols.xyz, midcols.yzw, offset.x);
            return dot(cols, vec4(1.0/25.0));
        ]])
    }
]]

smfilterg3 = [result [
    @(? (> $usetexgather 1) [
        #define shadowgather(center, xoff, yoff) textureGatherOffset(tex4, center, shadowtc.z, ivec2(xoff, yoff))
    ] [
        #define shadowgather(center, xoff, yoff) step(shadowtc.z, textureGatherOffset(tex4, center, ivec2(xoff, yoff)))
    ])
    #define colorshadowval(center, xoff, yoff) textureOffset(tex10, center, ivec2(xoff, yoff)).rgb

    @(? $arg1 "vec3" "float") filtershadow(vec3 shadowtc)
    {
        vec2 offset = fract(shadowtc.xy - 0.5);
        vec2 center = (shadowtc.xy - offset) * shadowatlasscale;
        vec4 group1 = shadowgather(center, -1, -1);
        vec4 group2 = shadowgather(center,  1, -1);
        vec4 group3 = shadowgather(center, -1,  1);
        vec4 group4 = shadowgather(center,  1,  1);

        @(if $arg1 [result [
            vec4 row0 = vec4(group1.ab, group2.ab);
            vec4 row1 = vec4(group1.rg, group2.rg);
            vec4 row2 = vec4(group3.ab, group4.ab);
            vec4 row3 = vec4(group3.rg, group4.rg);

            row0 *= 1.0 - offset.y;
            row3 *= offset.y;

            vec4 weights = (1.0/9.0)*vec4(1.0 - offset.x, 1.0, 1.0, offset.x);
            row0 *= weights;
            row1 *= weights;
            row2 *= weights;
            row3 *= weights;

            return row0.x * colorshadowval(center, -1, -1) +
                   row0.y * colorshadowval(center,  0, -1) +
                   row0.z * colorshadowval(center,  1, -1) +
                   row0.w * colorshadowval(center,  2, -1) +
                   row1.x * colorshadowval(center, -1, 0) +
                   row1.y * colorshadowval(center,  0, 0) +
                   row1.z * colorshadowval(center,  1, 0) +
                   row1.w * colorshadowval(center,  2, 0) +
                   row2.x * colorshadowval(center, -1, 1) +
                   row2.y * colorshadowval(center,  0, 1) +
                   row2.z * colorshadowval(center,  1, 1) +
                   row2.w * colorshadowval(center,  2, 1) +
                   row3.x * colorshadowval(center, -1, 2) +
                   row3.y * colorshadowval(center,  0, 2) +
                   row3.z * colorshadowval(center,  1, 2) +
                   row3.w * colorshadowval(center,  2, 2);
        ]] [result [
            vec4 cols = vec4(group1.rg, group2.rg) + vec4(group3.ab, group4.ab) + mix(vec4(group1.ab, group2.ab), vec4(group3.rg, group4.rg), offset.y);
            return dot(mix(cols.xyz, cols.yzw, offset.x), vec3(1.0/9.0));
        ]])
    }
]]

smfilterb5 = [result [
    #define shadowval(xy, xoff, yoff) float(shadow2DRect(tex4, vec3(xy + vec2(xoff, yoff), shadowtc.z)))
    #define colorshadowval(xy, xoff, yoff) shadowcolor(xy + vec2(xoff, yoff))

    @(? $arg1 "vec3" "float") filtershadow(vec3 shadowtc)
    {
        vec2 offset = fract(shadowtc.xy - 0.5);
        vec4 center = vec4(shadowtc.xy - offset + 0.5, shadowtc.xy - offset*0.5);
        vec4 size = vec4(offset + 1.0, 2.0 - offset);
        vec4 taps1 = vec4(shadowval(center.zw, -1.5, -1.5),
                          shadowval(center.zw, 2.0, -1.5),
                          shadowval(center.zw, -1.5, 2.0),
                          shadowval(center.zw, 2.0, 2.0));
        vec4 taps2 = vec4(shadowval(center.zy, 2.0, 0.0),
                          shadowval(center.xw, 0.0, 2.0),
                          shadowval(center.zy, -1.5, 0.0),
                          shadowval(center.xw, 0.0, -1.5));
        float taps3 = shadowval(center.xy, 0.0, 0.0);

        @(if $arg1 [result [
            taps1 *= (1.0/25.0)*size.zxzx*size.wwyy;
            taps2 *= (2.0/25.0)*size;
            taps3 *= 4.0/25.0;
            return taps1.x * colorshadowval(center.zw, -1.5, -1.5) +
                   taps1.y * colorshadowval(center.zw,  2.0, -1.5) +
                   taps1.z * colorshadowval(center.zw, -1.5,  2.0) +
                   taps1.w * colorshadowval(center.zw,  2.0,  2.0) +
                   taps2.x * colorshadowval(center.zy,  2.0,  0.0) +
                   taps2.y * colorshadowval(center.xw,  0.0,  2.0) +
                   taps2.z * colorshadowval(center.zy, -1.5,  0.0) +
                   taps2.w * colorshadowval(center.xw,  0.0, -1.5) +
                   taps3   * colorshadowval(center.xy,  0.0,  0.0);
        ]] [result [
            return (1.0/25.0)*dot(size.zxzx*size.wwyy, taps1) +
                   (2.0/25.0)*dot(size, taps2) + 
                   (4.0/25.0)*taps3;
        ]])
    }
]]

smfilterb3 = [result [
    #define shadowval(center, xoff, yoff) float(shadow2DRect(tex4, center + vec3(xoff, yoff, 0.0)))
    #define colorshadowval(center, xoff, yoff) shadowcolor(center.xy + vec2(xoff, yoff))

    @(? $arg1 "vec3" "float") filtershadow(vec3 shadowtc)
    {
        vec2 offset = fract(shadowtc.xy - 0.5);
        vec3 center = shadowtc;
        center.xy -= offset*0.5;
        vec4 size = vec4(offset + 1.0, 2.0 - offset);
        vec4 taps = vec4(shadowval(center, -0.5, -0.5),
                         shadowval(center, 1.0, -0.5),
                         shadowval(center, -0.5, 1.0),
                         shadowval(center, 1.0, 1.0));
        @(if $arg1 [result [
            taps *= (1.0/9.0)*size.zxzx*size.wwyy;
            return taps.x * colorshadowval(center, -0.5, -0.5) +
                   taps.y * colorshadowval(center,  1.0, -0.5) +
                   taps.z * colorshadowval(center, -0.5,  1.0) +
                   taps.w * colorshadowval(center,  1.0,  1.0);
        ]] [result [
            return (1.0/9.0)*dot(size.zxzx*size.wwyy, taps);
        ]])
    }
]]

smfilterrg = [result [
    #define shadowval(center, xoff, yoff) float(shadow2DRect(tex4, center + vec3(xoff, yoff, 0.0)))
    #define colorshadowval(center, xoff, yoff) shadowcolor(center.xy + vec2(xoff, yoff))

    @(? $arg1 "vec3" "float") filtershadow(vec3 shadowtc)
    {
        vec4 taps = vec4(shadowval(shadowtc, -0.4,  1.0),
                         shadowval(shadowtc, -1.0, -0.4),
                         shadowval(shadowtc,  0.4, -1.0),
                         shadowval(shadowtc,  1.0,  0.4));

        @(if $arg1 [result [
            taps *= 0.25;
            return taps.x * colorshadowval(shadowtc, -0.4,  1.0) +
                   taps.y * colorshadowval(shadowtc, -1.0, -0.4) +
                   taps.z * colorshadowval(shadowtc,  0.4, -1.0) +
                   taps.w * colorshadowval(shadowtc,  1.0,  0.4);
        ]] [result [
            return dot(vec4(0.25), taps);
        ]])
    }
]]

smfilternone = [result [
    @(? $arg1 "vec3" "float") filtershadow(vec3 shadowtc)
    {
        return @(? $arg1 [shadowcolor(shadowtc.xy) *]) float(shadow2DRect(tex4, shadowtc));
    }
]]
