
     
    // button for editing commands with built-in
    // key display, tooltip and r-click for console
    guieditbutton = [ 
        guibody [     
            guibutton $arg1
            guistrut 0.5
            guispring 1
            guibutton (dobindsearch [@arg2] edit) 
        ] [@arg2] [saycommand /@arg2] [guitooltip [/@@arg2]]
    ]


    // same for looking up an editvarbind on a checkbox
    // looks a bit complicated, but works fine
    guieditcheckbox = [
        guibody [     
            guicheckbox $arg1 $arg2
            guistrut 0.5
            guispring 1
            guibutton (dobindsearch [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]] edit) 
        ] [if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]] [saycommand /@arg2] [guitooltip [/@@arg2]]
    ]

    // for convenience
    guieditbutton2 = [guilist [guieditbutton [@@arg1] [@@arg2]]]
    guieditcheckbox2 = [guilist [guieditcheckbox [@@arg1] [@@arg2]]]

    // main edit menu bound to F3
    newgui edit [ guistayopen [
        guifont emphasis [
            guicenter [guieditbutton2 [map, waypoints, routes] [showgui mapinfo]]
            guicenter [guieditbutton2 [editing options] [showgui editoptions]]
            guicenter [guieditbutton2 [geometry menu] [showgui geometry]]
            guicenter [guieditbutton2 [world variables and lighting] [showgui world]]
            //guicenter [guieditbutton2 [texture list] [showtexgui]]
            // this does not work when any gui is drawn! so here is an ugly work around:
            guicenter [ guilist [ guibody [
                guibutton "texture list"
                guistrut 1
                guispring 1 
                guibutton (dobindsearch showtexgui edit)
            ] [cleargui ; showtexgui ] [saycommand /showtexgui] [guitooltip showtexgui]] ]    
            guicenter [guieditbutton2 [texture variations] [showgui textures]]
            guicenter [guieditbutton2 [entity menu] [showgui ents]]
            guicenter [guieditbutton2 [materials menu] [showgui materials]]
            guibar
            guicenter [guieditbutton2 [same as quick compass] [showcompass editing]]
            guicenter [guibutton "configure key binds" [sleep 50 [curbindtype = 2]; showgui options_controls]]
        ]
    ] ]


    // this one could go to config/compass.cfg, but it is convenient to have it here for comparison
    newcompass editing $editingtex [
        compass "map, waypoints, ^fcr^fwoutes" R [showgui mapinfo]
        compass "e^fcd^fwiting options" D [showgui editoptions]
        compass "^fcg^fweometry menu" G [showgui geometry]
        compass "^fcw^fworld and lighting" W [showgui world]
        compass "texture ^fcv^fwariations" V [showgui textures]
        compass "^fct^fwexture list" T [showtexgui]
        compass "^fcf^fwind and edit entities" F [showgui ents]
        compass "m^fca^fwterials" A [showgui materials]
        compass "^fce^fwscape" E [cleargui 1]
    ]

    newgui mapinfo [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guilist [
            guinohitfx [ guicenter [guiimage $mapname [] 9 1 "textures/emblem"]]
            guistrut 2
            guicenter [
                guilist [
                    guifield maptitle -20
                    guistrut 2
                    guitext  "map title"
                ]
                guilist [
                    guifield mapauthor -20
                    guistrut 2
                    guitext  "map author"
                ]
                guilist [
                    guifield mapname -20
                    guistrut 2
                    guitext  "file name"
                ]
                guilist [
                    guifield mapmusic -20
                    guistrut 2
                    guitext  "music"
                    guispring 1
                    guibutton "pick" [showgui music]
                ]
                guilist [
                    guifield numplayers -20
                    guistrut 2
                    guitext  "players"
                ]
                guilist [
                    guifield maxplayers -20
                    guistrut 2
                    guitext  "max players"
                ]
                guilist [
                        guifield mapbalance -20
                    guistrut 2
                    guieditcheckbox "asymmetric map" mapbalance
                ]
                guilist [
                    guitext (format "map revision: %1" $maprevision)
                    guispring 1
                    guieditbutton2 "save the map" [savemap]
                ]
                guilist [
                    guitext (format "bounding box size: %1" (<< 1 $mapsize))
                    guispring 1
                    guieditbutton2 "increase" [mapenlarge]
                ]
                guilist [
                    guitext (format "map size (power): %1" $mapsize)
                    guispring 1
                    guieditbutton2 "shrink where empty" [shrinkmap]
                ]
                guitext (format "map file format version: %1" $mapversion)
                guistrut 0.5
                if (isonline) [
                        guieditbutton "refresh map from server" getmap
                        guieditbutton "send map to server" sendmap
                ]
                guibutton "edit the config file" [notepad @mapname.cfg] []
                guibutton "reset the texture list" [showgui texreset] [] $warningtex
                guibutton "start a new map" [showgui newmapgui ] []  $editingtex
            ]
        ]

        guitab "waypoints and routes"
        if (= 0 $guipasses) [ idmax = 3 ; count = "" ]
        guistrut 1
        guicenter [
            guilist [
                guitext "bot navigation meshes:" $privbottex
                guieditcheckbox "show waypoints" showwaypoints
                guieditcheckbox "drop waypoints as you move" dropwaypoints
                guistrut 0.5
                guieditbutton "^fyclear^fw all waypoints" clearwaypoints
                guieditbutton "clear waypoints in selected volume" delselwaypoints
                guistrut 0.5
                if (findfile (format "%1.wpt" $mapname)) [
                    guieditbutton "reload waypoints from file" [loadwaypoints]
                    guieditbutton "save and overwrite waypoints file" [savewaypoints]

                ] [
                    guitext "^fano waypoint file found for this map"
                    guieditbutton "save current waypoints" [savewaypoints]
                ]
                guistrut 0.5
                guitext "^faoptions for testing"
                guilist [
                    guibutton "raise or " [gamespeed (+25 $gamespeed)]
                    guibutton "lower gamespeed:" [gamespeed (-25 $gamespeed)]
                    guispring 1
                    guifield gamespeed 4
                    guitext "%"
                ]
                guilist [
                    guieditbutton "add bot" addbot
                    guispring 1
                    guieditbutton "remove bot" delbbot
                ]

            ]
            guispring 1
            guilist [
                guistrut 20 1
                guitext "show players the way (optional):" $moderacetex
                guitext "^farender routes"
                guilist [
                    guiradio "disabled" routeid -1
                    guispring 1
                    guibutton "deselect all" [entcancel]
                ]
                loop i $idmax [
                    guilist [
                        guiradio (format "id %1" $i) routeid $i
                        guispring 1
                        guibutton "select" [entcancel; entfind route @i]
                    ]
                ]
                guilist [
                    guitext "^falook up more routes "
                    guispring 1
                    guifield idmax 3
                ] 
                guistrut 0.5
                guieditcheckbox "drop route ents as you move" droproute
                guistrut 0.5
                guitext (format "^faselected entities: %1" (enthavesel))
                guibutton "delete all selected ents" [delent]
                guistrut 0.5
            ]
        ]
    ] ]

    newgui music [ guistayopen [
        loopfiles i "sounds/music" "ogg" [
            guilist [ 
                guibutton $i [music music/@i] [] $arrowrighttex
                guispring 1
                if ($editing) [
                    guibutton "" [mapmusic sounds/music/@i; cleargui 1] [] $editingtex
                ]
            ]
        ]
        guilist [
        guibutton clear [music ""] [] $warningtex
            if ($editing) [
                guispring 1
                guibutton "" [mapmusic ""; cleargui 1] [] $editingtex
            ]
        ]
    ] ]

    newgui editoptions [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guilist [
            guilist [
                guieditcheckbox "toggle edit radar" showeditradar
                guieditcheckbox "show material volumes" showmat
                guistrut 0.5
                guieditcheckbox "toggle fullbright" "fullbright"
                guieditcheckbox "toggle wireframe" "wireframe"
                guieditcheckbox "toggle blank geometry" "blankgeom"
                guieditcheckbox "toggle outline" "outline"
                guibutton "change outline colour" [pickcolour outlinecolour] [] $editingtex
                guistrut 0.5
                guieditcheckbox "toggle paste grid" "showpastegrid"
                guieditcheckbox "toggle cursor grid" "showcursorgrid"
                guieditcheckbox "toggle selection grid" "showselgrid"
                guieditcheckbox "snap ents to grid" "entselsnap"
                guistrut 0.5
                guieditcheckbox "toggle allfaces texture mode" "allfaces"
                guistrut 0.5
                guilist [ 
                    guitext "threads to compute lightmaps in parallel "
                    guispring 1
                    guifield numcpus 2 
                ]
                guilist [ 
                    guitext "adjust your float speed "
                    guispring 1
                    guifield floatspeed 6 [ floatspeed $floatspeed ]
                ]
                guitext (format "current editing grid size: %1" (<< 1 $gridpower))
                guislider gridpower 0 (min (- $mapsize 1) 12) 
                guistrut 0.5
                guitext "undo cache size in MB"
                guislider undomegs 1 10"
            ]
        ]    
    ] ]

    newgui geometry [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guieditbutton   "undo" [undo; passthroughsel 0]
        guieditbutton   "redo" redo
        guieditbutton   "optimize" remip
        guistrut 0.5
        guieditbutton   "pull cube" [universaldelta -1]
        guieditbutton   "push cube" [universaldelta 1]
        guieditbutton   "push/pull gridsize" [domodifier 1]
        guieditbutton   "push/pull faces " [domodifier 2]
        guieditbutton   "push/pull corners" [domodifier 3]
        guieditbutton   "push/pull rotation" [domodifier 4]
        guieditbutton   "push/pull texture" [domodifier 6]
        guistrut 0.5
        guieditbutton   "deselect" "cancelsel"
        guieditbutton   "deselect only ents" "entcancel"
        guistrut 0.5
        guieditbutton   "copy" "editcopy"
        guieditbutton   "paste" "editpaste"
        guieditbutton   "cut and paste on release" "editcut"
        guieditbutton   "flip" "editflip"
        guieditbutton   "toggle heightmap mode" [hmapedit (! $hmapedit); blendpaintmode 0] 
        guistrut 0.5
        guieditbutton   "delete selected ents / geometry" editdel
        guieditbutton   "grab texture from selected face" gettex
        guieditbutton   "repeat last texture change across entire map" [allfaces 0; replace]
        guistrut 0.5

        guitab "prefabs"
        if (= 0 $guipasses) [
            prefabs = ""; slider = 0; pnew = "tmp"
            loopfiles i "prefab" "obr" [append prefabs $i] 
        ]
        guistrut 1
        guilist [
            guieditbutton "save the selected geometry as" [saveprefab @pnew]
            guistrut 1
            guifield pnew 10
        ]
        imax = (listlen $prefabs)
        if (imax) [
            guitext "^fapick a prefab to paste the geometry" 
            guistrut 1
            slidermax = (max (- $imax 15) 0)
            guicenter [
                guilist [
                    guistrut 30 1
                    loop i (min 15 $imax) [
                        p = (at $prefabs (+ $i $slider))
                        guieditbutton $p [pasteprefab @p] 
                    ]
                ]
                if (slidermax) [guislider slider 0 $slidermax [] 1 1]
            ]
        ] [
            guitext "no obr files found in home/prefab"
        ]
        guistrut 1
        guitext (format "prefabs are scaled by the current grid size: %1" (<< 1 $gridpower))
        guislider gridpower 0 (min (- $mapsize 1) 12) 
    ] ]

    newgui world [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        if (= $guipasses 0) [
            ambientval = (hexcolour $ambient)
            skylightval = (hexcolour $skylight)
            fogcolourval = (hexcolour $fogcolour)
            cloudlayercolourval = (hexcolour $cloudlayercolour)
        ]
        guilist [ 
            guifield fog 15
            guistrut 1
            guitext "fog distance"
        ]
        guilist [ 
            guifield fogcolourval 15 [fogcoulour $fogcolourval]
            guistrut 1
            guitext "fog colour"
            guispring 1
            guibutton "pick" [pickcolour fogcolour] [] $editingtex
        ]
        guilist [ 
            guifield cloudlayer 15
            guistrut 1
            guitext "cloud layer texture"
            guispring
            guieditbutton "01" [cloudlayer skyboxes/clouds01]
            guistrut 1
            guieditbutton "02" [cloudlayer skyboxes/clouds02]
            guistrut 1
            guieditbutton "03" [cloudlayer skyboxes/clouds03]
        ]
        guilist [ 
            guifield cloudlayercolourval 15 [cloudlayercoulour $cloudlayercolourval]
            guistrut 1
            guitext "cloudlayer colour"
            guispring 1
            guibutton "pick" [pickcolour cloudlayercolour] [] $editingtex
        ]
        guistrut 0.5
        guitext "game physics:"
        guilist [ 
            guifield gravity 15
            guistrut 1
            guitext "gravity"
        ]
        guilist [ 
            guifield floorcoast 15
            guistrut 1
            guitext "floor coast (slipperiness)
        ]
        guilist [ 
            guifield aircoast 15
            guistrut 1
            guitext "air coast"
        ]
        guistrut 0.5
        guieditbutton "set up a skybox" [showgui skyboxes] 
        guibutton "all world variables" [sleep 500 [varflags = 8; varsmore = 1]; showgui vars]

        guitab lighting
        guilist [ 
            guifield lightprecision 4
            guistrut 1
            guitext "light precision - small values are expensive"
        ]
        guiright [guitext "^fathis affects file size and calculation time"]
        guistrut 0.5
        guilist [ 
            guifield lighterror 10
            guistrut 1
            guitext "light error"
        ]
        guilist [ 
            guifield blurlms 10
            guistrut 1
            guitext "blurr lightmap (soften)"
        ]
        guilist [ 
            guifield ambientval 10 [ambient $ambientval]
            guistrut 1
            guitext "ambient colour (shadows)"
            guispring 1
            guibutton "pick" [pickcolour ambient] [] $editingtex
        ]
        guilist [ 
            guifield skylight 10 [skylight $skylightval]
            guistrut 1
            guitext "skylight colour (sunlight)"
            guispring 1
            guibutton "pick" [pickcolour skylight] [] $editingtex
        ]
        guistrut 0.5
        guieditbutton "optimize geometry" "remip"
        guieditbutton "patch existing lightmap " [fullbright 0; patchlight]
        guieditbutton "quick lightmap calculation" [fullbright 0; calclight -1]
        guieditbutton "full lightmap calculation" [fullbright 0; calclight 1]
        guieditbutton "save the map" [savemap]

        guitab preset
        guitext         "quick global lighting:"
        guitext " "
        guibutton      "night"                              [ambient 40 40 40; skylight 70 80 90; skybox skyboxes/stars; skycolour 255 255 255; cloudlayer skyboxes/clouds01; cloudlayercolour 50 60 100; cloudlayerblend 0.3; cloudscrollx 0.005; fogcolour 12 10 13; fog 2000; calclight -1]
        guibutton      "morning"                           [ambient 60 35 40; skylight 170 140 145; skybox freezurbern/harmony; skycolour 255 160 180; cloudlayer skyboxes/clouds03; cloudlayercolour 255 125 170; cloudlayerblend 0.2; cloudscrollx 0.01; fogcolour 129 79 59; fog 2000; calclight -1]
        guibutton      "evening"                           [ambient 50 30 10; skylight 180 100 70; skybox skyboxes/sunsetflat; skycolour 255 255 255; cloudlayer skyboxes/clouds01; cloudlayercolour 255 100 50; cloudlayerblend 0.2; cloudscrollx 0.005; fogcolour 40 20 5; fog 2000; calclight -1]
        guitext " "
        guibutton      "^foRESET ^fwenvironment to default"      [ambient 0 0 0; skylight 0 0 0; skybox ""; skycolour 255 255 255; cloudlayer ""; cloudlayercolour 255 255 255; cloudlayerblend 1.0; cloudscrollx 0; fogcolour 128 153 179; fog 4000; calclight -1]

    ] ]

    newgui textures [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        if (= $guipasses 0) [varpix = 32 ; vara = 0.5; vara2 = 0.0]
        guistrut 0.5
        guicenter [guitext "v-commands create variations of the currently selected texture"]
        guicenter [guieditbutton "reset variation on selected texture" [resettex]]

        guistrut 0.5
        guilist [
            guispring 1
            guilist [
                guitext "rotation or flip:"
                guiright [guieditbutton2 "90 degree"  [vrotate 1]]
                guiright [guieditbutton2 "180 degree"  [vrotate 2]]
                guiright [guieditbutton2 "270 degree"  [vrotate 3]]
                guiright [guieditbutton2 "left to right" [vrotate 4]]
                guiright [guieditbutton2 "top to bottom" [vrotate 5]]
                guiright [guieditbutton2 "reset"  [vrotate 0]]
            ]
            guispring 1
            guilist [
                guitext "texture offset:"
                guieditbutton "move up" [vdelta voffset 0 (* -1 @varpix)] [] $arrowtex
                guieditbutton "move down" [vdelta voffset 0  @varpix] [] $arrowdowntex
                guieditbutton "move left" [vdelta voffset (* -1 @varpix) 0] [] $arrowlefttex
                guieditbutton "move right" [vdelta voffset @varpix 0] [] $arrowrighttex
                guilist [
                    guitext "^faby "
                    guifield varpix 3
                    guitext " ^fapixels"
                ]
                guieditbutton "reset offset" [voffset 0 0] [] 
            ]
            guispring 1
            guilist [
                guitext "scale:"
                guieditbutton "800.0%" [vscale 8]
                guieditbutton "400.0%" [vscale 4]
                guieditbutton "200.0%" [vscale 2]
                guieditbutton "100.0%" [vscale 1]
                guieditbutton " 50.0%" [vscale 0.5]
                guieditbutton " 25.0%" [vscale 0.25]
                guieditbutton " 12.5%" [vscale 0.25]
            ]
            guispring 1
            guilist [
                guitext "scrolling:"
                guieditbutton "+horizontal" [vdelta vscroll 0.1 0]
                guieditbutton "-horizontal" [vdelta vscroll -0.1 0]
                guieditbutton "+vertical" [vdelta vscroll 0 0.1]
                guieditbutton "-vertical" [vdelta vscroll 0 -0.1]
                guieditbutton "stop moving" [vscroll 0 0]
            ]
            guispring 1
        ]
        guistrut 1
        guicenter [ guitext "customize the opacity when inside alpha material:" ]
        guicenter [ 
            guifield vara 3 
            guitext " front side"
            guistrut 3
            guifield vara2 3 
            guitext " back side"
            guistrut 3
            guieditbutton "apply" [valpha $vara $vara2]
        ]
        guistrut 1
        guicenter [guibutton "^fyclear^fw unused textures from the map configuration data" [showgui texreset]]

        guitab vcolour
        if (= $guipasses 0) [hex = 0xffffff]
        guistrut 1
        guicenter [ guitext "pick a colour to scale the red, green and blue component of the texture" ]
        guistrut 1
        guilist [
            guispring 1
            guihexpreview $hex multiplier
            guispring 1
            guilist [
                guirgbsliders hex
            ]
            guispring 1
            guilist [
                guieditbutton "replace the r g b factors" [vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                guieditbutton "alter the r g b factors" [voffset vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                guieditbutton "reset to original colour" [vcolour 1 1 1] 
            ]
        ]
        guistrut 1
        guipalette 0.6

        guitab glow
        if (= $guipasses 0) [hex = 0xffffff]
        guistrut 1
        guicenter [ guitext "pick a colour for the glow effect - requires a glowmap texture" ]
        guistrut 1
        guilist [
            guispring 1
            guihexpreview $hex glow
            guispring 1
            guilist [
                guirgbsliders hex
            ]
            guispring 1
            guilist [
                guieditbutton "set and keep other shaders" [vdelta vshaderparam glowcolor @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                guieditbutton "set and ^fyclear^fw other shaders" [vshaderparam glowcolor @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                guieditbutton "disable glow" [vdelta vshaderparam glowcolour 0 0 0] 
            ]
        ]
        guistrut 1
        guipalette 0.6

        guitab specular
        if (= $guipasses 0) [hex = 0xffffff]
        guistrut 1
        guicenter [ guitext "pick a colour for specular lighting - requires a normalmap texture" ]
        guistrut 1
        guilist [
            guispring 1
            guihexpreview $hex shine
            guispring 1
            guilist [
                guirgbsliders hex
            ]
            guispring 1
            guilist [
                guieditbutton "set and keep other shaders" [vdelta vshaderparam specscale @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                guieditbutton "set and ^fyclear^fw other shaders" [vshaderparam specscale @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                guieditbutton "disable specular" [vdelta vshaderparam specscale 0 0 0]
            ]
        ]
        guistrut 1
        guipalette 0.6

        guitab parallax
        if (= $guipasses 0) [val1 = 0; val2 = 0]
        guistrut 1
        guicenter [ guitext "parallax deforms the texture for a pseudo-3d-effect based on the normal map" ]
        guistrut 1
        guicenter [ guitext "The distortions depend on the distance to the observer"]
        guicenter [ guitext "WARNING: Use very small values or strange effects can and will result"]
        guistrut 1
        guilist [
            guifield val1 12
            guistrut 1
            guitext "scaling factor " 
            guispring 1
            guibutton "set and keep other shaders" [vdelta vshaderparam parallaxscale @val1 @val2] [] $editingtex
        ]
        guilist [
            guifield val2 12
            guistrut 1
            guitext "offset" 
            guispring 1
            guibutton "set and ^fyclear^fw other shaders" [vshaderparam parallaxscale @val1 @val2] [] $editingtex
        ]
        guilist [
            guispring 1
            guibutton "disable parallax" [vdelta vshaderparam parallaxscale 0 0 ] [] $editingtex
        ]
        guistrut 2
        guilist [
            guitext "some examples:"
            guispring 1
            guibutton "moderate"   [saycommand /vdelta vshaderparam parallaxscale 0.02 0.0]
            guispring 1
            guibutton "strong"     [saycommand /vdelta vshaderparam parallaxscale 0.05 0.0]
            guispring 1
            guibutton "inverted"   [saycommand /vdelta vshaderparam parallaxscale -0.02 0.0]
            guispring 1
            guibutton "soft blob"  [saycommand /vdelta vshaderparam parallaxscale 0.0 -0.02]
            guispring 1
            guibutton "fish eye"   [saycommand /vdelta vshaderparam parallaxscale 0.0 2.0]
        ]

        guitab blend
        if (= 0 $guipasses) [slotnum = 1]
        guistrut 1
        guicenter [guitext "a blendmap creates smooth transitions on two-layer textures"]
        guistrut 1
        guilist [
        guibutton "first look up a texture index in the texture list:  #" [cleargui; showtexgui]
            guistrut 1
            guifield slotnum 4
        ]
        guieditbutton "add the index as texture layer to the current selection" [vlayer @slotnum]
        guistrut 1
        guieditbutton "reset texture layer on selection" [vlayer ""]
        guistrut 1
        guibar
        guilist [
            guilist [
                guitext "cycle blend paint mode:"
                guiradio "off" blendpaintmode 0
                guiradio "overwrite" blendpaintmode 1
                guiradio "merge" blendpaintmode 2
                guiradio "max opacity" blendpaintmode 3
                guiradio "inverted merge" blendpaintmode 4
                guiradio "erase" blendpaintmode 5
            ]
            guispring 1
            guibutton (dobindsearch [hmapedit 0;
    blendpaintmode (? (= $blendpaintmode (getvarmax blendpaintmode)) 0 (+ $blendpaintmode 1))
    echo (concatword "^fgblend mode:^fw " (at $paintmodes $blendpaintmode) " (^fc" $blendpaintmode "^fw)")] edit)
            guispring 1
            guilist [
                guistrut 30 1
                guitext "blendmap paint brush:"
                guitext (getblendbrushname (curblendbrush))
                if ($blendpaintmode) [
                    guieditbutton "cycle" [universaldelta -1] 
                    guieditbutton "quit blendmap paint mode" [hmapedit 0; blendpaintmode 0]
                ] [    
                    guitext "enable blendmap mode first"
                    guistrut 1
                ]     
                guistrut 1
                guieditbutton "generate preview" showblendmap
                guieditbutton "^foclear^fw the entire blendmap" clearblendmap
            ]
        ]
    ] ]

    slider = 0
    efuitype = 0
    efuitypename = "*"
    
    efuireset = [
        loop i (? $efuitype (getentattr $efuitype) 12) [
            [efuiattr@i] = "*"
        ]
    ]
    efuireset
    
    newgui ents [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guistayopen [
            guistrut 0.5
            guicenter [
                guieditbutton "lights menu" [showgui lights] 
                guispring 1
                guieditbutton "sound list" [showgui sounds] 
                guispring 1
                guieditbutton "mapmodel list" [showgui mapmodels] 
            ]
            guicenter [guitext "quick insertion of entity presets:"]
            guistrut 0.5
            guilist [
                loop i 5 [
                    wp = (at $weapname (+ 2 $i))
                    guiimage $[@[wp]tex] [newent weapon (+ 2 @i)] 2 1 [] "" $[@[wp]colour]
                ]
            ]
            guilist [
                loop i 5 [
                    wp = (at $weapname (+ 7 $i))
                    guiimage $[@[wp]tex] [newent weapon (+ 7 @i)] 2 1 [] "" $[@[wp]colour]
                ]
            ]
            guilist [ 
                guiimage $flagtex [newent affinity ] 2 1 [] "" 
                guiimage $flagtex [newent affinity 1] 2 1 [] "" $teamalphacolour
                guiimage $flagtex [newent affinity 2] 2 1 [] "" $teamomegacolour
                guiimage $flagtex [newent affinity 3] 2 1 [] "" $teamkappacolour
                guiimage $flagtex [newent affinity 4] 2 1 [] "" $teamsigmacolour
            ]
            guilist [ 
                guiimage $playertex [newent playerstart ] 2 1 [] "" 
                guiimage $playertex [newent playerstart 1] 2 1 [] "" $teamalphacolour
                guiimage $playertex [newent playerstart 2] 2 1 [] "" $teamomegacolour
                guiimage $playertex [newent playerstart 3] 2 1 [] "" $teamkappacolour
                guiimage $playertex [newent playerstart 4] 2 1 [] "" $teamsigmacolour
            ]
            guilist [
                guiimage $arrowtex [newent pusher] 2 1
                guiimage $shockingtex [newent teleport] 2 1
                guiimage $burningtex [newent particles] 2 1
                guiimage $modeonslaughttex [newent actor] 2 1
                guiimage $moderacetex [newent checkpoint] 2 1
            ]

            guitab find // entities 
            guicenter [
                guilist [
                    if (efuitype) [
                        efuiattrs = (loopconcat i (getentattr $efuitype) [result $[efuiattr@i]])
                        guieditbutton "create this type of entity" [newent @efuitypename @efuiattrs] [] 
                    ] [
                        efuiattrs = "*"
                        guistrut 1
                    ]
                    guieditbutton "pick all matching entities..." [entfind @efuitypename @efuiattrs] [] // "textures/info"
                    guieditbutton "... in the selected volume" [entfindinsel @efuitypename [@@efuiattrs]] [] //"textures/spectator"
                    guieditbutton "deselect all ents" [entcancel] [] //"textures/warning"
                    if (enthavesel) [
                        guibutton "choose latest selection" [
                            efuitypename = $enttype
                            efuitype = (indexof $enttypelist $enttype)
                            efuireset
                            loop i 12 [
                                [efuiattr@i] = (entattr $i) 
                            ]
                        ] [] "textures/menu"
                    ] [guistrut 1]
                ]
            ]
            guistrut 1
            guilist [
                guilist [
                    guiradio (tabify "any type" 3) efuitype 0 [efuitypename = "*"; efuireset]
                    loop i (- (getentinfo) 2) [
                        type = (getentinfo (+ $i 1))
                        guiradio (tabify $type 3) efuitype (+ $i 1) [efuitypename = @type; efuireset]
                    ]
                ]
                guistrut 1
                guilist [
                    if (efuitype) [
                        loop i (getentattr $efuitype) [
                            guilist [
                                guifield [efuiattr@i] 11  
                                guistrut 1
                                guitext (getentattr $efuitype $i) 
                            ]
                        ]
                        //if (=s $efuitypename weapon) [
                        //    guiloop i $weapidxnum [
                        //        guiradio (at $weapname $i) [efuiattr0] $i
                        //    ]
                        //]
                    ] [
                        loop i 12 [
                            guilist [
                                guifield [efuiattr@i] 11  
                                guistrut 1
                                guitext (format "attribute %1" (+ $i 1))
                            ]
                        ]
                    ]
                    guistrut 25 1
                ]
            ]
    
            guitab "selected" // entities
            guicenter [
                if (= 0 (enthavesel)) [
                    slider = 0
                    guilist [
                        guistrut 5 
                        guitext "no entities selected"
                    ]
                ] [
                    guilist [
                        guitext (format "selected entities: %1" $enthavesel)
                        guieditbutton "cycle entity links  ->  <-  <-->  -" [entlink]
                        guieditbutton "view (next) entity" [entautoview 1] 
                        guibutton "reselect all matching ents" [entfind @efuitypename @efuiattrs] [] 
                        guistrut 1
                        guitext "click below to select one entity"
                        guitext "  or all with identical attributes"
                        guistrut 1
                        entgetlist = ""
                        entindexlist = ""
                        imax = 0
                        entloop [
                            append entgetlist (format "[%1]" (entget))
                            append entindexlist (format "[(%1)^t %2]" (entindex) (entget))
                            imax = (+ $imax 1) 
                        ]
                        slidermax = (max (- $imax 15) 0)
                        guilist [
                            guilist [
                                guistrut 30 1
                                loop i (min 15 $imax) [
                                    j = (+ $i $slider)
                                    guilist [
                                        guibutton (at $entindexlist $j) [
                                            entcancel
                                            entfind @(at $entgetlist @@j) 
                                            slider = 0
                                        ]
                                    ]
                                ]
                            ]
                            if (slidermax) [guislider slider 0 $slidermax [] 1 1]
                        ]
                    ]    
                ]
            ]
    
            guitab "edit" // entities
            guicenter [
                guilist [
                    if (> $enthavesel 1)  [
                        guitext (format "%1 entities selected!" $enthavesel) textures/waiting 0xffcc00
                        guitext "attributes according to latest selection"
                        guitext "changes affect all selected entities"
                        guistrut 1
                    ]
                    if (= $enthavesel 0)  [
                        guistrut 5 
                        guitext "no entities selected"
                    ] [
                        itype = (indexof $enttypelist (enttype))
                        guitext (format "%1 (index %2)" (enttype) (entindex))
                        guistrut 1
                        loop iattr (getentattr $itype) [
                            guilist [
                                guifont emphasis [
                                guibutton "+" [entprop @iattr 1]
                                guistrut 1
                                guibutton "-" [entprop @iattr -1]
                                guistrut 1
                                ]
                                field@iattr = (entattr $iattr)
                                guifield field@iattr -10 [entattr @iattr (field@iattr)]
                                guistrut 1
                                guitext (getentattr $itype $iattr)
                            ]    
                        ]    
                    ]    
                    guistrut 1
                    guieditbutton "edit entities via the console" selentedit
                    guilist [
                        guitext "quick-edit: mousewheel and "
                        guitext (dobindsearch [domodifier 11] edit)
                        guitext (dobindsearch [domodifier 12] edit)
                        guitext (dobindsearch [domodifier 13] edit)
                        guitext " etc."
                    ]
                ]    
            ]
        ]
    ]

    newgui sounds [ guistayopen [
        if (= 0 $guipasses) [
            slider = 0
            mapsounds = ""
            imax = (getsound 1)
            loop i $imax [
                append mapsounds (getsound 1 $i 3)
            ] 
        ] 
        if (imax) [
            guitext "^fapick a sound entity to create"
            guistrut 1
            slidermax = (max (- $imax 15) 0)
            guicenter [
                guilist [
                    guistrut 40 1
                    loop i (min 15 $imax) [
                        j = (+ $i $slider)
                        p = (at $mapsounds $j)
                        guieditbutton $p [newent sound @j] 
                    ]
                ]
                if (slidermax) [guislider slider 0 $slidermax [] 1 1]
            ]
        ] [
            guitext "no sounds found for this map configuration"
        ]
    ] ]

    newgui soundtest [ guistayopen [
        if (= 0 $guipasses) [
            slider = 0
            sounds = ""
            imax = (getsound 0)
            loop i $imax [
                append sounds (getsound 0 $i 3)
            ] 
        ] 
        if (imax) [
            guibutton "test the /sound command"
            guibutton "show sound debugger" [showui cursounds]
            guistrut 1
            slidermax = (max (- $imax 15) 0)
            guicenter [
                guilist [
                    guistrut 30 1
                    loop i (min 15 $imax) [
                        j = (+ $i $slider)
                        p = (format "%1^t%2" $j (at $sounds $j))
                        guibutton $p [sound @j] [] $arrowrighttex
                    ]
                ]
                if (slidermax) [guislider slider 0 $slidermax [] 1 1]
            ]
        ] [
            guitext "could not load sound data"
        ] 
    ] ]

    newgui lights [ guistayopen [
        if (= 0 $guipasses) [ hex = 0xffffff; rad = 64 ]
        guistrut 1
        guicenter [guitext "pick a colour for creating light or sunlight presets"]
        guistrut 1
        guilist [
            guispring 1
            guilist [
                guilist [
                    guihexpreview $hex colour
                ]
                guistrut 1
                guieditbutton "create lightfx" [newent lightfx]
                guieditbutton "link ents" [entlink]
            ]
            guistrut 1
            guilist [
                guirgbsliders hex
                guistrut 1
                guieditbutton "^tquick lightmap calculation" [fullbright 0; calclight -1]
                guieditbutton "^tfull lightmap calculation" [fullbright 0; calclight 1]
            ]
            guistrut 1
            guilist [
                guitext "create entitites:
                guieditbutton "tiny light" [newent light 32 @colr @colg @colb]
                guieditbutton "small light" [newent light 64 @colr @colg @colb]
                guieditbutton "medium light" [newent light 128 @colr @colg @colb]
                guieditbutton "large light" [newent light 256 @colr @colg @colb]
                guieditbutton "huge light" [newent light 512 @colr @colg @colb]
            ]
            guistrut 2
            guilist [
                guistrut 1
                guieditbutton "sunlight top" [newent sunlight 0 90 @colr @colg @colb 45]
                guieditbutton "sun light north" [newent sunlight 0 60 @colr @colg @colb 45]
                guieditbutton "sun light east" [newent sunlight 90 60 @colr @colg @colb 45]
                guieditbutton "sun light south" [newent sunlight 180 60 @colr @colg @colb 45]
                guieditbutton "sun light west" [newent sunlight 270 60 @colr @colg @colb 45]
            ]
        ]
        guistrut 1
        guipalette 0.6
        guistatus [Tip: Create lightfx first and then lights to link them quickly]
    ] ]


    findskyboxes = [
        skyboxes = ""
        loopfiles dir data "" [
            looplist ext "png jpg" [
                loopfiles i $dir $ext [
                    if (> (stringstr $i "_up") -1) [
                        ibox = (format "%1/%2" $dir $i)
                        append skyboxes (substring $ibox 0 (stringstr $ibox "_"))
                    ]
                ]
            ]
        ]
    ] 
    
    findskyboxes
    selbox =  $skybox 
    iselbox = (indexof $skyboxes $skybox) 
    slider = 0
    
    newgui skyboxes [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guilist [
            guinohitfx [ guiimage (format "%1_up" $selbox) [skybox $selbox] 6 0 "textures/sky" ]
            guispring
            guilist [
                guistrut 1
                guibutton "set selection as skybox" [skybox $selbox] [] $editingtex
                guistrut 1
                guilist [
                    guibutton "or cloudbox with blend  " [cloudbox $selbox]
                    guifield cloudblend 4
                ]
            ]
            guispring
            guilist [
                guistrut 25 1
                guistrut 1
                loop ii 10 [
                    i = (+ $slider $ii)
                    guiradio (at $skyboxes $i) iselbox $i [
                        sleep 20 [selbox = (at $skyboxes @iselbox)]
                    ]
                ]
                guistrut 1
            ]
            guislider slider 0 (- (listlen $skyboxes) 10) [] 1 1
            guispring
        ]
        guilist [
            looplist i "rt ft lf bk" [
                guinohitfx [ guiimage (format "%1_%2" $selbox $i) [] 6 0 "textures/sky" ]
            ]    
        ]    
        guilist [
            guinohitfx [ guiimage (format "%1_dn" $selbox) [skybox $selbox] 6 0 "textures/sky" ]
            guispring
            guilist [
                guispring 
                guilist [
                    guifield yawsky 4
                    guistrut 1
                    guitext yawsky
                ]    
                guilist [
                    guifield spinsky 4
                    guistrut 1
                    guitext spinsky
                ]    
                guilist [
                    guifield skyblend 4
                    guistrut 1
                    guitext "skyblend (if supported)"
                ]    
                guistrut 0.5
                guilist [
                    skycolourval = (hexcolour $skycolour)
                    guifield skycolourval 8 [skycolour $skycolourval]
                    guistrut 1
                    guitext "colour multiplier"
                    guispring 1
                    guibutton "pick" [pickcolour skycolour] [] $editingtex
                ]    
                guilist [
                    skybgcolourval = (hexcolour $skybgcolour)
                    guifield skybgcolourval 8 [skybgcolour $skybgcolourval]
                    guistrut 1
                    guitext "background colour"
                    guistrut 1
                    guibutton "pick" [pickcolour skybgcolour] [] $editingtex
                ]    
                guispring 
            ]    
            guispring 1
            guilist [
                guispring 
                guibutton "background colour only" [skybox ""] [] textures/warning
                guibutton "reset colours and spin" [skycolour 0xffffff; skybgcolour 0; skyalpha = 1; spinsky 0; yawsky 0] [] textures/action
                guicheckbox skyglare skyglare
                guicheckbox skyboxglare skyboxglare
                guicheckbox skybgglare skybgglare
                guicheckbox skytexture skytexture
                guicheckbox skytexturelight skytexturelight
                guispring 
            ]    
            guispring
        ]    
    ] ] 

    newgui serverpriv [
        guistayopen [
            guiradio   "veto server"               openservertype 0   [serveropen 3]
            guiradio   "open server"               openservertype 1   [serveropen 1]
            guiradio   "coop server"               openservertype 2   [serveropen 2]
            guitext " "
            guibutton   "claim privileges"            "setpriv 1"
            guibutton   "relinquish privileges"         "setpriv 0"
            guitext " "
            guibutton   "administrator password"      "saycommand [/adminpass ]"
        ]
    ]

    newgui newmapgui [
            if (= $guipasses 0) [newmapsize = 12]
            guibutton    "^foREVERT ^fwto last saved version of @savemap_name"      "map $savemap_name"
            guitext " "
            guibutton   "^foOPEN ^fwan existing map"                        "showgui maps 1"
            guitext " "
            guibutton    "^foCLEAR MAP ^fwand begin a new one ^fr(Warning!)"         "newmap $newmapsize"
            guitext      "size of new map"
            guislider    "newmapsize" 10 16
    ]

    newgui texreset [
        guilist [
            guilist [
            guieditbutton "^fyCLEAR ^fwall unused textures from map cfg" texturecull
            guieditbutton "^fyCLEAR ^fwunused textures and load the default packages" texturerehash
            guieditbutton "^frCLEAR ^fwall textures from map cfg" texturereset
            ]
        ]
    ]

    matmenu = [
        guieditcheckbox "show material volumes" showmat
        guistrut 1
        looplist mat [air alpha water lava clip noclip aiclip death ladder] [
            guieditbutton $mat [editmat @mat]
        ]
        guibar
        looplist m [water lava] [ 
            guilist [
                looplist i [2 3 4] [
                    guieditbutton2 [@m@i] [editmat @m@i]
                    guispring 1
                ]
            ]
        ]
    ]

    newgui materials [
        @matmenu
    ]

    newgui quickedit [
        @quickeditmenu
        guitab materials
        @matmenu
    ]

    mapmodelprevs = 3
    mapmodelprevs3d = 0

    alias showmapmodel [
        mapmodelpath = (mapmodelindex $arg1)
        mapmodelshot = (concatword $mapmodelpath "/thumb")
        mapmodelcmd = (concat newent mapmodel $arg1)
        guilistx 2 [
            guilist [
                if (mapmodelprevs) [
                    if (mapmodelprevs3d) [
                        guimodelpreview $mapmodelpath "mapmodel" $mapmodelcmd $mapmodelprevs 1
                    ] [
                        guiimage $mapmodelshot $mapmodelcmd $mapmodelprevs 1 "textures/nothumb"
                    ]
                ]
            ]
            guibutton $mapmodelpath $mapmodelcmd
        ]
    ]

    newgui mapmodels [
        guilist [
            guicheckbox "show previews" mapmodelprevs 3 0
            guistrut 2
            guicheckbox "rotating (can take a while to load)" mapmodelprevs3d
            guistrut 2
            guitext "change size "
            guifield mapmodelprevs 1
        ]
        mapmodelnum = (mapmodelindex)
        guilistloop 4 (? $mapmodelprevs 2 8) $mapmodelnum [ showmapmodel $i ]
    ]


